OBJECT Report 88 VAT- VIES Declaration Disk
{
  OBJECT-PROPERTIES
  {
    Date=05.04.13;
    Time=12:00:00;
    Version List=NAVW17.00.00.34587,NAVDE7.00.00.34587;
  }
  PROPERTIES
  {
    Permissions=TableData 254=imd;
    CaptionML=[DEU=Zusammenfassende Meldung Disk;
               ENU=VAT- VIES Declaration Disk];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FileMgt@1000 : Codeunit 419;
                BEGIN
                  FileName := FileMgt.ServerTempFileName('');
                END;

    OnPostReport=VAR
                   FileManagement@1001 : Codeunit 419;
                   ToFile@1000 : Text[1024];
                 BEGIN
                   IF NOT HideFileDialog THEN BEGIN
                     ToFile := Text007;
                     DOWNLOAD(FileName,Text005,'',Text006,ToFile);
                   END ELSE
                     FileManagement.DownloadToFile(FileName,FileName);
                 END;

  }
  DATASET
  {
    { 7612;    ;DataItem;                    ;
               DataItemTable=Table254;
               DataItemTableView=SORTING(Type,Country/Region Code,VAT Registration No.,VAT Bus. Posting Group,VAT Prod. Posting Group,Posting Date)
                                 WHERE(Type=CONST(Sale));
               OnPreDataItem=BEGIN
                               IF FileName = '' THEN
                                 ERROR(Text000);
                               CLEAR(VATFile);
                               VATFile.TEXTMODE := TRUE;
                               VATFile.WRITEMODE := TRUE;
                               VATFile.CREATE(FileName);

                               CompanyInfo.GET;
                               VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');
                               VATFile.WRITE('#v'+FileVersion);
                               VATFile.WRITE('#ve'+FileVersion2);
                               VATFile.WRITE('Laenderkennzeichen,USt-IdNr.,Betrag(Euro),Art der Leistung,Importmeldung');
                               NoOfGrTotal := 0;
                               Period := GETRANGEMAX("Posting Date");
                               InternalReferenceNo := FORMAT(Period,4,2) + '000000';
                             END;

               OnAfterGetRecord=VAR
                                  VATEntry@1001 : Record 254;
                                  TotalValueOfItemSupplies@1003 : Decimal;
                                  TotalValueOfServiceSupplies@1002 : Decimal;
                                  GroupTotal@1000 : Boolean;
                                BEGIN
                                  IF "EU Service" THEN BEGIN
                                    HasServiceSupplies := TRUE;
                                    IF UseAmtsInAddCurr THEN
                                      TotalValueOfServiceSupplies := "Additional-Currency Base"
                                    ELSE
                                      TotalValueOfServiceSupplies := Base;
                                  END ELSE BEGIN
                                    HasItemSupplies := TRUE;
                                    IF UseAmtsInAddCurr THEN
                                      TotalValueOfItemSupplies := "Additional-Currency Base"
                                    ELSE
                                      TotalValueOfItemSupplies := Base;
                                  END;

                                  IF "EU 3-Party Trade" THEN BEGIN
                                    HasEU3PartyTrade := TRUE;
                                    EU3PartyItemTradeAmt := EU3PartyItemTradeAmt + TotalValueOfItemSupplies;
                                    EU3PartyServiceTradeAmt := EU3PartyServiceTradeAmt + TotalValueOfServiceSupplies;
                                  END ELSE BEGIN
                                    TotalValueofItemSuppliesTotal += TotalValueOfItemSupplies;
                                    TotalValueofServiceSuppliesTot += TotalValueOfServiceSupplies;
                                  END;

                                  VATEntry.COPY("VAT Entry");
                                  IF VATEntry.NEXT = 1 THEN BEGIN
                                    IF (VATEntry."Country/Region Code" <> "Country/Region Code") OR
                                       (VATEntry."VAT Registration No." <> "VAT Registration No.")
                                    THEN
                                      GroupTotal := TRUE;
                                  END ELSE
                                    GroupTotal := TRUE;

                                  IF GroupTotal THEN BEGIN
                                    WriteGrTotalsToFile(TotalValueofServiceSuppliesTot,TotalValueofItemSuppliesTotal,
                                      EU3PartyServiceTradeAmt,EU3PartyItemTradeAmt,
                                      HasEU3PartyTrade,HasItemSupplies,HasServiceSupplies);
                                    EU3PartyItemTradeTotalAmt += EU3PartyItemTradeAmt;
                                    EU3PartyServiceTradeTotalAmt += EU3PartyServiceTradeAmt;

                                    TotalValueofItemSuppliesTotal := 0;
                                    TotalValueofServiceSuppliesTot := 0;

                                    EU3PartyItemTradeAmt := 0;
                                    EU3PartyServiceTradeAmt := 0;

                                    HasEU3PartyTrade := FALSE;
                                    HasItemSupplies := FALSE;
                                    HasServiceSupplies := FALSE;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                VATFile.CLOSE;
                              END;

               ReqFilterFields=VAT Bus. Posting Group,VAT Prod. Posting Group,Posting Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               IF FileVersion = '' THEN
                 FileVersion := '1.1';

               IF FileVersion2 = '' THEN
                 FileVersion2 := '1.1.2';
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Betr„ge in Berichtsw„hrung ausgeben;
                             ENU=Show Amounts in Add. Reporting Currency];
                  SourceExpr=UseAmtsInAddCurr;
                  MultiLine=Yes }

      { 3   ;2   ;Field     ;
                  Name=FileVersion;
                  OptionCaptionML=[DEU=Dateiversion (Elster online);
                                   ENU=File version (Elster online)];
                  SourceExpr=FileVersion }

      { 4   ;2   ;Field     ;
                  Name=FileVersion 2;
                  CaptionML=[DEU=Datei 2.Version (Elster online);
                             ENU=File version 2 (Elster online)];
                  SourceExpr=FileVersion2 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bitte Dateinamen eingeben.;ENU=Please enter the file name.';
      Text001@1001 : TextConst 'DEU=WwWw;ENU=WwWw';
      Text002@1002 : TextConst 'DEU=šBERSICHT;ENU=LIST';
      Text003@1003 : TextConst 'DEU="%1 wurde nicht in allen MwSt.-Posten ausgefllt, wo %2 = %3 ist";ENU="%1 was not filled in for all VAT entries in which %2 = %3."';
      Text004@1004 : TextConst 'DEU=Es ist nicht m”glich, %1 in einem Feld mit der L„nge %2 anzuzeigen;ENU=It is not possible to display %1 in a field with a length of %2.';
      CompanyInfo@1005 : Record 79;
      Country@1006 : Record 9;
      Cust@1007 : Record 18;
      VATFile@1008 : File;
      TotalValueofServiceSuppliesTot@1021 : Decimal;
      TotalValueofItemSuppliesTotal@1013 : Decimal;
      EU3PartyServiceTradeAmt@1022 : Decimal;
      EU3PartyItemTradeAmt@1014 : Decimal;
      EU3PartyItemTradeTotalAmt@1016 : Decimal;
      EU3PartyServiceTradeTotalAmt@1023 : Decimal;
      NoOfGrTotal@1020 : Integer;
      FileName@1009 : Text;
      VATRegNo@1010 : Code[20];
      InternalReferenceNo@1011 : Text[10];
      Period@1012 : Date;
      UseAmtsInAddCurr@1015 : Boolean;
      Text005@1017 : TextConst 'DEU=Exportieren;ENU=Export';
      Text006@1019 : TextConst 'DEU=Alle Dateien (*.*)|*.*;ENU=All Files (*.*)|*.*';
      Text007@1018 : TextConst 'DEU=Standard.txt;ENU=Default.txt';
      HideFileDialog@1024 : Boolean;
      FileVersion@1026 : Text[30];
      FileVersion2@1025 : Text[30];
      HasEU3PartyTrade@1010000 : Boolean;
      HasItemSupplies@1010001 : Boolean;
      HasServiceSupplies@1010002 : Boolean;

    LOCAL PROCEDURE DecimalNumeralSign@1(DecimalNumeral@1000 : Decimal) : Text[1];
    BEGIN
      IF DecimalNumeral >= 0 THEN
        EXIT('+');
      EXIT('-');
    END;

    LOCAL PROCEDURE DecimalNumeralZeroFormat@2(DecimalNumeral@1000 : Decimal;Length@1001 : Integer) : Text[250];
    BEGIN
      EXIT(TextZeroFormat(DELCHR(FORMAT(ROUND(ABS(DecimalNumeral),1,'<'),0,1)),Length));
    END;

    LOCAL PROCEDURE TextZeroFormat@3(Text@1000 : Text[250];Length@1001 : Integer) : Text[250];
    BEGIN
      IF STRLEN(Text) > Length THEN
        ERROR(
          Text004,
          Text,Length);
      EXIT(PADSTR('',Length - STRLEN(Text),'0') + Text);
    END;

    LOCAL PROCEDURE WriteGrTotalsToFile@4(TotalValueofServiceSupplies@1003 : Decimal;TotalValueofItemSupplies@1002 : Decimal;EU3PartyServiceTradeAmt@1001 : Decimal;EU3PartyItemTradeAmt@1000 : Decimal;HasEU3Party@1010004 : Boolean;HasItem@1010005 : Boolean;HasService@1010006 : Boolean);
    BEGIN
      WITH "VAT Entry" DO BEGIN
        IF "VAT Registration No." = '' THEN BEGIN
          Type := Type::Sale;
          ERROR(
            Text003,
            FIELDCAPTION("VAT Registration No."),FIELDCAPTION(Type),Type);
        END;

        Cust.GET("Bill-to/Pay-to No.");
        Cust.TESTFIELD("Country/Region Code");
        Country.GET(Cust."Country/Region Code");
        Cust.TESTFIELD("VAT Registration No.");
        Country.GET("Country/Region Code");
        Country.TESTFIELD("EU Country/Region Code");
        NoOfGrTotal := NoOfGrTotal + 1;

        InternalReferenceNo := INCSTR(InternalReferenceNo);
        SETRANGE("Country/Region Code","Country/Region Code");
        SETRANGE("Bill-to/Pay-to No.","Bill-to/Pay-to No.");
        MODIFYALL("Internal Ref. No.",InternalReferenceNo);
        SETRANGE("Country/Region Code");
        SETRANGE("Bill-to/Pay-to No.");

        IF HasItem THEN
          WriteLineToFile(-TotalValueofItemSupplies, 'L');
        IF HasService THEN
          WriteLineToFile(-TotalValueofServiceSupplies, 'S');
        IF HasEU3Party THEN
          WriteLineToFile(-(EU3PartyItemTradeAmt + EU3PartyServiceTradeAmt), 'D');
      END;
    END;

    PROCEDURE GetFileName@6() : Text[1024];
    BEGIN
      EXIT(FileName);
    END;

    PROCEDURE InitializeRequest@5(NewHideFileDialog@1000 : Boolean);
    BEGIN
      HideFileDialog := NewHideFileDialog;
    END;

    PROCEDURE WriteLineToFile@1010000(ExportAmount@1010000 : Decimal;DLS@1010001 : Text[1]);
    VAR
      VATRegNo@1010002 : Text[20];
    BEGIN
      WITH "VAT Entry" DO BEGIN
        VATRegNo := "VAT Registration No.";
        IF COPYSTR(VATRegNo,1,STRLEN(Country."EU Country/Region Code")) = Country."EU Country/Region Code" THEN
          VATRegNo := COPYSTR(VATRegNo,STRLEN(Country."EU Country/Region Code") + 1);
        VATFile.WRITE(
              FORMAT(Country."EU Country/Region Code",2) + ',' +
              FORMAT(VATRegNo) + ',' +
              DELCHR(FORMAT(ROUND(ExportAmount,1)),'=', '.,') +',' +
              DLS);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


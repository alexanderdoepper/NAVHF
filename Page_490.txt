OBJECT Page 490 Acc. Schedule Overview
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Acc. Schedule Overview;
               ESP=Panorama esq. cta.;
               DEA=Kontenschemamatrix];
    SaveValues=Yes;
    LinksAllowed=No;
    SourceTable=Table85;
    DataCaptionExpr=CurrentSchedName + ' - ' + CurrentColumnName;
    PageType=Worksheet;
    OnInit=BEGIN
             Dim4FilterEnable := TRUE;
             Dim3FilterEnable := TRUE;
             Dim2FilterEnable := TRUE;
             Dim1FilterEnable := TRUE;
           END;

    OnOpenPage=BEGIN
                 GLSetup.GET;
                 IF NewCurrentSchedName <> '' THEN
                   CurrentSchedName := NewCurrentSchedName;
                 IF CurrentSchedName = '' THEN
                   CurrentSchedName := Text000;
                 IF NewCurrentColumnName <> '' THEN
                   CurrentColumnName := NewCurrentColumnName;
                 IF CurrentColumnName = '' THEN
                   CurrentColumnName := Text000;
                 AccSchedManagement.CopyColumnsToTemp(CurrentColumnName,TempColumnLayout);
                 AccSchedManagement.OpenSchedule(CurrentSchedName,Rec);
                 AccSchedManagement.OpenColumns(CurrentColumnName,TempColumnLayout);
                 AccSchedManagement.CheckAnalysisView(CurrentSchedName,CurrentColumnName,TRUE);
                 UpdateColumnCaptions;
                 IF AccSchedName.GET(CurrentSchedName) THEN
                   IF AccSchedName."Analysis View Name" <> '' THEN
                     AnalysisView.GET(AccSchedName."Analysis View Name")
                   ELSE BEGIN
                     CLEAR(AnalysisView);
                     AnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
                     AnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
                   END;

                 FindPeriod('');
                 SETRANGE("Dimension 1 Filter");
                 SETRANGE("Dimension 2 Filter");
                 SETRANGE("Dimension 3 Filter");
                 SETRANGE("Dimension 4 Filter");
                 SETRANGE("Cost Center Filter");
                 SETRANGE("Cost Object Filter");
                 SETRANGE("Cash Flow Forecast Filter");
                 SETRANGE("Cost Budget Filter");
                 SETRANGE("G/L Budget Filter");
                 UpdateDimFilterControls;
                 DateFilter := GETFILTER("Date Filter");
               END;

    OnAfterGetRecord=VAR
                       ColumnNo@1000 : Integer;
                     BEGIN
                       IF Totaling = '' THEN
                         CLEAR(ColumnValues)
                       ELSE
                         IF TempColumnLayout.FINDSET THEN
                           REPEAT
                             ColumnNo := ColumnNo + 1;
                             IF (ColumnNo > ColumnOffset) AND (ColumnNo - ColumnOffset <= ARRAYLEN(ColumnValues)) THEN BEGIN
                               ColumnValues[ColumnNo - ColumnOffset] :=
                                 AccSchedManagement.CalcCell(Rec,TempColumnLayout,UseAmtsInAddCurr);
                               ColumnLayoutArr[ColumnNo - ColumnOffset] := TempColumnLayout;
                             END;
                           UNTIL TempColumnLayout.NEXT = 0;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 28      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESP=F&unciones;
                                 DEA=F&unktionen];
                      Image=Action }
      { 29      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Export to Excel;
                                 ESP=Exportar a Excel;
                                 DEA=In Excel exportieren];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ExportToExcel;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ExportAccSchedToExcel@1001 : Report 29;
                               BEGIN
                                 ExportAccSchedToExcel.SetOptions(Rec,CurrentColumnName,UseAmtsInAddCurr);
                                 ExportAccSchedToExcel.RUN;
                               END;
                                }
      { 40      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Print;
                                 ESP=Imprimir;
                                 DEA=Drucken];
                      Image=Print;
                      OnAction=VAR
                                 AccSched@1000 : Report 25;
                                 DateFilter2@1002 : Text[30];
                                 GLBudgetFilter2@1003 : Text[30];
                                 BusUnitFilter@1004 : Text[30];
                                 CostBudgetFilter2@1005 : Text[30];
                               BEGIN
                                 AccSched.SetAccSchedName(CurrentSchedName);
                                 AccSched.SetColumnLayoutName(CurrentColumnName);
                                 DateFilter2 := GETFILTER("Date Filter");
                                 GLBudgetFilter2 := GETFILTER("G/L Budget Filter");
                                 CostBudgetFilter2 := GETFILTER("Cost Budget Filter");
                                 BusUnitFilter := GETFILTER("Business Unit Filter");
                                 AccSched.SetFilters(DateFilter2,GLBudgetFilter2,CostBudgetFilter2,BusUnitFilter,Dim1Filter,Dim2Filter,Dim3Filter,Dim4Filter);
                                 AccSched.RUN;
                               END;
                                }
      { 2       ;1   ;Action    ;
                      CaptionML=[ENU=Previous Column;
                                 ESP=Columna anterior;
                                 DEA=Vorherige Spalte];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AdjustColumnOffset(-1);
                               END;
                                }
      { 27      ;1   ;Action    ;
                      CaptionML=[ENU=Next Period;
                                 ESP=Periodo siguiente;
                                 DEA=Folgeperiode];
                      ToolTipML=[ENU=Next Period;
                                 ESP=Periodo siguiente;
                                 DEA=Folgeperiode];
                      Promoted=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('>=');
                                 DateFilter := GETFILTER("Date Filter");
                               END;
                                }
      { 26      ;1   ;Action    ;
                      CaptionML=[ENU=Previous Period;
                                 ESP=Periodo anterior;
                                 DEA=Vorperiode];
                      ToolTipML=[ENU=Previous Period;
                                 ESP=Periodo anterior;
                                 DEA=Vorperiode];
                      Promoted=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('<=');
                                 DateFilter := GETFILTER("Date Filter");
                               END;
                                }
      { 1       ;1   ;Action    ;
                      CaptionML=[ENU=Next Column;
                                 ESP=Columna siguiente;
                                 DEA=N„chste Spalte];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AdjustColumnOffset(1);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 8   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General;
                           DEA=Allgemein] }

    { 10  ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Account Schedule Name;
                           ESP=Nombre esquema cuenta;
                           DEA=Kontenschemaname];
                SourceExpr=CurrentSchedName;
                LookupPageID=Account Schedule Names;
                OnValidate=BEGIN
                             AccSchedManagement.CheckName(CurrentSchedName);
                             CurrentSchedNameOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(AccSchedManagement.LookupName(CurrentSchedName,Text));
                         END;
                          }

    { 12  ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Column Layout Name;
                           ESP=Nombre plantilla columna;
                           DEA=Spaltenlayoutname];
                SourceExpr=CurrentColumnName;
                TableRelation="Column Layout Name".Name;
                OnValidate=BEGIN
                             AccSchedManagement.CheckColumnName(CurrentColumnName);
                             CurrentColumnNameOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(AccSchedManagement.LookupColumnName(CurrentColumnName,Text));
                         END;
                          }

    { 44  ;2   ;Field     ;
                CaptionML=[ENU=Show Amounts in Add. Reporting Currency;
                           ESP=Muestra importes en div.-adic.;
                           DEA=Betr„ge in Berichtsw„hrung ausgeben];
                SourceExpr=UseAmtsInAddCurr;
                MultiLine=Yes;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 42  ;2   ;Field     ;
                CaptionML=[ENU=View by;
                           ESP=Ver por;
                           DEA=Anzeigen nach];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 ESP=D¡a,Semana,Mes,Trimestre,A¤o,Periodo contable;
                                 DEA=Tag,Woche,Monat,Quartal,Jahr,Buchhaltungsperiode];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                             DateFilter := GETFILTER("Date Filter");
                             CurrPage.UPDATE;
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=DateFilter;
                CaptionML=[ENU=Date Filter;
                           ESP=Filtro fecha;
                           DEA=Datumsfilter];
                SourceExpr=DateFilter;
                OnValidate=VAR
                             ApplicationManagement@1002 : Codeunit 1;
                           BEGIN
                             IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                             SETFILTER("Date Filter",DateFilter);
                             DateFilter := GETFILTER("Date Filter");
                             CurrPage.UPDATE;
                           END;
                            }

    { 48  ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 49  ;2   ;Field     ;
                SourceExpr="Row No." }

    { 51  ;2   ;Field     ;
                SourceExpr=Description }

    { 53  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[1];
                CaptionClass='3,' + ColumnCaptions[1];
                OnDrillDown=BEGIN
                              DrillDown(1);
                            END;
                             }

    { 59  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[2];
                CaptionClass='3,' + ColumnCaptions[2];
                OnDrillDown=BEGIN
                              DrillDown(2);
                            END;
                             }

    { 61  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[3];
                CaptionClass='3,' + ColumnCaptions[3];
                OnDrillDown=BEGIN
                              DrillDown(3);
                            END;
                             }

    { 63  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[4];
                CaptionClass='3,' + ColumnCaptions[4];
                OnDrillDown=BEGIN
                              DrillDown(4);
                            END;
                             }

    { 65  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[5];
                CaptionClass='3,' + ColumnCaptions[5];
                OnDrillDown=BEGIN
                              DrillDown(5);
                            END;
                             }

    { 81  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[6];
                CaptionClass='3,' + ColumnCaptions[6];
                OnDrillDown=BEGIN
                              DrillDown(6);
                            END;
                             }

    { 79  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[7];
                CaptionClass='3,' + ColumnCaptions[7];
                OnDrillDown=BEGIN
                              DrillDown(7);
                            END;
                             }

    { 77  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[8];
                CaptionClass='3,' + ColumnCaptions[8];
                OnDrillDown=BEGIN
                              DrillDown(8);
                            END;
                             }

    { 75  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[9];
                CaptionClass='3,' + ColumnCaptions[9];
                OnDrillDown=BEGIN
                              DrillDown(9);
                            END;
                             }

    { 73  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[10];
                CaptionClass='3,' + ColumnCaptions[10];
                OnDrillDown=BEGIN
                              DrillDown(10);
                            END;
                             }

    { 71  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[11];
                CaptionClass='3,' + ColumnCaptions[11];
                OnDrillDown=BEGIN
                              DrillDown(11);
                            END;
                             }

    { 69  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ColumnValues[12];
                CaptionClass='3,' + ColumnCaptions[12];
                OnDrillDown=BEGIN
                              DrillDown(12);
                            END;
                             }

    { 1900242701;1;Group  ;
                CaptionML=[ENU=Dimension Filters;
                           ESP=Filtros dimensi¢n;
                           DEA=Dimensionsfilter] }

    { 37  ;2   ;Field     ;
                Name=Dim1Filter;
                CaptionML=[ENU=Dimension 1 Filter;
                           ESP=Filtro dimensi¢n 1;
                           DEA=Dimensionsfilter 1];
                SourceExpr=Dim1Filter;
                CaptionClass=FormGetCaptionClass(1);
                Enabled=Dim1FilterEnable;
                OnValidate=BEGIN
                             IF Dim1Filter = '' THEN
                               SETRANGE("Dimension 1 Filter")
                             ELSE
                               SETFILTER("Dimension 1 Filter",Dim1Filter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           DimValue@1000 : Record 349;
                         BEGIN
                           EXIT(DimValue.LookUpDimFilter(AnalysisView."Dimension 1 Code",Text));
                         END;
                          }

    { 36  ;2   ;Field     ;
                Name=Dim2Filter;
                CaptionML=[ENU=Dimension 2 Filter;
                           ESP=Filtro dimensi¢n 2;
                           DEA=Dimensionsfilter 2];
                SourceExpr=Dim2Filter;
                CaptionClass=FormGetCaptionClass(2);
                Enabled=Dim2FilterEnable;
                OnValidate=BEGIN
                             IF Dim2Filter = '' THEN
                               SETRANGE("Dimension 2 Filter")
                             ELSE
                               SETFILTER("Dimension 2 Filter",Dim2Filter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           DimValue@1000 : Record 349;
                         BEGIN
                           EXIT(DimValue.LookUpDimFilter(AnalysisView."Dimension 2 Code",Text));
                         END;
                          }

    { 39  ;2   ;Field     ;
                Name=Dim3Filter;
                CaptionML=[ENU=Dimension 3 Filter;
                           ESP=Filtro dimensi¢n 3;
                           DEA=Dimensionsfilter 3];
                SourceExpr=Dim3Filter;
                CaptionClass=FormGetCaptionClass(3);
                Enabled=Dim3FilterEnable;
                OnValidate=BEGIN
                             IF Dim3Filter = '' THEN
                               SETRANGE("Dimension 3 Filter")
                             ELSE
                               SETFILTER("Dimension 3 Filter",Dim3Filter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           DimValue@1000 : Record 349;
                         BEGIN
                           EXIT(DimValue.LookUpDimFilter(AnalysisView."Dimension 3 Code",Text));
                         END;
                          }

    { 38  ;2   ;Field     ;
                Name=Dim4Filter;
                CaptionML=[ENU=Dimension 4 Filter;
                           ESP=Filtro dimensi¢n 4;
                           DEA=Dimensionsfilter 4];
                SourceExpr=Dim4Filter;
                CaptionClass=FormGetCaptionClass(4);
                Enabled=Dim4FilterEnable;
                OnValidate=BEGIN
                             IF Dim4Filter = '' THEN
                               SETRANGE("Dimension 4 Filter")
                             ELSE
                               SETFILTER("Dimension 4 Filter",Dim4Filter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           DimValue@1000 : Record 349;
                         BEGIN
                           EXIT(DimValue.LookUpDimFilter(AnalysisView."Dimension 4 Code",Text));
                         END;
                          }

    { 3   ;2   ;Field     ;
                Name=CostCenterFilter;
                CaptionML=[ENU=Cost Center Filter;
                           ESP=Filtro centro coste;
                           DEA=Kostenstellenfilter];
                SourceExpr=CostCenterFilter;
                OnValidate=BEGIN
                             IF CostCenterFilter = '' THEN
                               SETRANGE("Cost Center Filter")
                             ELSE
                               SETFILTER("Cost Center Filter",CostCenterFilter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           CostCenter@1000 : Record 1112;
                         BEGIN
                           EXIT(CostCenter.LookupCostCenterFilter(Text));
                         END;
                          }

    { 4   ;2   ;Field     ;
                Name=CostObjectFilter;
                CaptionML=[ENU=Cost Object Filter;
                           ESP=Filtro objeto coste;
                           DEA=Kostentr„gerfilter];
                SourceExpr=CostObjectFilter;
                OnValidate=BEGIN
                             IF CostObjectFilter = '' THEN
                               SETRANGE("Cost Object Filter")
                             ELSE
                               SETFILTER("Cost Object Filter",CostObjectFilter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           CostObject@1000 : Record 1113;
                         BEGIN
                           EXIT(CostObject.LookupCostObjectFilter(Text));
                         END;
                          }

    { 5   ;2   ;Field     ;
                Name=CashFlowFilter;
                CaptionML=[ENU=Cash Flow Filter;
                           ESP=Filtro flujos efectivo;
                           DEA=Cashflowfilter];
                SourceExpr=CashFlowFilter;
                OnValidate=BEGIN
                             IF CashFlowFilter = '' THEN
                               SETRANGE("Cash Flow Forecast Filter")
                             ELSE
                               SETFILTER("Cash Flow Forecast Filter",CashFlowFilter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           CashFlowForecast@1000 : Record 840;
                         BEGIN
                           EXIT(CashFlowForecast.LookupCashFlowFilter(Text));
                         END;
                          }

    { 6   ;2   ;Field     ;
                Name=G/LBudgetFilter;
                CaptionML=[ENU=G/L Budget Filter;
                           ESP=Filtro presupuesto CG;
                           DEA=Finanzbudgetfilter];
                SourceExpr=GLBudgetFilter;
                OnValidate=BEGIN
                             IF GLBudgetFilter = '' THEN
                               SETRANGE("G/L Budget Filter")
                             ELSE
                               SETFILTER("G/L Budget Filter",GLBudgetFilter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupGLBudgetFilter(Text));
                         END;
                          }

    { 9   ;2   ;Field     ;
                Name=CostBudgetFilter;
                CaptionML=[ENU=Cost Budget Filter;
                           ESP=Filtro presupuesto costes;
                           DEA=Kostenbudgetfilter];
                SourceExpr=CostBudgetFilter;
                OnValidate=BEGIN
                             IF CostBudgetFilter = '' THEN
                               SETRANGE("Cost Budget Filter")
                             ELSE
                               SETFILTER("Cost Budget Filter",CostBudgetFilter);
                             CurrPage.UPDATE;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupCostBudgetFilter(Text));
                         END;
                          }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=DEFAULT;ESP=GENERICO;DEA=STANDARD';
      Text002@1002 : TextConst 'ENU=Column formula: %1.;ESP=F¢rmula columna: %1.;DEA=Spaltenformel: %1.';
      Text003@1003 : TextConst 'ENU=Row formula: %1.;ESP=F¢rmula fila: %1.;DEA=Rubrikformel: %1.';
      Text005@1021 : TextConst 'ENU=1,6,,Dimension %1 Filter;ESP=1,6,,Filtro dimensi¢n %1;DEA=1,6,,Dimensionsfilter %1';
      TempColumnLayout@1004 : TEMPORARY Record 334;
      ColumnLayoutArr@1018 : ARRAY [12] OF Record 334;
      AccSchedName@1005 : Record 84;
      AnalysisView@1020 : Record 363;
      GLSetup@1016 : Record 98;
      AccSchedManagement@1006 : Codeunit 8;
      CurrentSchedName@1007 : Code[10];
      CurrentColumnName@1008 : Code[10];
      NewCurrentSchedName@1009 : Code[10];
      NewCurrentColumnName@1010 : Code[10];
      ColumnValues@1012 : ARRAY [12] OF Decimal;
      ColumnCaptions@1017 : ARRAY [12] OF Text[80];
      PeriodType@1013 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      UseAmtsInAddCurr@1014 : Boolean;
      Dim1Filter@1019 : Text[250];
      Dim2Filter@1022 : Text[250];
      Dim3Filter@1023 : Text[250];
      Dim4Filter@1024 : Text[250];
      CostCenterFilter@1011 : Text[250];
      CostObjectFilter@1001 : Text[250];
      CashFlowFilter@1015 : Text[250];
      ColumnOffset@1025 : Integer;
      Dim1FilterEnable@19054192 : Boolean INDATASET;
      Dim2FilterEnable@19062512 : Boolean INDATASET;
      Dim3FilterEnable@19011605 : Boolean INDATASET;
      Dim4FilterEnable@19064250 : Boolean INDATASET;
      GLBudgetFilter@1026 : Text[10];
      CostBudgetFilter@1027 : Text[10];
      DateFilter@1028 : Text[30];

    PROCEDURE SetAccSchedName@6(NewAccSchedName@1000 : Code[10]);
    VAR
      AccSchedName@1001 : Record 84;
    BEGIN
      NewCurrentSchedName := NewAccSchedName;
      IF AccSchedName.GET(NewCurrentSchedName) THEN
        IF AccSchedName."Default Column Layout" <> '' THEN
          NewCurrentColumnName := AccSchedName."Default Column Layout";
    END;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Calendar@1001 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      IF GETFILTER("Date Filter") <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",GETFILTER("Date Filter"));
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      END;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
      IF GETRANGEMIN("Date Filter") = GETRANGEMAX("Date Filter") THEN
        SETRANGE("Date Filter",GETRANGEMIN("Date Filter"));
    END;

    LOCAL PROCEDURE FormGetCaptionClass@12(DimNo@1000 : Integer) : Text[250];
    BEGIN
      CASE DimNo OF
        1:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 1 Code");

            EXIT(STRSUBSTNO(Text005,DimNo));
          END;
        2:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 2 Code");

            EXIT(STRSUBSTNO(Text005,DimNo));
          END;
        3:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 3 Code");

            EXIT(STRSUBSTNO(Text005,DimNo));
          END;
        4:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 4 Code");

            EXIT(STRSUBSTNO(Text005,DimNo));
          END;
        5:
          EXIT(FIELDCAPTION("Date Filter"));
        6:
          EXIT(FIELDCAPTION("Cash Flow Forecast Filter"));
      END;
    END;

    LOCAL PROCEDURE DrillDown@5(ColumnNo@1003 : Integer);
    BEGIN
      TempColumnLayout := ColumnLayoutArr[ColumnNo];

      IF TempColumnLayout."Column Type" = TempColumnLayout."Column Type"::Formula THEN BEGIN
        MESSAGE(Text002,TempColumnLayout.Formula);
        EXIT;
      END;

      IF "Totaling Type" IN ["Totaling Type"::Formula,"Totaling Type"::"Set Base For Percent"] THEN BEGIN
        MESSAGE(Text003,Totaling);
        EXIT;
      END;

      IF Totaling = '' THEN
        EXIT;

      IF "Totaling Type" IN ["Totaling Type"::"Cash Flow Entry Accounts","Totaling Type"::"Cash Flow Total Accounts"] THEN
        DrillDownOnCFAccount
      ELSE
        DrillDownOnGLAccount;
    END;

    LOCAL PROCEDURE DrillDownOnGLAccount@1();
    VAR
      GLAcc@1002 : Record 15;
      GLAccAnalysisView@1001 : Record 376;
      CostType@1004 : Record 1103;
      ChartOfAccsAnalysisView@1000 : Page 569;
      Dimension1Totaling@1005 : Code[1024];
      Dimension2Totaling@1006 : Code[1024];
    BEGIN
      IF "Totaling Type" IN ["Totaling Type"::"Cost Type","Totaling Type"::"Cost Type Total"] THEN BEGIN
        AccSchedManagement.SetCostTypeRowFilters(CostType,Rec,TempColumnLayout);
        AccSchedManagement.SetCostTypeColumnFilters(CostType,Rec,TempColumnLayout);
        COPYFILTER("Cost Center Filter",CostType."Cost Center Filter");
        COPYFILTER("Cost Object Filter",CostType."Cost Object Filter");
        COPYFILTER("Cost Budget Filter",CostType."Budget Filter");
        PAGE.RUN(PAGE::"Chart of Cost Types",CostType);
      END ELSE BEGIN
        COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
        COPYFILTER("G/L Budget Filter",GLAcc."Budget Filter");
        AccSchedManagement.SetGLAccRowFilters(GLAcc,Rec);
        AccSchedManagement.SetGLAccColumnFilters(GLAcc,Rec,TempColumnLayout);
        AccSchedName.GET(CurrentSchedName);
        IF AccSchedName."Analysis View Name" = '' THEN BEGIN
          COPYFILTER("Dimension 1 Filter",GLAcc."Global Dimension 1 Filter");
          COPYFILTER("Dimension 2 Filter",GLAcc."Global Dimension 2 Filter");
          COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
          GLAcc.FILTERGROUP(2);
          Dimension1Totaling := AccSchedManagement.GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling");
          Dimension2Totaling := AccSchedManagement.GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling");
          GLAcc.SETFILTER("Global Dimension 1 Filter",Dimension1Totaling);
          GLAcc.SETFILTER("Global Dimension 2 Filter",Dimension2Totaling);
          GLAcc.FILTERGROUP(8);
          GLAcc.SETFILTER("Business Unit Filter",TempColumnLayout."Business Unit Totaling");
          IF Dimension1Totaling <> '' THEN
            GLAcc.SETFILTER("Global Dimension 1 Filter",Dimension1Totaling);
          IF Dimension2Totaling <> '' THEN
            GLAcc.SETFILTER("Global Dimension 2 Filter",Dimension2Totaling);
          GLAcc.FILTERGROUP(0);
          PAGE.RUN(PAGE::"Chart of Accounts (G/L)",GLAcc)
        END ELSE BEGIN
          GLAcc.COPYFILTER("Date Filter",GLAccAnalysisView."Date Filter");
          GLAcc.COPYFILTER("Budget Filter",GLAccAnalysisView."Budget Filter");
          GLAcc.COPYFILTER("Business Unit Filter",GLAccAnalysisView."Business Unit Filter");
          GLAccAnalysisView.SETRANGE("Analysis View Filter",AccSchedName."Analysis View Name");
          COPYFILTER("Dimension 1 Filter",GLAccAnalysisView."Dimension 1 Filter");
          COPYFILTER("Dimension 2 Filter",GLAccAnalysisView."Dimension 2 Filter");
          COPYFILTER("Dimension 3 Filter",GLAccAnalysisView."Dimension 3 Filter");
          COPYFILTER("Dimension 4 Filter",GLAccAnalysisView."Dimension 4 Filter");
          GLAccAnalysisView.FILTERGROUP(2);
          GLAccAnalysisView.SETFILTER("Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,"Dimension 1 Totaling"));
          GLAccAnalysisView.SETFILTER("Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,"Dimension 2 Totaling"));
          GLAccAnalysisView.SETFILTER("Dimension 3 Filter",AccSchedManagement.GetDimTotalingFilter(3,"Dimension 3 Totaling"));
          GLAccAnalysisView.SETFILTER("Dimension 4 Filter",AccSchedManagement.GetDimTotalingFilter(4,"Dimension 4 Totaling"));
          GLAccAnalysisView.FILTERGROUP(8);
          GLAccAnalysisView.SETFILTER(
            "Dimension 1 Filter",
            AccSchedManagement.GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"));
          GLAccAnalysisView.SETFILTER(
            "Dimension 2 Filter",
            AccSchedManagement.GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"));
          GLAccAnalysisView.SETFILTER(
            "Dimension 3 Filter",
            AccSchedManagement.GetDimTotalingFilter(3,TempColumnLayout."Dimension 3 Totaling"));
          GLAccAnalysisView.SETFILTER(
            "Dimension 4 Filter",
            AccSchedManagement.GetDimTotalingFilter(4,TempColumnLayout."Dimension 4 Totaling"));
          GLAccAnalysisView.SETFILTER("Business Unit Filter",TempColumnLayout."Business Unit Totaling");
          GLAccAnalysisView.FILTERGROUP(0);
          CLEAR(ChartOfAccsAnalysisView);
          ChartOfAccsAnalysisView.InsertTempGLAccAnalysisViews(GLAcc);
          ChartOfAccsAnalysisView.SETTABLEVIEW(GLAccAnalysisView);
          ChartOfAccsAnalysisView.RUN;
        END;
      END;
    END;

    LOCAL PROCEDURE DrillDownOnCFAccount@3();
    VAR
      CFAccount@1002 : Record 841;
      GLAccAnalysisView@1004 : Record 376;
      ChartOfAccsAnalysisView@1003 : Page 569;
    BEGIN
      COPYFILTER("Cash Flow Forecast Filter",CFAccount."Cash Flow Forecast Filter");

      AccSchedManagement.SetCFAccRowFilter(CFAccount,Rec);
      AccSchedManagement.SetCFAccColumnFilter(CFAccount,Rec,TempColumnLayout);
      AccSchedName.GET(CurrentSchedName);
      IF AccSchedName."Analysis View Name" = '' THEN BEGIN
        COPYFILTER("Dimension 1 Filter",CFAccount."Global Dimension 1 Filter");
        COPYFILTER("Dimension 2 Filter",CFAccount."Global Dimension 2 Filter");
        CFAccount.FILTERGROUP(2);
        CFAccount.SETFILTER("Global Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,"Dimension 1 Totaling"));
        CFAccount.SETFILTER("Global Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,"Dimension 2 Totaling"));
        CFAccount.FILTERGROUP(8);
        CFAccount.SETFILTER(
          "Global Dimension 1 Filter",
          AccSchedManagement.GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"));
        CFAccount.SETFILTER(
          "Global Dimension 2 Filter",
          AccSchedManagement.GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"));
        CFAccount.FILTERGROUP(0);
        PAGE.RUN(PAGE::"Chart of Cash Flow Accounts",CFAccount)
      END ELSE BEGIN
        CFAccount.COPYFILTER("Date Filter",GLAccAnalysisView."Date Filter");
        CFAccount.COPYFILTER("Cash Flow Forecast Filter",GLAccAnalysisView."Cash Flow Forecast Filter");
        GLAccAnalysisView.SETRANGE("Analysis View Filter",AccSchedName."Analysis View Name");
        COPYFILTER("Dimension 1 Filter",GLAccAnalysisView."Dimension 1 Filter");
        COPYFILTER("Dimension 2 Filter",GLAccAnalysisView."Dimension 2 Filter");
        COPYFILTER("Dimension 3 Filter",GLAccAnalysisView."Dimension 3 Filter");
        COPYFILTER("Dimension 4 Filter",GLAccAnalysisView."Dimension 4 Filter");
        GLAccAnalysisView.FILTERGROUP(2);
        GLAccAnalysisView.SETFILTER("Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,"Dimension 1 Totaling"));
        GLAccAnalysisView.SETFILTER("Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,"Dimension 2 Totaling"));
        GLAccAnalysisView.SETFILTER("Dimension 3 Filter",AccSchedManagement.GetDimTotalingFilter(3,"Dimension 3 Totaling"));
        GLAccAnalysisView.SETFILTER("Dimension 4 Filter",AccSchedManagement.GetDimTotalingFilter(4,"Dimension 4 Totaling"));
        GLAccAnalysisView.FILTERGROUP(8);
        GLAccAnalysisView.SETFILTER(
          "Dimension 1 Filter",
          AccSchedManagement.GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"));
        GLAccAnalysisView.SETFILTER(
          "Dimension 2 Filter",
          AccSchedManagement.GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"));
        GLAccAnalysisView.SETFILTER(
          "Dimension 3 Filter",
          AccSchedManagement.GetDimTotalingFilter(3,TempColumnLayout."Dimension 3 Totaling"));
        GLAccAnalysisView.SETFILTER(
          "Dimension 4 Filter",
          AccSchedManagement.GetDimTotalingFilter(4,TempColumnLayout."Dimension 4 Totaling"));
        GLAccAnalysisView.FILTERGROUP(0);
        CLEAR(ChartOfAccsAnalysisView);
        ChartOfAccsAnalysisView.InsertTempCFAccountAnalysisVie(CFAccount);
        ChartOfAccsAnalysisView.SETTABLEVIEW(GLAccAnalysisView);
        ChartOfAccsAnalysisView.RUN;
      END;
    END;

    LOCAL PROCEDURE UpdateColumnCaptions@19();
    VAR
      ColumnNo@1000 : Integer;
      i@1001 : Integer;
    BEGIN
      CLEAR(ColumnCaptions);
      IF TempColumnLayout.FINDSET THEN
        REPEAT
          ColumnNo := ColumnNo + 1;
          IF (ColumnNo > ColumnOffset) AND (ColumnNo - ColumnOffset <= ARRAYLEN(ColumnCaptions)) THEN
            ColumnCaptions[ColumnNo - ColumnOffset] := TempColumnLayout."Column Header";
        UNTIL (ColumnNo - ColumnOffset = ARRAYLEN(ColumnCaptions)) OR (TempColumnLayout.NEXT = 0);
      // Set unused columns to blank to prevent RTC to display control ID as caption
      FOR i := ColumnNo - ColumnOffset + 1 TO ARRAYLEN(ColumnCaptions) DO
        ColumnCaptions[i] := ' ';
    END;

    LOCAL PROCEDURE AdjustColumnOffset@18(Delta@1000 : Integer);
    VAR
      OldColumnOffset@1001 : Integer;
    BEGIN
      OldColumnOffset := ColumnOffset;
      ColumnOffset := ColumnOffset + Delta;
      IF ColumnOffset + 12 > TempColumnLayout.COUNT THEN
        ColumnOffset := TempColumnLayout.COUNT - 12;
      IF ColumnOffset < 0 THEN
        ColumnOffset := 0;
      IF ColumnOffset <> OldColumnOffset THEN BEGIN
        UpdateColumnCaptions;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE UpdateDimFilterControls@4();
    BEGIN
      Dim1Filter := GETFILTER("Dimension 1 Filter");
      Dim2Filter := GETFILTER("Dimension 2 Filter");
      Dim3Filter := GETFILTER("Dimension 3 Filter");
      Dim4Filter := GETFILTER("Dimension 4 Filter");
      CostCenterFilter := '';
      CostObjectFilter := '';
      CashFlowFilter := '';
      Dim1FilterEnable := AnalysisView."Dimension 1 Code" <> '';
      Dim2FilterEnable := AnalysisView."Dimension 2 Code" <> '';
      Dim3FilterEnable := AnalysisView."Dimension 3 Code" <> '';
      Dim4FilterEnable := AnalysisView."Dimension 4 Code" <> '';
      GLBudgetFilter := '';
      CostBudgetFilter := '';
    END;

    LOCAL PROCEDURE CurrentSchedNameOnAfterValidate@19053875();
    VAR
      AccSchedName@1001 : Record 84;
      PrevAnalysisView@1002 : Record 363;
    BEGIN
      CurrPage.SAVERECORD;
      AccSchedManagement.SetName(CurrentSchedName,Rec);
      IF AccSchedName.GET(CurrentSchedName) THEN
        IF (AccSchedName."Default Column Layout" <> '') AND
           (CurrentColumnName <> AccSchedName."Default Column Layout")
        THEN BEGIN
          CurrentColumnName := AccSchedName."Default Column Layout";
          AccSchedManagement.CopyColumnsToTemp(CurrentColumnName,TempColumnLayout);
          AccSchedManagement.SetColumnName(CurrentColumnName,TempColumnLayout);
        END;
      AccSchedManagement.CheckAnalysisView(CurrentSchedName,CurrentColumnName,TRUE);

      IF AccSchedName."Analysis View Name" <> AnalysisView.Code THEN BEGIN
        PrevAnalysisView := AnalysisView;
        IF AccSchedName."Analysis View Name" <> '' THEN
          AnalysisView.GET(AccSchedName."Analysis View Name")
        ELSE BEGIN
          CLEAR(AnalysisView);
          AnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
          AnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
        END;
        IF PrevAnalysisView."Dimension 1 Code" <> AnalysisView."Dimension 1 Code" THEN
          SETRANGE("Dimension 1 Filter");
        IF PrevAnalysisView."Dimension 2 Code" <> AnalysisView."Dimension 2 Code" THEN
          SETRANGE("Dimension 2 Filter");
        IF PrevAnalysisView."Dimension 3 Code" <> AnalysisView."Dimension 3 Code" THEN
          SETRANGE("Dimension 3 Filter");
        IF PrevAnalysisView."Dimension 4 Code" <> AnalysisView."Dimension 4 Code" THEN
          SETRANGE("Dimension 4 Filter");
      END;
      UpdateDimFilterControls;

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CurrentColumnNameOnAfterValidate@19064415();
    BEGIN
      AccSchedManagement.CopyColumnsToTemp(CurrentColumnName,TempColumnLayout);
      AccSchedManagement.SetColumnName(CurrentColumnName,TempColumnLayout);
      AccSchedManagement.CheckAnalysisView(CurrentSchedName,CurrentColumnName,TRUE);
      UpdateColumnCaptions;
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}


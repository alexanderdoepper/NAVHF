OBJECT Report 6665 Batch Post Purch. Ret. Orders
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purch. Ret. Orders;
               ESP=Reg. lotes pedidos devoluci¢n;
               DEA=Eink.-Rekl. stapelbuchen];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Return Order));
               ReqFilterHeadingML=[ENU=Purchase Return Order;
                                   DEA=Purchase Return Order];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               ApprovalTemplate.SETCURRENTKEY("Table ID","Document Type",Enabled);
                               ApprovalTemplate.SETRANGE("Table ID",DATABASE::"Purchase Header");
                               ApprovalTemplate.SETRANGE("Document Type","Document Type"::"Return Order");
                               ApprovalTemplate.SETRANGE(Enabled,TRUE);
                               IF ApprovalTemplate.FINDFIRST THEN
                                 SETRANGE(Status,Status::Released);

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  Ship := ShipReq;
                                  Invoice := InvReq;
                                  CLEAR(PurchPost);
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 9   ;2   ;Field     ;
                  Name=Ship;
                  CaptionML=[ENU=Ship;
                             ESP=Env°o;
                             DEA=Warenausgang];
                  SourceExpr=ShipReq }

      { 7   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             ESP=Factura;
                             DEA=Rechnung];
                  SourceExpr=InvReq }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro;
                             DEA=Buchungsdatum];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[ENU=Replace Posting Date;
                             ESP=Reemplazar fecha reg.;
                             DEA=Buchungsdatum ersetzen];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[ENU=Replace Document Date;
                             ESP=Reemplazar fecha documento;
                             DEA=Belegdatum ersetzen];
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             ESP=Calc. dto. factura;
                             DEA=Rechnungsrab. berechnen];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESP=Introduzca la fecha registro.;DEA=Bitte das Buchungsdatum eingeben.';
      Text001@1001 : TextConst 'ENU=Posting return orders  #1########## @2@@@@@@@@@@@@@;ESP=Registrando devoluciones  #1########## @2@@@@@@@@@@@@@;DEA=Reklamationen werden gebucht   #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 return orders out of a total of %2 have now been posted.;ESP=%1 devoluciones de un total de %2 han sido registrados.;DEA=%1 Reklamationen von insgesamt %2 wurden gebucht.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      ApprovalTemplate@1018 : Record 464;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      PostingDateReq@1008 : Date;
      CounterTotal@1009 : Integer;
      Counter@1010 : Integer;
      CounterOK@1011 : Integer;
      ShipReq@1012 : Boolean;
      InvReq@1013 : Boolean;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;ESP=El tipo de cambio asociado con la nueva fecha de registro indicada en el encabezado de compra no se aplicar† a las l°neas de compra.;DEA=Der mit dem neuen Buchungsdatum im Einkaufskopf verknÅpfte Wechselkurs wird nicht auf die Einkaufszeilen angewendet.';

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


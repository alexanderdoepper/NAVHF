OBJECT Table 1112 Cost Center
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               SETRECFILTER;
               ConfirmDeleteIfEntriesExist(Rec,TRUE);
               SETRANGE(Code);
             END;

    CaptionML=[ENU=Cost Center;
               ESP=Centro coste;
               DEA=Kostenstelle];
    LookupPageID=Page1122;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnLookup=VAR
                                                              CostAccMgt@1000 : Codeunit 1100;
                                                            BEGIN
                                                              CostAccMgt.LookupCostCenterFromDimValue(Code);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo;
                                                              DEA=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre;
                                                              DEA=Name] }
    { 3   ;   ;Cost Subtype        ;Option        ;CaptionML=[ENU=Cost Subtype;
                                                              ESP=Subtipo de coste;
                                                              DEA=Kostenunterart];
                                                   OptionCaptionML=[ENU=" ,Service Cost Center,Aux. Cost Center,Main Cost Center";
                                                                    ESP=" ,Centro coste servicio,Centro coste aux.,Centro coste ppal.";
                                                                    DEA=" ,Servicekostenstelle,Hilfskostenstelle,Hauptkostenstelle"];
                                                   OptionString=[ ,Service Cost Center,Aux. Cost Center,Main Cost Center] }
    { 4   ;   ;Cost Type Filter    ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Type";
                                                   CaptionML=[ENU=Cost Type Filter;
                                                              ESP=Filtro tipo coste;
                                                              DEA=Kostenartfilter] }
    { 5   ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ESP=Filtro fecha;
                                                              DEA=Datumsfilter] }
    { 6   ;   ;Net Change          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Center Code=FIELD(Code),
                                                                                              Cost Center Code=FIELD(FILTER(Totaling)),
                                                                                              Cost Type No.=FIELD(Cost Type Filter),
                                                                                              Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Net Change;
                                                              ESP=Saldo periodo;
                                                              DEA=Bewegung];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 7   ;   ;Balance at Date     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Center Code=FIELD(Code),
                                                                                              Cost Center Code=FIELD(FILTER(Totaling)),
                                                                                              Cost Type No.=FIELD(Cost Type Filter),
                                                                                              Posting Date=FIELD(UPPERLIMIT(Date Filter))));
                                                   CaptionML=[ENU=Balance at Date;
                                                              ESP=Saldo a la fecha;
                                                              DEA=Saldo bis Datum];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 8   ;   ;Balance to Allocate ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Center Code=FIELD(Code),
                                                                                              Cost Center Code=FIELD(FILTER(Totaling)),
                                                                                              Cost Type No.=FIELD(Cost Type Filter),
                                                                                              Posting Date=FIELD(Date Filter),
                                                                                              Allocated=CONST(No)));
                                                   CaptionML=[ENU=Balance to Allocate;
                                                              ESP=Saldo a asignar;
                                                              DEA=Zuzuordnender Saldo];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;Responsible Person  ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit 418;
                                                              BEGIN
                                                                UserMgt.ValidateUserID("Responsible Person");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Responsible Person");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Responsible Person;
                                                              ESP=Responsable;
                                                              DEA=ZustÑndige Person] }
    { 10  ;   ;Sorting Order       ;Code10        ;CaptionML=[ENU=Sorting Order;
                                                              ESP=Orden clasificaci¢n;
                                                              DEA=Sortierreihenfolge] }
    { 11  ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              ESP=Comentario;
                                                              DEA=Bemerkung] }
    { 12  ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                // Change to other type than cost type. Entries exist?
                                                                IF (("Line Type" <> "Line Type"::"Cost Center") AND
                                                                    (xRec."Line Type" = xRec."Line Type"::"Cost Center")) OR
                                                                   (("Line Type" <> "Line Type"::"Begin-Total") AND
                                                                    (xRec."Line Type" = xRec."Line Type"::"Begin-Total"))
                                                                THEN
                                                                  ConfirmModifyIfEntriesExist(Rec);

                                                                IF "Line Type" <> "Line Type"::"Cost Center" THEN BEGIN
                                                                  Blocked := TRUE;
                                                                  "Cost Subtype" := 0;
                                                                END ELSE
                                                                  Totaling := '';
                                                              END;

                                                   CaptionML=[ENU=Line Type;
                                                              ESP=Tipo l°nea;
                                                              DEA=Zeilenart];
                                                   OptionCaptionML=[ENU=Cost Center,Heading,Total,Begin-Total,End-Total;
                                                                    ESP=Centro coste,Encabezado,Total,Total inicio,Total fin;
                                                                    DEA=Kostenstelle,öberschrift,Summe,Von-Summe,Bis-Summe];
                                                   OptionString=Cost Center,Heading,Total,Begin-Total,End-Total }
    { 13  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado;
                                                              DEA=Gesperrt] }
    { 14  ;   ;New Page            ;Boolean       ;CaptionML=[ENU=New Page;
                                                              ESP=P†gina nueva;
                                                              DEA=Neue Seite] }
    { 15  ;   ;Blank Line          ;Boolean       ;CaptionML=[ENU=Blank Line;
                                                              ESP=L°nea en blanco;
                                                              DEA=Leere Zeile];
                                                   MinValue=No }
    { 16  ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ESP=Indentar;
                                                              DEA=EinrÅckung];
                                                   MinValue=0;
                                                   Editable=No }
    { 17  ;   ;Totaling            ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Line Type" IN ["Line Type"::Total,"Line Type"::"End-Total"]) THEN
                                                                  FIELDERROR("Line Type");

                                                                CALCFIELDS("Net Change");
                                                              END;

                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupCostCenterFilter(SelectionFilter) THEN
                                                                VALIDATE(Totaling,COPYSTR(SelectionFilter,1,MAXSTRLEN(Totaling)));
                                                            END;

                                                   CaptionML=[ENU=Totaling;
                                                              ESP=Sumatorio;
                                                              DEA=ZusammenzÑhlung] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Cost Subtype                             }
    {    ;Sorting Order                            }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Name,Field720                       }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There are general ledger entries, cost entries, or cost budget entries that are posted to the selected cost center. Are you sure that you want to delete the cost center?;ESP=Hay movimientos de contabilidad, movimientos de coste o movimientos de presupuesto de costes registrados en el centro de coste seleccionado. ®Est† seguro de que desea eliminar el centro de coste?;DEA=Auf die ausgewÑhlte Kostenstelle wurden Sachposten, Kostenposten oder Kostenbudgetposten gebucht. Mîchten Sie die Kostenstelle wirklich lîschen?';
      Text002@1002 : TextConst 'ENU=There are general ledger entries, cost entries, or cost budget entries that are posted to the selected cost center. Are you sure that you want to modify the cost center?;ESP=Hay movimientos de contabilidad, movimientos de coste o movimientos de presupuesto de costes registrados en el centro de coste seleccionado. ®Est† seguro de que desea modificar el centro de coste?;DEA=Auf die ausgewÑhlte Kostenstelle wurden Sachposten, Kostenposten oder Kostenbudgetposten gebucht. Mîchten Sie die Kostenstelle wirklich Ñndern?';

    PROCEDURE EntriesExist@1(VAR CostCenter@1007 : Record 1112) EntriesFound : Boolean;
    VAR
      CostAccSetup@1000 : Record 1108;
      GLEntry@1001 : Record 17;
      CostEntry@1002 : Record 1104;
      CostBudgetEntry@1003 : Record 1109;
      DimensionMgt@1004 : Codeunit 408;
      DimFilterChunk@1005 : Text[1024];
    BEGIN
      CostAccSetup.GET;
      IF CostCenter.FINDSET THEN
        REPEAT
          DimensionMgt.GetDimSetIDsForFilter(CostAccSetup."Cost Center Dimension",CostCenter.Code);
          DimFilterChunk := DimensionMgt.GetNextDimSetFilterChunk(MAXSTRLEN(DimFilterChunk));
          WHILE DimFilterChunk <> '' DO BEGIN
            GLEntry.SETFILTER("Dimension Set ID",DimFilterChunk);
            IF GLEntry.FINDFIRST THEN BEGIN
              DimFilterChunk := '';
              EntriesFound := TRUE
            END ELSE
              DimFilterChunk := DimensionMgt.GetNextDimSetFilterChunk(MAXSTRLEN(DimFilterChunk));
          END;

          IF NOT EntriesFound THEN BEGIN
            CostBudgetEntry.SETCURRENTKEY("Budget Name","Cost Center Code");
            CostBudgetEntry.SETRANGE("Cost Center Code",CostCenter.Code);
            EntriesFound := NOT CostBudgetEntry.ISEMPTY;
          END;

          IF NOT EntriesFound THEN BEGIN
            CostEntry.SETCURRENTKEY("Cost Center Code");
            CostEntry.SETRANGE("Cost Center Code",CostCenter.Code);
            EntriesFound := NOT CostEntry.ISEMPTY;
          END;
        UNTIL (CostCenter.NEXT = 0) OR EntriesFound;
    END;

    PROCEDURE ConfirmDeleteIfEntriesExist@3(VAR CostCenter@1000 : Record 1112;CalledFromOnInsert@1001 : Boolean);
    BEGIN
      IF EntriesExist(CostCenter) THEN
        IF NOT CONFIRM(Text001,TRUE) THEN
          ERROR('');
      IF NOT CalledFromOnInsert THEN
        CostCenter.DELETEALL;
    END;

    PROCEDURE ConfirmModifyIfEntriesExist@4(VAR CostCenter@1000 : Record 1112);
    VAR
      CostCenter2@1001 : Record 1112;
    BEGIN
      CostCenter2 := CostCenter;
      CostCenter2.SETRECFILTER;
      IF EntriesExist(CostCenter2) THEN
        IF NOT CONFIRM(Text002,TRUE) THEN
          ERROR('');
    END;

    PROCEDURE LookupCostCenterFilter@2(VAR Text@1000 : Text[1024]) : Boolean;
    VAR
      ChartOfCostCenters@1002 : Page 1122;
    BEGIN
      ChartOfCostCenters.LOOKUPMODE(TRUE);
      IF ChartOfCostCenters.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := ChartOfCostCenters.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    BEGIN
    END.
  }
}


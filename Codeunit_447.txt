OBJECT Codeunit 447 Record Link Management
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            IF CONFIRM(Text001,FALSE) THEN BEGIN
              RemoveOrphanedLink;
              MESSAGE(Text004,NoOfRemoved);
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Do you want to remove links with no record reference?;ESP=®Desea eliminar v°nculos con las referencias sin registro?;DEA=Mîchten Sie VerknÅpfungen ohne Datensatzverweis lîschen?';
      Text002@1001 : TextConst 'ENU=Removing Record Links without record reference.\;ESP=Eliminando v°nculos registro sin referencia de registro.\;DEA=DatensatzverknÅpfungen ohne Datensatzverweis werden entfernt.\';
      Text003@1002 : TextConst 'ENU=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ESP=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;DEA=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text004@1003 : TextConst 'ENU=%1 orphaned links were removed.;ESP=Se eliminaron %1 v°nculos huÇrfanos.;DEA=%1 nicht zugeordnete VerknÅpfungen wurden entfernt.';
      NoOfRemoved@1004 : Integer;

    PROCEDURE RemoveOrphanedLink@5();
    VAR
      RecordLink@1000 : Record 2000000068;
      RecordRef@1001 : RecordRef;
      Window@1002 : Dialog;
      i@1003 : Integer;
      Total@1004 : Integer;
      TimeLocked@1005 : Time;
      InTransaction@1011 : Boolean;
      PrevRecID@1007 : RecordID;
      RecordExists@1010 : Boolean;
    BEGIN
      Window.OPEN(Text002 + Text003);
      TimeLocked := TIME;
      WITH RecordLink DO BEGIN
        SETFILTER(Company,'%1|%2','',COMPANYNAME);
        SETCURRENTKEY("Record ID");
        Total := COUNT;
        IF Total = 0 THEN
          EXIT;
        IF FIND('-') THEN
          REPEAT
            i := i + 1;
            IF (i MOD 1000) = 0 THEN
              Window.UPDATE(1,ROUND(i / Total * 10000,1));
            IF FORMAT("Record ID") <> FORMAT(PrevRecID) THEN BEGIN  // Direct comparison doesn't work.
              PrevRecID := "Record ID";
              RecordExists := RecordRef.GET("Record ID");
            END;
            IF NOT RecordExists THEN BEGIN
              DELETE;
              NoOfRemoved := NoOfRemoved + 1;
              IF NOT InTransaction THEN
                TimeLocked := TIME;
              InTransaction := TRUE;
            END;
            IF InTransaction AND (TIME > (TimeLocked + 1000)) THEN BEGIN
              COMMIT;
              TimeLocked := TIME;
              InTransaction := FALSE;
            END;
          UNTIL NEXT = 0;
      END;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}


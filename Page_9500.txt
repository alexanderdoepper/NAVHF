OBJECT Page 9500 Debugger
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Debugger;
               ESP=Depurador;
               DEA=Debugger];
    LinksAllowed=No;
    SourceTable=Table2000000110;
    DataCaptionExpr=DataCaption;
    PageType=Card;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Code Tracking,Running Code,Breakpoints,Show;
                                ESP=Nuevo,Proceso,Informe,Seguimiento de c¢digo,C¢digo de ejecuci¢n,Puntos de interrupci¢n,Mostrar;
                                DEA=Neu,Verarbeiten,Bericht,Codeverfolgung,AusfÅhrungscode,Breakpoints,Anzeigen];
    ShowFilter=No;
    OnInit=BEGIN
             BreakOnError := TRUE;
             BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;
             BreakEnabled := NOT BreakpointHit;
           END;

    OnOpenPage=VAR
                 DebuggedSession@1000 : Record 2000000110;
               BEGIN
                 DebuggerManagement.GetDebuggedSession(DebuggedSession);
                 IF DebuggedSession."Session ID" = 0 THEN
                   DEBUGGER.ACTIVATE
                 ELSE BEGIN
                   DEBUGGER.ATTACH(DebuggedSession."Session ID");
                   SetAttachedSession := TRUE;
                 END;

                 IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
                   BreakOnError := UserPersonalization."Debugger Break On Error";
                   BreakOnRecordChanges := UserPersonalization."Debugger Break On Rec Changes";
                   SkipCodeunit1 := UserPersonalization."Debugger Skip System Triggers";
                 END;

                 IF BreakOnError THEN
                   DEBUGGER.BREAKONERROR(TRUE);
                 IF BreakOnRecordChanges THEN
                   DEBUGGER.BREAKONRECORDCHANGES(TRUE);
                 IF SkipCodeunit1 THEN
                   DEBUGGER.SKIPSYSTEMTRIGGERS(TRUE);

                 DebuggerManagement.ResetActionState;
               END;

    OnClosePage=BEGIN
                  IF DEBUGGER.DEACTIVATE THEN;
                  SetAttachedSession := FALSE;
                END;

    OnFindRecord=VAR
                   DebuggedSession@1003 : Record 2000000110;
                   IsBreakOnErrorMessageNew@1001 : Boolean;
                   BreakOnErrorMessage@1002 : Text[1024];
                 BEGIN
                   IF NOT DEBUGGER.ISACTIVE AND (Which = '=') THEN
                     MESSAGE(Text007Msg);

                   IF NOT DEBUGGER.ISACTIVE THEN BEGIN
                     CurrPage.CLOSE;
                     EXIT(FALSE);
                   END;

                   BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;

                   IF BreakpointHit THEN BEGIN
                     BreakOnErrorMessage := DebuggerManagement.GetLastErrorMessage(IsBreakOnErrorMessageNew);

                     IF IsBreakOnErrorMessageNew AND (BreakOnErrorMessage <> '') THEN
                       MESSAGE(STRSUBSTNO(Text002Msg,BreakOnErrorMessage));

                     ShowLastErrorEnabled := (BreakOnErrorMessage <> '') OR (DEBUGGER.GETLASTERRORTEXT <> '');

                     BreakEnabled := FALSE;
                     IF NOT SetAttachedSession THEN BEGIN
                       DebuggedSession."Session ID" := DEBUGGER.DEBUGGEDSESSIONID;
                       DebuggerManagement.SetDebuggedSession(DebuggedSession);
                       SetAttachedSession := TRUE;
                     END;
                   END ELSE BEGIN
                     IsBreakOnErrorMessageNew := FALSE;
                     ShowLastErrorEnabled := FALSE;
                     DataCaption := Text004Cap;
                   END;

                   EXIT(TRUE);
                 END;

    OnAfterGetRecord=BEGIN
                       IF BreakpointHit THEN BEGIN
                         CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);
                         WITH DebuggerCallstack DO BEGIN
                           IF ID <> 0 THEN
                             DataCaption := STRSUBSTNO(Text003Cap,"Object Type","Object ID","Object Name")
                           ELSE
                             DataCaption := Text004Cap;
                         END;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      Name=Debugger Actions;
                      CaptionML=[ENU=Debugger Actions;
                                 ESP=Acciones del depurador;
                                 DEA=Debuggeraktionen];
                      ActionContainerType=ActionItems }
      { 25      ;1   ;Separator  }
      { 26      ;1   ;ActionGroup;
                      Name=Code Tracking;
                      CaptionML=[ENU=Code Tracking;
                                 ESP=Seguimiento de c¢digo;
                                 DEA=Codeverfolgung] }
      { 8       ;2   ;Action    ;
                      Name=Step Into;
                      ShortCutKey=F11;
                      CaptionML=[ENU=Step Into;
                                 ESP=Entrar;
                                 DEA=Schritt bis zur ersten Anweisung innerhalb der Funktion];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement inside the function.;
                                 ESP=Ejecuta el extracto actual. Si el extracto contiene una llamada de funci¢n, ejecuta la funci¢n e interrumpe en la primera instrucci¢n dentro de la funci¢n.;
                                 DEA=FÅhrt die aktuelle Anweisung aus. EnthÑlt die Anweisung einen Funktionsaufruf, wird die Funktion ausgefÅhrt, und die AusfÅhrung wird bei der ersten Anweisung innerhalb der Funktion unterbrochen.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepInto;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPINTO;
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Name=Step Over;
                      ShortCutKey=F10;
                      CaptionML=[ENU=Step Over;
                                 ESP=Saltar;
                                 DEA=Schritt bis zur ersten Anweisung au·erhalb der Funktion];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement outside the function.;
                                 ESP=Ejecuta la instrucci¢n actual. Si la instrucci¢n contiene una llamada de funci¢n, ejecuta la funci¢n e interrumpe en la primera instrucci¢n fuera de la funci¢n.;
                                 DEA=FÅhrt die aktuelle Anweisung aus. EnthÑlt die Anweisung einen Funktionsaufruf, wird die Funktion ausgefÅhrt, und die AusfÅhrung wird bei der ersten Anweisung au·erhalb der Funktion unterbrochen.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOver;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOVER;
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Step Out;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=Step Out;
                                 ESP=Salir;
                                 DEA=Schritt bis zur nÑchsten Anweisung in der aufrufenden Funktion];
                      ToolTipML=[ENU=Execute the remaining statements in the current function and break at the next statement in the calling function.;
                                 ESP=Ejecuta las instrucciones restantes en la funci¢n actual e interrumpe en el siguiente extracto en la funci¢n de llamada.;
                                 DEA=FÅhrt die restlichen Anweisungen in der aktuellen Funktion aus und unterbricht die AusfÅhrung bei der nÑchsten Anweisung in der aufrufenden Funktion.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOut;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOUT;
                               END;
                                }
      { 11      ;1   ;Separator  }
      { 27      ;1   ;ActionGroup;
                      Name=Running Code;
                      CaptionML=[ENU=Running Code;
                                 ESP=C¢digo en ejecuci¢n;
                                 DEA=AusfÅhrungscode] }
      { 12      ;2   ;Action    ;
                      Name=Continue;
                      ShortCutKey=F5;
                      CaptionML=[ENU=Continue;
                                 ESP=Continuar;
                                 DEA=Fortfahren];
                      ToolTipML=[ENU=Continue until the next break.;
                                 ESP=Continuar hasta el siguiente salto.;
                                 DEA=FÑhrt bis zur nÑchsten Unterbrechung fort.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Continue;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.CONTINUE;
                               END;
                                }
      { 31      ;2   ;Action    ;
                      Name=Break;
                      ShortCutKey=Shift+Ctrl+B;
                      CaptionML=[ENU=Break;
                                 ESP=Salto;
                                 DEA=Unterbrechung];
                      ToolTipML=[ENU=Break at the next statement.;
                                 ESP=Salto en el siguiente extracto.;
                                 DEA=Unterbricht bei der nÑchsten Anweisung.];
                      Promoted=Yes;
                      Enabled=BreakEnabled;
                      PromotedIsBig=Yes;
                      Image=Pause;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 BreakEnabled := FALSE;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.BREAK;
                               END;
                                }
      { 15      ;2   ;Action    ;
                      Name=Stop;
                      ShortCutKey=Shift+F5;
                      CaptionML=[ENU=Stop;
                                 ESP=Detener;
                                 DEA=Beenden];
                      ToolTipML=[ENU=Stop the current activity in the session being debugged while continuing to debug the session.;
                                 ESP=Detiene la actividad actual en la sesi¢n que se est† depurando mientras se contin£a depurando la sesi¢n.;
                                 DEA=Beendet die aktuelle Aktion in der Session, die gedebuggt wird. Das Debuggen der Session wird dabei fortgesetzt.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Stop;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.STOP;
                               END;
                                }
      { 21      ;1   ;Separator  }
      { 28      ;1   ;ActionGroup;
                      Name=Breakpoints Group;
                      CaptionML=[ENU=Breakpoints;
                                 ESP=Puntos de interrupci¢n;
                                 DEA=Breakpoints] }
      { 14      ;2   ;Action    ;
                      Name=Toggle;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Toggle;
                                 ESP=Alternar;
                                 DEA=Umschalten];
                      ToolTipML=[ENU=Toggle a breakpoint at the current line.;
                                 ESP=Alterna punto de interrupci¢n en la l°nea actual.;
                                 DEA=Dient zum Umschalten eines Breakpoints fÅr die aktuelle Zeile.];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ToggleBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.ToggleBreakpoint;
                               END;
                                }
      { 13      ;2   ;Action    ;
                      Name=Set/Clear Condition;
                      ShortCutKey=Shift+F9;
                      CaptionML=[ENU=Set/Clear Condition;
                                 ESP=Establecer/borrar condici¢n;
                                 DEA=Bedingung festlegen/lîschen];
                      ToolTipML=[ENU=Set or clear a breakpoint condition at the current line.;
                                 ESP=Establece o borra una condici¢n de punto de interrupci¢n en la l°nea actual.;
                                 DEA=Dient zum Festlegen oder Lîschen einer Breakpointbedingung fÅr die aktuelle Zeile.];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ConditionalBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.SetBreakpointCondition;
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Name=Disable All;
                      CaptionML=[ENU=Disable All;
                                 ESP=Deshabilitar todo;
                                 DEA=Alles deaktivieren];
                      ToolTipML=[ENU=Disable all breakpoints in the breakpoint list. You can edit the list by using the Breakpoints action.;
                                 ESP=Deshabilita todos los puntos de interrupci¢n en la lista de puntos de interrupci¢n. Puede editar la lista mediante la acci¢n Puntos de interrupci¢n.;
                                 DEA=Deaktiviert alle Breakpoints in der BreakpointÅbersicht. Verwenden Sie zum Bearbeiten der öbersicht die Aktion 'Breakpoints'.];
                      Promoted=Yes;
                      Image=DisableAllBreakpoints;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakpoint@1001 : Record 2000000100;
                               BEGIN
                                 DebuggerBreakpoint.SETRANGE("User SID",USERSECURITYID);
                                 DebuggerBreakpoint.MODIFYALL(Enabled,FALSE,TRUE);
                               END;
                                }
      { 20      ;2   ;Action    ;
                      Name=Breakpoints;
                      CaptionML=[ENU=Breakpoints;
                                 ESP=Puntos de interrupci¢n;
                                 DEA=Breakpoints];
                      ToolTipML=[ENU=Edit the breakpoint list for all objects.;
                                 ESP=Edita la lista de puntos de interrupci¢n para todos los objetos.;
                                 DEA=Dient zum Bearbeiten der BreakpointÅbersicht fÅr alle Objekte.];
                      RunObject=Page 9505;
                      Promoted=Yes;
                      Image=BreakpointsList;
                      PromotedCategory=Category6 }
      { 35      ;2   ;Action    ;
                      Name=Break Rules;
                      CaptionML=[ENU=Break Rules;
                                 ESP=Reglas de salto;
                                 DEA=Unterbrechungsregeln];
                      ToolTipML=[ENU=Change settings for break rules. The debugger breaks code execution for certain configurable rules as well as for breakpoints.;
                                 ESP=Cambia la configuraci¢n para las reglas de salto. El depurador interrumpe la ejecuci¢n del c¢digo para determinadas reglas configurables as° como para puntos de interrupci¢n.;
                                 DEA=Dient zum éndern der Einstellungen fÅr Unterbrechungsregeln. Die CodeausfÅhrung wird vom Debugger auf der Grundlage bestimmter konfigurierbarer Regeln sowie an Breakpoints unterbrochen.];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=BreakRulesList;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakRulesPage@1000 : Page 9509;
                               BEGIN
                                 DebuggerBreakRulesPage.SetBreakOnError(BreakOnError);
                                 DebuggerBreakRulesPage.SetBreakOnRecordChanges(BreakOnRecordChanges);
                                 DebuggerBreakRulesPage.SetSkipCodeunit1(SkipCodeunit1);

                                 IF DebuggerBreakRulesPage.RUNMODAL = ACTION::OK THEN BEGIN
                                   BreakOnError := DebuggerBreakRulesPage.GetBreakOnError;
                                   DEBUGGER.BREAKONERROR(BreakOnError);
                                   BreakOnRecordChanges := DebuggerBreakRulesPage.GetBreakOnRecordChanges;
                                   DEBUGGER.BREAKONRECORDCHANGES(BreakOnRecordChanges);
                                   SkipCodeunit1 := DebuggerBreakRulesPage.GetSkipCodeunit1;
                                   DEBUGGER.SKIPSYSTEMTRIGGERS(SkipCodeunit1);

                                   SaveConfiguration;
                                 END;
                               END;
                                }
      { 22      ;1   ;Separator  }
      { 33      ;1   ;ActionGroup;
                      Name=Show;
                      CaptionML=[ENU=Show;
                                 ESP=Mostrar;
                                 DEA=Anzeigen];
                      Image=View }
      { 6       ;2   ;Action    ;
                      Name=Variables;
                      ShortCutKey=Shift+Ctrl+V;
                      CaptionML=[ENU=Variables;
                                 ESP=Variables;
                                 DEA=Variablen];
                      ToolTipML=[ENU=View the list of variables in the current scope.;
                                 ESP=Ve la lista de variables en el †mbito actual.;
                                 DEA=Zeigt die öbersicht mit den Variablen des aktuellen Bereichs an.];
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=VariableList;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerCallstack@1001 : Record 2000000101;
                                 DebuggerVariable@1003 : Record 2000000102;
                               BEGIN
                                 CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);

                                 DebuggerVariable.FILTERGROUP(2);
                                 DebuggerVariable.SETRANGE("Call Stack ID",DebuggerCallstack.ID);
                                 DebuggerVariable.FILTERGROUP(0);

                                 PAGE.RUNMODAL(PAGE::"Debugger Variable List",DebuggerVariable);
                               END;
                                }
      { 36      ;2   ;Action    ;
                      Name=LastErrorMessage;
                      CaptionML=[ENU=Last Error;
                                 ESP=Èltimo error;
                                 DEA=Letzter Fehler];
                      ToolTipML=[ENU=View the last error message shown by the session being debugged.;
                                 ESP=Ve el £ltimo mensaje de error mostrado por la sesi¢n que se est† depurando.;
                                 DEA=Zeigt die letzte Fehlermeldung fÅr die Session an, die gedebuggt wird.];
                      Promoted=Yes;
                      Enabled=ShowLastErrorEnabled;
                      PromotedIsBig=Yes;
                      Image=PrevErrorMessage;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerManagement@1003 : Codeunit 9500;
                                 LastErrorMessage@1001 : Text[1024];
                                 IsLastErrorMessageNew@1002 : Boolean;
                               BEGIN
                                 LastErrorMessage := DebuggerManagement.GetLastErrorMessage(IsLastErrorMessageNew);

                                 IF LastErrorMessage = '' THEN
                                   LastErrorMessage := DEBUGGER.GETLASTERRORTEXT;

                                 MESSAGE(STRSUBSTNO(Text005Msg,LastErrorMessage));
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Part      ;
                Name=CodeViewer;
                CaptionML=[ENU=Code;
                           ESP=C¢digo;
                           DEA=Code];
                SubPageLink=Object Type=FIELD(Object Type),
                            Object ID=FIELD(Object ID),
                            Line No.=FIELD(Line No.),
                            ID=FIELD(ID);
                PagePartID=Page9504;
                ProviderID=10 }

    { 3   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 5   ;1   ;Part      ;
                Name=Watches;
                CaptionML=[ENU=Watches;
                           ESP=Inspecciones;
                           DEA=öberwachungen];
                SubPageLink=Call Stack ID=FIELD(ID);
                PagePartID=Page9503;
                ProviderID=10 }

    { 10  ;1   ;Part      ;
                Name=Callstack;
                CaptionML=[ENU=Call Stack;
                           ESP=Pila de llamadas;
                           DEA=Aufrufliste];
                PagePartID=Page9502 }

  }
  CODE
  {
    VAR
      DebuggerCallstack@1005 : Record 2000000101;
      UserPersonalization@1009 : Record 2000000073;
      DebuggerManagement@1012 : Codeunit 9500;
      BreakEnabled@1004 : Boolean INDATASET;
      BreakpointHit@1002 : Boolean INDATASET;
      BreakOnError@1000 : Boolean INDATASET;
      Text002Msg@1007 : TextConst '@@@=Message shown when Break On Error occurs. Include the original error message.;ENU=Break On Error Message:\ \%1;ESP=Interrumpir en mensaje de error:\ \%1;DEA=Unterbrechen bei Fehlermeldung:\ \%1';
      BreakOnRecordChanges@1003 : Boolean INDATASET;
      SkipCodeunit1@1010 : Boolean;
      DataCaption@1001 : Text[100];
      ShowLastErrorEnabled@1006 : Boolean INDATASET;
      Text003Cap@1011 : TextConst '@@@=DataCaption when debugger is broken in application code. Example: Codeunit 1:  Application Management;ENU=%1 %2 : %3;ESP=%1 %2 : %3;DEA=%1 %2 : %3';
      Text004Cap@1013 : TextConst '@@@=DataCaption when waiting for break;ENU=Waiting for break...;ESP=Esperando pausa...;DEA=Auf Unterbrechung wird gewartet...';
      SetAttachedSession@1008 : Boolean;
      Text005Msg@1014 : TextConst 'ENU=Last Error Message:\ \%1;ESP=Èltimo mensaje de error:\ \%1;DEA=Letzte Fehlermeldung:\ \%1';
      Text007Msg@1016 : TextConst 'ENU=The session that was being debugged has closed. The Debugger Page must now close.;ESP=La sesi¢n que se estaba depurando se ha cerrado. La p†gina del depurador debe cerrarse ahora.;DEA=Die Session, fÅr die das Debuggen ausgefÅhrt wurde, wurde geschlossen. Die Debugger-Seite muss nun geschlossen werden.';

    LOCAL PROCEDURE SaveConfiguration@1();
    BEGIN
      IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
        UserPersonalization."Debugger Break On Error" := BreakOnError;
        UserPersonalization."Debugger Break On Rec Changes" := BreakOnRecordChanges;
        UserPersonalization."Debugger Skip System Triggers" := SkipCodeunit1;
        UserPersonalization.MODIFY;
      END;
    END;

    PROCEDURE WaitingForBreak@3();
    BEGIN
      BreakEnabled := TRUE;
      CurrPage.Callstack.PAGE.ResetCallstackToTop;
    END;

    BEGIN
    END.
  }
}


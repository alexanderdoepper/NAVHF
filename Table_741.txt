OBJECT Table 741 VAT Report Line
{
  OBJECT-PROPERTIES
  {
    Date=05.04.13;
    Time=12:00:00;
    Version List=NAVW17.00.00.34587,NAVDE7.00.00.34587;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VATReportHeader.GET("VAT Report No.");
               IF VATReportHeader."Original Report No." = '' THEN
                 VALIDATE("VAT Report to Correct",VATReportHeader."No.")
               ELSE
                 VALIDATE("VAT Report to Correct",VATReportHeader."Original Report No.");
             END;

    OnModify=VAR
               VATReportSetup@1000 : Record 743;
             BEGIN
               CheckEditingAllowed;
             END;

    OnDelete=VAR
               VATReportLineRelation@1140000 : Record 744;
             BEGIN
               CheckEditingAllowed;

               VATReportLineRelation.RESET;
               VATReportLineRelation.SETRANGE("VAT Report No.","VAT Report No.");
               VATReportLineRelation.SETRANGE("VAT Report Line No.","Line No.");
               VATReportLineRelation.DELETEALL;
             END;

    CaptionML=[DEU=MwSt.-Berichtszeile;
               ENU=VAT Report Line];
  }
  FIELDS
  {
    { 1   ;   ;VAT Report No.      ;Code20        ;TableRelation="VAT Report Header".No.;
                                                   CaptionML=[DEU=MwSt.-Berichtsnr.;
                                                              ENU=VAT Report No.];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.];
                                                   Editable=No }
    { 3   ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[DEU=Produktbuchungsgruppe;
                                                              ENU=Gen. Prod. Posting Group];
                                                   Editable=No }
    { 4   ;   ;Posting Date        ;Date          ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   Editable=No }
    { 5   ;   ;Document No.        ;Code20        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.];
                                                   Editable=No }
    { 6   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,Zahlung,Rechnung,Gutschrift,Zinsrechnung,Mahnung,Erstattung";
                                                                    ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund];
                                                   Editable=No }
    { 7   ;   ;Type                ;Option        ;CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=" ,Einkauf,Verkauf,Ausgleich";
                                                                    ENU=" ,Purchase,Sale,Settlement"];
                                                   OptionString=[ ,Purchase,Sale,Settlement];
                                                   Editable=No }
    { 8   ;   ;Base                ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;

                                                                Base := RoundBase(Base);
                                                              END;

                                                   CaptionML=[DEU=Bemessungsgrundlage;
                                                              ENU=Base];
                                                   AutoFormatType=1 }
    { 9   ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;

                                                                Amount := RoundBase(Amount);
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount];
                                                   AutoFormatType=1 }
    { 10  ;   ;VAT Calculation Type;Option        ;CaptionML=[DEU=MwSt.-Berechnungsart;
                                                              ENU=VAT Calculation Type];
                                                   OptionCaptionML=[DEU=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer;
                                                                    ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 12  ;   ;Bill-to/Pay-to No.  ;Code20        ;TableRelation=IF (Type=CONST(Purchase)) Vendor
                                                                 ELSE IF (Type=CONST(Sale)) Customer;
                                                   CaptionML=[DEU=Rech. an/Zahlung an Nr.;
                                                              ENU=Bill-to/Pay-to No.];
                                                   Editable=No }
    { 13  ;   ;EU 3-Party Trade    ;Boolean       ;CaptionML=[DEU=EU-DreiecksgeschÑft;
                                                              ENU=EU 3-Party Trade];
                                                   Editable=No }
    { 15  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[DEU=Herkunftscode;
                                                              ENU=Source Code];
                                                   Editable=No }
    { 16  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code];
                                                   Editable=No }
    { 17  ;   ;EU Service          ;Boolean       ;CaptionML=[DEU=EU-Service;
                                                              ENU=EU Service];
                                                   Editable=No }
    { 19  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[DEU=LÑnder-/Regionscode;
                                                              ENU=Country/Region Code];
                                                   Editable=No }
    { 20  ;   ;Internal Ref. No.   ;Text30        ;CaptionML=[DEU=Interne Referenznr.;
                                                              ENU=Internal Ref. No.];
                                                   Editable=No }
    { 22  ;   ;Unrealized Amount   ;Decimal       ;CaptionML=[DEU=Unreal. Betrag;
                                                              ENU=Unrealized Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 23  ;   ;Unrealized Base     ;Decimal       ;CaptionML=[DEU=Unreal. Bemessungsgrundlage;
                                                              ENU=Unrealized Base];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 24  ;   ;Number of Supplies  ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;
                                                              END;

                                                   CaptionML=[DEU=Anzahl der Lieferungen;
                                                              ENU=Number of Supplies];
                                                   DecimalPlaces=0:0;
                                                   BlankNumbers=DontBlank;
                                                   Editable=No }
    { 26  ;   ;External Document No.;Code35       ;CaptionML=[DEU=Externe Belegnummer;
                                                              ENU=External Document No.];
                                                   Editable=No }
    { 30  ;   ;Trade Type          ;Option        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;
                                                              END;

                                                   CaptionML=[DEU=Handelstyp;
                                                              ENU=Trade Type];
                                                   OptionCaptionML=[DEU=Einkauf,Verkauf;
                                                                    ENU=Purchase,Sale];
                                                   OptionString=Purchase,Sale;
                                                   Editable=No }
    { 31  ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   CaptionML=[DEU=Zeilenart;
                                                              ENU=Line Type];
                                                   OptionCaptionML=[DEU=Neu,Stornierung,Korrektur;
                                                                    ENU=New,Cancellation,Correction];
                                                   OptionString=New,Cancellation,Correction;
                                                   Editable=No }
    { 32  ;   ;Related Line No.    ;Integer       ;CaptionML=[DEU=VerknÅpfte Positionsnummer;
                                                              ENU=Related Line No.];
                                                   Editable=No }
    { 33  ;   ;Trade Role Type     ;Option        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;
                                                              END;

                                                   CaptionML=[DEU=Handelsrollentyp;
                                                              ENU=Trade Role Type];
                                                   OptionCaptionML=[DEU=Direkter Handel,Zwischenhandel,Besitzumlagerung;
                                                                    ENU=Direct Trade,Intermediate Trade,Property Movement];
                                                   OptionString=Direct Trade,Intermediate Trade,Property Movement;
                                                   Editable=No }
    { 39  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[DEU=MwSt.-GeschÑftsbuchungsgruppe;
                                                              ENU=VAT Bus. Posting Group];
                                                   Editable=No }
    { 40  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[DEU=MwSt.-Produktbuchungsgruppe;
                                                              ENU=VAT Prod. Posting Group];
                                                   Editable=No }
    { 50  ;   ;Corrected Reg. No.  ;Boolean       ;CaptionML=[DEU=Korrigierte Reg.nr.;
                                                              ENU=Corrected Reg. No.];
                                                   Editable=No }
    { 51  ;   ;Corrected Amount    ;Boolean       ;CaptionML=[DEU=Korrigierter Betrag;
                                                              ENU=Corrected Amount];
                                                   Editable=No }
    { 54  ;   ;Registration No.    ;Text20        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                CheckLineType;
                                                              END;

                                                   CaptionML=[DEU=Steuernummer;
                                                              ENU=Registration No.];
                                                   Editable=No }
    { 55  ;   ;VAT Registration No.;Text20        ;CaptionML=[DEU=USt-IdNr.;
                                                              ENU=VAT Registration No.];
                                                   Editable=No }
    { 56  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[DEU=GeschÑftsbuchungsgruppe;
                                                              ENU=Gen. Bus. Posting Group];
                                                   Editable=No }
    { 99  ;   ;System-Created      ;Boolean       ;CaptionML=[DEU=Vom System erstellt;
                                                              ENU=System-Created];
                                                   Editable=No }
    { 100 ;   ;Record Identifier   ;Code30        ;CaptionML=[DEU=Datensatzkennzeichen;
                                                              ENU=Record Identifier];
                                                   Editable=No }
    { 101 ;   ;VAT Report to Correct;Code20       ;CaptionML=[DEU=Zu korrigierende MwSt.-Berichte;
                                                              ENU=VAT Report to Correct] }
    { 102 ;   ;Able to Correct Line;Boolean       ;CaptionML=[DEU=In der Lage, die Position zu korrigieren;
                                                              ENU=Able to Correct Line] }
  }
  KEYS
  {
    {    ;VAT Report No.,Line No.                 ;Clustered=Yes }
    {    ;Trade Type,Country/Region Code,VAT Registration No.,Registration No.,EU 3-Party Trade }
    {    ;VAT Report to Correct,Able to Correct Line }
    {    ;VAT Report No.,Line Type                ;SumIndexFields=Base,Amount,Number of Supplies }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      VATReportHeader@1000 : Record 740;
      LineCannotBeEditedErr@1001 : TextConst 'DEU=Cancellation line cannot be changed.;ENU=Cancellation line cannot be changed.';

    LOCAL PROCEDURE TestStatusOpen@33();
    BEGIN
      VATReportHeader.GET("VAT Report No.");
      VATReportHeader.TESTFIELD(Status,VATReportHeader.Status::Open);
    END;

    PROCEDURE GetTradeRole@1470000() : Code[10];
    BEGIN
      CASE "Trade Role Type" OF
        "Trade Role Type"::"Direct Trade":
          CASE "Trade Type" OF
            "Trade Type"::Sale:
              EXIT('0');
            "Trade Type"::Purchase:
              EXIT('0');
          END;
        "Trade Role Type"::"Property Movement":
          EXIT('1');
        "Trade Role Type"::"Intermediate Trade":
          EXIT('2');
      END;
    END;

    PROCEDURE GetCancelCode@1470001() : Code[10];
    BEGIN
      IF "Line Type" = "Line Type"::Cancellation THEN
        EXIT('1');

      EXIT('0');
    END;

    PROCEDURE GetNextLineNo@1140001(VATReportNo@1140000 : Code[20]) : Integer;
    VAR
      VATReportLine2@1140001 : Record 741;
    BEGIN
      VATReportLine2.SETRANGE("VAT Report No.",VATReportNo);
      IF VATReportLine2.FINDLAST THEN
        EXIT(VATReportLine2."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    PROCEDURE GetVATEntries@1140003(VAR VATReportLine@1140000 : Record 741) : Decimal;
    VAR
      VATEntry@1140001 : Record 254;
    BEGIN
      WITH VATEntry DO BEGIN
        RESET;
        CASE VATReportLine."Trade Type" OF
          VATReportLine."Trade Type"::Sale:
            SETRANGE(Type,VATEntry.Type::Sale);
          VATReportLine."Trade Type"::Purchase:
            SETRANGE(Type,VATEntry.Type::Purchase);
        END;
        VATReportHeader.GET(VATReportLine."VAT Report No.");
        SETRANGE("Posting Date",VATReportHeader."Start Date",VATReportHeader."End Date");
        SETRANGE("Country/Region Code",VATReportLine."Country/Region Code");
        SETRANGE("VAT Registration No.",VATReportLine."VAT Registration No.");
        SETRANGE("EU 3-Party Trade",VATReportLine."EU 3-Party Trade");
        SETRANGE("EU Service",VATReportLine."EU Service");
        IF FINDSET THEN BEGIN
          VATReportLine.Base := 0;
          VATReportLine.Amount := 0;
          REPEAT
            VATReportLine.Base += -Base;
            VATReportLine.Amount += -Amount;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE RoundBase@1140004(AmountToRound@1140000 : Decimal) : Decimal;
    BEGIN
      EXIT(ROUND(AmountToRound,1));
    END;

    PROCEDURE CheckLineType@1470007();
    VAR
      VATReportSetup@1140000 : Record 743;
    BEGIN
      IF "Line Type" = "Line Type"::Cancellation THEN
        ERROR(LineCannotBeEditedErr,VATReportSetup.TABLECAPTION);
    END;

    PROCEDURE CheckEditingAllowed@1140002();
    VAR
      VATReportSetup@1140000 : Record 743;
    BEGIN
      VATReportSetup.GET;
      VATReportHeader.GET("VAT Report No.");
      VATReportHeader.CheckEditingAllowed;
    END;

    PROCEDURE InsertCorrLine@1140000(VATReportHeader@1140002 : Record 740;CancellationVATReportLine@1140001 : Record 741;CorrectionVATReportLine@1140003 : Record 741;VAR TempVATReportLineRelation@1140004 : TEMPORARY Record 744);
    VAR
      VATReportLine2@1140000 : Record 741;
      VATReportLineRelation@1140005 : Record 744;
    BEGIN
      WITH VATReportLine2 DO BEGIN
        INIT;
        "VAT Report No." := VATReportHeader."No.";
        "Line No." := GetNextLineNo(VATReportHeader."No.");
        "Trade Type" := CancellationVATReportLine."Trade Type";
        "Line Type" := "Line Type"::Cancellation;
        "Related Line No." := CancellationVATReportLine."Line No.";
        "Country/Region Code" := CancellationVATReportLine."Country/Region Code";
        "VAT Registration No." := CancellationVATReportLine."VAT Registration No.";
        "EU 3-Party Trade" := CancellationVATReportLine."EU 3-Party Trade";
        "EU Service" := CancellationVATReportLine."EU Service";
        "Trade Role Type" := CancellationVATReportLine."Trade Role Type";
        "Number of Supplies" := CancellationVATReportLine."Number of Supplies";
        Base := -CancellationVATReportLine.Base;
        Amount := -CancellationVATReportLine.Amount;
        "System-Created" := TRUE;
        INSERT(TRUE);

        INIT;
        "VAT Report No." := VATReportHeader."No.";
        "Line No." += 10000;
        "Trade Type" := CorrectionVATReportLine."Trade Type";
        "Line Type" := "Line Type"::Correction;
        "Related Line No." := CorrectionVATReportLine."Line No.";
        "Country/Region Code" := CorrectionVATReportLine."Country/Region Code";
        "VAT Registration No." := CorrectionVATReportLine."VAT Registration No.";
        "EU 3-Party Trade" := CorrectionVATReportLine."EU 3-Party Trade";
        "EU Service" := CorrectionVATReportLine."EU Service";
        "Trade Role Type" := CorrectionVATReportLine."Trade Role Type";
        "Number of Supplies" := CorrectionVATReportLine."Number of Supplies";
        GetVATEntries(CorrectionVATReportLine);
        Base := RoundBase(CorrectionVATReportLine.Base);
        Amount := RoundBase(CorrectionVATReportLine.Amount);
        "System-Created" := TRUE;
        INSERT(TRUE);
        TempVATReportLineRelation.SETRANGE("VAT Report No.","VAT Report No.");
        TempVATReportLineRelation.SETRANGE("VAT Report Line No.",CorrectionVATReportLine."Line No.");
        IF TempVATReportLineRelation.FINDSET THEN
          REPEAT
            VATReportLineRelation := TempVATReportLineRelation;
            VATReportLineRelation."VAT Report Line No." := "Line No.";
            VATReportLineRelation.INSERT;
          UNTIL TempVATReportLineRelation.NEXT = 0;
      END;
    END;

    PROCEDURE GetVATRegNo@1140006() : Text[20];
    VAR
      Country@1140000 : Record 9;
      VATRegNo@1140001 : Text[20];
    BEGIN
      IF "VAT Registration No." = '' THEN
        EXIT('');

      Country.GET("Country/Region Code");
      VATRegNo := "VAT Registration No.";
      IF COPYSTR(VATRegNo,1,STRLEN(Country."EU Country/Region Code")) = Country."EU Country/Region Code" THEN
        EXIT(VATRegNo);

      EXIT(Country."EU Country/Region Code" + VATRegNo);
    END;

    BEGIN
    END.
  }
}


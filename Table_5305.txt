OBJECT Table 5305 Outlook Synch. User Setup
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=BEGIN
               IF NOT CheckSetupDetail(Rec) THEN
                 ERROR('');

               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("User ID","User ID");
               OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchSetupDetail.DELETEALL;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    OnRename=BEGIN
               IF NOT CheckSetupDetail(xRec) THEN
                 ERROR('');

               IF xRec."Synch. Entity Code" = "Synch. Entity Code" THEN
                 EXIT;

               Condition := '';
               "Synch. Direction" := "Synch. Direction"::Bidirectional;
               "Last Synch. Time" := 0DT;

               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("User ID","User ID");
               OSynchSetupDetail.SETRANGE("Synch. Entity Code",xRec."Synch. Entity Code");
               OSynchSetupDetail.DELETEALL;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    CaptionML=[ENU=Outlook Synch. User Setup;
               ESP=Configuraci¢n usuario sinc. Outlook;
               DEA=Outlook-Synch.-Benutzereinrichtung];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. usuario;
                                                              DEA=Benutzer-ID];
                                                   NotBlank=Yes }
    { 2   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                IF "Synch. Entity Code" = xRec."Synch. Entity Code" THEN
                                                                  EXIT;

                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OSynchEntity.TESTFIELD(Description);
                                                                OSynchEntity.TESTFIELD("Table No.");
                                                                OSynchEntity.TESTFIELD("Outlook Item");

                                                                CALCFIELDS(Description,"No. of Elements");
                                                              END;

                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              ESP=C¢digo entidad sinc.;
                                                              DEA=Synch.-Einheitscode];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity".Description WHERE (Code=FIELD(Synch. Entity Code)));
                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n;
                                                              DEA=Beschreibung];
                                                   Editable=No }
    { 4   ;   ;Condition           ;Text250       ;CaptionML=[ENU=Condition;
                                                              ESP=Condici¢n;
                                                              DEA=Bedingung];
                                                   Editable=No }
    { 5   ;   ;Synch. Direction    ;Option        ;OnValidate=VAR
                                                                OSynchDependency@1000 : Record 5311;
                                                                RecRef@1002 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                              BEGIN
                                                                IF "Synch. Direction" = xRec."Synch. Direction" THEN
                                                                  EXIT;

                                                                IF "Synch. Direction" = "Synch. Direction"::Bidirectional THEN
                                                                  EXIT;

                                                                CALCFIELDS("No. of Elements");
                                                                IF "No. of Elements" <> 0 THEN BEGIN
                                                                  OSynchSetupDetail.RESET;
                                                                  OSynchSetupDetail.SETRANGE("User ID","User ID");
                                                                  OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                  IF OSynchSetupDetail.FIND('-') THEN
                                                                    REPEAT
                                                                      OSynchEntityElement.GET(OSynchSetupDetail."Synch. Entity Code",OSynchSetupDetail."Element No.");
                                                                      MODIFY;
                                                                      OSynchEntityElement.CALCFIELDS("No. of Dependencies");
                                                                      IF OSynchEntityElement."No. of Dependencies" > 0 THEN
                                                                        IF NOT OSynchSetupMgt.CheckOCollectionAvailability(OSynchEntityElement,"User ID") THEN
                                                                          "Synch. Direction" := xRec."Synch. Direction";
                                                                    UNTIL OSynchSetupDetail.NEXT = 0;
                                                                END;

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Depend. Synch. Entity Code","Synch. Entity Code");
                                                                IF OSynchDependency.FIND('-') THEN
                                                                  REPEAT
                                                                    IF OSynchUserSetup.GET("User ID",OSynchDependency."Synch. Entity Code") THEN
                                                                      IF OSynchSetupDetail.GET(
                                                                           OSynchUserSetup."User ID",
                                                                           OSynchUserSetup."Synch. Entity Code",
                                                                           OSynchDependency."Element No.")
                                                                      THEN
                                                                        IF "Synch. Direction" <> OSynchUserSetup."Synch. Direction" THEN BEGIN
                                                                          CLEAR(RecRef);
                                                                          CLEAR(FldRef);
                                                                          RecRef.GETTABLE(Rec);
                                                                          FldRef := RecRef.FIELD(FIELDNO("Synch. Direction"));
                                                                          ERROR(
                                                                            Text001,
                                                                            FIELDCAPTION("Synch. Direction"),
                                                                            SELECTSTR(OSynchUserSetup."Synch. Direction"::Bidirectional + 1,FldRef.OPTIONSTRING),
                                                                            OSynchDependency."Synch. Entity Code");
                                                                        END;
                                                                  UNTIL OSynchDependency.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Synch. Direction;
                                                              ESP=Direcci¢n sinc.;
                                                              DEA=Synch.-Richtung];
                                                   OptionCaptionML=[ENU=Bidirectional,Microsoft Dynamics NAV to Outlook,Outlook to Microsoft Dynamics NAV;
                                                                    ESP=Bidireccional,Microsoft Dynamics NAV a Outlook,Outlook a Microsoft Dynamics NAV;
                                                                    DEA=Bidirektional,Microsoft Dynamics NAV nach Outlook,Outlook nach Microsoft Dynamics NAV];
                                                   OptionString=Bidirectional,Microsoft Dynamics NAV to Outlook,Outlook to Microsoft Dynamics NAV }
    { 6   ;   ;Last Synch. Time    ;DateTime      ;CaptionML=[ENU=Last Synch. Time;
                                                              ESP=éltima sinc.;
                                                              DEA=Zeitpunkt letzte Synch.] }
    { 7   ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              ESP=GUID registro;
                                                              DEA=Datensatz-GUID];
                                                   Editable=No }
    { 8   ;   ;No. of Elements     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Outlook Synch. Setup Detail" WHERE (User ID=FIELD(User ID),
                                                                                                          Synch. Entity Code=FIELD(Synch. Entity Code),
                                                                                                          Outlook Collection=FILTER(<>'')));
                                                   CaptionML=[ENU=No. of Elements;
                                                              ESP=N§ elementos;
                                                              DEA=Anzahl Elemente];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;User ID,Synch. Entity Code              ;Clustered=Yes }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1004 : Record 5300;
      OSynchEntityElement@1002 : Record 5301;
      OSynchFilter@1000 : Record 5303;
      OSynchUserSetup@1005 : Record 5305;
      OSynchSetupDetail@1003 : Record 5310;
      UserMgt@1001 : Codeunit 418;
      OSynchSetupMgt@1008 : Codeunit 5300;
      Text001@1007 : TextConst 'ENU=The value of the %1 field must either be %2 or match the synchronization direction of the %3 entity because these entities are dependant.;ESP=El valor del campo %1 debe ser %2 o coincidir con la direcci¢n de sincronizaci¢n del objeto %3 porque estos objetos son dependientes.;DEA=Der Wert des %1-Felds muss entweder %2 sein oder mit der Synchronisierungsrichtung der %3-Einheit bereinstimmen, weil diese Einheiten voneinander abhngen.';
      Text002@1009 : TextConst 'ENU=The %1 entity is used for the synchronization of one or more Outlook item collections.\If you delete this entity, all collections will be removed from synchronization. Do you want to proceed?;ESP=El objeto %1 se utiliza para la sincronizaci¢n de una o m s colecciones de elementos de Outlook.\Si elimina este objeto, todas las colecciones se eliminar n de la sincronizaci¢n. ¨Desea continuar?;DEA=Die %1-Einheit wird fr die Synchronisierung mindestens einer Outlook-Eintragssammlung verwendet.\Wenn Sie diese Einheit lschen, wird die Synchronisierung fr alle Sammlungen aufgehoben. Mchten Sie den Vorgang fortsetzen?';

    PROCEDURE CheckSetupDetail@2(OSynchUserSetup1@1002 : Record 5305) : Boolean;
    VAR
      OSynchDependency@1003 : Record 5311;
    BEGIN
      OSynchSetupDetail.RESET;
      OSynchSetupDetail.SETRANGE("User ID",OSynchUserSetup1."User ID");
      IF OSynchSetupDetail.FIND('-') THEN
        REPEAT
          IF OSynchDependency.GET(
               OSynchSetupDetail."Synch. Entity Code",
               OSynchSetupDetail."Element No.",
               OSynchUserSetup1."Synch. Entity Code")
          THEN
            OSynchSetupDetail.MARK(TRUE);
        UNTIL OSynchSetupDetail.NEXT = 0;

      OSynchSetupDetail.MARKEDONLY(TRUE);
      IF OSynchSetupDetail.COUNT > 0 THEN BEGIN
        IF CONFIRM(Text002,FALSE,OSynchUserSetup1."Synch. Entity Code") THEN BEGIN
          OSynchSetupDetail.DELETEALL;
          EXIT(TRUE);
        END;
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}


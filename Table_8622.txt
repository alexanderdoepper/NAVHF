OBJECT Table 8622 Config. Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name;
    OnDelete=VAR
               ConfigRelatedTable@1000 : Record 8625;
             BEGIN
               IF CountWorksheetTableOccurrences("Table ID") = 1 THEN BEGIN
                 ConfigRelatedTable.SETRANGE("Table ID","Table ID");
                 ConfigRelatedTable.DELETEALL(TRUE);
               END;
             END;

    CaptionML=[ENU=Config. Line;
               ESP=L°nea de configuraci¢n;
               DEA=Zeile konfigurieren];
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=Nß l°nea;
                                                              DEA=Zeilennr.] }
    { 2   ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                IF "Line Type" <> "Line Type"::Table THEN
                                                                  TESTFIELD("Table ID",0);
                                                              END;

                                                   CaptionML=[ENU=Line Type;
                                                              ESP=Tipo l°nea;
                                                              DEA=Zeilenart];
                                                   OptionCaptionML=[ENU=Area,Group,Table;
                                                                    ESP=µrea,Grupo,Tabla;
                                                                    DEA=FlÑche,Gruppe,Tabelle];
                                                   OptionString=Area,Group,Table }
    { 3   ;   ;Table ID            ;Integer       ;TableRelation=IF (Line Type=CONST(Table)) Object.ID WHERE (Type=CONST(Table),
                                                                                                              ID=FILTER(..99000999|2000000004|2000000005));
                                                   OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                              BEGIN
                                                                IF CurrFieldNo > 0 THEN
                                                                  TESTFIELD("Line Type","Line Type"::Table);

                                                                IF ("Table ID" <> xRec."Table ID") AND (xRec."Table ID" > 0) THEN
                                                                  TESTFIELD("Dimensions as Columns",FALSE);

                                                                IF ("Table ID" <> xRec."Table ID") AND ("Package Code" <> '') THEN
                                                                  IF CONFIRM(Text003,FALSE) THEN
                                                                    "Package Code" := ''
                                                                  ELSE BEGIN
                                                                    "Table ID" := xRec."Table ID";
                                                                    EXIT;
                                                                  END;

                                                                IF "Table ID" > 0 THEN BEGIN
                                                                  RecRef.OPEN("Table ID");
                                                                  VALIDATE(Name,RecRef.CAPTION);
                                                                  "Page ID" := ConfigMgt.FindPage("Table ID");
                                                                  "Copying Available" := ConfigMgt.TransferContents("Table ID",'',FALSE);
                                                                  GetRelatedTables;
                                                                END ELSE
                                                                  IF xRec."Table ID" > 0 THEN
                                                                    ERROR(Text001);
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigValidateMgt@1000 : Codeunit 8617;
                                                            BEGIN
                                                              TESTFIELD("Line Type","Line Type"::Table);
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              ESP=Id. tabla;
                                                              DEA=Tabellen-ID] }
    { 4   ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              ESP=Nombre;
                                                              DEA=Name] }
    { 5   ;   ;Company Filter      ;Text30        ;FieldClass=FlowFilter;
                                                   TableRelation=Company;
                                                   CaptionML=[ENU=Company Filter;
                                                              ESP=Filtro empresa;
                                                              DEA=Mandantenfilter] }
    { 6   ;   ;Company Filter (Source Table);Text30;
                                                   FieldClass=FlowFilter;
                                                   TableRelation=Company;
                                                   CaptionML=[ENU=Company Filter (Source Table);
                                                              ESP=Filtro empresa (tabla origen);
                                                              DEA=Mandantenfilter (Ursp.Tabelle)] }
    { 8   ;   ;No. of Records      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Table Information"."No. of Records" WHERE (Company Name=FIELD(Company Filter),
                                                                                                               Table No.=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Records;
                                                              ESP=Nß registros;
                                                              DEA=Anzahl DatensÑtze];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;No. of Records (Source Table);Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Table Information"."No. of Records" WHERE (Company Name=FIELD("Company Filter (Source Table)"),
                                                                                                               Table No.=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Records (Source Table);
                                                              ESP=Nß registros (tabla origen);
                                                              DEA=Anzahl der DatensÑtze (Urspr. Tabelle)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;Licensed Table      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("License Permission" WHERE (Object Type=CONST(TableData),
                                                                                                 Object Number=FIELD(Table ID),
                                                                                                 Read Permission=CONST(Yes),
                                                                                                 Insert Permission=CONST(Yes),
                                                                                                 Modify Permission=CONST(Yes),
                                                                                                 Delete Permission=CONST(Yes)));
                                                   CaptionML=[ENU=Licensed Table;
                                                              ESP=Tabla con licencia;
                                                              DEA=Lizenzierte Tabelle];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 11  ;   ;Promoted Table      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Promoted Table" THEN
                                                                  TESTFIELD("Line Type","Line Type"::Table);
                                                              END;

                                                   CaptionML=[ENU=Promoted Table;
                                                              ESP=Tabla promocionada;
                                                              DEA=Heraufgestufte Tabelle] }
    { 12  ;   ;Dimensions as Columns;Boolean      ;OnValidate=VAR
                                                                ConfigPackageTable@1000 : Record 8613;
                                                              BEGIN
                                                                TESTFIELD("Line Type","Line Type"::Table);
                                                                TESTFIELD("Table ID");
                                                                TESTFIELD("Package Code");
                                                                ConfigPackageTable.GET("Package Code","Table ID");
                                                                ConfigPackageTable.VALIDATE("Dimensions as Columns","Dimensions as Columns");
                                                                ConfigPackageTable.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Dimensions as Columns;
                                                              ESP=Dimensiones como columnas;
                                                              DEA=Dimensionen als Spalten] }
    { 13  ;   ;Copying Available   ;Boolean       ;CaptionML=[ENU=Copying Available;
                                                              ESP=Copia disponible;
                                                              DEA=Kopieren verfÅgbar];
                                                   Editable=No }
    { 14  ;   ;Page ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Page));
                                                   OnLookup=VAR
                                                              ConfigValidateMgt@1000 : Codeunit 8617;
                                                            BEGIN
                                                              ConfigValidateMgt.LookupPage("Page ID");
                                                              VALIDATE("Page ID");
                                                            END;

                                                   CaptionML=[ENU=Page ID;
                                                              ESP=Id. p†gina;
                                                              DEA=Seiten-ID] }
    { 15  ;   ;Page Name           ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Page),
                                                                                                             Object ID=FIELD(Page ID)));
                                                   CaptionML=[ENU=Page Name;
                                                              ESP=Nombre p†gina;
                                                              DEA=Seitenname];
                                                   Editable=No }
    { 18  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" <> 0D) AND (xRec."Starting Date" <> 0D) AND ("Ending Date" <> 0D) THEN
                                                                  "Ending Date" := "Ending Date" + ("Starting Date" - xRec."Starting Date");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial;
                                                              DEA=Startdatum] }
    { 19  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final;
                                                              DEA=Enddatum] }
    { 20  ;   ;Responsible ID      ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit 418;
                                                              BEGIN
                                                                UserMgt.ValidateUserID("Responsible ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Responsible ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Responsible ID;
                                                              ESP=Id. responsable;
                                                              DEA=ID des Verantwortlichen] }
    { 21  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESP=Estado;
                                                              DEA=Status];
                                                   OptionCaptionML=[ENU=" ,In Progress,Completed,Ignored,Blocked";
                                                                    ESP=" ,En curso,Completado,Ignorado,Bloqueado";
                                                                    DEA=" ,In Bearbeitung,Erledigt,Ignoriert,Blockiert"];
                                                   OptionString=[ ,In Progress,Completed,Ignored,Blocked] }
    { 25  ;   ;Vertical Sorting    ;Integer       ;CaptionML=[ENU=Vertical Sorting;
                                                              ESP=Ordenaci¢n vertical;
                                                              DEA=Vertikale Sortierung] }
    { 26  ;   ;Data Origin         ;Text50        ;CaptionML=[ENU=Data Origin;
                                                              ESP=Origen de datos;
                                                              DEA=Datenursprung] }
    { 28  ;   ;Reference           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Reference;
                                                              ESP=Referencia;
                                                              DEA=Referenz] }
    { 30  ;   ;Licensed Page       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("License Permission" WHERE (Object Type=CONST(Page),
                                                                                                 Object Number=FIELD(Page ID),
                                                                                                 Execute Permission=CONST(Yes)));
                                                   CaptionML=[ENU=Licensed Page;
                                                              ESP=P†gina con licencia;
                                                              DEA=Lizenzierte Seite];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 31  ;   ;No. of Question Groups;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Question Area" WHERE (Table ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Question Groups;
                                                              ESP=Nß grupos de preguntas;
                                                              DEA=Anzahl der Fragengruppen];
                                                   Editable=No }
    { 32  ;   ;Parent Line No.     ;Integer       ;CaptionML=[ENU=Parent Line No.;
                                                              ESP=Nß l°nea maestro;
                                                              DEA=öbergeordnete Zeilennr.] }
    { 36  ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=[ENU=Package Code;
                                                              ESP=C¢digo de paquete;
                                                              DEA=Paketcode] }
    { 37  ;   ;Package Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Config. Package"."Package Name" WHERE (Code=FIELD(Package Code)));
                                                   CaptionML=[ENU=Package Name;
                                                              ESP=Nombre de paquete;
                                                              DEA=Paketname];
                                                   Editable=No }
    { 38  ;   ;Package Exists      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Package" WHERE (Code=FIELD(Package Code)));
                                                   CaptionML=[ENU=Package Exists;
                                                              ESP=El paquete existe;
                                                              DEA=Paket vorhanden];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Line No.                                ;Clustered=Yes }
    {    ;Line Type,Status,Promoted Table          }
    {    ;Table ID                                 }
    {    ;Vertical Sorting                         }
    {    ;Line Type,Parent Line No.                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfigMgt@1001 : Codeunit 8616;
      Text001@1000 : TextConst 'ENU=Delete the line instead.;ESP=Elimine la l°nea.;DEA=Lîschen Sie die Zeile stattdessen.';
      Text002@1002 : TextConst 'ENU=The status %1 is not supported.;ESP=El estado %1 no es compatible.;DEA=Der Status %1 ist nicht unterstÅtzt.';
      Text003@1003 : TextConst 'ENU=The table you are trying to rename is linked to a package. Do you want to remove the link?;ESP=La tabla cuyo nombre est† intentado cambiar est† vinculada a un paquete. ®Desea quitar este v°nculo?;DEA=Die Tabelle, die Sie umbenennen mîchten, ist mit einem Paket verknÅpft. Mîchten Sie die VerknÅpfung entfernen?';
      Text004@1004 : TextConst 'ENU=You cannot process line for table %1 and package code %2 because it is blocked.;ESP=No se puede procesar la l°nea para la tabla %1 y el c¢digo de paquete %2 porque est† bloqueado.;DEA=Sie kînnen keine Zeile fÅr Tabelle %1 und Paketcode %2 verarbeiten, da diese blockiert ist.';

    PROCEDURE CheckBlocked@4();
    BEGIN
      IF Status = Status::Blocked THEN
        ERROR(Text004,"Table ID","Package Code");
    END;

    PROCEDURE ShowTableData@3();
    BEGIN
      IF ("Line Type" = "Line Type"::Table) AND ("Page ID" <> 0) THEN
        PAGE.RUN("Page ID");
    END;

    PROCEDURE ShowQuestions@10();
    VAR
      ConfigQuestionArea@1000 : Record 8611;
      ConfigQuestionAreaPage@1001 : Page 8611;
    BEGIN
      TESTFIELD("Line Type","Line Type"::Table);
      TESTFIELD("Table ID");

      ConfigQuestionArea.SETRANGE("Table ID","Table ID");
      ConfigQuestionArea.FINDFIRST;

      ConfigQuestionArea.RESET;
      ConfigQuestionArea.FILTERGROUP(2);
      ConfigQuestionArea.SETRANGE("Table ID","Table ID");
      ConfigQuestionArea.FILTERGROUP(0);
      ConfigQuestionAreaPage.SETTABLEVIEW(ConfigQuestionArea);
      ConfigQuestionAreaPage.RUNMODAL;
      CLEAR(ConfigQuestionAreaPage);
    END;

    PROCEDURE GetProgress@1() : Integer;
    VAR
      Total@1001 : Integer;
      TotalStatusWeight@1005 : Decimal;
    BEGIN
      Total := GetNoTables;
      TotalStatusWeight := GetTotalStatusWeight;

      IF Total = 0 THEN
        EXIT(0);

      EXIT(ROUND(100 * TotalStatusWeight / Total,1));
    END;

    PROCEDURE GetNoOfDirectChildrenTables@2() : Integer;
    VAR
      ConfigLine@1000 : Record 8622;
    BEGIN
      WITH ConfigLine DO BEGIN
        RESET;
        SETCURRENTKEY("Line Type");
        SETRANGE("Line Type","Line Type"::Table);
        SETRANGE("Parent Line No.",Rec."Line No.");
        EXIT(COUNT);
      END;
    END;

    PROCEDURE GetDirectChildrenTablesStatusWeight@8() StatusWeight : Decimal;
    VAR
      ConfigLine@1000 : Record 8622;
    BEGIN
      WITH ConfigLine DO BEGIN
        RESET;
        SETCURRENTKEY("Line Type");
        SETRANGE("Line Type","Line Type"::Table);
        SETRANGE("Parent Line No.",Rec."Line No.");
        IF FINDSET THEN
          REPEAT
            StatusWeight += GetLineStatusWeight;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetNoTables@7() Total : Integer;
    VAR
      ConfigLine@1001 : Record 8622;
    BEGIN
      CASE "Line Type" OF
        "Line Type"::Table:
          Total := 0;
        "Line Type"::Group:
          Total := GetNoOfDirectChildrenTables;
        "Line Type"::Area:
          BEGIN
            Total := GetNoOfDirectChildrenTables;

            ConfigLine.SETRANGE("Line Type",ConfigLine."Line Type"::Group);
            ConfigLine.SETRANGE("Parent Line No.","Line No.");
            IF ConfigLine.FINDSET THEN
              REPEAT
                Total += ConfigLine.GetNoOfDirectChildrenTables;
              UNTIL ConfigLine.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE GetTotalStatusWeight@13() Total : Decimal;
    VAR
      ConfigLine@1001 : Record 8622;
    BEGIN
      CASE "Line Type" OF
        "Line Type"::Table:
          Total := 0;
        "Line Type"::Group:
          Total := GetDirectChildrenTablesStatusWeight;
        "Line Type"::Area:
          BEGIN
            Total := GetDirectChildrenTablesStatusWeight;

            ConfigLine.SETRANGE("Line Type",ConfigLine."Line Type"::Group);
            ConfigLine.SETRANGE("Parent Line No.","Line No.");
            IF ConfigLine.FINDSET THEN
              REPEAT
                Total += ConfigLine.GetDirectChildrenTablesStatusWeight;
              UNTIL ConfigLine.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE GetRelatedTables@5();
    VAR
      ConfigRelatedTable@1000 : Record 8625;
      ConfigRelatedField@1002 : Record 8624;
      Field@1001 : Record 2000000041;
    BEGIN
      Field.SETRANGE(TableNo,"Table ID");
      Field.SETRANGE(Class,Field.Class::Normal);
      IF Field.FINDSET THEN
        REPEAT
          IF Field.RelationTableNo <> 0 THEN
            IF NOT ConfigRelatedField.GET("Table ID",Field."No.") THEN BEGIN
              ConfigRelatedField.INIT;
              ConfigRelatedField."Table ID" := "Table ID";
              ConfigRelatedField."Field ID" := Field."No.";
              ConfigRelatedField."Relation Table ID" := Field.RelationTableNo;
              ConfigRelatedField.INSERT;
            END;
        UNTIL Field.NEXT = 0;

      IF ConfigRelatedField.FINDSET THEN
        REPEAT
          IF NOT ConfigRelatedTable.GET(ConfigRelatedField."Table ID",ConfigRelatedField."Relation Table ID") THEN BEGIN
            ConfigRelatedTable."Table ID" := ConfigRelatedField."Table ID";
            ConfigRelatedTable."Relation Table ID" := ConfigRelatedField."Relation Table ID";
            ConfigRelatedTable.INSERT;
          END;
        UNTIL ConfigRelatedField.NEXT = 0;
    END;

    PROCEDURE GetLineStatusWeight@6() : Decimal;
    BEGIN
      CASE Status OF
        Status::" ":
          EXIT(0);
        Status::Completed,Status::Ignored:
          EXIT(1);
        Status::"In Progress",Status::Blocked:
          EXIT(0.5);
        ELSE
          ERROR(Text002,Status);
      END;
    END;

    LOCAL PROCEDURE CountWorksheetTableOccurrences@9(TableID@1000 : Integer) : Integer;
    VAR
      ConfigLine@1001 : Record 8622;
    BEGIN
      ConfigLine.SETRANGE("Table ID",TableID);
      EXIT(ConfigLine.COUNT);
    END;

    BEGIN
    END.
  }
}


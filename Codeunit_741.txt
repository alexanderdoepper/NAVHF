OBJECT Codeunit 741 VAT Report Release/Reopen
{
  OBJECT-PROPERTIES
  {
    Date=05.04.13;
    Time=12:00:00;
    Version List=NAVW17.00.00.34587,NAVDE7.00.00.34587;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE Release@1(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportHeader.CheckIfCanBeReleased(VATReportHeader);
      CODEUNIT.RUN(CODEUNIT::"VAT Report Validate",VATReportHeader);

      VATReportHeader.Status := VATReportHeader.Status::Released;
      VATReportHeader.MODIFY;
    END;

    PROCEDURE Reopen@2(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportHeader.CheckIfCanBeReopened(VATReportHeader);

      VATReportHeader.Status := VATReportHeader.Status::Open;
      VATReportHeader.MODIFY;
    END;

    PROCEDURE Submit@5(VAR VATReportHeader@1000 : Record 740);
    VAR
      VATReportLine@1140000 : Record 741;
    BEGIN
      VATReportHeader.CheckIfCanBeSubmitted;

      VATReportHeader.Status := VATReportHeader.Status::Submitted;
      VATReportHeader.MODIFY;

      UpdateLinesToCorrect(VATReportHeader."No.");

      WITH VATReportLine DO BEGIN
        SETRANGE("VAT Report No.",VATReportHeader."No.");
        SETFILTER("Line Type",'%1|%2',"Line Type"::New,"Line Type"::Correction);
        MODIFYALL("Able to Correct Line",TRUE,FALSE);
      END;
    END;

    LOCAL PROCEDURE UpdateLinesToCorrect@1140000(VATReportNo@1140001 : Code[20]);
    VAR
      VATReportHeader@1140002 : Record 740;
      VATReportLine@1140000 : Record 741;
      CorrVATReportLine@1140003 : Record 741;
    BEGIN
      VATReportHeader.GET(VATReportNo);

      IF VATReportHeader."Original Report No." <> '' THEN BEGIN
        CorrVATReportLine.SETRANGE("VAT Report No.",VATReportNo);
        CorrVATReportLine.SETRANGE("Line Type",CorrVATReportLine."Line Type"::Correction);
        IF CorrVATReportLine.FINDSET THEN
          REPEAT
            VATReportLine.RESET;
            VATReportLine.SETRANGE("VAT Report to Correct",VATReportHeader."Original Report No.");
            VATReportLine.SETRANGE("Related Line No.",CorrVATReportLine."Related Line No.");
            VATReportLine.SETRANGE("Able to Correct Line",TRUE);
            VATReportLine.MODIFYALL("Able to Correct Line",FALSE,FALSE);

            VATReportLine.RESET;
            VATReportLine.SETRANGE("VAT Report No.",VATReportHeader."Original Report No.");
            VATReportLine.SETRANGE("Line No.",CorrVATReportLine."Related Line No.");
            VATReportLine.SETRANGE("Able to Correct Line",TRUE);
            VATReportLine.MODIFYALL("Able to Correct Line",FALSE,FALSE);
          UNTIL CorrVATReportLine.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}


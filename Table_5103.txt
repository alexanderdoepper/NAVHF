OBJECT Table 5103 Interaction Tmpl. Language
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               InteractionTemplate@1000 : Record 5064;
             BEGIN
               InteractionTemplate.GET("Interaction Template Code");
               IF xRec."Language Code" <> "Language Code" THEN
                 IF InteractionTemplate."Language Code (Default)" = xRec."Language Code" THEN BEGIN
                   InteractionTemplate."Language Code (Default)" := "Language Code";
                   InteractionTemplate.MODIFY;
                 END;
             END;

    CaptionML=[ENU=Interaction Tmpl. Language;
               ESP=Idioma plant. interacci¢n;
               DEA=Aktivit„tenvorlagensprache];
    LookupPageID=Page5154;
  }
  FIELDS
  {
    { 1   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              ESP=C¢d. plantilla interacci¢n;
                                                              DEA=Aktivit„tenvorlagencode];
                                                   Editable=No }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESP=C¢d. idioma;
                                                              DEA=Sprachcode] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n;
                                                              DEA=Beschreibung] }
    { 4   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              ESP=N§ anexo;
                                                              DEA=Dateianhangsnr.] }
  }
  KEYS
  {
    {    ;Interaction Template Code,Language Code ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AttachmentRecord@1003 : Record 5062;
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;ESP=Ha cancelado el proceso crear.;DEA=Sie haben den Erstellungsprozess abgebrochen.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;ESP=¨Sustituir el anexo actual?;DEA=M”chten Sie den vorhandenen Dateianhang ersetzen?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;ESP=Ha cancelado el proceso importar.;DEA=Sie haben den Importvorgang abgebrochen.';
      Text005@1004 : TextConst 'ENU=Export Attachment;ESP=Exportar anexo;DEA=Dateianhang exportieren';

    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 5062;
      InteractTmplLanguage@1004 : Record 5103;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      NewAttachNo := WordManagement.CreateWordAttachment("Interaction Template Code" + ' ' + Description);
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        IF InteractTmplLanguage.GET("Interaction Template Code","Language Code") THEN
          MODIFY
        ELSE
          INSERT;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("Interaction Template Code" + ' ' + Description,FALSE);
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      InteractTmplLanguage@1000 : Record 5103;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      InteractTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,InteractTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(InteractTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF Attachment.ImportAttachment('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      TempBlob@1002 : Record 99008535;
      MarketingSetup@1004 : Record 5079;
      FileMgt@1003 : Codeunit 419;
      FileName@1001 : Text[1024];
      FileFilter@1005 : Text[250];
      ExportToFile@1006 : Text[100];
    BEGIN
      MarketingSetup.GET;
      ExportToFile := '';
      IF AttachmentRecord.GET("Attachment No.") THEN
        WITH AttachmentRecord DO BEGIN
          IF "Storage Type" = "Storage Type"::Embedded THEN BEGIN
            CALCFIELDS(Attachment);
            IF Attachment.HASVALUE THEN BEGIN
              FileName := "Interaction Template Code" + '.' + "File Extension";
              TempBlob.Blob := Attachment;
              FileMgt.BLOBExport(TempBlob,FileName,TRUE);
            END;
          END ELSE BEGIN
            IF "Storage Type" = "Storage Type"::"Disk File" THEN BEGIN
              IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
                MarketingSetup.TESTFIELD("Attachment Storage Location");
              FileFilter := UPPERCASE("File Extension");
              DOWNLOAD(ConstDiskFileName,Text005,'',FileFilter,ExportToFile);
            END;
          END;
        END;
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    PROCEDURE ConstDiskFileName@4() DiskFileName@1000 : Text[1024];
    BEGIN
      DiskFileName := AttachmentRecord."Storage Pointer" + '\' + FORMAT(AttachmentRecord."No.") + '.';
    END;

    BEGIN
    END.
  }
}


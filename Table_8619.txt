OBJECT Table 8619 Config. Template Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ConfigTemplateHeader@1000 : Record 8618;
             BEGIN
               IF ConfigTemplateHeader.GET("Data Template Code") THEN
                 "Table ID" := ConfigTemplateHeader."Table ID";
             END;

    CaptionML=[ENU=Config. Template Line;
               ESP=L°nea de plantilla de configuraci¢n;
               DEA=Vorlagenzeile konfigurieren];
  }
  FIELDS
  {
    { 1   ;   ;Data Template Code  ;Code10        ;TableRelation="Config. Template Header";
                                                   CaptionML=[ENU=Data Template Code;
                                                              ESP=C¢digo plantilla datos;
                                                              DEA=Datenvorlagencode];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=Nß l°nea;
                                                              DEA=Zeilennr.];
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;InitValue=Field;
                                                   OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Field:
                                                                    CLEAR("Template Code");
                                                                  Type::Template:
                                                                    BEGIN
                                                                      CLEAR("Field Name");
                                                                      CLEAR("Field ID");
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo;
                                                              DEA=Art];
                                                   OptionCaptionML=[ENU=Field,Template;
                                                                    ESP=Campo,Plantilla;
                                                                    DEA=Feld,Vorlage];
                                                   OptionString=Field,Template }
    { 4   ;   ;Field ID            ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(Table ID),
                                                                                                         Class=CONST(Normal));
                                                   CaptionML=[ENU=Field ID;
                                                              ESP=Id. campo;
                                                              DEA=Feld-ID] }
    { 5   ;   ;Field Name          ;Text30        ;FieldClass=Normal;
                                                   OnValidate=VAR
                                                                ConfigTemplateLine@1000 : Record 8619;
                                                                ConfigTemplateMgt@1001 : Codeunit 8612;
                                                              BEGIN
                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Field Name","Field Name");
                                                                IF ConfigTemplateLine.FINDFIRST THEN
                                                                  ERROR(STRSUBSTNO(Text004,"Field Name"));

                                                                ConfigTemplateMgt.TestHierarchy(Rec);
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigTemplateHeader@1000 : Record 8618;
                                                              Field@1002 : Record 2000000041;
                                                              FieldList@1001 : Page 6218;
                                                            BEGIN
                                                              IF Type = Type::Template THEN
                                                                EXIT;

                                                              ConfigTemplateHeader.GET("Data Template Code");

                                                              IF ConfigTemplateHeader."Table ID" = 0 THEN
                                                                EXIT;

                                                              CLEAR(FieldList);
                                                              Field.SETRANGE(TableNo,ConfigTemplateHeader."Table ID");
                                                              FieldList.SETTABLEVIEW(Field);
                                                              FieldList.LOOKUPMODE := TRUE;
                                                              IF FieldList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                FieldList.GETRECORD(Field);
                                                                "Table ID" := Field.TableNo;
                                                                VALIDATE("Field ID",Field."No.");
                                                                VALIDATE("Field Name",Field.FieldName);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo;
                                                              DEA=Feldname];
                                                   Editable=No }
    { 6   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              ESP=Id. tabla;
                                                              DEA=Tabellen-ID] }
    { 7   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=FILTER(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla;
                                                              DEA=Tabellenname] }
    { 8   ;   ;Template Code       ;Code10        ;TableRelation="Config. Template Header";
                                                   OnValidate=VAR
                                                                ConfigTemplateHeader@1000 : Record 8618;
                                                                ConfigTemplateHeader2@1001 : Record 8618;
                                                                ConfigTemplateLine@1003 : Record 8619;
                                                                ConfigTemplateMgt@1002 : Codeunit 8612;
                                                              BEGIN
                                                                IF Type = Type::Field THEN
                                                                  ERROR(Text005);

                                                                IF "Template Code" = "Data Template Code" THEN
                                                                  ERROR(Text000);

                                                                IF ConfigTemplateHeader.GET("Template Code") THEN
                                                                  IF ConfigTemplateHeader2.GET("Data Template Code") THEN
                                                                    IF ConfigTemplateHeader."Table ID" <> ConfigTemplateHeader2."Table ID" THEN
                                                                      ERROR(STRSUBSTNO(Text002,ConfigTemplateHeader.Code,ConfigTemplateHeader2."Table ID"));

                                                                ConfigTemplateMgt.TestHierarchy(Rec);

                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Template Code","Template Code");
                                                                IF ConfigTemplateLine.FINDFIRST THEN
                                                                  ERROR(STRSUBSTNO(Text003,"Template Code"));
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigTemplateHeader@1002 : Record 8618;
                                                              ConfigTemplateList@1000 : Page 8620;
                                                            BEGIN
                                                              IF Type = Type::Field THEN
                                                                EXIT;

                                                              ConfigTemplateHeader.GET("Data Template Code");
                                                              IF ConfigTemplateHeader."Table ID" = 0 THEN
                                                                EXIT;

                                                              ConfigTemplateHeader.SETRANGE("Table ID",ConfigTemplateHeader."Table ID");
                                                              ConfigTemplateList.SETTABLEVIEW(ConfigTemplateHeader);
                                                              ConfigTemplateList.LOOKUPMODE := TRUE;
                                                              ConfigTemplateList.EDITABLE := FALSE;
                                                              IF ConfigTemplateList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ConfigTemplateList.GETRECORD(ConfigTemplateHeader);
                                                                IF ConfigTemplateHeader.Code = "Data Template Code" THEN
                                                                  ERROR(Text000);
                                                                "Template Description" := ConfigTemplateHeader.Description;
                                                                VALIDATE("Template Code",ConfigTemplateHeader.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Template Code;
                                                              ESP=C¢digo plantilla;
                                                              DEA=Vorlagencode] }
    { 9   ;   ;Template Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Config. Template Header".Description WHERE (Code=FIELD(Data Template Code)));
                                                   CaptionML=[ENU=Template Description;
                                                              ESP=Descripci¢n plantilla;
                                                              DEA=Vorlagenbeschreibung];
                                                   Editable=No }
    { 10  ;   ;Mandatory           ;Boolean       ;CaptionML=[ENU=Mandatory;
                                                              ESP=Obligatoria;
                                                              DEA=Notwendig] }
    { 11  ;   ;Reference           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Reference;
                                                              ESP=Referencia;
                                                              DEA=Referenz] }
    { 12  ;   ;Default Value       ;Text250       ;OnValidate=VAR
                                                                TempConfigPackageField@1005 : TEMPORARY Record 8616;
                                                                ConfigPackageManagement@1004 : Codeunit 8611;
                                                                ConfigValidateMgt@1003 : Codeunit 8617;
                                                                RecRef@1002 : RecordRef;
                                                                FieldRef@1001 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                IF ("Field ID" <> 0) AND ("Default Value" <> '') THEN BEGIN
                                                                  RecRef.OPEN("Table ID",TRUE);
                                                                  FieldRef := RecRef.FIELD("Field ID");
                                                                  ValidationError := ConfigValidateMgt.EvaluateValue(FieldRef,"Default Value",FALSE);
                                                                  IF ValidationError <> '' THEN
                                                                    ERROR(ValidationError);

                                                                  "Default Value" := FORMAT(FieldRef.VALUE);

                                                                  IF NOT "Skip Relation Check" THEN BEGIN
                                                                    ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
                                                                    ValidationError := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);

                                                                    IF ValidationError <> '' THEN
                                                                      ERROR(ValidationError);
                                                                  END;
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Default Value;
                                                              ESP=Valor predeterminado;
                                                              DEA=Standardwert] }
    { 13  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              ESP=T°tulo tabla;
                                                              DEA=Tabellencaption] }
    { 14  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field ID)));
                                                   CaptionML=[ENU=Field Caption;
                                                              ESP=T°tulo campo;
                                                              DEA=Feldbezeichnung];
                                                   Editable=No }
    { 15  ;   ;Skip Relation Check ;Boolean       ;CaptionML=[ENU=Skip Relation Check;
                                                              ESP=Omitir comprobaci¢n de relaci¢n;
                                                              DEA=BeziehungsprÅfung Åberspringen] }
  }
  KEYS
  {
    {    ;Data Template Code,Line No.             ;Clustered=Yes }
    {    ;Data Template Code,Type                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A template cannot relate to itself. Specify a different template.;ESP=Una plantilla no se puede relacionar consigo misma. Especifique otra plantilla distinta.;DEA=Eine Vorlage kann nicht mit sich selbst verknÅpft sein. Geben Sie eine andere Vorlage an.';
      Text002@1002 : TextConst 'ENU=The template %1 does not relate to table %2.;ESP=La plantilla %1 no est† relacionada con la tabla %2.;DEA=Die Vorlage %1 bezieht sich nicht auf Tabelle %2.';
      Text003@1003 : TextConst 'ENU=The template %1 is already in this hierarchy.;ESP=La plantilla %1 ya est† incluida en esta jerarqu°a.;DEA=Die Vorlage %1 ist bereits in dieser Hierarchie vorhanden.';
      Text004@1004 : TextConst 'ENU=Field %1 is already in the template.;ESP=El campo %1 ya est† en esta plantilla.;DEA=Feld %1 ist bereits in der Vorlage vorhanden.';
      Text005@1005 : TextConst 'ENU=The template line cannot be edited if type is Field.;ESP=La l°nea de la plantilla no se puede editar si el tipo es Campo.;DEA=Die Vorlagenzeile kann im Zeilentyp ''Feld'' nicht bearbeitet werden.';

    BEGIN
    END.
  }
}


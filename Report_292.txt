OBJECT Report 292 Copy Sales Document
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00,NAVDACH7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Sales Document;
               ESP=Copiar doc. venta;
               DEA=Verkaufsbeleg kopieren];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  SalesSetup.GET;
                  CopyDocMgt.SetProperties(
                    IncludeHeader,RecalculateLines,FALSE,FALSE,FALSE,SalesSetup."Exact Cost Reversing Mandatory",FALSE);
                  CopyDocMgt.SetArchDocVal(DocNoOccurrence,DocVersionNo);
                  CopyDocMgt.CopySalesDoc(DocType,DocNo,SalesHeader)
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DocNo <> '' THEN BEGIN
                     CASE DocType OF
                       DocType::Quote:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Quote,DocNo) THEN;
                       DocType::"Blanket Order":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Blanket Order",DocNo) THEN;
                       DocType::Order:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Order,DocNo) THEN;
                       DocType::Invoice:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Invoice,DocNo) THEN;
                       DocType::"Return Order":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Return Order",DocNo) THEN;
                       DocType::"Credit Memo":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Credit Memo",DocNo) THEN;
                       DocType::"Posted Shipment":
                         IF FromSalesShptHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesShptHeader);
                       DocType::"Posted Invoice":
                         IF FromSalesInvHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesInvHeader);
                       DocType::"Posted Return Receipt":
                         IF FromReturnRcptHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromReturnRcptHeader);
                       DocType::"Posted Credit Memo":
                         IF FromSalesCrMemoHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesCrMemoHeader);
                       DocType::"Arch. Order":
                         IF FromArchSalesHeader.GET(FromArchSalesHeader."Document Type"::Order,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromArchSalesHeader);
                       DocType::"Arch. Quote":
                         IF FromArchSalesHeader.GET(FromArchSalesHeader."Document Type"::Quote,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromArchSalesHeader);
                       DocType::"Arch. Blanket Order":
                         IF FromArchSalesHeader.GET(FromArchSalesHeader."Document Type"::"Blanket Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromArchSalesHeader);
                       DocType::"Arch. Return Order":
                         IF FromArchSalesHeader.GET(FromArchSalesHeader."Document Type"::"Return Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromArchSalesHeader);
                     END;
                     IF FromSalesHeader."No." = '' THEN
                       DocNo := '';
                   END;
                   ValidateDocNo;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 3   ;2   ;Field     ;
                  Name=DocumentType;
                  CaptionML=[ENU=Document Type;
                             ESP=Tipo documento;
                             DEA=Belegart];
                  OptionCaptionML=[ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order;
                                   ESP=Oferta,Pedido abierto,Pedido,Factura,Devoluci¢n,Abono,Albar†n venta regis.,Factura regis.,Recep. dev. regis.,Abono regis.;
                                   DEA=Angebot,Rahmenauftrag,Auftrag,Rechnung,Reklamation,Gutschrift,Geb. Lieferung,Geb. Rechnung,Geb. RÅcksendung,Geb. Gutschrift,Arch. Angebot,Arch. Auftrag,Arch. Rahmenauftrag,Arch. Reklamation];
                  SourceExpr=DocType;
                  OnValidate=BEGIN
                               DocNo := '';
                               ValidateDocNo;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             ESP=Nß documento;
                             DEA=Belegnr.];
                  SourceExpr=DocNo;
                  OnValidate=BEGIN
                               ValidateDocNo;
                             END;

                  OnLookup=BEGIN
                             LookupDocNo;
                           END;
                            }

      { 1140000;2;Field     ;
                  CaptionML=[ENU=Doc. No. Occurrence;
                             DEA=Belegnr.-HÑufigkeit];
                  BlankZero=Yes;
                  SourceExpr=DocNoOccurrence;
                  Editable=FALSE }

      { 1140002;2;Field     ;
                  CaptionML=[ENU=Version No.;
                             DEA=Versionsnr.];
                  BlankZero=Yes;
                  SourceExpr=DocVersionNo;
                  Editable=FALSE }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Sell-to Customer No.;
                             ESP=Venta a-Nß cliente;
                             DEA=Verk. an Deb.-Nr.];
                  SourceExpr=FromSalesHeader."Sell-to Customer No.";
                  Editable=FALSE }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Sell-to Customer Name;
                             ESP=Venta a-Nombre;
                             DEA=Verk. an Name];
                  SourceExpr=FromSalesHeader."Sell-to Customer Name";
                  Editable=FALSE }

      { 1   ;2   ;Field     ;
                  Name=IncludeHeader_Options;
                  CaptionML=[ENU=Include Header;
                             ESP=Incluir cabecera;
                             DEA=Inklusive Kopf];
                  SourceExpr=IncludeHeader;
                  OnValidate=BEGIN
                               ValidateIncludeHeader;
                             END;
                              }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Recalculate Lines;
                             ESP=Recalcular l°neas;
                             DEA=Zeilen neu berechnen];
                  SourceExpr=RecalculateLines;
                  OnValidate=BEGIN
                               IF (DocType = DocType::"Posted Shipment") OR (DocType = DocType::"Posted Return Receipt") THEN
                                 RecalculateLines := TRUE;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesHeader@1007 : Record 36;
      FromSalesHeader@1010 : Record 36;
      FromSalesShptHeader@1012 : Record 110;
      FromSalesInvHeader@1014 : Record 112;
      FromReturnRcptHeader@1016 : Record 6660;
      FromSalesCrMemoHeader@1018 : Record 114;
      SalesSetup@1003 : Record 311;
      CopyDocMgt@1008 : Codeunit 6620;
      DocType@1026 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order';
      DocNo@1027 : Code[20];
      IncludeHeader@1028 : Boolean;
      RecalculateLines@1029 : Boolean;
      Text000@1000 : TextConst 'ENU=The price information may not be reversed correctly, if you copy a %1. If possible copy a %2 instead or use %3 functionality.;ESP=Es posible que la informaci¢n sobre el precio no se revierta correctamente si copia un documento de %1. Si es posible, copie un documento de %2 en su lugar o utilice la funcionalidad %3.;DEA=Wenn Sie %1 kopieren, werden die Preisinformationen mîglicherweise nicht ordnungsgemÑ· storniert. Kopieren Sie stattdessen mîglichst %2, oder verwenden Sie die %3-FunktionalitÑt.';
      Text001@1001 : TextConst 'ENU=Undo Shipment;ESP=Deshacer env°o;DEA=Warenausgang stornieren';
      Text002@1002 : TextConst 'ENU=Undo Return Receipt;ESP=Deshacer recep. devoluc.;DEA=RÅcksendung stornieren';
      Text003@1004 : TextConst 'ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo;ESP=Oferta,Pedido abierto,Pedido,Factura,Devoluci¢n,Abono,Albar†n venta regis.,Factura regis.,Recep. dev. regis.,Abono regis.;DEA=Angebot,Rahmenauftrag,Auftrag,Rechnung,Reklamation,Gutschrift,Geb. Lieferung,Geb. Rechnung,Geb. RÅcksendung,Geb. Gutschrift';
      FromArchSalesHeader@1140000 : Record 5107;
      DocNoOccurrence@1140001 : Integer;
      DocVersionNo@1140002 : Integer;

    PROCEDURE SetSalesHeader@2(VAR NewSalesHeader@1000 : Record 36);
    BEGIN
      NewSalesHeader.TESTFIELD("No.");
      SalesHeader := NewSalesHeader;
    END;

    LOCAL PROCEDURE ValidateDocNo@10();
    VAR
      DocType2@1000 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo';
    BEGIN
      IF DocNo = '' THEN BEGIN
        FromSalesHeader.INIT;
        DocNoOccurrence := 0;
        DocVersionNo := 0;
      END ELSE
        IF FromSalesHeader."No." = '' THEN BEGIN
          FromSalesHeader.INIT;
          CASE DocType OF
            DocType::Quote,
            DocType::"Blanket Order",
            DocType::Order,
            DocType::Invoice,
            DocType::"Return Order",
            DocType::"Credit Memo":
              FromSalesHeader.GET(CopyDocMgt.SalesHeaderDocType(DocType),DocNo);
            DocType::"Posted Shipment":
              BEGIN
                FromSalesShptHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesShptHeader);
                IF SalesHeader."Document Type" IN
                   [SalesHeader."Document Type"::"Return Order",SalesHeader."Document Type"::"Credit Memo"]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Invoice";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text001);
                END;
              END;
            DocType::"Posted Invoice":
              BEGIN
                FromSalesInvHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesInvHeader);
              END;
            DocType::"Posted Return Receipt":
              BEGIN
                FromReturnRcptHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromReturnRcptHeader);
                IF SalesHeader."Document Type" IN
                   [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Credit Memo";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text002);
                END;
              END;
            DocType::"Posted Credit Memo":
              BEGIN
                FromSalesCrMemoHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesCrMemoHeader);
              END;
            DocType::"Arch. Quote",DocType::"Arch. Order",DocType::"Arch. Blanket Order",DocType::"Arch. Return Order":
              BEGIN
                IF NOT FromArchSalesHeader.GET(
                         CopyDocMgt.ArchSalesHeaderDocType(DocType),DocNo,DocNoOccurrence,DocVersionNo)
                THEN BEGIN
                  FromArchSalesHeader.SETRANGE("No.",DocNo);
                  IF FromArchSalesHeader.FINDLAST THEN BEGIN
                    DocNoOccurrence := FromArchSalesHeader."Doc. No. Occurrence";
                    DocVersionNo := FromArchSalesHeader."Version No.";
                  END;
                END;
                FromSalesHeader.TRANSFERFIELDS(FromArchSalesHeader);
              END;
          END;
        END;
      FromSalesHeader."No." := '';

      IncludeHeader :=
        (DocType IN [DocType::"Posted Invoice",DocType::"Posted Credit Memo"]) AND
        ((DocType = DocType::"Posted Credit Memo") <>
         (SalesHeader."Document Type" IN
          [SalesHeader."Document Type"::"Return Order",SalesHeader."Document Type"::"Credit Memo"])) AND
        (SalesHeader."Bill-to Customer No." IN [FromSalesHeader."Bill-to Customer No.",'']);
      ValidateIncludeHeader;
    END;

    LOCAL PROCEDURE LookupDocNo@3();
    BEGIN
      PageLookupDocNo;
      EXIT;
    END;

    LOCAL PROCEDURE ValidateIncludeHeader@4();
    BEGIN
      RecalculateLines :=
        (DocType IN [DocType::"Posted Shipment",DocType::"Posted Return Receipt"]) OR NOT IncludeHeader;
    END;

    PROCEDURE InitializeRequest@1(NewDocType@1001 : Option;NewDocNo@1000 : Code[20];NewIncludeHeader@1002 : Boolean;NewRecalcLines@1003 : Boolean);
    BEGIN
      DocType := NewDocType;
      DocNo := NewDocNo;
      IncludeHeader := NewIncludeHeader;
      RecalculateLines := NewRecalcLines;
    END;

    LOCAL PROCEDURE PageLookupDocNo@19053290();
    BEGIN
      CASE DocType OF
        DocType::Quote,
        DocType::"Blanket Order",
        DocType::Order,
        DocType::Invoice,
        DocType::"Return Order",
        DocType::"Credit Memo":
          BEGIN
            FromSalesHeader.FILTERGROUP := 0;
            FromSalesHeader.SETRANGE("Document Type",CopyDocMgt.SalesHeaderDocType(DocType));
            IF SalesHeader."Document Type" = CopyDocMgt.SalesHeaderDocType(DocType) THEN
              FromSalesHeader.SETFILTER("No.",'<>%1',SalesHeader."No.");
            FromSalesHeader.FILTERGROUP := 2;
            FromSalesHeader."Document Type" := CopyDocMgt.SalesHeaderDocType(DocType);
            FromSalesHeader."No." := DocNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromSalesHeader.SETCURRENTKEY("Document Type","Sell-to Customer No.") THEN BEGIN
                FromSalesHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromSalesHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromSalesHeader) = ACTION::LookupOK THEN
              DocNo := FromSalesHeader."No.";
          END;
        DocType::"Posted Shipment":
          BEGIN
            FromSalesShptHeader."No." := DocNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromSalesShptHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
                FromSalesShptHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromSalesShptHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromSalesShptHeader) = ACTION::LookupOK THEN
              DocNo := FromSalesShptHeader."No.";
          END;
        DocType::"Posted Invoice":
          BEGIN
            FromSalesInvHeader."No." := DocNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromSalesInvHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
                FromSalesInvHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromSalesInvHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromSalesInvHeader) = ACTION::LookupOK THEN
              DocNo := FromSalesInvHeader."No.";
          END;
        DocType::"Posted Return Receipt":
          BEGIN
            FromReturnRcptHeader."No." := DocNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromReturnRcptHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
                FromReturnRcptHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromReturnRcptHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromReturnRcptHeader) = ACTION::LookupOK THEN
              DocNo := FromReturnRcptHeader."No.";
          END;
        DocType::"Posted Credit Memo":
          BEGIN
            FromSalesCrMemoHeader."No." := DocNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromSalesCrMemoHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
                FromSalesCrMemoHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromSalesCrMemoHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromSalesCrMemoHeader) = ACTION::LookupOK THEN
              DocNo := FromSalesCrMemoHeader."No.";
          END;
        DocType::"Arch. Quote",DocType::"Arch. Order",DocType::"Arch. Blanket Order",DocType::"Arch. Return Order":
          BEGIN
            FromArchSalesHeader.RESET;
            FromArchSalesHeader.FILTERGROUP := 0;
            FromArchSalesHeader.SETRANGE("Document Type", CopyDocMgt.ArchSalesHeaderDocType(DocType));
            FromArchSalesHeader.FILTERGROUP := 2;
            FromArchSalesHeader."Document Type" := CopyDocMgt.ArchSalesHeaderDocType(DocType);
            FromArchSalesHeader."No." := DocNo;
            FromArchSalesHeader."Doc. No. Occurrence" := DocNoOccurrence;
            FromArchSalesHeader."Version No." := DocVersionNo;
            IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
              IF FromArchSalesHeader.SETCURRENTKEY("Document Type","Sell-to Customer No.") THEN BEGIN
                FromArchSalesHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
                IF FromArchSalesHeader.FIND('=><') THEN;
              END;
            IF PAGE.RUNMODAL(0,FromArchSalesHeader) = ACTION::LookupOK THEN
              BEGIN
                DocNo := FromArchSalesHeader."No.";
                DocNoOccurrence := FromArchSalesHeader."Doc. No. Occurrence";
                DocVersionNo := FromArchSalesHeader."Version No.";
                RequestOptionsPage.UPDATE(FALSE);
              END;
          END;
      END;
      ValidateDocNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


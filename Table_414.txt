OBJECT Table 414 IC Outbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICOutboxJnlLine@1000 : Record 415;
               ICOutboxPurchHdr@1001 : Record 428;
               ICOutboxSalesHdr@1002 : Record 426;
               ICCommentLine@1003 : Record 424;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::"Journal Line":
                   BEGIN
                     ICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     IF ICOutboxJnlLine.FINDFIRST THEN
                       ICOutboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   BEGIN
                     ICOutboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICOutboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICOutboxSalesHdr.FINDFIRST THEN
                       ICOutboxSalesHdr.DELETE(TRUE);
                   END;
                 "Source Type"::"Purchase Document":
                   BEGIN
                     ICOutboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICOutboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICOutboxPurchHdr.FINDFIRST THEN
                       ICOutboxPurchHdr.DELETE(TRUE);
                   END;
               END;

               ICCommentLine.SETRANGE("Table Name",ICCommentLine."Table Name"::"IC Outbox Transaction");
               ICCommentLine.SETRANGE("Transaction No.","Transaction No.");
               ICCommentLine.SETRANGE("IC Partner Code","IC Partner Code");
               ICCommentLine.SETRANGE("Transaction Source","Transaction Source");
               IF ICCommentLine.FIND('-') THEN
                 REPEAT
                   ICCommentLine.DELETE(TRUE);
                 UNTIL ICCommentLine.NEXT = 0;
             END;

    CaptionML=[ENU=IC Outbox Transaction;
               ESP=Transacci¢n en bandeja de salida IC;
               DEA=IC-Ausgangstransaktion];
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=[ENU=Transaction No.;
                                                              ESP=Nß asiento;
                                                              DEA=Transaktionsnr.];
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner".Code;
                                                   CaptionML=[ENU=IC Partner Code;
                                                              ESP=C¢digo socio IC;
                                                              DEA=IC-Partnercode];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.;
                                                              DEA=Herkunftsart];
                                                   OptionCaptionML=[ENU=Journal Line,Sales Document,Purchase Document;
                                                                    ESP=L°nea del diario,Documento de venta,Documento de compra;
                                                                    DEA=Buch.-Blattzeile,Verkaufsbeleg,Einkaufsbeleg];
                                                   OptionString=Journal Line,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento;
                                                              DEA=Belegart];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                                    ESP=" ,Pago,Factura,Abono,Reembolso,Pedido,Devoluci¢n";
                                                                    DEA=" ,Zahlung,Rechnung,Gutschrift,Erstattung,Auftrag,Reklamation"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESP=Nß documento;
                                                              DEA=Belegnr.];
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro;
                                                              DEA=Buchungsdatum];
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=[ENU=Transaction Source;
                                                              ESP=Origen de la transacci¢n;
                                                              DEA=Transaktionsursprung];
                                                   OptionCaptionML=[ENU=Rejected by Current Company,Created by Current Company;
                                                                    ESP=Rechazada por la compa§°a actual,Creada por la compa§°a actual;
                                                                    DEA=Abgelehnt durch aktuellen Mandanten,Erstellt durch aktuellen Mandanten];
                                                   OptionString=Rejected by Current Company,Created by Current Company;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              ESP=Fecha emisi¢n documento;
                                                              DEA=Belegdatum];
                                                   Editable=No }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                CASE "Line Action" OF
                                                                  "Line Action"::"Return to Inbox":
                                                                    TESTFIELD("Transaction Source","Transaction Source"::"Rejected by Current Company");
                                                                  "Line Action"::"Send to IC Partner":
                                                                    OutboxCheckSend;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Line Action;
                                                              ESP=Acci¢n de la l°nea;
                                                              DEA=Zeilenaktion];
                                                   OptionCaptionML=[ENU=No Action,Send to IC Partner,Return to Inbox,Cancel;
                                                                    ESP=Ninguna acci¢n,Enviar a IC asociada,Devolver a bandeja de entrada,Cancelar;
                                                                    DEA=Keine Aktion,An IC-Partner senden,ZurÅck in Eingang,Stornieren];
                                                   OptionString=No Action,Send to IC Partner,Return to Inbox,Cancel }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=[ENU=IC Partner G/L Acc. No.;
                                                              ESP=Nß cuenta IC asociada;
                                                              DEA=IC-Partner Sachkontonr.] }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              ESP=Nß l°n. origen;
                                                              DEA=Herkunftszeilennr.] }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source,Document Type;
                                                   Clustered=Yes }
    {    ;IC Partner Code                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ShowDetails@1();
    VAR
      ICOutBoxJnlLine@1000 : Record 415;
      ICOutboxSalesHeader@1001 : Record 426;
      ICOutboxPurchHeader@1002 : Record 428;
      FrmICOutBoxJnlLine@1005 : Page 612;
      FrmICOutboxSalesHeader@1004 : Page 636;
      FrmICOutboxPurchHeader@1003 : Page 638;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::"Journal Line":
          BEGIN
            ICOutBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICOutBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            FrmICOutBoxJnlLine.SETTABLEVIEW(ICOutBoxJnlLine);
            FrmICOutBoxJnlLine.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            ICOutboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICOutboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICOutboxSalesHeader.SETTABLEVIEW(ICOutboxSalesHeader);
            FrmICOutboxSalesHeader.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            ICOutboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICOutboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICOutboxPurchHeader.SETTABLEVIEW(ICOutboxPurchHeader);
            FrmICOutboxPurchHeader.RUNMODAL;
          END;
      END;
    END;

    PROCEDURE OutboxCheckSend@36();
    VAR
      HandledICOutboxTrans@1001 : Record 416;
      ICOutboxTransaction2@1002 : Record 414;
      Text001@1003 : TextConst 'ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Send to IC Partner.\Do you also want to send Transaction No. %2?;ESP=Nß asiento %2 es una copia de Nß asiento %1, que se ha establecido en Enviar a IC asociada.\®Desea tambiÇn enviar Nß asiento %2?;DEA=Transaktion Nr. %2 ist eine Kopie von Transaktion Nr. %1, fÅr die bereits ''Senden an IC-Partner'' festgelegt wurde.\Soll Transaktion Nr. %2 ebenfalls gesendet werden?';
      Text002@1000 : TextConst 'ENU=A copy of Transaction No. %1 has already been sent to IC Partner and is now in the Handled IC Outbox Transactions window.\Do you also want to send Transaction No. %1?;ESP=Ya se ha enviado una copia de Nß asiento %1 al socio IC y ahora est† en la ventana Transacciones bandeja de salida IC procesadas.\®Desea tambiÇn enviar Nß asiento %1?;DEA=Eine Kopie von Transaktion Nr. %1 wurde bereits an den IC-Partner gesendet und befindet sich jetzt im Fenster ''Bearbeitete IC-Ausgangstrans.''.\Mîchten Sie Transaktion Nr. %1 ebenfalls senden?';
    BEGIN
      HandledICOutboxTrans.SETRANGE("Source Type","Source Type");
      HandledICOutboxTrans.SETRANGE("Document Type","Document Type");
      HandledICOutboxTrans.SETRANGE("Document No.","Document No.");
      IF HandledICOutboxTrans.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text002,"Transaction No."),FALSE) THEN
          ERROR('');

      ICOutboxTransaction2.SETRANGE("Source Type","Source Type");
      ICOutboxTransaction2.SETRANGE("Document Type","Document Type");
      ICOutboxTransaction2.SETRANGE("Document No.","Document No.");
      ICOutboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICOutboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICOutboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICOutboxTransaction2.SETRANGE("Line Action","Line Action"::"Send to IC Partner");
      IF ICOutboxTransaction2.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text001,ICOutboxTransaction2."Transaction No.","Transaction No."),FALSE) THEN
          ERROR('');
    END;

    BEGIN
    END.
  }
}


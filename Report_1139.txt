OBJECT Report 1139 Delete Cost Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 96=rm;
    CaptionML=[ENU=Delete Cost Budget Entries;
               ESP=Eliminar movs. ppto. costes;
               DEA=Kostenbudgetposten lîschen];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF CostBudgetRegister2."No." > CostBudgetRegister3."No." THEN
                    ERROR(Text000);

                  IF NOT CONFIRM(Text001,FALSE,CostBudgetRegister2."No.",CostBudgetRegister3."No.") THEN
                    ERROR('');

                  IF NOT CONFIRM(Text004) THEN
                    ERROR('');

                  Window.OPEN(Text005 +
                    Text006 );
                END;

  }
  DATASET
  {
    { 6875;    ;DataItem;                    ;
               DataItemTable=Table1111;
               DataItemTableView=SORTING(No.)
                                 ORDER(Descending);
               OnPreDataItem=BEGIN
                               // Sort descending. Registers are deleted backwards
                               SETRANGE("No.",CostBudgetRegister2."No.",CostBudgetRegister3."No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");

                                  IF Closed THEN
                                    ERROR(Text007,"No.");

                                  IF Source = Source::Allocation THEN BEGIN
                                    CostBudgetEntry.SETRANGE("Allocated with Journal No.","No.");
                                    CostBudgetEntry.MODIFYALL(Allocated,FALSE);
                                    CostBudgetEntry.MODIFYALL("Allocated with Journal No.",0);
                                  END;

                                  CostBudgetEntry.SETRANGE("Entry No.","From Cost Budget Entry No.","To Cost Budget Entry No.");
                                  CostBudgetEntry.DELETEALL;
                                  CostBudgetEntry.RESET;
                                END;

               OnPostDataItem=VAR
                                CostAccSetup@1000 : Record 1108;
                              BEGIN
                                DELETEALL;
                                RESET;
                                SETRANGE(Source,Source::Allocation);

                                IF FINDLAST THEN BEGIN
                                  CostBudgetEntry.GET("To Cost Budget Entry No.");
                                  CostAccSetup.GET;
                                  CostAccSetup."Last Allocation Doc. No." := CostBudgetEntry."Document No.";
                                  CostAccSetup.MODIFY;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CostBudgetRegister2.FINDLAST;
                   CostBudgetRegister3.FINDLAST;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 1   ;2   ;Field     ;
                  Name=FromRegisterNo;
                  Lookup=Yes;
                  CaptionML=[ENU=From Register No.;
                             ESP=Desde nß registro;
                             DEA=Von Journalnr.];
                  SourceExpr=CostBudgetRegister2."No.";
                  TableRelation="Cost Budget Register" WHERE (Closed=CONST(No)) }

      { 3   ;2   ;Field     ;
                  Name=ToRegisterNo;
                  CaptionML=[ENU=To Register No.;
                             ESP=Hasta nß registro;
                             DEA=Bis Journalnr.];
                  SourceExpr=CostBudgetRegister3."No.";
                  TableRelation="Cost Budget Register" WHERE (Closed=CONST(No));
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=From Register No. must not be higher than To Register No..;ESP=Desde nß reg. no puede ser superior a Hasta nß registro.;DEA=Die Anfangsjournalnummer darf nicht grî·er sein als die Endjournalnummer.';
      Text001@1001 : TextConst 'ENU=All corresponding cost budget entries and budget register entries will be deleted. Do you want to delete cost budget register %1 to %2?;ESP=Se eliminar†n todos los movs. presupuesto de costes y registro de costes correspondientes. ®Desea eliminar el registro de presupuesto de costes %1 hasta %2?;DEA=Alle entsprechenden Kostenbudgetposten und Budgeterfassungsposten werden gelîscht. Mîchten Sie die Kostenbudgeterfassung ''%1'' fÅr ''%2'' lîschen?';
      Text004@1002 : TextConst 'ENU=Are you sure?;ESP=®Est† seguro?;DEA=Sind Sie sicher?';
      Text005@1003 : TextConst 'ENU=Delete cost register\;ESP=Eliminar reg. costes\;DEA=Kostenjournal lîschen\';
      Text006@1004 : TextConst 'ENU=Register  no.      #1######;ESP=Nß reg.       #1######;DEA=Journalnr.      #1######';
      Text007@1005 : TextConst 'ENU=Register %1 can no longer be deleted because it is marked as closed.;ESP=El registro %1 ya no se puede borrar porque est† marcado como cerrado.;DEA=Das Journal %1 kann nicht mehr gelîscht werden, da es als geschlossen markiert ist.';
      CostBudgetRegister2@1006 : Record 1111;
      CostBudgetRegister3@1007 : Record 1111;
      CostBudgetEntry@1008 : Record 1109;
      Window@1009 : Dialog;

    PROCEDURE InitializeRequest@1(FromEntryNo@1000 : Integer;ToEntryNo@1001 : Integer);
    BEGIN
      CostBudgetRegister2."No." := FromEntryNo;
      CostBudgetRegister3."No." := ToEntryNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


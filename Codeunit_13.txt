OBJECT Codeunit 13 Gen. Jnl.-Post Batch
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 232=imd;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            Rec := GenJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot exceed %1 characters;ESP=no puede superar %1 caracteres;DEA=kann nicht %1 Zeichen Åberschreiten';
      Text001@1001 : TextConst 'ENU=Journal Batch Name    #1##########\\;ESP=Nombre secci¢n diario #1##########\\;DEA=Buch.-Blattname       #1##########\\';
      Text002@1002 : TextConst 'ENU=Checking lines        #2######\;ESP=Comprobando l°neas    #2######\;DEA=Zeilen prÅfen         #2######\';
      Text003@1003 : TextConst 'ENU=Checking balance      #3###### @4@@@@@@@@@@@@@\;ESP=Comprobando saldo     #3###### @4@@@@@@@@@@@@@\;DEA=Saldo prÅfen          #3###### @4@@@@@@@@@@@@@\';
      Text004@1004 : TextConst 'ENU=Posting lines         #5###### @6@@@@@@@@@@@@@\;ESP=Registrando l°neas    #5###### @6@@@@@@@@@@@@@\;DEA=Zeilen buchen         #5###### @6@@@@@@@@@@@@@\';
      Text005@1005 : TextConst 'ENU=Posting revers. lines #7###### @8@@@@@@@@@@@@@\;ESP=Regs. l°ns. reversi¢n #7###### @8@@@@@@@@@@@@@\;DEA=Umgek. Zeilen buchen  #7###### @8@@@@@@@@@@@@@\';
      Text006@1006 : TextConst 'ENU=Updating lines        #9###### @10@@@@@@@@@@@@;ESP=Actualizando l°neas   #9###### @10@@@@@@@@@@@@;DEA=Zeilen aktualisieren  #9###### @10@@@@@@@@@@@@';
      Text007@1007 : TextConst 'ENU=Posting lines         #5###### @6@@@@@@@@@@@@@;ESP=Registrando l°neas    #5###### @6@@@@@@@@@@@@@;DEA=Zeilen buchen         #5###### @6@@@@@@@@@@@@@';
      Text008@1008 : TextConst 'ENU=must be the same on all lines for the same document.;DEA=muss auf allen Zeilen desselben Belegs gesetzt sein.';
      Text009@1009 : TextConst 'ENU="%1 %2 posted on %3 includes more than one customer or vendor. ";ESP="%1 %2 registrado en %3 incluye m†s de un cliente o proveedor. ";DEA="%1 %2 gebucht am %3 enthÑlt mehr als einen Debitoren oder Kreditoren. "';
      Text010@1010 : TextConst 'ENU=In order for the program to calculate VAT, the entries must be separated by another document number or by an empty line.;ESP=Para el c†lculo de IVA, los movs. se deben separar por otro n£mero de documento o por una l°nea en blanco.;DEA=Damit das Programm die MwSt. ermitteln kann, mÅssen die Buchungen durch eine andere Belegnummer oder eine Leerzeile getrennt werden.';
      Text012@1012 : TextConst 'ENU="%5 %2 is out of balance by %1. ";ESP="%5 %2 tiene un descuadre de %1. ";DEA="%5 %2 Saldo stimmt nicht um %1. "';
      Text013@1013 : TextConst 'ENU=Please check that %3, %4, %5 and %6 are correct for each line.;ESP=Compruebe que son correctos para cada l°nea %3, %4, %5 y %6.;DEA=Bitte prÅfen, ob %3, %4, %5 und %6 fÅr jede Zeile korrekt sind.';
      Text014@1014 : TextConst 'ENU="The lines in %1 are out of balance by %2. ";ESP="Hay un descuadre de %2 en las l°neas de %1. ";DEA="Der Saldo fÅr die Zeilen in %1 stimmen nicht um %2. "';
      Text015@1015 : TextConst 'ENU=Check that %3 and %4 are correct for each line.;ESP=Compruebe que %3 y/e %4 son correctos para cada l°nea.;DEA=Bitte prÅfen, dass %3 und %4 fÅr jede Zeile korrekt sind.';
      Text016@1016 : TextConst 'ENU="Your reversing entries in %4 %2 are out of balance by %1. ";ESP="Hay un descuadre de %1 en los movs. del contraasiento de %4 %2. ";DEA="Der Saldo der umgekehrten Buchungen in %4 %2 stimmen um %1 nicht Åberein. "';
      Text017@1017 : TextConst 'ENU=Please check whether %3 is correct for each line for this %4.;ESP=Compruebe que la %3 es correcta para este/a %4.;DEA=PrÅfen Sie, ob %3 fÅr jede Zeile mit diesem %4 korrekt ist.';
      Text018@1018 : TextConst 'ENU="Your reversing entries for %1 are out of balance by %2. ";ESP="Hay un descuadre de %2 en los movs. de contraasiento del %1. ";DEA="Der Saldo der umgekehrten Buchungen fÅr %1 stimmt um %2 nicht. "';
      Text019@1019 : TextConst 'ENU="%3 %1 is out of balance due to the additional reporting currency. ";ESP="%3 %1 no cuadra debido a la divisa adicional. ";DEA="%3 %1 ist der Saldo fÅr die BerichtswÑhrung nicht ausgeglichen. "';
      Text020@1020 : TextConst 'ENU=Please check that %2 is correct for each line.;ESP=Compruebe que %2 es correcto para cada l°nea.;DEA=Bitte ÅberprÅfen Sie, dass der %2 in jeder Zeile korrekt erfasst ist.';
      Text021@1021 : TextConst 'ENU=cannot be specified when using recurring journals.;ESP=no se puede utilizar en diarios peri¢dicos.;DEA=kann nicht in wiederkehrenden Buch.-BlÑttern angegeben werden.';
      Text022@1022 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only for G/L accounts.;ESP=Los mÇtodos recurrentes Saldo y Contrasiento saldo s¢lo se pueden com movs. de tipo Cuenta.;DEA=Die Wiederholungsart Ausgleich und umgekehrter Ausgleich kann nur fÅr Sachkonten verwendet werden.';
      Text023@1023 : TextConst 'ENU=Allocations can only be used with recurring journals.;ESP=Distribuciones s¢lo se permiten en diarios peri¢dicos.;DEA=Verteilungen kînnen nur in wiederkehrenden Buch.-BlÑttern verwendet werden.';
      Text024@1024 : TextConst 'ENU=<Month Text>;ESP=<Month Text>;DEA=<Month Text>';
      Text025@1025 : TextConst 'ENU=A maximum of %1 posting number series can be used in each journal.;ESP=Se puede utilizar un m†ximo de %1 n£meros de serie de registro en cada diario.;DEA=Ein Maximum von %1 Buchungsnr.-Serien';
      Text026@1026 : TextConst 'ENU="%5 %2 is out of balance by %1 %7. ";ESP="%5 %2 descuadra por %1 %7. ";DEA="%5 %2 Saldo stimmt nicht um %1 %7. "';
      Text027@1027 : TextConst 'ENU="The lines in %1 are out of balance by %2 %5. ";ESP="Las l°neas en %1 descuadran por %2 %5. ";DEA="In den Zeilen fÅr den %1 stimmt der Saldo um %2 %5 nicht. "';
      Text028@1028 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only with Allocations.;ESP=Los mÇtodos rec. de saldo y saldo rev. pueden usarse s¢lo con cuotas.;DEA=Die Wiederholungsart Ausgleich und umgekehrter Ausgleich kann nur fÅr Verteilungen verwendet werden.';
      GenJnlTemplate@1029 : Record 80;
      GenJnlBatch@1030 : Record 232;
      GenJnlLine@1031 : Record 81;
      GenJnlLine2@1032 : Record 81;
      GenJnlLine3@1033 : Record 81;
      TempGenJnlLine4@1034 : TEMPORARY Record 81;
      GenJnlLine5@1035 : Record 81;
      GLEntry@1037 : Record 17;
      GLReg@1038 : Record 45;
      GLAcc@1039 : Record 15;
      GenJnlAlloc@1042 : Record 221;
      AccountingPeriod@1043 : Record 50;
      NoSeries@1044 : TEMPORARY Record 308;
      GLSetup@1045 : Record 98;
      FAJnlSetup@1046 : Record 5605;
      GenJnlLineTemp@1102601000 : TEMPORARY Record 81;
      GenJnlCheckLine@1047 : Codeunit 11;
      GenJnlPostLine@1048 : Codeunit 12;
      NoSeriesMgt@1049 : Codeunit 396;
      NoSeriesMgt2@1050 : ARRAY [10] OF Codeunit 396;
      ICOutboxMgt@1078 : Codeunit 427;
      Window@1052 : Dialog;
      GLRegNo@1053 : Integer;
      StartLineNo@1054 : Integer;
      StartLineNoReverse@1055 : Integer;
      LastDate@1056 : Date;
      LastDocType@1057 : Integer;
      LastDocNo@1058 : Code[20];
      LastPostedDocNo@1059 : Code[20];
      CurrentBalance@1060 : Decimal;
      CurrentBalanceReverse@1061 : Decimal;
      Day@1062 : Integer;
      Week@1063 : Integer;
      Month@1064 : Integer;
      MonthText@1065 : Text[30];
      NoOfRecords@1066 : Integer;
      NoOfReversingRecords@1067 : Integer;
      LineCount@1068 : Integer;
      NoOfPostingNoSeries@1069 : Integer;
      PostingNoSeriesNo@1070 : Integer;
      DocCorrection@1071 : Boolean;
      VATEntryCreated@1073 : Boolean;
      LastFAAddCurrExchRate@1074 : Decimal;
      LastCurrencyCode@1075 : Code[10];
      CurrencyBalance@1076 : Decimal;
      "0DF"@1077 : DateFormula;
      Text029@1041 : TextConst 'ENU="%1 %2 posted on %3 includes more than one customer, vendor or IC Partner. ";ESP="%1 %2, registrado el %3, incluye m†s de un cliente, proveedor o IC asociada. ";DEA="%1 %2, gebucht am %3, enthÑlt mehrere Debitoren, Kreditoren oder IC-Partner. "';
      Text030@1011 : TextConst 'ENU=You cannot enter G/L Account or Bank Account in both %1 and %2.;ESP=No puede introducir una cuenta o una cuenta bancaria en %1 y en %2.;DEA=Sie kînnen nicht sowohl in %1 als auch in %2 ein Sach- bzw. Bankkonto eingeben.';
      Text031@1040 : TextConst 'ENU=Line No. %1 does not contain a G/L Account or Bank Account. When the %2 field contains an account number, either the %3 field or the %4 field must contain a G/L Account or Bank Account.;ESP=La l°nea nß %1 no incluye una cuenta o una cuenta bancaria. Si el campo %2 contiene un n£mero de cuenta, el campo %3 o el campo %4 deben contener una cuenta o una cuenta bancaria.;DEA=Zeile Nr. %1 enthÑlt kein Sach- oder Bankkonto. Wenn Feld %2 eine Kontonummer enthÑlt, muss entweder Feld %3 oder Feld %4 ein Sach- bzw. Bankkonto enthalten.';
      GLSetupFound@1079 : Boolean;

    LOCAL PROCEDURE Code@7();
    VAR
      TempGenJnlLine@1010 : TEMPORARY Record 81;
      ICHandledInboxTransaction@1007 : Record 420;
      TempGenJnlLine2@1011 : Record 81;
      UpdateAnalysisView@1003 : Codeunit 410;
      ICLastDate@1014 : Date;
      ICTransactionNo@1004 : Integer;
      ICLastDocType@1013 : Integer;
      CurrentICPartner@1009 : Code[20];
      ICLastDocNo@1012 : Code[20];
      VATPostingSetup@1015 : Record 325;
      BalVatPostingSetup@1016 : Record 325;
      OldVATAmount@1001 : Decimal;
      OldVATPct@1000 : Decimal;
    BEGIN
      WITH GenJnlLine DO BEGIN
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");

        LOCKTABLE;
        GenJnlAlloc.LOCKTABLE;

        GenJnlTemplate.GET("Journal Template Name");
        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF STRLEN(INCSTR(GenJnlBatch.Name)) > MAXSTRLEN(GenJnlBatch.Name) THEN
          GenJnlBatch.FIELDERROR(
            Name,
            STRSUBSTNO(
              Text000,
              MAXSTRLEN(GenJnlBatch.Name)));

        IF GenJnlTemplate.Recurring THEN BEGIN
          SETRANGE("Posting Date",0D,WORKDATE);
          SETFILTER("Expiration Date",'%1 | %2..',0D,WORKDATE);
          GLSetup.GET;
        END;

        IF NOT FIND('=><') THEN BEGIN
          "Line No." := 0;
          COMMIT;
          EXIT;
        END;

        IF GenJnlTemplate.Recurring THEN
          Window.OPEN(
            Text001 +
            Text002 +
            Text003 +
            Text004 +
            Text005 +
            Text006)
        ELSE
          Window.OPEN(
            Text001 +
            Text002 +
            Text003 +
            Text007);
        Window.UPDATE(1,"Journal Batch Name");

        // Check lines
        LineCount := 0;
        StartLineNo := "Line No.";
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(2,LineCount);
          CheckRecurringLine(GenJnlLine);
          UpdateRecurringAmt(GenJnlLine);
          CheckAllocations(GenJnlLine);
          GenJnlLine5.COPY(GenJnlLine);
          PrepareGenJnlLineAddCurr(GenJnlLine5);
          GenJnlCheckLine.RunCheck(GenJnlLine5);
          TempGenJnlLine := GenJnlLine5;
          TempGenJnlLine.INSERT;
          IF NEXT = 0 THEN
            FINDFIRST;
        UNTIL "Line No." = StartLineNo;
        IF GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany THEN
          CheckICDocument(TempGenJnlLine);
        NoOfRecords := LineCount;

        // Check balance
        SETCURRENTKEY("Document No.");
        LineCount := 0;
        LastDate := 0D;
        LastDocType := 0;
        LastDocNo := '';
        LastFAAddCurrExchRate := 0;
        GenJnlLineTemp.RESET;
        GenJnlLineTemp.DELETEALL;
        VATEntryCreated := FALSE;
        CurrentBalance := 0;
        CurrentBalanceReverse := 0;
        CurrencyBalance := 0;

        FINDSET(TRUE,FALSE);
        LastCurrencyCode := "Currency Code";
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(3,LineCount);
          Window.UPDATE(4,ROUND(LineCount / NoOfRecords * 10000,1));

          IF NOT EmptyLine THEN BEGIN
            IF (GenJnlBatch."No. Series" <> '') AND
               ("Document No." <> LastDocNo)
            THEN
              TESTFIELD("Document No.",NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series","Posting Date",FALSE));
            IF "Posting No. Series" <> '' THEN
              TESTFIELD("Posting No. Series",GenJnlBatch."Posting No. Series");
            IF ("Posting Date" <> LastDate) OR
               ("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo)
            THEN BEGIN
              IF Correction THEN
                GenJnlTemplate.TESTFIELD("Force Doc. Balance",TRUE);
              DocCorrection := Correction;
            END ELSE
              IF Correction <> DocCorrection THEN
                FIELDERROR(Correction,Text008);
          END;
          IF ("Posting Date" <> LastDate) OR
             GenJnlTemplate."Force Doc. Balance" AND
             (("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo))
          THEN BEGIN
            CheckBalance;
            CurrencyBalance := 0;
            LastCurrencyCode := "Currency Code";
            GenJnlLineTemp.RESET;
            GenJnlLineTemp.DELETEALL;
            VATEntryCreated := FALSE;
          END;

          IF Amount <> 0 THEN BEGIN
            IF LastFAAddCurrExchRate <> "FA Add.-Currency Factor" THEN
              CheckAddExchRateBalance;
            IF (CurrentBalance = 0) AND (CurrentICPartner = '') THEN BEGIN
              StartLineNo := "Line No.";
              GenJnlLineTemp.RESET;
              GenJnlLineTemp.DELETEALL;
              VATEntryCreated := FALSE;
            END;
            IF CurrentBalanceReverse = 0 THEN
              StartLineNoReverse := "Line No.";
            UpdateLineBalance;
            CurrentBalance := CurrentBalance + "Balance (LCY)";
            IF "Recurring Method" >= "Recurring Method"::"RF Reversing Fixed" THEN
              CurrentBalanceReverse := CurrentBalanceReverse + "Balance (LCY)";

            IF "Recurring Method" <> "Recurring Method"::" " THEN
              CALCFIELDS("Allocated Amt. (LCY)");
            IF ("Recurring Method" = "Recurring Method"::" ") OR ("Amount (LCY)" <> -"Allocated Amt. (LCY)") THEN
              IF "Currency Code" <> LastCurrencyCode THEN
                LastCurrencyCode := ''
              ELSE
                IF ("Currency Code" <> '') AND (("Account No." = '') XOR ("Bal. Account No." = '')) THEN
                  IF "Account No." <> '' THEN
                    CurrencyBalance := CurrencyBalance + Amount
                  ELSE
                    CurrencyBalance := CurrencyBalance - Amount;
          END;

          LastDate := "Posting Date";
          LastDocType := "Document Type";
          IF NOT EmptyLine THEN
            LastDocNo := "Document No.";
          LastFAAddCurrExchRate := "FA Add.-Currency Factor";
          IF GenJnlTemplate."Force Doc. Balance" THEN BEGIN
            IF NOT VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
              CLEAR(VATPostingSetup);
            IF NOT BalVatPostingSetup.GET("Bal. VAT Bus. Posting Group","Bal. VAT Prod. Posting Group") THEN
              CLEAR(BalVatPostingSetup);
            VATEntryCreated :=
              VATEntryCreated OR
              (("Account Type" = "Account Type"::"G/L Account") AND ("Account No." <> '') AND
               ("Gen. Posting Type" IN ["Gen. Posting Type"::Purchase,"Gen. Posting Type"::Sale]) AND
               (VATPostingSetup."VAT %" <> 0)) OR
              (("Bal. Account Type" = "Bal. Account Type"::"G/L Account") AND ("Bal. Account No." <> '') AND
               ("Bal. Gen. Posting Type" IN ["Bal. Gen. Posting Type"::Purchase,"Bal. Gen. Posting Type"::Sale]) AND
               (BalVatPostingSetup."VAT %" <> 0));
            IF (("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"IC Partner"]) AND
                ("Account No." <> ''))
            THEN
              AddCustVendIC("Account Type","Account No.");
            IF (("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,
                                         "Bal. Account Type"::"IC Partner"]) AND
                ("Bal. Account No." <> ''))
            THEN
              AddCustVendIC("Bal. Account Type","Bal. Account No.");
            IF (GenJnlLineTemp.COUNT > 1) AND VATEntryCreated THEN
              ERROR(
                Text009 +
                Text010,
                "Document Type","Document No.","Posting Date");
            IF (GenJnlLineTemp.COUNT > 1) AND (CurrentICPartner <> '') AND
               (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany)
            THEN
              ERROR(
                Text029,
                "Document Type","Document No.","Posting Date");
          END;
        UNTIL NEXT = 0;
        CheckBalance;
        CopyFields;

        // Find next register no.
        GLEntry.LOCKTABLE;

        IF GLEntry.FINDLAST THEN;
        GLReg.LOCKTABLE;
        IF GLReg.FINDLAST THEN
          GLRegNo := GLReg."No." + 1
        ELSE
          GLRegNo := 1;
        // Post lines
        LineCount := 0;
        LastDocNo := '';
        LastPostedDocNo := '';
        TempGenJnlLine4.DELETEALL;
        NoOfReversingRecords := 0;
        FINDSET(TRUE,FALSE);
        REPEAT
          IF (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) AND NOT EmptyLine AND
             (("Posting Date" <> ICLastDate) OR ("Document Type" <> ICLastDocType) OR ("Document No." <> ICLastDocNo))
          THEN BEGIN
            CurrentICPartner := '';
            ICLastDate := "Posting Date";
            ICLastDocType := "Document Type";
            ICLastDocNo := "Document No.";
            TempGenJnlLine.RESET;
            TempGenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
            TempGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
            TempGenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
            TempGenJnlLine.SETRANGE("Posting Date","Posting Date");
            TempGenJnlLine.SETRANGE("Document No.","Document No.");
            TempGenJnlLine.SETFILTER("IC Partner Code",'<>%1','');
            IF TempGenJnlLine.FINDFIRST AND (TempGenJnlLine."IC Partner Code" <> '') THEN BEGIN
              CurrentICPartner := TempGenJnlLine."IC Partner Code";
              IF TempGenJnlLine."IC Direction" = TempGenJnlLine."IC Direction"::Outgoing THEN BEGIN
                ICTransactionNo := ICOutboxMgt.CreateOutboxJnlTransaction(TempGenJnlLine,FALSE);
              END ELSE BEGIN
                IF ICHandledInboxTransaction.GET(
                     TempGenJnlLine."IC Partner Transaction No.",TempGenJnlLine."IC Partner Code",
                     ICHandledInboxTransaction."Transaction Source"::"Created by Partner",TempGenJnlLine."Document Type")
                THEN BEGIN
                  ICHandledInboxTransaction.LOCKTABLE;
                  ICHandledInboxTransaction.Status := ICHandledInboxTransaction.Status::Posted;
                  ICHandledInboxTransaction.MODIFY;
                END
              END;
            END
          END;
          GenJnlLine3 := GenJnlLine;
          WITH GenJnlLine3 DO BEGIN
            LineCount := LineCount + 1;
            IF CurrentICPartner <> '' THEN
              "IC Partner Code" := CurrentICPartner;
            Window.UPDATE(5,LineCount);
            Window.UPDATE(6,ROUND(LineCount / NoOfRecords * 10000,1));
            MakeRecurringTexts(GenJnlLine3);
            CheckDocumentNo(GenJnlLine3);
            GenJnlLine5.COPY(GenJnlLine3);
            PrepareGenJnlLineAddCurr(GenJnlLine5);
            GenJnlPostLine.RunWithoutCheck(GenJnlLine5);
            IF (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) AND (CurrentICPartner <> '') AND
               ("IC Direction" = "IC Direction"::Outgoing) AND (ICTransactionNo > 0)
            THEN
              ICOutboxMgt.CreateOutboxJnlLine(ICTransactionNo,1,GenJnlLine5);
            IF ("Recurring Method" >= "Recurring Method"::"RF Reversing Fixed") AND ("Posting Date" <> 0D) THEN BEGIN
              "Posting Date" := "Posting Date" + 1;
              "Document Date" := "Posting Date";
              MultiplyAmounts(GenJnlLine3,-1);
              TempGenJnlLine4 := GenJnlLine3;
              TempGenJnlLine4."Reversing Entry" := TRUE;
              TempGenJnlLine4.INSERT;
              NoOfReversingRecords := NoOfReversingRecords + 1;
              "Posting Date" := "Posting Date" - 1;
              "Document Date" := "Posting Date";
            END;
            PostAllocations(GenJnlLine3,FALSE);
          END;
        UNTIL NEXT = 0;

        // Post reversing lines
        LineCount := 0;
        LastDocNo := '';
        LastPostedDocNo := '';
        IF TempGenJnlLine4.FIND('-') THEN
          REPEAT
            GenJnlLine3 := TempGenJnlLine4;
            WITH GenJnlLine3 DO BEGIN
              LineCount := LineCount + 1;
              Window.UPDATE(7,LineCount);
              Window.UPDATE(8,ROUND(LineCount / NoOfReversingRecords * 10000,1));
              CheckDocumentNo(GenJnlLine3);
              GenJnlLine5.COPY(GenJnlLine3);
              PrepareGenJnlLineAddCurr(GenJnlLine5);
              GenJnlPostLine.RunWithCheck(GenJnlLine5);
              PostAllocations(GenJnlLine3,TRUE);
            END;
          UNTIL TempGenJnlLine4.NEXT = 0;

        // Copy register no. and current journal batch name to general journal
        IF NOT GLReg.FINDLAST OR (GLReg."No." <> GLRegNo) THEN
          GLRegNo := 0;

        INIT;
        "Line No." := GLRegNo;

        // Update/delete lines
        IF GLRegNo <> 0 THEN BEGIN
          IF GenJnlTemplate.Recurring THEN BEGIN
            // Recurring journal
            LineCount := 0;
            GenJnlLine2.COPY(GenJnlLine);
            GenJnlLine2.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
            GenJnlLine2.FINDSET(TRUE,FALSE);
            REPEAT
              LineCount := LineCount + 1;
              Window.UPDATE(9,LineCount);
              Window.UPDATE(10,ROUND(LineCount / NoOfRecords * 10000,1));
              OldVATAmount := GenJnlLine2."VAT Amount";
              OldVATPct := GenJnlLine2."VAT %";
              IF GenJnlLine2."Posting Date" <> 0D THEN
                GenJnlLine2.VALIDATE(
                  "Posting Date",CALCDATE(GenJnlLine2."Recurring Frequency",GenJnlLine2."Posting Date"));
              IF NOT
                 (GenJnlLine2."Recurring Method" IN
                  [GenJnlLine2."Recurring Method"::"F  Fixed",
                   GenJnlLine2."Recurring Method"::"RF Reversing Fixed"])
              THEN
                MultiplyAmounts(GenJnlLine2,0)
              ELSE
                IF (GenJnlLine2."VAT %" = OldVATPct) AND (GenJnlLine2."VAT Amount" <> OldVATAmount) THEN
                  GenJnlLine2.VALIDATE("VAT Amount",OldVATAmount);
              GenJnlLine2.MODIFY;
            UNTIL GenJnlLine2.NEXT = 0;
          END ELSE BEGIN
            // Not a recurring journal
            GenJnlLine2.COPY(GenJnlLine);
            GenJnlLine2.SETFILTER("Account No.",'<>%1','');
            IF GenJnlLine2.FINDLAST THEN; // Remember the last line
            GenJnlLine3.COPY(GenJnlLine);
            GenJnlLine3.DELETEALL;
            GenJnlLine3.RESET;
            GenJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
            GenJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
            IF NOT GenJnlLine3.FINDLAST THEN
              IF INCSTR("Journal Batch Name") <> '' THEN BEGIN
                GenJnlBatch.DELETE;
                IF GenJnlTemplate.Type = GenJnlTemplate.Type::Assets THEN
                  FAJnlSetup.IncGenJnlBatchName(GenJnlBatch);
                GenJnlBatch.Name := INCSTR("Journal Batch Name");
                IF GenJnlBatch.INSERT THEN;
                "Journal Batch Name" := GenJnlBatch.Name;
              END;

            GenJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
            IF (GenJnlBatch."No. Series" = '') AND NOT GenJnlLine3.FINDLAST THEN BEGIN
              GenJnlLine3.INIT;
              GenJnlLine3."Journal Template Name" := "Journal Template Name";
              GenJnlLine3."Journal Batch Name" := "Journal Batch Name";
              GenJnlLine3."Line No." := 10000;
              GenJnlLine3.INSERT;
              TempGenJnlLine2 := GenJnlLine2;
              TempGenJnlLine2."Balance (LCY)" := 0;
              GenJnlLine3.SetUpNewLine(TempGenJnlLine2,0,TRUE);
              GenJnlLine3.MODIFY;
            END;
          END;
        END;
        IF GenJnlBatch."No. Series" <> '' THEN
          NoSeriesMgt.SaveNoSeries;
        IF NoSeries.FINDSET THEN
          REPEAT
            EVALUATE(PostingNoSeriesNo,NoSeries.Description);
            NoSeriesMgt2[PostingNoSeriesNo].SaveNoSeries;
          UNTIL NoSeries.NEXT = 0;
        IF GLSetupFound THEN
          GLSetup.MODIFY;
        COMMIT;
        CLEAR(GenJnlCheckLine);
        CLEAR(GenJnlPostLine);
      END;
      UpdateAnalysisView.UpdateAll(0,TRUE);
      COMMIT;
    END;

    LOCAL PROCEDURE CheckBalance@8();
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF CurrentBalance <> 0 THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text012 +
              Text013,
              CurrentBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount));
          ERROR(
            Text014 +
            Text015,
            LastDate,CurrentBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount));
        END;
        IF CurrentBalanceReverse <> 0 THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNoReverse);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text016 +
              Text017,
              CurrentBalanceReverse,LastDocNo,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Document No."));
          ERROR(
            Text018 +
            Text017,
            LastDate,CurrentBalanceReverse,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Posting Date"));
        END;
        IF (LastCurrencyCode <> '') AND (CurrencyBalance <> 0) THEN BEGIN
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          IF GenJnlTemplate."Force Doc. Balance" THEN
            ERROR(
              Text026 +
              Text013,
              CurrencyBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount),
              LastCurrencyCode);
          ERROR(
            Text027 +
            Text015,
            LastDate,CurrencyBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount),LastCurrencyCode);
        END;
      END;
    END;

    PROCEDURE CheckAddExchRateBalance@9();
    BEGIN
      WITH GenJnlLine DO
        IF CurrentBalance <> 0 THEN
          ERROR(
            Text019 +
            Text020,
            LastDocNo,FIELDCAPTION("FA Add.-Currency Factor"),FIELDCAPTION("Document No."));
    END;

    LOCAL PROCEDURE CheckRecurringLine@1(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Account No." <> '' THEN
          IF GenJnlTemplate.Recurring THEN BEGIN
            TESTFIELD("Recurring Method");
            TESTFIELD("Recurring Frequency");
            IF "Bal. Account No." <> '' THEN
              FIELDERROR("Bal. Account No.",Text021);
            CASE "Recurring Method" OF
              "Recurring Method"::"V  Variable","Recurring Method"::"RV Reversing Variable",
              "Recurring Method"::"F  Fixed","Recurring Method"::"RF Reversing Fixed":
                TESTFIELD(Amount);
              "Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance":
                TESTFIELD(Amount,0);
            END;
          END ELSE BEGIN
            TESTFIELD("Recurring Method",0);
            TESTFIELD("Recurring Frequency","0DF");
          END;
      END;
    END;

    LOCAL PROCEDURE UpdateRecurringAmt@2(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF ("Account No." <> '') AND
           ("Recurring Method" IN
            ["Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance"])
        THEN BEGIN
          GLEntry.LOCKTABLE;
          IF "Account Type" = "Account Type"::"G/L Account" THEN BEGIN
            GLAcc."No." := "Account No.";
            GLAcc.SETRANGE("Date Filter",0D,"Posting Date");
            IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
              "Source Currency Code" := GLSetup."Additional Reporting Currency";
              GLAcc.CALCFIELDS("Additional-Currency Net Change");
              "Source Currency Amount" := -GLAcc."Additional-Currency Net Change";
              GenJnlAlloc.UpdateAllocationsAddCurr(GenJnlLine2,"Source Currency Amount");
            END;
            GLAcc.CALCFIELDS("Net Change");
            VALIDATE(Amount,-GLAcc."Net Change");
          END ELSE
            ERROR(
              Text022);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckAllocations@3(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Account No." <> '' THEN BEGIN
          IF "Recurring Method" IN
             ["Recurring Method"::"B  Balance",
              "Recurring Method"::"RB Reversing Balance"]
          THEN BEGIN
            GenJnlAlloc.RESET;
            GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
            GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
            GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
            IF NOT GenJnlAlloc.FINDFIRST THEN
              ERROR(
                Text028);
          END;

          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER(Amount,'<>0');
          IF GenJnlAlloc.FINDFIRST THEN BEGIN
            IF NOT GenJnlTemplate.Recurring THEN
              ERROR(Text023);
            GenJnlAlloc.SETRANGE("Account No.",'');
            IF GenJnlAlloc.FINDFIRST THEN
              GenJnlAlloc.TESTFIELD("Account No.");
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE MakeRecurringTexts@4(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF ("Account No." <> '') AND ("Recurring Method" <> 0) THEN BEGIN
          Day := DATE2DMY("Posting Date",1);
          Week := DATE2DWY("Posting Date",2);
          Month := DATE2DMY("Posting Date",2);
          MonthText := FORMAT("Posting Date",0,Text024);
          AccountingPeriod.SETRANGE("Starting Date",0D,"Posting Date");
          IF NOT AccountingPeriod.FINDLAST THEN
            AccountingPeriod.Name := '';
          "Document No." :=
            DELCHR(
              PADSTR(
                STRSUBSTNO("Document No.",Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN("Document No.")),
              '>');
          Description :=
            DELCHR(
              PADSTR(
                STRSUBSTNO(Description,Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN(Description)),
              '>');
        END;
      END;
    END;

    LOCAL PROCEDURE PostAllocations@5(VAR AllocateGenJnlLine@1000 : Record 81;Reversing@1001 : Boolean);
    BEGIN
      WITH AllocateGenJnlLine DO BEGIN
        IF "Account No." <> '' THEN BEGIN
          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER("Account No.",'<>%1','');
          IF GenJnlAlloc.FINDSET(TRUE,FALSE) THEN BEGIN
            GenJnlLine2.INIT;
            GenJnlLine2."Account Type" := GenJnlLine2."Account Type"::"G/L Account";
            GenJnlLine2."Posting Date" := "Posting Date";
            GenJnlLine2."Document Type" := "Document Type";
            GenJnlLine2."Document No." := "Document No.";
            GenJnlLine2.Description := Description;
            GenJnlLine2."Source Code" := "Source Code";
            GenJnlLine2."Journal Batch Name" := "Journal Batch Name";
            GenJnlLine2."Line No." := "Line No.";
            GenJnlLine2."Reason Code" := "Reason Code";
            GenJnlLine2.Correction := Correction;
            GenJnlLine2."Recurring Method" := "Recurring Method";
            IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor] THEN BEGIN
              GenJnlLine2."Bill-to/Pay-to No." := "Bill-to/Pay-to No.";
              GenJnlLine2."Ship-to/Order Address Code" := "Ship-to/Order Address Code";
            END;
            REPEAT
              GenJnlLine2."Account No." := GenJnlAlloc."Account No.";
              GenJnlLine2.Amount := GenJnlAlloc.Amount;
              GenJnlLine2."Amount (LCY)" := GenJnlAlloc.Amount;
              GenJnlLine2."Shortcut Dimension 1 Code" := GenJnlAlloc."Shortcut Dimension 1 Code";
              GenJnlLine2."Shortcut Dimension 2 Code" := GenJnlAlloc."Shortcut Dimension 2 Code";
              GenJnlLine2."Dimension Set ID" := GenJnlAlloc."Dimension Set ID";
              GenJnlLine2."Gen. Posting Type" := GenJnlAlloc."Gen. Posting Type";
              GenJnlLine2."Gen. Bus. Posting Group" := GenJnlAlloc."Gen. Bus. Posting Group";
              GenJnlLine2."Gen. Prod. Posting Group" := GenJnlAlloc."Gen. Prod. Posting Group";
              GenJnlLine2."VAT Bus. Posting Group" := GenJnlAlloc."VAT Bus. Posting Group";
              GenJnlLine2."VAT Prod. Posting Group" := GenJnlAlloc."VAT Prod. Posting Group";
              GenJnlLine2."Tax Area Code" := GenJnlAlloc."Tax Area Code";
              GenJnlLine2."Tax Liable" := GenJnlAlloc."Tax Liable";
              GenJnlLine2."Tax Group Code" := GenJnlAlloc."Tax Group Code";
              GenJnlLine2."Use Tax" := GenJnlAlloc."Use Tax";
              GenJnlLine2."VAT Calculation Type" := GenJnlAlloc."VAT Calculation Type";
              GenJnlLine2."VAT Amount" := GenJnlAlloc."VAT Amount";
              GenJnlLine2."VAT Base Amount" := GenJnlAlloc.Amount - GenJnlAlloc."VAT Amount";
              GenJnlLine2."VAT %" := GenJnlAlloc."VAT %";
              GenJnlLine2."Source Currency Amount" := GenJnlAlloc."Additional-Currency Amount";
              GenJnlLine2."Allow Zero-Amount Posting" := TRUE;
              PrepareGenJnlLineAddCurr(GenJnlLine2);
              IF NOT Reversing THEN BEGIN
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                IF "Recurring Method" IN
                   ["Recurring Method"::"V  Variable","Recurring Method"::"B  Balance"]
                THEN BEGIN
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                END;
              END ELSE BEGIN
                MultiplyAmounts(GenJnlLine2,-1);
                GenJnlLine2."Reversing Entry" := TRUE;
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                IF "Recurring Method" IN
                   ["Recurring Method"::"RV Reversing Variable",
                    "Recurring Method"::"RB Reversing Balance"]
                THEN BEGIN
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                END;
              END;
            UNTIL GenJnlAlloc.NEXT = 0;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE MultiplyAmounts@6(VAR GenJnlLine2@1000 : Record 81;Factor@1001 : Decimal);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Account No." <> '' THEN BEGIN
          Amount := Amount * Factor;
          "Debit Amount" := "Debit Amount" * Factor;
          "Credit Amount" := "Credit Amount" * Factor;
          "Amount (LCY)" := "Amount (LCY)" * Factor;
          "Balance (LCY)" := "Balance (LCY)" * Factor;
          "Sales/Purch. (LCY)" := "Sales/Purch. (LCY)" * Factor;
          "Profit (LCY)" := "Profit (LCY)" * Factor;
          "Inv. Discount (LCY)" := "Inv. Discount (LCY)" * Factor;
          Quantity := Quantity * Factor;
          "VAT Amount" := "VAT Amount" * Factor;
          "VAT Base Amount" := "VAT Base Amount" * Factor;
          "VAT Amount (LCY)" := "VAT Amount (LCY)" * Factor;
          "VAT Base Amount (LCY)" := "VAT Base Amount (LCY)" * Factor;
          "Source Currency Amount" := "Source Currency Amount" * Factor;
          IF "Job No." <> '' THEN BEGIN
            "Job Quantity" := "Job Quantity" * Factor;
            "Job Total Cost (LCY)" := "Job Total Cost (LCY)" * Factor;
            "Job Total Price (LCY)" := "Job Total Price (LCY)" * Factor;
            "Job Line Amount (LCY)" := "Job Line Amount (LCY)" * Factor;
            "Job Total Cost" := "Job Total Cost" * Factor;
            "Job Total Price" := "Job Total Price" * Factor;
            "Job Line Amount" := "Job Line Amount" * Factor;
            "Job Line Discount Amount" := "Job Line Discount Amount" * Factor;
            "Job Line Disc. Amount (LCY)" := "Job Line Disc. Amount (LCY)" * Factor;
          END;
        END;
      END;
    END;

    PROCEDURE CheckDocumentNo@11(VAR GenJnlLine2@1000 : Record 81);
    BEGIN
      WITH GenJnlLine2 DO BEGIN
        IF "Posting No. Series" = '' THEN
          "Posting No. Series" := GenJnlBatch."No. Series"
        ELSE
          IF NOT EmptyLine THEN
            IF "Document No." = LastDocNo THEN
              "Document No." := LastPostedDocNo
            ELSE BEGIN
              IF NOT NoSeries.GET("Posting No. Series") THEN BEGIN
                NoOfPostingNoSeries := NoOfPostingNoSeries + 1;
                IF NoOfPostingNoSeries > ARRAYLEN(NoSeriesMgt2) THEN
                  ERROR(
                    Text025,
                    ARRAYLEN(NoSeriesMgt2));
                NoSeries.Code := "Posting No. Series";
                NoSeries.Description := FORMAT(NoOfPostingNoSeries);
                NoSeries.INSERT;
              END;
              LastDocNo := "Document No.";
              EVALUATE(PostingNoSeriesNo,NoSeries.Description);
              "Document No." :=
                NoSeriesMgt2[PostingNoSeriesNo].GetNextNo("Posting No. Series","Posting Date",TRUE);
              LastPostedDocNo := "Document No.";
            END;
      END;
    END;

    PROCEDURE PrepareGenJnlLineAddCurr@10(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      IF (GLSetup."Additional Reporting Currency" <> '') AND
         (GenJnlLine."Recurring Method" IN
          [GenJnlLine."Recurring Method"::"B  Balance",
           GenJnlLine."Recurring Method"::"RB Reversing Balance"])
      THEN BEGIN
        GenJnlLine."Source Currency Code" := GLSetup."Additional Reporting Currency";
        IF (GenJnlLine.Amount = 0) AND
           (GenJnlLine."Source Currency Amount" <> 0)
        THEN BEGIN
          GenJnlLine."Additional-Currency Posting" :=
            GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
          GenJnlLine.Amount := GenJnlLine."Source Currency Amount";
          GenJnlLine."Source Currency Amount" := 0;
        END;
      END;
    END;

    PROCEDURE CopyFields@12();
    VAR
      GenJnlLine4@1000 : Record 81;
      GenJnlLine6@1001 : Record 81;
      CheckAmount@1002 : Decimal;
      GenJnlLine7@1003 : Record 81;
    BEGIN
      GenJnlLine6.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
      GenJnlLine4.FILTERGROUP(2);
      GenJnlLine4.COPY(GenJnlLine);
      GenJnlLine4.FILTERGROUP(0);
      GenJnlLine6.FILTERGROUP(2);
      GenJnlLine6.COPY(GenJnlLine);
      GenJnlLine6.FILTERGROUP(0);
      GenJnlLine4.SETFILTER(
        "Account Type",'%1|%2',GenJnlLine4."Account Type"::Customer,GenJnlLine4."Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Bal. Account No.",'');
      IF GenJnlLine4.FINDSET THEN
        REPEAT
          GenJnlLine6.SETRANGE("Posting Date",GenJnlLine4."Posting Date");
          GenJnlLine6.SETRANGE("Document No.",GenJnlLine4."Document No.");
          GenJnlLine6.SETRANGE("Bill-to/Pay-to No.",'');
          CASE GenJnlLine4."Account Type" OF
            GenJnlLine4."Account Type"::Customer:
              GenJnlLine6.SETRANGE("Gen. Posting Type",GenJnlLine6."Gen. Posting Type"::Sale);
            GenJnlLine4."Account Type"::Vendor:
              GenJnlLine6.SETRANGE("Gen. Posting Type",GenJnlLine6."Gen. Posting Type"::Purchase);
          END;
          CheckAmount := 0;
          IF GenJnlLine6.FINDSET(TRUE,TRUE) THEN
            REPEAT
              IF (GenJnlLine6."Account No." = '') <> (GenJnlLine6."Bal. Account No." = '') THEN BEGIN
                IF (GenJnlLine4."Account Type" = GenJnlLine4."Account Type"::Customer) AND
                   (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Purchase) OR
                   (GenJnlLine4."Account Type" = GenJnlLine4."Account Type"::Vendor) AND
                   (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Sale)
                THEN
                  GenJnlLine6.FIELDERROR("Gen. Posting Type");
                IF (GenJnlLine4."Account Type" = GenJnlLine4."Account Type"::Customer) AND
                   (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Purchase) OR
                   (GenJnlLine4."Account Type" = GenJnlLine4."Account Type"::Vendor) AND
                   (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Sale)
                THEN
                  GenJnlLine6.FIELDERROR("Bal. Gen. Posting Type");
                GenJnlLine7 := GenJnlLine6;
                GenJnlLine7."Bill-to/Pay-to No." := GenJnlLine4."Bill-to/Pay-to No.";
                GenJnlLine7."Ship-to/Order Address Code" := GenJnlLine4."Ship-to/Order Address Code";
                GenJnlLine7."VAT Registration No." := GenJnlLine4."VAT Registration No.";
                GenJnlLine7."Country/Region Code" := GenJnlLine4."Country/Region Code";
                GenJnlLine7.MODIFY;
                CheckAmount := CheckAmount + GenJnlLine6.Amount;
              END;
            UNTIL (GenJnlLine6.NEXT = 0) OR (-GenJnlLine4.Amount = CheckAmount);
        UNTIL GenJnlLine4.NEXT = 0;

      GenJnlLine4.SETRANGE("Account Type");
      GenJnlLine4.SETRANGE("Bal. Account No.");
      GenJnlLine4.SETFILTER(
        "Bal. Account Type",'%1|%2',GenJnlLine4."Bal. Account Type"::Customer,GenJnlLine4."Bal. Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Account No.",'');
      IF GenJnlLine4.FINDSET THEN
        REPEAT
          GenJnlLine6.SETRANGE("Posting Date",GenJnlLine4."Posting Date");
          GenJnlLine6.SETRANGE("Document No.",GenJnlLine4."Document No.");
          GenJnlLine6.SETRANGE("Bill-to/Pay-to No.",'');
          CheckAmount := 0;
          IF GenJnlLine6.FINDSET(TRUE,TRUE) THEN
            REPEAT
              IF (GenJnlLine6."Account No." = '') <> (GenJnlLine6."Bal. Account No." = '') THEN BEGIN
                IF (GenJnlLine4."Bal. Account Type" = GenJnlLine4."Bal. Account Type"::Customer) AND
                   (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Purchase) OR
                   (GenJnlLine4."Bal. Account Type" = GenJnlLine4."Bal. Account Type"::Vendor) AND
                   (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Sale)
                THEN
                  GenJnlLine6.FIELDERROR("Bal. Gen. Posting Type");
                IF (GenJnlLine4."Bal. Account Type" = GenJnlLine4."Bal. Account Type"::Customer) AND
                   (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Purchase) OR
                   (GenJnlLine4."Bal. Account Type" = GenJnlLine4."Bal. Account Type"::Vendor) AND
                   (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Sale)
                THEN
                  GenJnlLine6.FIELDERROR("Gen. Posting Type");
                GenJnlLine7 := GenJnlLine6;
                GenJnlLine7."Bill-to/Pay-to No." := GenJnlLine4."Bill-to/Pay-to No.";
                GenJnlLine7."Ship-to/Order Address Code" := GenJnlLine4."Ship-to/Order Address Code";
                GenJnlLine7."VAT Registration No." := GenJnlLine4."VAT Registration No.";
                GenJnlLine7."Country/Region Code" := GenJnlLine4."Country/Region Code";
                GenJnlLine7.MODIFY;
                CheckAmount := CheckAmount + GenJnlLine6.Amount;
              END;
            UNTIL (GenJnlLine6.NEXT = 0) OR (-GenJnlLine4.Amount = CheckAmount);
        UNTIL GenJnlLine4.NEXT = 0;
    END;

    PROCEDURE CheckICDocument@13(VAR TempGenJnlLine1@1001 : TEMPORARY Record 81);
    VAR
      CurrentICPartner@1000 : Code[20];
      TempGenJnlLine2@1002 : TEMPORARY Record 81;
    BEGIN
      WITH TempGenJnlLine1 DO BEGIN
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        FIND('-');
        REPEAT
          IF ("Posting Date" <> LastDate) OR ("Document Type" <> LastDocType) OR ("Document No." <> LastDocNo) THEN BEGIN
            TempGenJnlLine2 := TempGenJnlLine1;
            SETRANGE("Posting Date","Posting Date");
            SETRANGE("Document No.","Document No.");
            SETFILTER("IC Partner Code",'<>%1','');
            IF FIND('-') THEN
              CurrentICPartner := "IC Partner Code"
            ELSE
              CurrentICPartner := '';
            SETRANGE("Posting Date");
            SETRANGE("Document No.");
            SETRANGE("IC Partner Code");
            LastDate := "Posting Date";
            LastDocType := "Document Type";
            LastDocNo := "Document No.";
            TempGenJnlLine1 := TempGenJnlLine2;
          END;
          IF (CurrentICPartner <> '') AND ("IC Direction" = "IC Direction"::Outgoing) THEN BEGIN
            IF ("Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
               ("Bal. Account Type" IN ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
               ("Account No." <> '') AND
               ("Bal. Account No." <> '')
            THEN
              ERROR(Text030,FIELDCAPTION("Account No."),FIELDCAPTION("Bal. Account No."));
            IF (("Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND ("Account No." <> '')) XOR
               (("Bal. Account Type" IN ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) AND
                ("Bal. Account No." <> ''))
            THEN
              TESTFIELD("IC Partner G/L Acc. No.")
            ELSE
              IF "IC Partner G/L Acc. No." <> '' THEN
                ERROR(Text031,
                  "Line No.",FIELDCAPTION("IC Partner G/L Acc. No."),FIELDCAPTION("Account No."),
                  FIELDCAPTION("Bal. Account No."));
          END ELSE
            TESTFIELD("IC Partner G/L Acc. No.",'');
        UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE AddCustVendIC@14(AccountType@1000 : 'G/L Account, Customer, Vendor, Bank Account, Fixed Asset, IC Partner';AccountNo@1001 : Code[20]);
    BEGIN
      GenJnlLineTemp.SETRANGE("Account Type",AccountType);
      GenJnlLineTemp.SETRANGE("Account No.",AccountNo);
      IF GenJnlLineTemp.FINDFIRST THEN
        EXIT;

      GenJnlLineTemp.RESET;
      IF GenJnlLineTemp.FINDLAST THEN
        GenJnlLineTemp."Line No." := GenJnlLineTemp."Line No." + 10000
      ELSE
        GenJnlLineTemp."Line No." := 10000;

      GenJnlLineTemp."Account Type" := AccountType;
      GenJnlLineTemp."Account No." := AccountNo;
      GenJnlLineTemp.INSERT;
    END;

    BEGIN
    END.
  }
}


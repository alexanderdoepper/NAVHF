OBJECT Report 1137 Transfer Budget to Actual
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Transfer Budget to Actual;
               ESP=Transferir presupuesto a real;
               DEA=Budget in Ist-Werte bertragen];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
               OnPreDataItem=BEGIN
                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text000);

                               IF GETFILTER(Date) = '' THEN
                                 ERROR(Text001);

                               IF NOT CONFIRM(Text002,TRUE,GETFILTER("Budget Name"),GETFILTER(Date)) THEN
                                 ERROR('');

                               LOCKTABLE;

                               Window.OPEN(Text003);

                               Window.UPDATE(1,COUNT);
                             END;

               OnAfterGetRecord=VAR
                                  SourceCodeSetup@1001 : Record 242;
                                BEGIN
                                  SourceCodeSetup.GET;
                                  SourceCodeSetup.TESTFIELD("Transfer Budget to Actual");
                                  TempCostJnlLine.INIT;
                                  LastEntryNo := LastEntryNo + 1;
                                  TempCostJnlLine."Line No." := LastEntryNo;
                                  TempCostJnlLine."Cost Type No." := "Cost Type No.";
                                  TempCostJnlLine."Posting Date" := Date;
                                  TempCostJnlLine."Document No." := "Document No.";
                                  IF TempCostJnlLine."Document No." = '' THEN
                                    TempCostJnlLine."Document No." := 'BUDGET';
                                  TempCostJnlLine.Description := Description;
                                  TempCostJnlLine.Amount := Amount;
                                  TempCostJnlLine."Cost Center Code" := "Cost Center Code";
                                  TempCostJnlLine."Cost Object Code" := "Cost Object Code";
                                  TempCostJnlLine."Source Code" := SourceCodeSetup."Transfer Budget to Actual";
                                  TempCostJnlLine."Allocation Description" := "Allocation Description";
                                  TempCostJnlLine."Allocation ID" := "Allocation ID";
                                  TempCostJnlLine.INSERT;

                                  NoInserted := NoInserted + 1;
                                  IF (NoInserted MOD 100) = 0 THEN
                                    Window.UPDATE(2,NoInserted);
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NOT CONFIRM(Text004,TRUE,NoInserted) THEN
                                  ERROR('');

                                PostCostJournalLines;
                              END;

               ReqFilterFields=Budget Name,Date,Allocated,Cost Type No.,Cost Center Code,Cost Object Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempCostJnlLine@1000 : TEMPORARY Record 1101;
      Window@1001 : Dialog;
      LastEntryNo@1002 : Integer;
      NoInserted@1003 : Integer;
      Text000@1004 : TextConst 'ENU=Define the name of the source budget.;ESP=Defina el nombre del presupuesto de origen.;DEA=Definieren Sie den Namen des Herkunftsbudgets.';
      Text001@1005 : TextConst 'ENU=Date range must be defined.;ESP=Debe definirse un rango de fechas.;DEA=Geben Sie den Datumsbereich an.';
      Text002@1006 : TextConst 'ENU=The cost budget "%1" for the date range of "%2" will be transferred to the actual cost entries. \Do you want to start the job?;ESP=El ppto. costes "%1" para el rango de fechas de "%2" se transferir  a movs. de coste real. \¨Desea iniciar el proyecto?;DEA=Das Kostenbudget ''%1'' fr den Datumsbereich %2 wird in die Ist-Kostenposten bertragen. \M”chten Sie das Projekt starten?';
      Text003@1007 : TextConst 'ENU=Copying budget entries\No of entries #1#####\Copied        #2#####;ESP=Copiando movs. ppto.\N§ movs.    #1#####\Copiados        #2#####;DEA=Budgetposten werden kopiert...\Postenanzahl #1#####\Kopiert      #2#####';
      Text004@1008 : TextConst 'ENU=%1 budget entries were transferred to actual cost entries.\\Do you want to copy entries?;ESP=%1 movs. ppto. transferidos a movs. de coste real.\\¨Desea copiar los movs.?;DEA=%1 Budgetposten wurden in Ist-Kostenposten bertragen.\\M”chten Sie Posten kopieren?';
      Text005@1009 : TextConst 'ENU=Posting Cost Entries @1@@@@@@@@@@\;ESP=Registrando movimientos de coste @1@@@@@@@@@@\;DEA=Kostenposten werden gebucht...              @1@@@@@@@@@@\';

    PROCEDURE PostCostJournalLines@1();
    VAR
      CostJnlLine@1000 : Record 1101;
      CAJnlPostLine@1001 : Codeunit 1102;
      Window2@1002 : Dialog;
      JournalLineCount@1003 : Integer;
      CostJnlLineStep@1005 : Integer;
    BEGIN
      TempCostJnlLine.RESET;
      Window2.OPEN(
        Text005);
      IF TempCostJnlLine.COUNT > 0 THEN
        JournalLineCount := 10000 * 100000 DIV TempCostJnlLine.COUNT;
      IF TempCostJnlLine.FINDSET THEN
        REPEAT
          CostJnlLineStep := CostJnlLineStep + JournalLineCount;
          Window2.UPDATE(1,CostJnlLineStep DIV 100000);
          CostJnlLine := TempCostJnlLine;
          CAJnlPostLine.RunWithCheck(CostJnlLine);
        UNTIL TempCostJnlLine.NEXT = 0;
      Window2.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


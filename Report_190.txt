OBJECT Report 190 Issue Reminders
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Reminders;
               ESP=Emitir recordatorios;
               DEA=Mahnungen registrieren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4775;    ;DataItem;                    ;
               DataItemTable=Table295;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Reminder;
                                   DEA=Reminder];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldTime := TIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(ReminderIssue);
                                  ReminderIssue.Set("Reminder Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    ReminderIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewTime := TIME;
                                    IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldTime := TIME;
                                    END;
                                    COMMIT;
                                    MARK := NOT ReminderIssue.RUN;
                                  END;

                                  IF PrintDoc THEN BEGIN
                                    ReminderIssue.GetIssuedReminder(IssuedReminderHeader);
                                    IF FilterText <> '' THEN
                                      FilterText += '|';
                                    FilterText += IssuedReminderHeader."No.";
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc THEN BEGIN
                                  IssuedReminderHeader.SETFILTER("No.",FilterText);
                                  IssuedReminderHeader.PrintRecords(FALSE);
                                END;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Reminder Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Print;
                             ESP=Imprimir;
                             DEA=Drucken];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             ESP=Reemplazar fecha reg.;
                             DEA=Buchungsdatum ersetzen];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro;
                             DEA=Buchungsdatum];
                  SourceExpr=PostingDateReq }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESP=Introduzca la fecha registro.;DEA=Bitte das Buchungsdatum eingeben.';
      Text001@1001 : TextConst 'ENU=Issuing reminder...;ESP=Emitiendo recordatorio...;DEA=Mahnung registrieren...';
      Text002@1002 : TextConst 'ENU=Issuing reminders @1@@@@@@@@@@@@@;ESP=Emitiendo recordatorio @1@@@@@@@@@@@@@;DEA=Mahnungen registrieren @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected reminders.\Do you want to see these reminders?;ESP=No se han podido emitir algunos de los recordatorios seleccionados.\¨Desea ver estos recordatorios?;DEA=Einige der ausgew„hlten Mahnungen konnten nicht registriert werden.\Sollen diese Mahnungen angezeigt werden?';
      IssuedReminderHeader@1005 : Record 297;
      ReminderIssue@1006 : Codeunit 393;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;
      FilterText@1004 : Text[1024];

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


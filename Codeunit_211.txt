OBJECT Codeunit 211 Res. Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=207;
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be a closing date;ESP=no puede ser una fecha £ltima;DEA=darf kein Ultimodatum sein';
      Text001@1001 : TextConst 'ENU=is not within your range of allowed posting dates;ESP=no est  dentro del periodo de fechas de registro permitidas;DEA=liegt nicht innerhalb des zugelassenen Buchungszeitraums.';
      Text002@1002 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;ESP=La combin. de dimensiones utilizada en %1 %2, %3, %4 est  bloq. %5;DEA=Die Kombination der im %1 %2, %3, %4 verwendeten Dimensionen ist gesperrt. %5';
      Text003@1003 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;ESP=La dimensi¢n util. en %1 %2, %3, %4 ha causado error. %5;DEA=Eine im %1 %2, %3, %4 verwendete Dimension hat einen Fehler hervorgerufen. %5';
      GLSetup@1004 : Record 98;
      UserSetup@1005 : Record 91;
      DimMgt@1007 : Codeunit 408;
      TimeSheetMgt@1006 : Codeunit 950;
      AllowPostingFrom@1008 : Date;
      AllowPostingTo@1009 : Date;

    PROCEDURE RunCheck@1(VAR ResJnlLine@1000 : Record 207);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH ResJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        TESTFIELD("Resource No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Gen. Prod. Posting Group");

        IF "Posting Date" <> NORMALDATE("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          IF USERID <> '' THEN
            IF UserSetup.GET(USERID) THEN BEGIN
              AllowPostingFrom := UserSetup."Allow Posting From";
              AllowPostingTo := UserSetup."Allow Posting To";
            END;
          IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
            GLSetup.GET;
            AllowPostingFrom := GLSetup."Allow Posting From";
            AllowPostingTo := GLSetup."Allow Posting To";
          END;
          IF AllowPostingTo = 0D THEN
            AllowPostingTo := 31129999D;
        END;
        IF ("Posting Date" < AllowPostingFrom) OR ("Posting Date" > AllowPostingTo) THEN
          FIELDERROR("Posting Date",Text001);

        IF "Document Date" <> 0D THEN
          IF "Document Date" <> NORMALDATE("Document Date") THEN
            FIELDERROR("Document Date",Text000);

        IF ("Entry Type" = "Entry Type"::Usage) AND ("Time Sheet No." <> '') THEN
          TimeSheetMgt.CheckResJnlLine(ResJnlLine);

        IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
          ERROR(
            Text002,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Resource;
        No[1] := "Resource No.";
        TableID[2] := DATABASE::"Resource Group";
        No[2] := "Resource Group No.";
        TableID[3] := DATABASE::Job;
        No[3] := "Job No.";
        IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
          IF "Line No." <> 0 THEN
            ERROR(
              Text003,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          ELSE
            ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 1107 Cost Allocation Target
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // Default value according to previous line
               "Allocation Target Type" := xRec."Allocation Target Type";
               IF xRec."Target Cost Type" <> '' THEN
                 "Target Cost Type" := xRec."Target Cost Type";

               Base := xRec.Base;
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    CaptionML=[ENU=Cost Allocation Target;
               ESP=Destino asignaci¢n costes;
               DEA=Kostenzuteilungsziel];
    LookupPageID=Page1114;
    DrillDownPageID=Page1114;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Code10        ;TableRelation="Cost Allocation Source";
                                                   CaptionML=[ENU=ID;
                                                              ESP=Id.;
                                                              DEA=ID];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea;
                                                              DEA=Zeilennr.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 5   ;   ;Target Cost Type    ;Code20        ;TableRelation="Cost Type";
                                                   CaptionML=[ENU=Target Cost Type;
                                                              ESP=Tipo coste destino;
                                                              DEA=Zielkostenart] }
    { 6   ;   ;Target Cost Center  ;Code20        ;TableRelation="Cost Center";
                                                   OnValidate=BEGIN
                                                                IF ("Target Cost Center" <> '') AND ("Target Cost Object" <> '') THEN
                                                                  ERROR(Text000);
                                                              END;

                                                   CaptionML=[ENU=Target Cost Center;
                                                              ESP=Centro coste destino;
                                                              DEA=Zielkostenstelle] }
    { 7   ;   ;Target Cost Object  ;Code20        ;TableRelation="Cost Object";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Target Cost Center")
                                                              END;

                                                   CaptionML=[ENU=Target Cost Object;
                                                              ESP=Objeto coste destino;
                                                              DEA=Zielkostentr„ger] }
    { 8   ;   ;Static Base         ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Static Weighting");
                                                              END;

                                                   CaptionML=[ENU=Static Base;
                                                              ESP=Base est tica;
                                                              DEA=Statische Bemessungsgrundlage];
                                                   DecimalPlaces=0:2;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 9   ;   ;Static Weighting    ;Decimal       ;OnValidate=BEGIN
                                                                IF Base = Base::Static THEN BEGIN
                                                                  Share := "Static Base" * "Static Weighting";
                                                                  VALIDATE(Share);
                                                                END ELSE BEGIN
                                                                  IF "Static Base" > 0 THEN
                                                                    ERROR(Text001,"Static Base");
                                                                  IF "Static Weighting" > 0 THEN
                                                                    ERROR(Text001,"Static Weighting");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Static Weighting;
                                                              ESP=Ponderaci¢n est tica;
                                                              DEA=Statische Gewichtung];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 10  ;   ;Share               ;Decimal       ;OnValidate=BEGIN
                                                                CalcPercent;
                                                              END;

                                                   CaptionML=[ENU=Share;
                                                              ESP=Acci¢n;
                                                              DEA=Anteil];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0 }
    { 11  ;   ;Percent             ;Decimal       ;CaptionML=[ENU=Percent;
                                                              ESP=Porcentaje;
                                                              DEA=Prozent];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 20  ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              ESP=Comentario;
                                                              DEA=Bemerkung] }
    { 30  ;   ;Base                ;Option        ;OnValidate=BEGIN
                                                                IF Base <> xRec.Base THEN BEGIN
                                                                  Share := 0;
                                                                  Percent := 0;
                                                                  "No. Filter" := '';
                                                                  "Group Filter" := '';
                                                                END;

                                                                IF Base = Base::Static THEN BEGIN
                                                                  "No. Filter" := '';
                                                                  "Cost Center Filter" := '';
                                                                  "Cost Object Filter" := '';
                                                                  "Date Filter Code" := 0;
                                                                  "Group Filter" := '';
                                                                END ELSE BEGIN
                                                                  "Static Base" := 0;
                                                                  "Static Weighting" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Base;
                                                              ESP=Base;
                                                              DEA=Bemessungsgrundlage];
                                                   OptionCaptionML=[ENU=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount);
                                                                    ESP=Est tica,Movs. CG,Movs ppto. CG,Movs. tipo coste,Movs. ppto. costes,,,,,N§ empleados,,Productos vendidos (cdad.),Productos comprados (cdad.),Productos vendidos (importe),Productos comprados(importe);
                                                                    DEA=Statisch,Sachposten,Finanzbudgetposten,Kostenartposten,Kostenbudgetposten,,,,,Mitarbeiteranzahl,,Verkaufte Artikel (Menge),Erworbene Artikel (Menge),Verkaufte Artikel (Betrag),Erworbene Artikel (Betrag)];
                                                   OptionString=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount) }
    { 31  ;   ;No. Filter          ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupNoFilter(SelectionFilter) THEN
                                                                VALIDATE("No. Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("No. Filter")));
                                                            END;

                                                   CaptionML=[ENU=No. Filter;
                                                              ESP=Filtro N§;
                                                              DEA=Nummernfilter] }
    { 32  ;   ;Cost Center Filter  ;Text250       ;TableRelation="Cost Center";
                                                   OnLookup=VAR
                                                              CostCenter@1000 : Record 1112;
                                                              SelectionFilter@1001 : Text[1024];
                                                            BEGIN
                                                              IF CostCenter.LookupCostCenterFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Center Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Center Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Center Filter;
                                                              ESP=Filtro centro coste;
                                                              DEA=Kostenstellenfilter] }
    { 33  ;   ;Cost Object Filter  ;Text250       ;TableRelation="Cost Object";
                                                   OnLookup=VAR
                                                              CostObject@1001 : Record 1113;
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF CostObject.LookupCostObjectFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Object Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Object Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Object Filter;
                                                              ESP=Filtro objeto coste;
                                                              DEA=Kostentr„gerfilter] }
    { 34  ;   ;Date Filter Code    ;Option        ;CaptionML=[ENU=Date Filter Code;
                                                              ESP=Filtro fecha vto.;
                                                              DEA=Datumsfiltercode];
                                                   OptionCaptionML=[ENU=" ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year";
                                                                    ESP=" ,Semana,Semana pasada,Mes,Mes pasado,Mes del a¤o pasado,A¤o,A¤o pasado,Periodo,éltimo periodo,Periodo del a¤o pasado,Ejercicio,éltimo ejercicio";
                                                                    DEA=" ,Woche,Vorwoche,Monat,Vormonat,Vorjahresmonat,Jahr,Vorjahr,Periode,Vorperiode,Vorjahresperiode,Gesch„ftsjahr,Vorheriges Gesch„ftsjahr"];
                                                   OptionString=[ ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year] }
    { 35  ;   ;Group Filter        ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupGroupFilter(SelectionFilter) THEN
                                                                VALIDATE("Group Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Group Filter")));
                                                            END;

                                                   CaptionML=[ENU=Group Filter;
                                                              ESP=Filtro grupo;
                                                              DEA=Gruppenfilter] }
    { 38  ;   ;Allocation Target Type;Option      ;OnValidate=BEGIN
                                                                IF "Allocation Target Type" <> xRec."Allocation Target Type" THEN BEGIN
                                                                  "Percent per Share" := 0;
                                                                  "Amount per Share" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allocation Target Type;
                                                              ESP=Tipo destino asignaci¢n;
                                                              DEA=Verteilungszielart];
                                                   OptionCaptionML=[ENU=All Costs,Percent per Share,Amount per Share;
                                                                    ESP=Todos los costes,Porcentaje por acci¢n,Importe por acci¢n;
                                                                    DEA=Alle Kosten,Prozent pro Aktie,Betrag pro Aktie];
                                                   OptionString=All Costs,Percent per Share,Amount per Share }
    { 40  ;   ;Percent per Share   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Percent per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Percent per Share";
                                                                  "Amount per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Percent per Share;
                                                              ESP=Porcentaje por acci¢n;
                                                              DEA=Prozent pro Aktie];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 41  ;   ;Amount per Share    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Amount per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Amount per Share";
                                                                  "Percent per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Amount per Share;
                                                              ESP=Importe por acci¢n;
                                                              DEA=Betrag pro Aktie];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 50  ;   ;Share Updated on    ;Date          ;CaptionML=[ENU=Share Updated on;
                                                              ESP=Acci¢n actualizada el d¡a;
                                                              DEA=Aktie aktualisiert am];
                                                   Editable=No }
    { 60  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n;
                                                              DEA=Korrigiert am];
                                                   Editable=No }
    { 61  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. usuario;
                                                              DEA=Benutzer-ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID,Line No.                             ;SumIndexFields=Share,Percent;
                                                   Clustered=Yes }
    {    ;Target Cost Type                         }
    {    ;Target Cost Center                       }
    {    ;Target Cost Object                       }
    {    ;ID,Allocation Target Type                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot define both cost center and cost object.;ESP=No se puede definir el centro de coste y el objeto de coste.;DEA=Kostenstelle und Kostentr„ger k”nnen nicht gemeinsam angegeben werden.';
      Text001@1001 : TextConst 'ENU=%1 can only be used with static allocations.;ESP=%1 s¢lo se puede usar con asignaciones est ticas.;DEA=''%1'' kann nur mit statischen Verteilungen verwendet werden.';

    PROCEDURE CalcPercent@1();
    VAR
      CostAllocationTarget@1002 : Record 1107;
      PctTotal@1000 : Decimal;
      TotalShare@1001 : Decimal;
    BEGIN
      WITH CostAllocationTarget DO BEGIN
        SETRANGE(ID,Rec.ID);
        SETFILTER("Line No.",'<>%1',Rec."Line No.");
        CALCSUMS(Share);
        TotalShare := Share + Rec.Share;

        IF TotalShare = 0 THEN BEGIN
          MODIFYALL(Percent,0);
          MODIFYALL("Share Updated on",TODAY);
          Rec.Percent := 0;
          Rec."Share Updated on" := TODAY;
          EXIT;
        END;

        IF FINDSET THEN
          REPEAT
            Percent := ROUND(100 * Share / TotalShare,0.00001);
            "Share Updated on" := TODAY;
            MODIFY;
          UNTIL NEXT = 0;
        Rec.Percent := ROUND(100 * Rec.Share / TotalShare,0.00001);
        Rec."Share Updated on" := TODAY;
        Rec.MODIFY;
        // distribute rounding error:
        CALCSUMS(Percent);
        PctTotal := Percent + Rec.Percent;
        IF PctTotal <> 100 THEN BEGIN
          IF (Rec."Line No." <> 0) AND FINDLAST AND ("Line No." > Rec."Line No.") THEN BEGIN
            Percent := Percent + (100 - PctTotal);
            MODIFY;
          END ELSE
            Rec.Percent := Rec.Percent + (100 - PctTotal);
        END;
      END;
    END;

    LOCAL PROCEDURE LookupNoFilter@2(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      CostType@1001 : Record 1103;
    BEGIN
      CASE Base OF
        Base::"G/L Entries",
        Base::"G/L Budget Entries":
          EXIT(CostType.LookupGLAccFilter(SelectionFilter));
        Base::"Cost Type Entries",
        Base::"Cost Budget Entries":
          EXIT(CostType.LookupCostTypeFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupItemFilter(SelectionFilter));
      END;
    END;

    PROCEDURE LookupItemFilter@14(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      ItemList@1001 : Page 31;
    BEGIN
      ItemList.LOOKUPMODE(TRUE);
      IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := ItemList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupGroupFilter@4(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    BEGIN
      CASE Base OF
        Base::"G/L Budget Entries":
          EXIT(LookupGLBudgetFilter(SelectionFilter));
        Base::"Cost Budget Entries":
          EXIT(LookupCostBudgetFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupInvtPostingGrFilter(SelectionFilter));
      END;
    END;

    PROCEDURE LookupGLBudgetFilter@6(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      GLBudgetNames@1001 : Page 121;
    BEGIN
      GLBudgetNames.LOOKUPMODE(TRUE);
      IF GLBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := GLBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE LookupCostBudgetFilter@7(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      CostBudgetNames@1001 : Page 1116;
    BEGIN
      CostBudgetNames.LOOKUPMODE(TRUE);
      IF CostBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := CostBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE LookupInvtPostingGrFilter@8(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      InvtPostingGroups@1001 : Page 112;
    BEGIN
      InvtPostingGroups.LOOKUPMODE(TRUE);
      IF InvtPostingGroups.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := InvtPostingGroups.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    BEGIN
    END.
  }
}


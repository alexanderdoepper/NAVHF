OBJECT Table 273 Bank Acc. Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Bank Account No.,Statement No.;
    OnInsert=BEGIN
               TESTFIELD("Bank Account No.");
               TESTFIELD("Statement No.");
               BankAccRecon.RESET;
               BankAccRecon.SETRANGE("Bank Account No.","Bank Account No.");
               IF PostedBankAccStmt.GET("Bank Account No.","Statement No.") THEN
                 ERROR(Text000,"Statement No.");
             END;

    OnDelete=BEGIN
               BankAccReconLine.RESET;
               BankAccReconLine.SETRANGE("Bank Account No.","Bank Account No.");
               BankAccReconLine.SETRANGE("Statement No.","Statement No.");
               BankAccReconLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Bank Acc. Reconciliation;
               ESP=Conciliaci¢n banco;
               DEA=Bankkontoabstimmung];
    LookupPageID=Page388;
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Statement No." = '' THEN BEGIN
                                                                  BankAcc.LOCKTABLE;
                                                                  BankAcc.GET("Bank Account No.");
                                                                  "Statement No." := INCSTR(BankAcc."Last Statement No.");
                                                                  "Balance Last Statement" := BankAcc."Balance Last Statement";
                                                                  BankAcc."Last Statement No." := "Statement No.";
                                                                  BankAcc.MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESP=N§ banco;
                                                              DEA=Bankkontonummer];
                                                   NotBlank=Yes }
    { 2   ;   ;Statement No.       ;Code20        ;OnValidate=BEGIN
                                                                TESTFIELD("Bank Account No.");
                                                                BankAcc.LOCKTABLE;
                                                                BankAcc.GET("Bank Account No.");
                                                                BankAcc."Last Statement No." := "Statement No.";
                                                                BankAcc.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Statement No.;
                                                              ESP=N§ extracto;
                                                              DEA=Auszugsnr.];
                                                   NotBlank=Yes }
    { 3   ;   ;Statement Ending Balance;Decimal   ;CaptionML=[ENU=Statement Ending Balance;
                                                              ESP=Saldo final extracto;
                                                              DEA=Auszug Schluss-Saldo];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 4   ;   ;Statement Date      ;Date          ;CaptionML=[ENU=Statement Date;
                                                              ESP=Fecha extracto;
                                                              DEA=Auszugsdatum] }
    { 5   ;   ;Balance Last Statement;Decimal     ;OnValidate=BEGIN
                                                                BankAcc.GET("Bank Account No.");
                                                                IF "Balance Last Statement" <> BankAcc."Balance Last Statement" THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       Text002,FALSE,
                                                                       FIELDCAPTION("Balance Last Statement"),BankAcc.FIELDCAPTION("Balance Last Statement"),
                                                                       BankAcc.TABLECAPTION)
                                                                  THEN
                                                                    "Balance Last Statement" := xRec."Balance Last Statement";
                                                              END;

                                                   CaptionML=[ENU=Balance Last Statement;
                                                              ESP=Saldo £lt. extracto;
                                                              DEA=Saldo letzter Auszug];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
  }
  KEYS
  {
    {    ;Bank Account No.,Statement No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Statement %1 already exists.;ESP=El extracto %1 ya existe.;DEA=Auszug %1 ist bereits vorhanden.';
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;ESP=No se puede cambiar el nombre a %1.;DEA=Sie k”nnen eine %1 nicht umbenennen.';
      Text002@1002 : TextConst 'ENU=%1 is different from %2 on the %3. Do you want to change the value?;ESP=%1 es diferente del %2 en el %3. ¨Confirma que desea cambiar el valor?;DEA=%1 unterscheidet sich von %2 auf dem %3. M”chten Sie den Wert „ndern?';
      BankAcc@1003 : Record 270;
      BankAccRecon@1004 : Record 273;
      BankAccReconLine@1005 : Record 274;
      PostedBankAccStmt@1006 : Record 275;

    PROCEDURE GetCurrencyCode@1() : Code[10];
    VAR
      BankAcc2@1000 : Record 270;
    BEGIN
      IF "Bank Account No." = BankAcc2."No." THEN
        EXIT(BankAcc2."Currency Code");

      IF BankAcc2.GET("Bank Account No.") THEN
        EXIT(BankAcc2."Currency Code");

      EXIT('');
    END;

    BEGIN
    END.
  }
}


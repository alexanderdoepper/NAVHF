OBJECT Table 325 VAT Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=22.05.13;
    Time=15:52:01;
    Modified=Yes;
    Version List=NAVW17.00,NAVDACH7.00,CIC001;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "VAT %" = 0 THEN
                 "VAT %" := GetVATPtc;
             END;

    CaptionML=[ENU=VAT Posting Setup;
               ESP=Config. grupos registro IVA;
               DEA=MwSt.-Buchungsmatrix Einr.];
    LookupPageID=Page472;
    DrillDownPageID=Page472;
  }
  FIELDS
  {
    { 1   ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              ESP=Grupo registro IVA neg.;
                                                              DEA=MwSt.-GeschÑftsbuchungsgruppe] }
    { 2   ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              ESP=Grupo registro IVA prod.;
                                                              DEA=MwSt.-Produktbuchungsgruppe] }
    { 3   ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              ESP=Tipo c†lculo IVA;
                                                              DEA=MwSt.-Berechnungsart];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    ESP=Normal,Reversi¢n,Total,Impto. venta,No sujeto;
                                                                    DEA=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax }
    { 4   ;   ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("VAT %"));
                                                                CheckVATIdentifier;
                                                              END;

                                                   CaptionML=[ENU=VAT %;
                                                              ESP=IVA+CE %;
                                                              DEA=MwSt. %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Unrealized VAT Type"));

                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  IF NOT GLSetup."Unrealized VAT" AND NOT GLSetup."Prepayment Unrealized VAT" THEN
                                                                    GLSetup.TESTFIELD("Unrealized VAT",TRUE)
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unrealized VAT Type;
                                                              ESP=Tipo IVA no realizado;
                                                              DEA=Unreal. MwSt.-Art];
                                                   OptionCaptionML=[ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)";
                                                                    ESP=" ,Porcentual,Inicial,Final,Inicial (pago completo),Final (pago completo)";
                                                                    DEA=" ,Prozentsatz,Erste,Letzte,Erste (ganz bezahlt),Letzte (ganz bezahlt)"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 6   ;   ;Adjust for Payment Discount;Boolean;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Adjust for Payment Discount"));

                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Adjust for Payment Disc.",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjust for Payment Discount;
                                                              ESP=Ajuste para dto. p.p.;
                                                              DEA=Skonto berichtigen] }
    { 7   ;   ;Sales VAT Account   ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Account"));

                                                                CheckGLAcc("Sales VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Account;
                                                              ESP=Cta. IVA por pagar;
                                                              DEA=Umsatzsteuerkonto] }
    { 8   ;   ;Sales VAT Unreal. Account;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Unreal. Account"));

                                                                CheckGLAcc("Sales VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Unreal. Account;
                                                              ESP=Cta. IVA por pagar no realizado;
                                                              DEA=Unreal. Umsatzsteuerkonto] }
    { 9   ;   ;Purchase VAT Account;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purchase VAT Account"));

                                                                CheckGLAcc("Purchase VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Purchase VAT Account;
                                                              ESP=Cta. IVA acreditable;
                                                              DEA=Vorsteuerkonto] }
    { 10  ;   ;Purch. VAT Unreal. Account;Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purch. VAT Unreal. Account"));

                                                                CheckGLAcc("Purch. VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Purch. VAT Unreal. Account;
                                                              ESP=Cta. IVA acreditable no realizado;
                                                              DEA=Unreal. Vorsteuerkonto] }
    { 11  ;   ;Reverse Chrg. VAT Acc.;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Acc.;
                                                              ESP=Cta. reversi¢n IVA;
                                                              DEA=Erwerbssteuerkonto] }
    { 12  ;   ;Reverse Chrg. VAT Unreal. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Unreal. Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Unreal. Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Unreal. Acc.;
                                                              ESP=Cta. reversi¢n IVA no realiz.;
                                                              DEA=Unreal. Erwerbssteuerkonto] }
    { 13  ;   ;VAT Identifier      ;Code10        ;OnValidate=BEGIN
                                                                "VAT %" := GetVATPtc;
                                                              END;

                                                   CaptionML=[ENU=VAT Identifier;
                                                              ESP=Identific. IVA;
                                                              DEA=MwSt.-Kennzeichen] }
    { 14  ;   ;EU Service          ;Boolean       ;CaptionML=[ENU=EU Service;
                                                              ESP=Servicio a UE;
                                                              DEA=EU-Service] }
    { 50000;  ;Invoice Text        ;Text50        ;CaptionML=[ENU=Invoice Text;
                                                              DEA=Rechnungstext];
                                                   Description=CIC001 }
  }
  KEYS
  {
    {    ;VAT Bus. Posting Group,VAT Prod. Posting Group;
                                                   Clustered=Yes }
    {    ;VAT Prod. Posting Group,VAT Bus. Posting Group }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be entered on the tax jurisdiction line when %2 is %3.;ESP=Se debe indicar %1 en l°nea jurisdicci¢n impuesto cuando %2 es %3.;DEA=%1 muss auf der SteuerzustÑndigkeitszeile angegeben werden, wenn die %2 %3 ist.';
      Text001@1001 : TextConst 'ENU="%1 = %2 has already been used for %3 = %4 in %5 for %6 = %7 and %8 = %9.";ESP="%1 = %2 ya ha sido utilizado por %3 = %4 en %5 para %6 = %7 y %8 = %9.";DEA="Das %1 = %2 wurde bereits fÅr %3 = %4 in der %5 fÅr die %6 = %7 und die %8 = %9 verwendet."';
      GLSetup@1002 : Record 98;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    PROCEDURE TestNotSalesTax@1(FromFieldName@1000 : Text[100]);
    BEGIN
      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        ERROR(
          Text000,
          FromFieldName,FIELDCAPTION("VAT Calculation Type"),
          "VAT Calculation Type");
    END;

    LOCAL PROCEDURE CheckVATIdentifier@14();
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETFILTER("VAT %",'<>%1',"VAT %");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF VATPostingSetup.FINDFIRST THEN
        ERROR(
          Text001,
          FIELDCAPTION("VAT Identifier"),VATPostingSetup."VAT Identifier",
          FIELDCAPTION("VAT %"),VATPostingSetup."VAT %",TABLECAPTION,
          FIELDCAPTION("VAT Bus. Posting Group"),VATPostingSetup."VAT Bus. Posting Group",
          FIELDCAPTION("VAT Prod. Posting Group"),VATPostingSetup."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE GetVATPtc@3() : Decimal;
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF NOT VATPostingSetup.FINDFIRST THEN
        VATPostingSetup."VAT %" := "VAT %";
      EXIT(VATPostingSetup."VAT %");
    END;

    BEGIN
    {
      CICERO CONSULTING

      VERSION TICKET NAME DATE       DESCRIPTION
      CIC001  xxxxxx PIC  2013-05-22 Added field "Invoice Text" to this table
    }
    END.
  }
}


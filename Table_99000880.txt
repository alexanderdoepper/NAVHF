OBJECT Table 99000880 Order Promising Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Order Promising Line;
               ESP=L°n. compr. entrega ped.;
               DEA=Lieferterminzusagenzeile];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=Nß mov.;
                                                              DEA=Lfd. Nr.] }
    { 10  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESP=Nß producto;
                                                              DEA=Artikelnr.] }
    { 11  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante;
                                                              DEA=Variantencode];
                                                   Editable=No }
    { 12  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almacÇn;
                                                              DEA=Lagerortcode];
                                                   Editable=No }
    { 13  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad;
                                                              DEA=Menge];
                                                   DecimalPlaces=0:5 }
    { 14  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida;
                                                              DEA=Einheitencode];
                                                   Editable=No }
    { 15  ;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ESP=Cdad. por unidad medida;
                                                              DEA=Menge pro Einheit];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 16  ;   ;Unavailable Quantity;Decimal       ;CaptionML=[ENU=Unavailable Quantity;
                                                              ESP=Cdad. no disponible;
                                                              DEA=Nicht verfÅgbare Menge];
                                                   DecimalPlaces=0:5 }
    { 17  ;   ;Quantity (Base)     ;Decimal       ;CaptionML=[ENU=Quantity (Base);
                                                              ESP=Cantidad (base);
                                                              DEA=Menge (Basis)];
                                                   DecimalPlaces=0:5 }
    { 18  ;   ;Unavailable Quantity (Base);Decimal;CaptionML=[ENU=Unavailable Quantity (Base);
                                                              ESP=Cdad. (base) no disponible;
                                                              DEA=Nicht verfÅgbare Menge (Basis)];
                                                   DecimalPlaces=0:5 }
    { 20  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.;
                                                              DEA=Herkunftsart];
                                                   OptionCaptionML=[ENU=" ,Sales,Requisition Line,Purchase,Item Journal,BOM Journal,Item Ledger Entry,Prod. Order Line,Prod. Order Component,Planning Line,Planning Component,Transfer,Service Order,Job";
                                                                    ESP=" ,Ventas,L°n. repos.,Compras,Diario prod.,Diario L.M.,Mov. prod.,L°n. orden producc.,Comp. orden producc.,Planif. l°n.,Planif. componente,Transfer.,Pedido servicio,Proyecto";
                                                                    DEA=" ,Verkauf,Bestellvorschlagszeile,Einkauf,Artikel Buch.-Blatt,StÅckliste Buch.-Blatt,Artikelposten,FA-Zeile,FA-Komponente,Planungszeile,Planungskomponente,Umlagerung,Serviceauftrag,Projekt"];
                                                   OptionString=[ ,Sales,Requisition Line,Purchase,Item Journal,BOM Journal,Item Ledger Entry,Prod. Order Line,Prod. Order Component,Planning Line,Planning Component,Transfer,Service Order,Job] }
    { 21  ;   ;Source Subtype      ;Integer       ;CaptionML=[ENU=Source Subtype;
                                                              ESP=Subtipo origen;
                                                              DEA=Herkunftsunterart] }
    { 22  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              ESP=Id. origen;
                                                              DEA=Herkunfts-ID] }
    { 23  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              ESP=Nombre secci¢n origen;
                                                              DEA=Herkunftsbuch.-Blattname] }
    { 25  ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              ESP=Nß l°n. origen;
                                                              DEA=Herkunftszeilennr.] }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n;
                                                              DEA=Beschreibung] }
    { 40  ;   ;Requested Delivery Date;Date       ;OnValidate=VAR
                                                                SalesLine@1000 : Record 37;
                                                                ServLine@1001 : Record 5902;
                                                                JobPlanningLine@1002 : Record 1003;
                                                              BEGIN
                                                                CASE "Source Type" OF
                                                                  "Source Type"::Sales:
                                                                    BEGIN
                                                                      SalesLine.GET("Source Subtype","Source ID","Source Line No.");
                                                                      "Requested Shipment Date" := CalcReqShipDate(SalesLine);
                                                                    END;
                                                                  "Source Type"::"Service Order":
                                                                    BEGIN
                                                                      ServLine.GET("Source Subtype","Source ID","Source Line No.");
                                                                      "Requested Shipment Date" := ServLine."Needed by Date";
                                                                    END;
                                                                  "Source Type"::Job:
                                                                    BEGIN
                                                                      JobPlanningLine.SETRANGE("Job No.","Source ID");
                                                                      JobPlanningLine.SETRANGE("Job Contract Entry No.","Source Line No.");
                                                                      JobPlanningLine.FINDFIRST;
                                                                      "Requested Shipment Date" := JobPlanningLine."Planning Date";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Requested Delivery Date;
                                                              ESP=Fecha entrega requerida;
                                                              DEA=GewÅnschtes Lieferdatum] }
    { 41  ;   ;Planned Delivery Date;Date         ;OnValidate=VAR
                                                                SalesLine@1000 : Record 37;
                                                                ServLine@1001 : Record 5902;
                                                                JobPlanningLine@1002 : Record 1003;
                                                              BEGIN
                                                                IF "Planned Delivery Date" <> 0D THEN
                                                                  CASE "Source Type" OF
                                                                    "Source Type"::Sales:
                                                                      BEGIN
                                                                        SalesLine.GET("Source Subtype","Source ID","Source Line No.");
                                                                        SalesLine.SuspendStatusCheck(TRUE);
                                                                        SalesLine.VALIDATE("Planned Delivery Date","Planned Delivery Date");
                                                                        "Planned Delivery Date" := SalesLine."Planned Delivery Date";
                                                                        "Earliest Shipment Date" := SalesLine."Shipment Date";
                                                                      END;
                                                                    "Source Type"::"Service Order":
                                                                      BEGIN
                                                                        ServLine.GET("Source Subtype","Source ID","Source Line No.");
                                                                        ServLine.VALIDATE("Planned Delivery Date","Planned Delivery Date");
                                                                        "Planned Delivery Date" := ServLine."Planned Delivery Date";
                                                                        "Earliest Shipment Date" := ServLine."Needed by Date";
                                                                      END;
                                                                    "Source Type"::Job:
                                                                      BEGIN
                                                                        JobPlanningLine.SETRANGE("Job No.","Source ID");
                                                                        JobPlanningLine.SETRANGE("Job Contract Entry No.","Source Line No.");
                                                                        JobPlanningLine.FINDFIRST;
                                                                        JobPlanningLine.VALIDATE("Planned Delivery Date","Planned Delivery Date");
                                                                        "Planned Delivery Date" := JobPlanningLine."Planned Delivery Date";
                                                                        "Earliest Shipment Date" := JobPlanningLine."Planning Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Planned Delivery Date;
                                                              ESP=Fecha entrega planificada;
                                                              DEA=Geplantes Lieferdatum] }
    { 42  ;   ;Original Shipment Date;Date        ;CaptionML=[ENU=Original Shipment Date;
                                                              ESP=Fecha env°o original;
                                                              DEA=Urspr. Warenausgangsdatum] }
    { 43  ;   ;Earliest Shipment Date;Date        ;OnValidate=VAR
                                                                SalesLine@1000 : Record 37;
                                                              BEGIN
                                                                CASE "Source Type" OF
                                                                  "Source Type"::Sales:
                                                                    IF "Earliest Shipment Date" <> 0D THEN BEGIN
                                                                      SalesLine.GET("Source Subtype","Source ID","Source Line No.");
                                                                      SalesLine."Shipment Date" := "Earliest Shipment Date";
                                                                      "Planned Delivery Date" := SalesLine.CalcPlannedDeliveryDate(SalesLine.FIELDNO("Shipment Date"));
                                                                    END;
                                                                  "Source Type"::"Service Order":
                                                                    IF "Earliest Shipment Date" <> 0D THEN
                                                                      "Planned Delivery Date" := "Earliest Shipment Date";
                                                                  "Source Type"::Job:
                                                                    IF "Earliest Shipment Date" <> 0D THEN
                                                                      "Planned Delivery Date" := "Earliest Shipment Date";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Earliest Shipment Date;
                                                              ESP=Primera fecha env°o;
                                                              DEA=FrÅhestmîg. Warenausgangsdatum] }
    { 44  ;   ;Requested Shipment Date;Date       ;CaptionML=[ENU=Requested Shipment Date;
                                                              ESP=Fecha de env°o solicitada;
                                                              DEA=GewÅnschtes Versanddatum];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Requested Shipment Date                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TransferFromSalesLine@2(VAR SalesLine@1000 : Record 37);
    BEGIN
      "Source Type" := "Source Type"::Sales;
      "Source Subtype" := SalesLine."Document Type";
      "Source ID" := SalesLine."Document No.";
      "Source Line No." := SalesLine."Line No.";

      "Item No." := SalesLine."No.";
      "Variant Code" := SalesLine."Variant Code";
      "Location Code" := SalesLine."Location Code";
      VALIDATE("Requested Delivery Date",SalesLine."Requested Delivery Date");
      "Original Shipment Date" := SalesLine."Shipment Date";
      Description := SalesLine.Description;
      Quantity := SalesLine."Outstanding Quantity";
      "Unit of Measure Code" := SalesLine."Unit of Measure Code";
      "Qty. per Unit of Measure" := SalesLine."Qty. per Unit of Measure";
      "Quantity (Base)" := SalesLine."Outstanding Qty. (Base)";
    END;

    PROCEDURE TransferFromServLine@3(VAR ServLine@1000 : Record 5902);
    BEGIN
      "Source Type" := "Source Type"::"Service Order";
      "Source Subtype" := ServLine."Document Type";
      "Source ID" := ServLine."Document No.";
      "Source Line No." := ServLine."Line No.";

      "Item No." := ServLine."No.";
      "Variant Code" := ServLine."Variant Code";
      "Location Code" := ServLine."Location Code";
      VALIDATE("Requested Delivery Date",ServLine."Requested Delivery Date");
      "Original Shipment Date" := ServLine."Needed by Date";
      Description := ServLine.Description;
      Quantity := ServLine."Outstanding Quantity";
      "Unit of Measure Code" := ServLine."Unit of Measure Code";
      "Qty. per Unit of Measure" := ServLine."Qty. per Unit of Measure";
      "Quantity (Base)" := ServLine."Outstanding Qty. (Base)";
    END;

    PROCEDURE TransferFromJobPlanningLine@4(VAR JobPlanningLine@1000 : Record 1003);
    BEGIN
      "Source Type" := "Source Type"::Job;
      "Source Subtype" := JobPlanningLine.Status;
      "Source ID" := JobPlanningLine."Job No.";
      "Source Line No." := JobPlanningLine."Job Contract Entry No.";

      "Item No." := JobPlanningLine."No.";
      "Variant Code" := JobPlanningLine."Variant Code";
      "Location Code" := JobPlanningLine."Location Code";
      VALIDATE("Requested Delivery Date",JobPlanningLine."Requested Delivery Date");
      "Original Shipment Date" := JobPlanningLine."Planning Date";
      Description := JobPlanningLine.Description;
      Quantity := JobPlanningLine."Remaining Qty.";
      "Unit of Measure Code" := JobPlanningLine."Unit of Measure Code";
      "Qty. per Unit of Measure" := JobPlanningLine."Qty. per Unit of Measure";
      "Quantity (Base)" := JobPlanningLine."Remaining Qty. (Base)";
    END;

    PROCEDURE CalcReqShipDate@1(SalesLine@1000 : Record 37) : Date;
    BEGIN
      IF (SalesLine."Requested Delivery Date" <> 0D) AND
         (SalesLine."Promised Delivery Date" = 0D)
      THEN BEGIN
        SalesLine.SuspendStatusCheck(TRUE);
        SalesLine.VALIDATE("Requested Delivery Date",SalesLine."Requested Delivery Date");
      END;
      EXIT(SalesLine."Shipment Date");
    END;

    PROCEDURE GetLastEntryNo@5() LastEntryNo : Integer;
    VAR
      CopyOfOrderPromisingLine@1000 : Record 99000880;
    BEGIN
      CopyOfOrderPromisingLine.COPY(Rec);
      RESET;
      IF FINDLAST THEN
        LastEntryNo := "Entry No.";
      COPY(CopyOfOrderPromisingLine);
    END;

    BEGIN
    END.
  }
}


OBJECT Table 99000771 Production BOM Header
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 MfgSetup.TESTFIELD("Production BOM Nos.");
                 NoSeriesMgt.InitSeries(MfgSetup."Production BOM Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Creation Date" := TODAY;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=BEGIN
               Item.SETCURRENTKEY("Production BOM No.");
               Item.SETRANGE("Production BOM No.","No.");
               IF Item.FINDFIRST THEN
                 ERROR(Text000);

               ProdBOMLine.SETRANGE("Production BOM No.","No.");
               ProdBOMLine.DELETEALL(TRUE);

               ProdBOMVersion.SETRANGE("Production BOM No.","No.");
               ProdBOMVersion.DELETEALL;

               MfgComment.SETRANGE("Table Name",MfgComment."Table Name"::"Production BOM Header");
               MfgComment.SETRANGE("No.","No.");
               MfgComment.DELETEALL;
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text002,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[ENU=Production BOM Header;
               ESP=Cab. L.M. producci｢n;
               DEA=Fert.-St…klistenkopf];
    LookupPageID=Page99000787;
    DrillDownPageID=Page99000787;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ESP=Nｧ;
                                                              DEA=Nr.] }
    { 10  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                "Search Name" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci｢n;
                                                              DEA=Beschreibung] }
    { 11  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESP=Descripci｢n 2;
                                                              DEA=Beschreibung 2] }
    { 12  ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              ESP=Alias;
                                                              DEA=Suchbegriff] }
    { 21  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                ItemUnitOfMeasure@1001 : Record 5404;
                                                              BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                                Item.SETCURRENTKEY("Production BOM No.");
                                                                Item.SETRANGE("Production BOM No.","No.");
                                                                IF Item.FINDSET THEN
                                                                  REPEAT
                                                                    ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code");
                                                                  UNTIL Item.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C｢d. unidad medida;
                                                              DEA=Einheitencode] }
    { 22  ;   ;Low-Level Code      ;Integer       ;CaptionML=[ENU=Low-Level Code;
                                                              ESP=C｢d. nivel ms bajo;
                                                              DEA=St…klistenebene];
                                                   Editable=No }
    { 25  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Manufacturing Comment Line" WHERE (Table Name=CONST(Production BOM Header),
                                                                                                         No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario;
                                                              DEA=Bemerkung];
                                                   Editable=No }
    { 40  ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creaci｢n;
                                                              DEA=Errichtungsdatum];
                                                   Editable=No }
    { 43  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha ｣lt. modificaci｢n;
                                                              DEA=Korrigiert am];
                                                   Editable=No }
    { 45  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                PlanningAssignment@1000 : Record 99000850;
                                                                MfgSetup@1004 : Record 99000765;
                                                                ProdBOMCheck@1001 : Codeunit 99000769;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  MfgSetup.LOCKTABLE;
                                                                  MfgSetup.GET;
                                                                  ProdBOMCheck.ProdBOMLineCheck("No.",'');
                                                                  "Low-Level Code" := 0;
                                                                  ProdBOMCheck.RUN(Rec);
                                                                  PlanningAssignment.NewBOM("No.");
                                                                END;
                                                                IF Status = Status::Closed THEN BEGIN
                                                                  IF CONFIRM(
                                                                       Text001,FALSE)
                                                                  THEN BEGIN
                                                                    ProdBOMVersion.SETRANGE("Production BOM No.","No.");
                                                                    IF ProdBOMVersion.FIND('-') THEN
                                                                      REPEAT
                                                                        ProdBOMVersion.Status := ProdBOMVersion.Status::Closed;
                                                                        ProdBOMVersion.MODIFY;
                                                                      UNTIL ProdBOMVersion.NEXT = 0;
                                                                  END ELSE
                                                                    Status := xRec.Status;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              ESP=Estado;
                                                              DEA=Status];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    ESP=Nueva,Certificada,En desarrollo,Cerrada;
                                                                    DEA=Neu,Zertifiziert,In Entwicklung,Abgelaufen];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 50  ;   ;Version Nos.        ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Version Nos.;
                                                              ESP=Nos. versi｢n;
                                                              DEA=Versionsnummern] }
    { 51  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie;
                                                              DEA=Nummernserie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Description                             ;KeyGroups=SearchCol }
    {    ;Status                                  ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Production BOM is being used on Items.;ESP=Esta L.M. producci｢n se utiliza en productos.;DEA=Diese Fertigungsst…kliste wird bei Artikeln verwendet.';
      Text001@1001 : TextConst 'ENU=All versions attached to the BOM will be closed. Close BOM?;ESP=Se cerrarn todas las versiones asociadas con la L.M. ｨSe cierra la L.M.?;DEA=Alle angekn｝ften Versionen der St…kliste werden auf Abgelaufen gestellt. Status auf abgelaufen stellen?';
      MfgSetup@1002 : Record 99000765;
      Item@1003 : Record 27;
      ProdBOMHeader@1004 : Record 99000771;
      ProdBOMVersion@1005 : Record 99000779;
      ProdBOMLine@1006 : Record 99000772;
      MfgComment@1007 : Record 99000770;
      NoSeriesMgt@1008 : Codeunit 396;
      Text002@1009 : TextConst 'ENU=You cannot rename the %1 when %2 is %3.;ESP=No puede renombrar el %1 cuando %2 es %3.;DEA=Sie k馬nen den %1 nicht umbenennen, wenn der %2 %3 ist.';

    PROCEDURE AssistEdit@2(OldProdBOMHeader@1000 : Record 99000771) : Boolean;
    BEGIN
      WITH ProdBOMHeader DO BEGIN
        ProdBOMHeader := Rec;
        MfgSetup.GET;
        MfgSetup.TESTFIELD("Production BOM Nos.");
        IF NoSeriesMgt.SelectSeries(MfgSetup."Production BOM Nos.",OldProdBOMHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ProdBOMHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}


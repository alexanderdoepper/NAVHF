OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Finance Charge Memos;
               ESP=Emitir doc. inter‚s;
               DEA=Zinsrechnungen registrieren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8733;    ;DataItem;                    ;
               DataItemTable=Table302;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Finance Charge Memo;
                                   DEA=Finance Charge Memo];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldTime := TIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(FinChrgMemoIssue);
                                  FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    FinChrgMemoIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewTime := TIME;
                                    IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldTime := TIME;
                                    END;
                                    MARK := NOT FinChrgMemoIssue.RUN;
                                  END;

                                  IF PrintDoc AND NOT MARK THEN BEGIN
                                    FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                                    IF FilterText <> '' THEN
                                      FilterText := FilterText + '|';
                                    FilterText := FilterText + IssuedFinChrgMemoHeader."No.";
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc THEN BEGIN
                                  IssuedFinChrgMemoHeader.SETFILTER("No.",FilterText);
                                  IssuedFinChrgMemoHeader.PrintRecords(FALSE);
                                END;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Finance Charge Memo Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Print;
                             ESP=Imprimir;
                             DEA=Drucken];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             ESP=Reemplazar fecha reg.;
                             DEA=Buchungsdatum ersetzen];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro;
                             DEA=Buchungsdatum];
                  SourceExpr=PostingDateReq }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESP=Introduzca la fecha registro.;DEA=Bitte das Buchungsdatum eingeben.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...;ESP=Emitiendo docs. inter‚s...;DEA=Zinsrechnung registrieren...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@;ESP=Emitiendo docs. inter‚s @1@@@@@@@@@@@@@;DEA=Zinsrechnungen registrieren @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?;ESP=No se han podido emitir algunos de los documentos de inter‚s seleccionados.\¨Desea ver estos documentos de inter‚s?;DEA=Einige der ausgew„hlten Zinsrechnungen konnten nicht registriert werden.\Sollen diese Zinsrechnungen angezeigt werden?';
      IssuedFinChrgMemoHeader@1005 : Record 304;
      FinChrgMemoIssue@1006 : Codeunit 395;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;
      FilterText@1017 : Text;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


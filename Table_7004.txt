OBJECT Table 7004 Sales Line Discount
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    OnRename=BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    CaptionML=[ENU=Sales Line Discount;
               ESP=Descuento l¡nea venta;
               DEA=Verkaufszeilenrabatt];
    LookupPageID=Page7004;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Item Disc. Group)) "Item Discount Group";
                                                   OnValidate=BEGIN
                                                                IF xRec.Code <> Code THEN BEGIN
                                                                  "Variant Code" := '';
                                                                  "Unit of Measure Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo;
                                                              DEA=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Disc. Group)) "Customer Discount Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF "Sales Code" <> '' THEN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Sales Code;
                                                              ESP=C¢digo ventas;
                                                              DEA=Verkaufscode] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa;
                                                              DEA=W„hrungscode] }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;
                                                                IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial;
                                                              DEA=Startdatum] }
    { 5   ;   ;Line Discount %     ;Decimal       ;CaptionML=[ENU=Line Discount %;
                                                              ESP=% Descuento l¡nea;
                                                              DEA=Zeilenrabatt %];
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   AutoFormatType=2 }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=[ENU=Sales Type;
                                                              ESP=Tipo venta;
                                                              DEA=Verkaufsart];
                                                   OptionCaptionML=[ENU=Customer,Customer Disc. Group,All Customers,Campaign;
                                                                    ESP=Cliente,Grupo dto. cliente,Todos los clientes,Campa¤a;
                                                                    DEA=Debitor,Debitorenrabattgruppe,Alle Debitoren,Kampagne];
                                                   OptionString=Customer,Customer Disc. Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[ENU=Minimum Quantity;
                                                              ESP=Cantidad m¡nima;
                                                              DEA=Mindestmenge];
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;
                                                                IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final;
                                                              DEA=Enddatum] }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF xRec.Type <> Type THEN
                                                                  VALIDATE(Code,'');
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo;
                                                              DEA=Art];
                                                   OptionCaptionML=[ENU=Item,Item Disc. Group;
                                                                    ESP=Producto,Grupo dto. pdto.;
                                                                    DEA=Artikel,Artikelrabattgruppe];
                                                   OptionString=Item,Item Disc. Group }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida;
                                                              DEA=Einheitencode] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante;
                                                              DEA=Variantencode] }
  }
  KEYS
  {
    {    ;Type,Code,Sales Type,Sales Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Sales Type,Sales Code,Type,Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2;ESP=%1 no puede ser posterior %2;DEA=Das %1 kann nicht nach dem %2 sein.';
      Text001@1003 : TextConst 'ENU=%1 must be blank.;ESP=%1 debe ser blanco.;DEA=%1 muss leer sein.';
      Campaign@1004 : Record 5071;
      Text003@1006 : TextConst 'ENU="You can only change the %1 and %2 from the Campaign Card when %3 = %4.";ESP="Solo puede modificar el %1 y %2 desde la Ficha campa¤a cuando %3 = %4.";DEA="Sie k”nnen %1 und %2 nur in der Kampagnenkarte „ndern, wenn %3 = %4 ist."';

    BEGIN
    END.
  }
}


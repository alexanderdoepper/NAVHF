OBJECT Table 7002 Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    OnRename=BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    CaptionML=[ENU=Sales Price;
               ESP=Precio venta;
               DEA=Verkaufspreis];
    LookupPageID=Page7002;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> xRec."Item No." THEN BEGIN
                                                                  "Unit of Measure Code" := '';
                                                                  "Variant Code" := '';
                                                                END;

                                                                IF "Sales Type" = "Sales Type"::"Customer Price Group" THEN
                                                                  IF CustPriceGr.GET("Sales Code") AND
                                                                     (CustPriceGr."Allow Invoice Disc." = "Allow Invoice Disc.")
                                                                  THEN
                                                                    EXIT;
                                                                IF Item.GET("Item No.") THEN
                                                                  "Allow Invoice Disc." := Item."Allow Invoice Disc.";
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=Nß producto;
                                                              DEA=Artikelnr.];
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Price Group)) "Customer Price Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF "Sales Code" <> '' THEN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::"Customer Price Group":
                                                                      BEGIN
                                                                        CustPriceGr.GET("Sales Code");
                                                                        "Price Includes VAT" := CustPriceGr."Price Includes VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := CustPriceGr."VAT Bus. Posting Gr. (Price)";
                                                                        "Allow Line Disc." := CustPriceGr."Allow Line Disc.";
                                                                        "Allow Invoice Disc." := CustPriceGr."Allow Invoice Disc.";
                                                                      END;
                                                                    "Sales Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Sales Code");
                                                                        "Currency Code" := Cust."Currency Code";
                                                                        "Price Includes VAT" := Cust."Prices Including VAT";
                                                                        "Allow Line Disc." := Cust."Allow Line Disc.";
                                                                      END;
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Sales Code;
                                                              ESP=C¢digo ventas;
                                                              DEA=Verkaufscode] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa;
                                                              DEA=WÑhrungscode] }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                IF "Starting Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial;
                                                              DEA=Startdatum] }
    { 5   ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              ESP=Precio venta;
                                                              DEA=VK-Preis];
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Price Includes VAT  ;Boolean       ;CaptionML=[ENU=Price Includes VAT;
                                                              ESP=Precio IVA incluido;
                                                              DEA=VK-Preis inkl. MwSt.] }
    { 10  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Invoice Disc.;
                                                              ESP=Permitir dto. factura;
                                                              DEA=Rech.-Rabatt zulassen] }
    { 11  ;   ;VAT Bus. Posting Gr. (Price);Code10;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Gr. (Price);
                                                              ESP=Gr.regis. IVA negocio (precio);
                                                              DEA=MwSt.-GeschÑftsbuch.-G.(Preis)] }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=[ENU=Sales Type;
                                                              ESP=Tipo venta;
                                                              DEA=Verkaufsart];
                                                   OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,Campaign;
                                                                    ESP=Cliente,Grupo precio cliente,Todos clientes,Campa§a;
                                                                    DEA=Debitor,Debitorenpreisgruppe,Alle Debitoren,Kampagne];
                                                   OptionString=Customer,Customer Price Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[ENU=Minimum Quantity;
                                                              ESP=Cantidad m°nima;
                                                              DEA=Mindestmenge];
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                VALIDATE("Starting Date");

                                                                IF "Ending Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final;
                                                              DEA=Enddatum] }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida;
                                                              DEA=Einheitencode] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante;
                                                              DEA=Variantencode] }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Line Disc.;
                                                              ESP=Permite dto. l°nea;
                                                              DEA=Zeilenrabatt zulassen] }
  }
  KEYS
  {
    {    ;Item No.,Sales Type,Sales Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Sales Type,Sales Code,Item No.,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CustPriceGr@1000 : Record 6;
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2;ESP=%1 no puede ser posterior %2;DEA=Das %1 kann nicht nach dem %2 sein.';
      Cust@1002 : Record 18;
      Text001@1003 : TextConst 'ENU=%1 must be blank.;ESP=%1 debe ser blanco.;DEA=%1 muss leer sein.';
      Campaign@1005 : Record 5071;
      Item@1004 : Record 27;
      Text002@1006 : TextConst 'ENU="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card.";ESP="Si el tipo de ventas es = %1, solo puede cambiar la Fecha de inicio y la Fecha final de la Ficha campa§a.";DEA="Bei Verkaufsart = ''%1'' kînnen Åber die Kampagnenkarte nur das Start- und das Enddatum geÑndert werden."';

    BEGIN
    END.
  }
}


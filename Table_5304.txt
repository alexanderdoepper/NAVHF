OBJECT Table 5304 Outlook Synch. Field
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Synch. Entity Code;
    OnInsert=BEGIN
               TESTFIELD("Field No.");

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");

               CheckDuplicatedRecords;

               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnModify=BEGIN
               TESTFIELD("Field No.");
               CheckDuplicatedRecords;

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");
             END;

    OnDelete=VAR
               OSynchFilter@1000 : Record 5303;
               OSynchOptionCorrel@1001 : Record 5307;
             BEGIN
               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchOptionCorrel.RESET;
               OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
               OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
               OSynchOptionCorrel.DELETEALL;
             END;

    CaptionML=[ENU=Outlook Synch. Field;
               ESP=Campo sinc. Outlook;
               DEA=Outlook-Synch.-Feld];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                GetMasterInformation;
                                                              END;

                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              ESP=C¢digo entidad sinc.;
                                                              DEA=Synch.-Einheitscode];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;TableRelation="Outlook Synch. Entity Element"."Element No." WHERE (Synch. Entity Code=FIELD(Synch. Entity Code));
                                                   CaptionML=[ENU=Element No.;
                                                              ESP=Nß elemento;
                                                              DEA=Elementnr.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=Nß l°nea;
                                                              DEA=Zeilennr.] }
    { 4   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Master Table No.;
                                                              ESP=Nß tabla principal;
                                                              DEA=Mastertabellennr.];
                                                   Editable=No }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[ENU=Outlook Object;
                                                              ESP=Objeto Outlook;
                                                              DEA=Outlook-Objekt];
                                                   Editable=No }
    { 6   ;   ;Outlook Property    ;Text80        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;

                                                                IF "Outlook Property" = xRec."Outlook Property" THEN
                                                                  EXIT;

                                                                IF NOT "User-Defined" AND ("Outlook Object" = '') THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    ERROR(
                                                                      Text009,
                                                                      FIELDCAPTION("Outlook Property"),
                                                                      OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                                END;
                                                                IF SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Outlook Property" := xRec."Outlook Property";
                                                                    "User-Defined" := xRec."User-Defined";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                IF "Outlook Property" <> '' THEN
                                                                  "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              PropertyName@1000 : Text[80];
                                                            BEGIN
                                                              IF "Outlook Object" = '' THEN BEGIN
                                                                IF "Element No." = 0 THEN
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                ERROR(
                                                                  Text009,
                                                                  FIELDCAPTION("Outlook Property"),
                                                                  OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                              END;

                                                              IF "User-Defined" THEN
                                                                ERROR(Text001);

                                                              IF "Element No." = 0 THEN
                                                                PropertyName := OSynchSetupMgt.ShowOItemProperties("Outlook Object")
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                PropertyName := OSynchSetupMgt.ShowOCollectionProperties(OSynchEntity."Outlook Item","Outlook Object");
                                                              END;

                                                              IF PropertyName <> '' THEN
                                                                VALIDATE("Outlook Property",PropertyName);
                                                            END;

                                                   CaptionML=[ENU=Outlook Property;
                                                              ESP=Propiedad Outlook;
                                                              DEA=Outlook-Eigenschaft] }
    { 7   ;   ;User-Defined        ;Boolean       ;OnValidate=BEGIN
                                                                "Outlook Property" := '';
                                                                IF NOT "User-Defined" THEN
                                                                  EXIT;

                                                                VALIDATE("Outlook Property");
                                                              END;

                                                   CaptionML=[ENU=User-Defined;
                                                              ESP=Definido por el usuario;
                                                              DEA=Benutzerdefiniert] }
    { 8   ;   ;Search Field        ;Boolean       ;CaptionML=[ENU=Search Field;
                                                              ESP=Campo b£squeda;
                                                              DEA=Suchfeld] }
    { 9   ;   ;Condition           ;Text250       ;CaptionML=[ENU=Condition;
                                                              ESP=Condici¢n;
                                                              DEA=Bedingung];
                                                   Editable=No }
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");

                                                                IF "Table No." = xRec."Table No." THEN
                                                                  EXIT;

                                                                IF NOT OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") THEN
                                                                  EXIT;

                                                                IF ("Table Relation" <> '') OR SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Table No." := xRec."Table No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;

                                                                  OSynchFilter.RESET;
                                                                  OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                  OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                                                  OSynchFilter.DELETEALL;

                                                                  "Table Relation" := '';
                                                                END;

                                                                "Field No." := 0;
                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              IF TableNo <> 0 THEN
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=[ENU=Table No.;
                                                              ESP=Nß tabla;
                                                              DEA=Tabellennr.];
                                                   BlankZero=Yes }
    { 11  ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla;
                                                              DEA=Tabellenname];
                                                   Editable=No }
    { 12  ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[ENU=Table Relation;
                                                              ESP=Relaci¢n de tabla;
                                                              DEA=Tabellenrelation];
                                                   Editable=No }
    { 13  ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                TESTFIELD("Field No.");
                                                                CheckReadOnlyStatus;

                                                                IF "Field No." = xRec."Field No." THEN
                                                                  EXIT;

                                                                IF "Table No." = 0 THEN
                                                                  Field.GET("Master Table No.","Field No.")
                                                                ELSE
                                                                  Field.GET("Table No.","Field No.");

                                                                IF Field.Class = Field.Class::FlowFilter THEN
                                                                  ERROR(Text002,Field.Class);

                                                                IF NOT Field.Enabled THEN
                                                                  ERROR(Text012,Field.FieldName);

                                                                IF "User-Defined" THEN
                                                                  VALIDATE("Outlook Property",Field."Field Caption");

                                                                IF SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Field No." := xRec."Field No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF "Table No." = 0 THEN
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.")
                                                              ELSE
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.");

                                                              IF FieldNo <> 0 THEN
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=Nß campo;
                                                              DEA=Feldnr.];
                                                   BlankZero=Yes }
    { 15  ;   ;Read-Only Status    ;Option        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[ENU=Read-Only Status;
                                                              ESP=Estado de s¢lo lectura;
                                                              DEA=Status 'SchreibgeschÅtzt'];
                                                   OptionCaptionML=[ENU=" ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook";
                                                                    ESP=" ,S¢lo lectura en Microsoft Dynamics NAV,S¢lo lectura en Outlook";
                                                                    DEA=" ,SchreibgeschÅtzt in Microsoft Dynamics NAV,SchreibgeschÅtzt in Outlook"];
                                                   OptionString=[ ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook];
                                                   Editable=No }
    { 16  ;   ;Field Default Value ;Text250       ;OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                                BooleanValue@1002 : Boolean;
                                                              BEGIN
                                                                TESTFIELD("Master Table No.");
                                                                TESTFIELD("Field No.");

                                                                IF "Field Default Value" = xRec."Field Default Value" THEN
                                                                  EXIT;

                                                                IF "Outlook Property" <> '' THEN
                                                                  ERROR(Text005,FIELDCAPTION("Field Default Value"),FIELDCAPTION("Outlook Property"));

                                                                CLEAR(RecRef);
                                                                CLEAR(FldRef);

                                                                IF "Table No." = 0 THEN BEGIN
                                                                  Field.GET("Master Table No.","Field No.");
                                                                  RecRef.OPEN("Master Table No.",TRUE);
                                                                END ELSE BEGIN
                                                                  Field.GET("Table No.","Field No.");
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                END;

                                                                IF Field.Class = Field.Class::FlowField THEN
                                                                  ERROR(Text010,0,Field.Class);

                                                                FldRef := RecRef.FIELD("Field No.");

                                                                IF Field.Type = Field.Type::Option THEN BEGIN
                                                                  IF NOT OSynchTypeConversion.EvaluateOptionField(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text004,"Field Default Value",FldRef.TYPE,FldRef.OPTIONCAPTION);
                                                                  DefaultValueExpression := FORMAT(OSynchTypeConversion.TextToOptionValue("Field Default Value",FldRef.OPTIONCAPTION))
                                                                END ELSE BEGIN
                                                                  IF NOT EVALUATE(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text003,FIELDCAPTION("Field Default Value"),FldRef.TYPE);

                                                                  IF Field.Type = Field.Type::Boolean THEN BEGIN
                                                                    EVALUATE(BooleanValue,"Field Default Value");
                                                                    IF BooleanValue THEN
                                                                      DefaultValueExpression := '1'
                                                                    ELSE
                                                                      DefaultValueExpression := '0';
                                                                  END;
                                                                END;

                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=Field Default Value;
                                                              ESP=Valor predeterminado campo;
                                                              DEA=Feldstandardwert] }
    { 17  ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              ESP=GUID registro;
                                                              DEA=Datensatz-GUID];
                                                   Editable=No }
    { 99  ;   ;DefaultValueExpression;Text250     ;CaptionML=[ENU=DefaultValueExpression;
                                                              ESP=DefaultValueExpression;
                                                              DEA=DefaultValueExpression] }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Line No. ;Clustered=Yes }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1001 : Record 2000000041;
      OSynchEntity@1003 : Record 5300;
      OSynchEntityElement@1004 : Record 5301;
      OSynchFilter@1019 : Record 5303;
      OSynchOptionCorrel@1013 : Record 5307;
      OSynchSetupMgt@1000 : Codeunit 5300;
      OSynchTypeConversion@1008 : Codeunit 5302;
      Text001@1015 : TextConst 'ENU=You cannot choose from a list of Outlook item collections for user-defined fields.;ESP=No puede seleccionar de una lista de colecciones de elementos de Outlook para campos definidos por el usuario.;DEA=FÅr benutzerdefinierte Felder kann keine Auswahl aus einer Liste der Outlook-Eintragssammlungen vorgenommen werden.';
      Text002@1012 : TextConst 'ENU=You cannot use a %1 field for synchronization.;ESP=No puede utilizar un campo %1 para la sincronizaci¢n.;DEA=Ein %1-Feld darf nicht fÅr die Synchronisierung verwendet werden.';
      Text003@1011 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.;ESP=El valor del campo %1 no se puede convertir al tipo de datos %2.;DEA=Der Wert des %1-Felds kann nicht in den %2-Datentyp konvertiert werden.';
      Text004@1010 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.\The possible option values are: ''%3''.;ESP=El valor del campo %1 no se puede convertir al tipo de datos %2.\Los valores de opci¢n posibles son: ''%3''.;DEA=Der Wert des %1-Felds kann nicht in den %2-Datentyp konvertiert werden.\Die mîglichen Werte fÅr die Option sind: ''%3''.';
      Text005@1009 : TextConst 'ENU=The %1 field should be blank when the %2 field is used.;ESP=El campo %1 debe estar vac°o cuando se utilice el campo %2.;DEA=Das %1-Feld muss leer sein, wenn das %2-Feld verwendet wird.';
      Text006@1006 : TextConst 'ENU=This is not a valid Outlook property name.;ESP=No es un nombre de propiedad de Outlook v†lido.;DEA=Dies ist kein gÅltiger Outlook-Eigenschaftsname.';
      Text007@1005 : TextConst 'ENU=You cannot synchronize the %1 and the %2 fields because they are both write protected.;ESP=No puede sincronizar los campos %1 y %2 porque est†n protegidos contra escritura.;DEA=Die %1- und die %2-Felder kînnen nicht synchronisiert werden, weil beide schreibgeschÅtzt sind.';
      Text008@1016 : TextConst 'ENU=If you change the value of this field, %1 and %2 records related to this entry will be removed.\Do you want to change this field anyway?;ESP=Si cambia el valor de este campo, se eliminar†n los registros %1 y %2 relacionados con este movimiento.\®Desea cambiar este campo de todos modos?;DEA=Wenn Sie den Wert dieses Felds Ñndern, werden die diesem Eintrag zugeordneten %1- und %2-DatensÑtze entfernt.\Mîchten Sie das Feld trotzdem Ñndern?';
      Text009@1018 : TextConst 'ENU=You cannot change the %1 field if the %2 is not specified for this entity.;ESP=No puede cambiar el campo %1 si %2 no se especific¢ para este objeto.;DEA=Sie kînnen das %1-Feld nicht Ñndern, wenn %2 fÅr diese Einheit nicht angegeben ist.';
      Text010@1020 : TextConst 'ENU=You cannot use this field for %2 fields.;ESP=No puede utilizar este campo para los campos %2.;DEA=Dieses Feld kann nicht fÅr %2-Felder verwendet werden.';
      Text011@1007 : TextConst 'ENU=The %1 table cannot be open, because the %2 or %3 fields are empty.\Fill in these fields with the appropriate values and try again.;ESP=La tabla %1 no se puede abrir porque los campos %2 o %3 est†n vac°os.\Cumplimente estos campos con los valores apropiados y vuelva a intentarlo.;DEA=Die %1-Tabelle darf nicht geîffnet sein, weil die %2- oder %3-Felder leer sind.\Setzen Sie die entsprechenden Werte in diese Felder ein, und wiederholen Sie den Vorgang.';
      Text012@1021 : TextConst 'ENU=You cannot select the %1 field because it is disabled.;ESP=No puede seleccionar el campo %1 porque est† deshabilitado.;DEA=Das %1-Feld kann nicht ausgewÑhlt werden, weil es deaktiviert ist.';
      Text013@1002 : TextConst 'ENU=You cannot use this value because an Outlook property with this name exists.;ESP=No puede utilizar este valor porque ya existe una propiedad de Outlook con este nombre.;DEA=Sie kînnen diesen Wert nicht verwenden, weil eine Outlook-Eigenschaft mit diesem Namen vorhanden ist.';
      Text014@1022 : TextConst 'ENU=The entry you are trying to create already exists.;ESP=El movimiento que intenta crear ya existe.;DEA=Der Eintrag, den Sie zu erstellen versuchen, ist bereits vorhanden.';

    LOCAL PROCEDURE GetMasterInformation@2();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Master Table No." := OSynchEntity."Table No.";
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Master Table No." := OSynchEntityElement."Table No.";
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;
    END;

    LOCAL PROCEDURE CheckReadOnlyStatus@9();
    VAR
      OSynchProcessLine@1002 : Codeunit 5305;
      IsReadOnlyOutlook@1000 : Boolean;
      IsReadOnlyNavision@1001 : Boolean;
    BEGIN
      IsReadOnlyOutlook := CheckOtlookPropertyName;

      IF ("Outlook Property" <> '') AND ("Field No." <> 0) THEN BEGIN
        IF "Table No." = 0 THEN BEGIN
          Field.GET("Master Table No.","Field No.");
          IF OSynchProcessLine.CheckKeyField("Master Table No.","Field No.") OR (Field.Class = Field.Class::FlowField) THEN
            IsReadOnlyNavision := TRUE;
        END ELSE BEGIN
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID","Record GUID");
          OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
          OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
          IF OSynchFilter.FIND('-') THEN
            REPEAT
              Field.GET(OSynchFilter."Master Table No.",OSynchFilter."Master Table Field No.");
              IF OSynchProcessLine.CheckKeyField("Master Table No.",OSynchFilter."Master Table Field No.") OR
                 (Field.Class = Field.Class::FlowField)
              THEN
                IsReadOnlyNavision := TRUE;
            UNTIL OSynchFilter.NEXT = 0;
        END;

        IF IsReadOnlyOutlook THEN BEGIN
          IF IsReadOnlyNavision THEN
            ERROR(Text007,"Outlook Property",GetFieldCaption);
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook";
        END ELSE BEGIN
          IF IsReadOnlyNavision THEN
            "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV"
          ELSE
            "Read-Only Status" := "Read-Only Status"::" ";
        END;
      END ELSE BEGIN
        IF "Field No." = 0 THEN
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook"
        ELSE
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV";
      END;
    END;

    LOCAL PROCEDURE CheckOtlookPropertyName@3() : Boolean;
    VAR
      IsReadOnly@1000 : Boolean;
    BEGIN
      IF "Outlook Property" = '' THEN
        EXIT(FALSE);

      IF "User-Defined" THEN
        IF "Element No." = 0 THEN BEGIN
          IF OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,TRUE) THEN
            ERROR(Text013);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               TRUE)
          THEN
            ERROR(Text013);
        END
      ELSE
        IF "Element No." = 0 THEN BEGIN
          IF NOT OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,FALSE) THEN
            ERROR(Text006);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF NOT
             OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               FALSE)
          THEN
            ERROR(Text006);
        END;

      EXIT(IsReadOnly);
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@4();
    VAR
      OSynchField@1000 : Record 5304;
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");
      OSynchField.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchField.SETRANGE("Outlook Property","Outlook Property");
      OSynchField.SETRANGE("Table No.","Table No.");
      OSynchField.SETRANGE("Field No.","Field No.");
      IF NOT OSynchField.ISEMPTY THEN
        ERROR(Text014);
    END;

    PROCEDURE ShowOOptionCorrelForm@1();
    BEGIN
      IF ("Field No." = 0) OR ("Outlook Property" = '') THEN
        ERROR(Text011,
          OSynchOptionCorrel.TABLECAPTION,
          FIELDCAPTION("Field No."),
          FIELDCAPTION("Outlook Property"));
      OSynchSetupMgt.ShowOOptionCorrelForm(Rec);
    END;

    LOCAL PROCEDURE SetOSynchOptionCorrelFilter@5(VAR outlookSynchOptionCorrel@1000 : Record 5307) : Boolean;
    BEGIN
      outlookSynchOptionCorrel.RESET;
      outlookSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      outlookSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      outlookSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
      EXIT(NOT outlookSynchOptionCorrel.ISEMPTY);
    END;

    PROCEDURE GetFieldCaption@6() : Text;
    BEGIN
      IF "Table No." <> 0 THEN BEGIN
        IF Field.GET("Table No.","Field No.") THEN
          EXIT(Field."Field Caption")
      END ELSE
        IF Field.GET("Master Table No.","Field No.") THEN
          EXIT(Field."Field Caption");

      EXIT('');
    END;

    BEGIN
    END.
  }
}


OBJECT Report 81 Import Budget from Excel
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00,NAVDACH7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Budget from Excel;
               ESP=Importar ppto. de Excel;
               DEA=Budget aus Excel importieren];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  BusUnit@1000 : Record 220;
                BEGIN
                  IF ToGLBudgetName = '' THEN
                    ERROR(STRSUBSTNO(Text000));

                  BusUnitDimCode := 'BUSINESSUNIT_TAB220';
                  TempDim.INIT;
                  TempDim.Code := BusUnitDimCode;
                  TempDim."Code Caption" := UPPERCASE(BusUnit.TABLECAPTION);
                  TempDim.INSERT;

                  IF Dim.FIND('-') THEN BEGIN
                    REPEAT
                      TempDim.INIT;
                      TempDim := Dim;
                      TempDim."Code Caption" := UPPERCASE(TempDim."Code Caption");
                      TempDim.INSERT;
                    UNTIL Dim.NEXT = 0;
                  END;

                  IF GLAcc.FIND('-') THEN BEGIN
                    REPEAT
                      TempGLAcc.INIT;
                      TempGLAcc := GLAcc;
                      TempGLAcc.INSERT;
                    UNTIL GLAcc.NEXT = 0;
                  END;

                  ExcelBuf.LOCKTABLE;
                  BudgetBuf.LOCKTABLE;
                  GLBudgetEntry.SETRANGE("Budget Name",ToGLBudgetName);
                  IF NOT GLBudgetName.GET(ToGLBudgetName) THEN
                    CLEAR(GLBudgetName);

                  GLSetup.GET;
                  GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                  GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                  BudgetDim1Code := GLBudgetName."Budget Dimension 1 Code";
                  BudgetDim2Code := GLBudgetName."Budget Dimension 2 Code";
                  BudgetDim3Code := GLBudgetName."Budget Dimension 3 Code";
                  BudgetDim4Code := GLBudgetName."Budget Dimension 4 Code";

                  ExcelBuf.OpenBook(ServerFileName,SheetName);
                  ExcelBuf.ReadSheet;

                  AnalyzeData;
                END;

    OnPostReport=BEGIN
                   ExcelBuf.DELETEALL;
                   BudgetBuf.DELETEALL;
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;BudgetBuf           ;
               DataItemTable=Table371;
               DataItemTableView=SORTING(G/L Account No.,Dimension Value Code 1,Dimension Value Code 2,Dimension Value Code 3,Dimension Value Code 4,Dimension Value Code 5,Dimension Value Code 6,Dimension Value Code 7,Dimension Value Code 8,Date);
               OnPreDataItem=BEGIN
                               RecNo := 0;

                               GLBudgetName.SETRANGE(Name,ToGLBudgetName);
                               IF NOT GLBudgetName.FINDFIRST THEN BEGIN
                                 IF NOT CONFIRM(Text001,FALSE,GLBudgetName.TABLECAPTION,ToGLBudgetName)
                                 THEN
                                   CurrReport.BREAK;
                                 GLBudgetName.Name := ToGLBudgetName;
                                 GLBudgetName.INSERT;
                               END ELSE BEGIN
                                 IF GLBudgetName.Blocked THEN BEGIN
                                   MESSAGE(Text002,
                                     GLBudgetEntry.FIELDCAPTION("Budget Name"),ToGLBudgetName);
                                   CurrReport.BREAK;
                                 END;
                                 IF NOT CONFIRM(Text003,FALSE,FORMAT(SELECTSTR(ImportOption + 1,Text027)),ToGLBudgetName) THEN
                                   CurrReport.BREAK;
                               END;

                               IF GLBudgetEntry3.FINDLAST THEN
                                 EntryNo := GLBudgetEntry3."Entry No." + 1
                               ELSE
                                 EntryNo := 1;
                               LastEntryNoBeforeImport := GLBudgetEntry3."Entry No.";
                             END;

               OnAfterGetRecord=BEGIN
                                  RecNo := RecNo + 1;

                                  IF ImportOption = ImportOption::"Replace entries" THEN BEGIN
                                    GLBudgetEntry.SETRANGE("G/L Account No.","G/L Account No.");
                                    GLBudgetEntry.SETRANGE(Date,Date);
                                    GLBudgetEntry.SETFILTER("Entry No.",'<=%1',LastEntryNoBeforeImport);
                                    IF DimCode[1] <> '' THEN
                                      SetBudgetDimFilter(DimCode[1],"Dimension Value Code 1",GLBudgetEntry);
                                    IF DimCode[2] <> '' THEN
                                      SetBudgetDimFilter(DimCode[2],"Dimension Value Code 2",GLBudgetEntry);
                                    IF DimCode[3] <> '' THEN
                                      SetBudgetDimFilter(DimCode[3],"Dimension Value Code 3",GLBudgetEntry);
                                    IF DimCode[4] <> '' THEN
                                      SetBudgetDimFilter(DimCode[4],"Dimension Value Code 4",GLBudgetEntry);
                                    IF DimCode[5] <> '' THEN
                                      SetBudgetDimFilter(DimCode[5],"Dimension Value Code 5",GLBudgetEntry);
                                    IF DimCode[6] <> '' THEN
                                      SetBudgetDimFilter(DimCode[6],"Dimension Value Code 6",GLBudgetEntry);
                                    IF DimCode[7] <> '' THEN
                                      SetBudgetDimFilter(DimCode[7],"Dimension Value Code 7",GLBudgetEntry);
                                    IF DimCode[8] <> '' THEN
                                      SetBudgetDimFilter(DimCode[8],"Dimension Value Code 8",GLBudgetEntry);
                                    IF NOT GLBudgetEntry.ISEMPTY THEN
                                      GLBudgetEntry.DELETEALL(TRUE);
                                  END;

                                  IF Amount = 0 THEN
                                    CurrReport.SKIP;
                                  IF NOT PostingAccount("G/L Account No.") THEN
                                    CurrReport.SKIP;
                                  GLBudgetEntry.INIT;
                                  GLBudgetEntry."Entry No." := EntryNo;
                                  GLBudgetEntry."Budget Name" := ToGLBudgetName;
                                  GLBudgetEntry."G/L Account No." := "G/L Account No.";
                                  GLBudgetEntry.Date := Date;
                                  GLBudgetEntry.Amount := ROUND(Amount);
                                  GLBudgetEntry.Description := Description;

                                  // Clear any entries in the temporary dimension set entry table
                                  IF NOT TempDimSetEntry.ISEMPTY THEN
                                    TempDimSetEntry.DELETEALL(TRUE);

                                  IF "Dimension Value Code 1" <> '' THEN
                                    InsertGLBudgetDim(DimCode[1],"Dimension Value Code 1",GLBudgetEntry);
                                  IF "Dimension Value Code 2" <> '' THEN
                                    InsertGLBudgetDim(DimCode[2],"Dimension Value Code 2",GLBudgetEntry);
                                  IF "Dimension Value Code 3" <> '' THEN
                                    InsertGLBudgetDim(DimCode[3],"Dimension Value Code 3",GLBudgetEntry);
                                  IF "Dimension Value Code 4" <> '' THEN
                                    InsertGLBudgetDim(DimCode[4],"Dimension Value Code 4",GLBudgetEntry);
                                  IF "Dimension Value Code 5" <> '' THEN
                                    InsertGLBudgetDim(DimCode[5],"Dimension Value Code 5",GLBudgetEntry);
                                  IF "Dimension Value Code 6" <> '' THEN
                                    InsertGLBudgetDim(DimCode[6],"Dimension Value Code 6",GLBudgetEntry);
                                  IF "Dimension Value Code 7" <> '' THEN
                                    InsertGLBudgetDim(DimCode[7],"Dimension Value Code 7",GLBudgetEntry);
                                  IF "Dimension Value Code 8" <> '' THEN
                                    InsertGLBudgetDim(DimCode[8],"Dimension Value Code 8",GLBudgetEntry);
                                  GLBudgetEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  GLBudgetEntry.INSERT;
                                  EntryNo := EntryNo + 1;
                                END;

               OnPostDataItem=BEGIN
                                IF RecNo > 0 THEN
                                  MESSAGE(Text004,GLBudgetEntry.TABLECAPTION,RecNo);

                                IF ImportOption = ImportOption::"Replace entries" THEN BEGIN
                                  AnalysisView.SETRANGE("Include Budgets",TRUE);
                                  IF AnalysisView.FINDSET(TRUE,FALSE) THEN
                                    REPEAT
                                      AnalysisView.AnalysisviewBudgetReset;
                                      AnalysisView.MODIFY;
                                    UNTIL AnalysisView.NEXT = 0;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   FileName := '';
                   SheetName := '';
                   Description := Text005 + FORMAT(WORKDATE);
                 END;

      OnQueryClosePage=BEGIN
                         IF CloseAction = ACTION::OK THEN
                           ValidateServerFileName;
                       END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 12  ;2   ;Group     ;
                  CaptionML=[ENU=Import from;
                             ESP=Importar de;
                             DEA=Importieren aus] }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Workbook File Name;
                             ESP=Nombre fichero libro;
                             DEA=Arbeitsmappendateiname];
                  SourceExpr=FileName;
                  Editable=FALSE;
                  OnValidate=BEGIN
                               FileNameOnAfterValidate;
                             END;

                  OnAssistEdit=BEGIN
                                 RequestFile;
                                 SheetName := ExcelBuf.SelectSheetsName(ServerFileName);
                               END;
                                }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Worksheet Name;
                             ESP=Nombre hoja;
                             DEA=Tabellenname];
                  SourceExpr=SheetName;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 IF ServerFileName = '' THEN
                                   RequestFile;

                                 SheetName := ExcelBuf.SelectSheetsName(ServerFileName);
                               END;
                                }

      { 11  ;2   ;Group     ;
                  CaptionML=[ENU=Import to;
                             ESP=Importar a;
                             DEA=Importieren nach] }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.;
                             DEA=Budgetname];
                  SourceExpr=ToGLBudgetName;
                  TableRelation="G/L Budget Name" }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Option;
                             ESP=Opci¢n;
                             DEA=Option];
                  OptionCaptionML=[ENU=Replace entries,Add entries;
                                   ESP=Reemplazar movs.,A§adir movs.;
                                   DEA=Posten ersetzen,Posten hinzufÅgen];
                  SourceExpr=ImportOption }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=Description;
                             ESP=Descripci¢n;
                             DEA=Beschreibung];
                  SourceExpr=Description }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify a budget name to import to.;ESP=Debe especif. nombre de ppto. para importar a.;DEA=Sie mÅssen einen Budgetnamen als Ziel fÅr den Import angeben.';
      Text001@1001 : TextConst 'ENU=Do you want to create a %1 with the name %2?;ESP=®Desea crear un %1 con el nombre %2?;DEA=Mîchten Sie ein Element vom Typ ''%1'' mit dem Namen ''%2'' erstellen?';
      Text002@1002 : TextConst 'ENU=%1 %2 is blocked. You cannot import entries.;ESP=%1 %2 est† bloqueado. No puede importar movs.;DEA=%1 %2 ist gesperrt. Sie kînnen keine Posten importieren.';
      Text003@1003 : TextConst 'ENU=Are you sure that you want to %1 for the budget name %2?;ESP=®Est† seguro de que desea %1 para el nombre ppto. %2?;DEA=Mîchten Sie fÅr den Budgetnamen ''%2'' wirklich die folgende Aktion ausfÅhren: %1?';
      Text004@1004 : TextConst 'ENU=%1 table has been successfully updated with %2 entries.;ESP=%1 tabla se ha actualizado correctamente con %2 movs.;DEA=Die Tabelle %1 wurde erfolgreich mit %2 Posten aktualisiert.';
      Text005@1005 : TextConst 'ENU="Imported from Excel ";ESP="Importado de Excel ";DEA="Aus Excel importiert "';
      Text006@1006 : TextConst 'ENU=Import Excel File;ESP=Importar fich. Excel;DEA=Exceldatei importieren';
      Text007@1007 : TextConst 'ENU=Analyzing Data...\\;ESP=Analizar Datos...\\;DEA=Daten werden analysiert ...\\';
      Text008@1008 : TextConst 'ENU=You cannot specify more than 8 dimensions in your Excel worksheet.;ESP=No puede especificar m†s de 8 dimensiones en su hoja de Excel.;DEA=Sie kînnen in Ihrer Exceltabelle nicht mehr als acht Dimensionen festlegen.';
      Text009@1009 : TextConst 'ENU=G/L ACCOUNT NO;ESP=Nß CUENTA;DEA=SACHKONTONR.';
      Text010@1010 : TextConst 'ENU=G/L Account No.;ESP=Nß cuenta;DEA=Sachkontonr.';
      Text011@1011 : TextConst 'ENU=The text G/L Account No. can only be specified once in the Excel worksheet.;ESP=El texto de nos. cuenta s¢lo puede ser especif. una vez en la hoja de Excel.;DEA=Der Text Sachkontonr. kann in der Exceltabelle nur einmal angegeben werden.';
      Text012@1012 : TextConst 'ENU=The dimensions specified by worksheet must be placed in the lines before the table.;ESP=Las dim. especificadas en la hoja deben reemplazarse en las l°n. antes de la tabla.;DEA=Die in der Exceltabelle angegebenen Dimensionen mÅssen in die Zeilen vor der Tabelle Åbertragen werden.';
      Text013@1013 : TextConst 'ENU="Dimension ";ESP="Dimensi¢n ";DEA="Dimension "';
      Text014@1014 : TextConst 'ENU=Date;ESP=Fecha;DEA=Datum';
      Text015@1015 : TextConst 'ENU=Dimension 1;ESP=Dimensi¢n 1;DEA=Dimension 1';
      Text016@1016 : TextConst 'ENU=Dimension 2;ESP=Dimensi¢n 2;DEA=Dimension 2';
      Text017@1017 : TextConst 'ENU=Dimension 3;ESP=Dimensi¢n 3;DEA=Dimension 3';
      Text018@1018 : TextConst 'ENU=Dimension 4;ESP=Dimensi¢n 4;DEA=Dimension 4';
      Text019@1019 : TextConst 'ENU=Dimension 5;ESP=Dimensi¢n 5;DEA=Dimension 5';
      Text020@1020 : TextConst 'ENU=Dimension 6;ESP=Dimensi¢n 6;DEA=Dimension 6';
      Text021@1021 : TextConst 'ENU=Dimension 7;ESP=Dimensi¢n 7;DEA=Dimension 7';
      Text022@1022 : TextConst 'ENU=Dimension 8;ESP=Dimensi¢n 8;DEA=Dimension 8';
      Text023@1023 : TextConst 'ENU=You cannot import the same information twice.\;ESP=No puede importar la misma info. dos veces.\;DEA=Sie kînnen dieselben Daten nicht ein zweites Mal importieren.\';
      Text024@1024 : TextConst 'ENU=The combination G/L Account No. - Dimensions - Date must be unique.;ESP=La combinaci¢n no. cuenta - dimensiones - fecha debe ser £nica.;DEA=Die Kombination Sachkontonr. - Dimensionen - Datum muss eindeutig sein.';
      Text025@1025 : TextConst 'ENU=G/L Accounts have not been found in the Excel worksheet.;ESP=Las cuentas no se han encontrado en la hoja de Excel.;DEA=Es wurden keine Sachkonten in der Exceltabelle gefunden.';
      Text026@1026 : TextConst 'ENU=Dates have not been recognized in the Excel worksheet.;ESP=Las fechas no se han reorganizado en la hoja de Excel.;DEA=Es wurden keine Daten in der Exceltabelle gefunden.';
      ExcelBuf@1027 : Record 370;
      Dim@1028 : Record 348;
      TempDim@1030 : TEMPORARY Record 348;
      GLBudgetEntry@1032 : Record 96;
      TempDimSetEntry@1029 : TEMPORARY Record 480;
      GLSetup@1034 : Record 98;
      GLAcc@1035 : Record 15;
      TempGLAcc@1036 : TEMPORARY Record 15;
      GLBudgetName@1048 : Record 95;
      GLBudgetEntry3@1049 : Record 96;
      AnalysisView@1057 : Record 363;
      DimMgt@1061 : Codeunit 408;
      FileMgt@1059 : Codeunit 419;
      FileName@1037 : Text;
      ServerFileName@1033 : Text;
      SheetName@1038 : Text[250];
      ToGLBudgetName@1039 : Code[10];
      DimCode@1040 : ARRAY [8] OF Code[20];
      EntryNo@1041 : Integer;
      LastEntryNoBeforeImport@1058 : Integer;
      GlobalDim1Code@1042 : Code[20];
      GlobalDim2Code@1043 : Code[20];
      TotalRecNo@1044 : Integer;
      RecNo@1045 : Integer;
      Window@1046 : Dialog;
      Description@1047 : Text[50];
      BusUnitDimCode@1031 : Code[20];
      BudgetDim1Code@1050 : Code[20];
      BudgetDim2Code@1051 : Code[20];
      BudgetDim3Code@1052 : Code[20];
      BudgetDim4Code@1053 : Code[20];
      ImportOption@1054 : 'Replace entries,Add entries';
      Text027@1055 : TextConst 'ENU=Replace entries,Add entries;ESP=Reemplazar movs.,A§adir movs.;DEA=Posten ersetzen,Posten hinzufÅgen';
      Text028@1056 : TextConst 'ENU=A filter has been used on the %1 when the budget was exported. When a filter on a dimension has been used, a column with the same dimension must be present in the worksheet imported. The column in the worksheet must specify the dimension value codes the program should use when importing the budget.;ESP=Se utiliz¢ un filtro en el %1 cuando se export¢ el presupuesto. Si utiliza un filtro en una dimensi¢n, debe presentarse una columna con la misma dimensi¢n en la hoja importada. La columna debe especificar los c¢digos de valor de la dimensi¢n que el programa debe utilizar al importar el presupuesto.;DEA=Beim Exportieren des Budgets wurde ein Filter auf %1 verwendet. Wenn ein Filter auf einer Dimension verwendet wurde, muss in der importierten Tabelle eine Spalte mit derselben Dimension vorhanden sein.';
      Text029@1060 : TextConst 'ENU=You must enter a file name.;ESP=Debe introd. nombre fichero.;DEA=Sie mÅssen einen Dateinamen angeben.';

    LOCAL PROCEDURE AnalyzeData@2();
    VAR
      TempExcelBuf@1000 : TEMPORARY Record 370;
      BudgetBuf@1001 : Record 371;
      TempBudgetBuf@1002 : TEMPORARY Record 371;
      HeaderRowNo@1003 : Integer;
      CountDim@1004 : Integer;
      TestDateTime@1005 : DateTime;
      OldRowNo@1006 : Integer;
      DimRowNo@1007 : Integer;
      DimCode3@1008 : Code[20];
    BEGIN
      Window.OPEN(
        Text007 +
        '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      Window.UPDATE(1,0);
      TotalRecNo := ExcelBuf.COUNT;
      RecNo := 0;
      BudgetBuf.DELETEALL;

      IF ExcelBuf.FIND('-') THEN BEGIN
        REPEAT
          RecNo := RecNo + 1;
          Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
          TempDim.SETRANGE("Code Caption",UPPERCASE(FormatData(ExcelBuf."Cell Value as Text")));
          CASE TRUE OF
            (STRPOS(UPPERCASE(ExcelBuf."Cell Value as Text"),Text009) <> 0):
              BEGIN
                IF HeaderRowNo = 0 THEN BEGIN
                  HeaderRowNo := ExcelBuf."Row No.";
                  TempExcelBuf := ExcelBuf;
                  TempExcelBuf.Comment := Text010;
                  TempExcelBuf.INSERT;
                END ELSE
                  ERROR(Text011);
              END;
            TempDim.FINDFIRST AND (ExcelBuf."Column No." > 2) AND (ExcelBuf."Row No." <> HeaderRowNo):
              BEGIN
                IF HeaderRowNo <> 0 THEN
                  ERROR(Text012);

                TempDim.MARK(TRUE);
                DimRowNo := ExcelBuf."Row No.";
                DimCode3 := TempDim.Code;
              END;
            (ExcelBuf."Row No." = DimRowNo) AND (ImportOption = ImportOption::"Replace entries"):
              CASE DimCode3 OF
                BusUnitDimCode:
                  GLBudgetEntry.SETFILTER("Business Unit Code",ExcelBuf."Cell Value as Text");
                GlobalDim1Code:
                  GLBudgetEntry.SETFILTER("Global Dimension 1 Code",ExcelBuf."Cell Value as Text");
                GlobalDim2Code:
                  GLBudgetEntry.SETFILTER("Global Dimension 2 Code",ExcelBuf."Cell Value as Text");
                BudgetDim1Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 1 Code",ExcelBuf."Cell Value as Text");
                BudgetDim2Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 2 Code",ExcelBuf."Cell Value as Text");
                BudgetDim3Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 3 Code",ExcelBuf."Cell Value as Text");
                BudgetDim4Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 4 Code",ExcelBuf."Cell Value as Text");
              END;
            ExcelBuf."Row No." = HeaderRowNo:
              BEGIN
                TempExcelBuf := ExcelBuf;
                CASE TRUE OF
                  TempDim.FINDFIRST:
                    BEGIN
                      TempDim.MARK(FALSE);
                      CountDim := CountDim + 1;
                      IF CountDim > 8 THEN
                        ERROR(Text008);
                      TempExcelBuf.Comment := Text013 + FORMAT(CountDim);
                      TempExcelBuf.INSERT;
                      DimCode[CountDim] := TempDim.Code;
                    END;
                  EVALUATE(TestDateTime,TempExcelBuf."Cell Value as Text"):
                    BEGIN
                      TempExcelBuf."Cell Value as Text" := FORMAT(DT2DATE(TestDateTime));
                      TempExcelBuf.Comment := Text014;
                      TempExcelBuf.INSERT;
                    END;
                END;
              END;
            (ExcelBuf."Row No." > HeaderRowNo) AND (HeaderRowNo > 0):
              BEGIN
                IF ExcelBuf."Row No." <> OldRowNo THEN BEGIN
                  OldRowNo := ExcelBuf."Row No.";
                  CLEAR(TempBudgetBuf);
                END;

                TempExcelBuf.SETRANGE("Column No.",ExcelBuf."Column No.");
                IF TempExcelBuf.FINDFIRST THEN
                  CASE TempExcelBuf.Comment OF
                    Text010:
                      BEGIN
                        TempGLAcc.SETRANGE(
                          "No.",
                          COPYSTR(
                            ExcelBuf."Cell Value as Text",
                            1,MAXSTRLEN(TempBudgetBuf."G/L Account No.")));
                        IF TempGLAcc.FINDFIRST THEN
                          TempBudgetBuf."G/L Account No." :=
                            COPYSTR(
                              ExcelBuf."Cell Value as Text",
                              1,MAXSTRLEN(TempBudgetBuf."G/L Account No."))
                        ELSE
                          TempBudgetBuf."G/L Account No." := '';
                      END;
                    Text015:
                      TempBudgetBuf."Dimension Value Code 1" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 1"));
                    Text016:
                      TempBudgetBuf."Dimension Value Code 2" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 2"));
                    Text017:
                      TempBudgetBuf."Dimension Value Code 3" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 3"));
                    Text018:
                      TempBudgetBuf."Dimension Value Code 4" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 4"));
                    Text019:
                      TempBudgetBuf."Dimension Value Code 5" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 5"));
                    Text020:
                      TempBudgetBuf."Dimension Value Code 6" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 6"));
                    Text021:
                      TempBudgetBuf."Dimension Value Code 7" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 7"));
                    Text022:
                      TempBudgetBuf."Dimension Value Code 8" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(TempBudgetBuf."Dimension Value Code 8"));
                    Text014:
                      IF TempBudgetBuf."G/L Account No." <> '' THEN BEGIN
                        BudgetBuf := TempBudgetBuf;
                        EVALUATE(BudgetBuf.Date,TempExcelBuf."Cell Value as Text");
                        EVALUATE(BudgetBuf.Amount,ExcelBuf."Cell Value as Text");
                        IF NOT BudgetBuf.FIND('=') THEN
                          BudgetBuf.INSERT
                        ELSE
                          ERROR(Text023 + Text024);
                      END;
                  END;
              END;
          END;
        UNTIL ExcelBuf.NEXT = 0;
      END;

      TempDim.SETRANGE("Code Caption");
      TempDim.MARKEDONLY(TRUE);
      IF TempDim.FINDFIRST THEN BEGIN
        Dim.GET(TempDim.Code);
        ERROR(Text028,Dim."Code Caption");
      END;

      Window.CLOSE;
      TempExcelBuf.RESET;
      TempExcelBuf.SETRANGE(Comment,Text010);
      IF NOT TempExcelBuf.FINDFIRST THEN
        ERROR(Text025);
      TempExcelBuf.SETRANGE(Comment,Text014);
      IF NOT TempExcelBuf.FINDFIRST THEN
        ERROR(Text026);
    END;

    LOCAL PROCEDURE InsertGLBudgetDim@4(DimCode2@1000 : Code[20];DimValCode2@1001 : Code[20];VAR GLBudgetEntry2@1002 : Record 96);
    VAR
      DimValue@1003 : Record 349;
    BEGIN
      IF DimCode2 <> BusUnitDimCode THEN BEGIN
        DimValue.GET(DimCode2,DimValCode2);
        TempDimSetEntry.INIT;
        TempDimSetEntry.VALIDATE("Dimension Code",DimCode2);
        TempDimSetEntry.VALIDATE("Dimension Value Code",DimValCode2);
        TempDimSetEntry.VALIDATE("Dimension Value ID",DimValue."Dimension Value ID");
        TempDimSetEntry.INSERT;
      END;
      CASE DimCode2 OF
        BusUnitDimCode:
          GLBudgetEntry2."Business Unit Code" := COPYSTR(DimValCode2,1,MAXSTRLEN(GLBudgetEntry2."Business Unit Code"));
        GlobalDim1Code:
          GLBudgetEntry2."Global Dimension 1 Code" := DimValCode2;
        GlobalDim2Code:
          GLBudgetEntry2."Global Dimension 2 Code" := DimValCode2;
        BudgetDim1Code:
          GLBudgetEntry2."Budget Dimension 1 Code" := DimValCode2;
        BudgetDim2Code:
          GLBudgetEntry2."Budget Dimension 2 Code" := DimValCode2;
        BudgetDim3Code:
          GLBudgetEntry2."Budget Dimension 3 Code" := DimValCode2;
        BudgetDim4Code:
          GLBudgetEntry2."Budget Dimension 4 Code" := DimValCode2;
      END;
    END;

    LOCAL PROCEDURE FormatData@8(TextToFormat@1000 : Text[250]) : Text[250];
    VAR
      FormatInteger@1001 : Integer;
      FormatDecimal@1002 : Decimal;
      FormatDate@1003 : Date;
    BEGIN
      CASE TRUE OF
        EVALUATE(FormatInteger,TextToFormat):
          EXIT(FORMAT(FormatInteger));
        EVALUATE(FormatDecimal,TextToFormat):
          EXIT(FORMAT(FormatDecimal));
        EVALUATE(FormatDate,TextToFormat):
          EXIT(FORMAT(FormatDate));
        ELSE
          EXIT(TextToFormat);
      END;
    END;

    PROCEDURE SetGLBudgetName@3(NewToGLBudgetName@1000 : Code[10]);
    BEGIN
      ToGLBudgetName := NewToGLBudgetName;
    END;

    PROCEDURE RequestFile@6();
    BEGIN
      IF FileName <> '' THEN
        ServerFileName := FileMgt.UploadFile(Text006,FileName)
      ELSE
        ServerFileName := FileMgt.UploadFile(Text006,'.xlsx');

      ValidateServerFileName;
      FileName := FileMgt.GetFileName(ServerFileName);
    END;

    PROCEDURE SetBudgetDimFilter@1102601000(DimCode2@1102601002 : Code[20];DimValCode2@1102601001 : Code[20];VAR GLBudgetEntry2@1102601000 : Record 96);
    BEGIN
      CASE DimCode2 OF
        BusUnitDimCode:
          GLBudgetEntry2.SETRANGE("Business Unit Code",DimValCode2);
        GlobalDim1Code:
          GLBudgetEntry2.SETRANGE("Global Dimension 1 Code",DimValCode2);
        GlobalDim2Code:
          GLBudgetEntry2.SETRANGE("Global Dimension 2 Code",DimValCode2);
        BudgetDim1Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 1 Code",DimValCode2);
        BudgetDim2Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 2 Code",DimValCode2);
        BudgetDim3Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 3 Code",DimValCode2);
        BudgetDim4Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 4 Code",DimValCode2);
      END;
    END;

    PROCEDURE PostingAccount@1102601001(AccNo@1102601000 : Code[20]) : Boolean;
    VAR
      GLAccount@1102601001 : Record 15;
    BEGIN
      IF NOT GLAccount.GET(AccNo) THEN
        EXIT(FALSE);
      EXIT(GLAccount."Account Type" IN [GLAccount."Account Type"::Posting,GLAccount."Account Type"::"Begin-Total"]);
    END;

    LOCAL PROCEDURE FileNameOnAfterValidate@19040090();
    BEGIN
      RequestFile;
    END;

    LOCAL PROCEDURE ValidateServerFileName@9();
    BEGIN
      IF ServerFileName = '' THEN BEGIN
        FileName := '';
        SheetName := '';
        ERROR(Text029);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


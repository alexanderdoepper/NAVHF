OBJECT Report 1135 Copy G/L Budget to Cost Acctg.
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy G/L Budget to Cost Acctg.;
               ESP=Copiar Ppto. contable en Ppto. costes;
               DEA=Finanzbudget in Kostenrechnung kopieren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3459;    ;DataItem;                    ;
               DataItemTable=Table96;
               DataItemTableView=SORTING(Budget Name,G/L Account No.,Date);
               OnPreDataItem=BEGIN
                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text004);

                               IF CostBudgetEntryTarget."Budget Name" = '' THEN
                                 ERROR(Text005);

                               IF NOT CONFIRM(Text000,TRUE,GETFILTER("Budget Name"),CostBudgetEntryTarget."Budget Name") THEN
                                 ERROR('');

                               LOCKTABLE;

                               IF CostBudgetEntry.FINDLAST THEN
                                 NextEntryNo := CostBudgetEntry."Entry No.";
                               NextEntryNo := NextEntryNo + 1;

                               Window.OPEN(Text002);

                               Window.UPDATE(1,COUNT);

                               FirstCostBudgetEntryNo := NextEntryNo;
                               FINDFIRST;
                               FirstGLBudgetEntryNo := "Entry No.";
                             END;

               OnAfterGetRecord=BEGIN
                                  CostBudgetEntry.INIT;
                                  CostBudgetEntry."Entry No." := NextEntryNo;

                                  CostBudgetEntry."Budget Name" := CostBudgetEntryTarget."Budget Name";

                                  IF DateFormulaChange <> '' THEN
                                    CostBudgetEntry.Date := CALCDATE(DateFormula,Date)
                                  ELSE
                                    CostBudgetEntry.Date := Date;

                                  IF CostBudgetEntryTarget."Cost Type No." <> '' THEN
                                    CostBudgetEntry."Cost Type No." := CostBudgetEntryTarget."Cost Type No."
                                  ELSE BEGIN
                                    IF NOT GLAccount.GET("G/L Account No.") OR (GLAccount."Cost Type No." = '') THEN BEGIN
                                      NoSkipped := NoSkipped + 1;
                                      CurrReport.SKIP;
                                    END;
                                    CostBudgetEntry."Cost Type No." := GLAccount."Cost Type No.";
                                  END;

                                  IF CostBudgetEntryTarget."Cost Center Code" <> '' THEN
                                    CostBudgetEntry."Cost Center Code" := CostBudgetEntryTarget."Cost Center Code"
                                  ELSE BEGIN
                                    CostBudgetEntry."Cost Center Code" := CostAccMgt.GetCostCenterCodeFromDimSet("Dimension Set ID");
                                    IF NOT CostAccMgt.CostCenterExists(CostBudgetEntry."Cost Center Code") THEN
                                      CostBudgetEntry."Cost Center Code" := '';
                                  END;

                                  IF CostBudgetEntry."Cost Center Code" = '' THEN
                                    IF CostBudgetEntryTarget."Cost Object Code" <> '' THEN
                                      CostBudgetEntry."Cost Object Code" := CostBudgetEntryTarget."Cost Object Code"
                                    ELSE BEGIN
                                      CostBudgetEntry."Cost Object Code" := CostAccMgt.GetCostObjectCodeFromDimSet("Dimension Set ID");
                                      IF NOT CostAccMgt.CostObjectExists(CostBudgetEntry."Cost Object Code") THEN
                                        CostBudgetEntry."Cost Object Code" := '';
                                    END;

                                  IF (CostBudgetEntry."Cost Center Code" = '') AND (CostBudgetEntry."Cost Object Code" = '') THEN BEGIN
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  END;

                                  CostBudgetEntry.Amount := Amount;
                                  CostBudgetEntry.Description := Description;
                                  TotalAmount := TotalAmount + Amount;

                                  CostBudgetEntry.INSERT;
                                  NextEntryNo := NextEntryNo + 1;

                                  NoInserted := NoInserted + 1;
                                  IF (NoInserted MOD 100) = 0 THEN
                                    Window.UPDATE(2,NoInserted);
                                END;

               OnPostDataItem=BEGIN
                                LastGLBudgetEntryNo := "Entry No.";
                                Window.CLOSE;

                                IF NoInserted = 0 THEN BEGIN
                                  MESSAGE(Text006,NoSkipped);
                                  ERROR('');
                                END;

                                IF NOT CONFIRM(Text003,TRUE,NoInserted,CostBudgetEntryTarget."Budget Name",NoSkipped) THEN
                                  ERROR('');

                                LastCostBudgetEntryNo := NextEntryNo - 1;

                                CostBudgetRegister.LOCKTABLE;
                                IF CostBudgetRegister.FINDLAST THEN
                                  LastRegisterNo := CostBudgetRegister."No.";

                                CostBudgetRegister.INIT;
                                CostBudgetRegister."No." := LastRegisterNo + 1;
                                CostBudgetRegister."Journal Batch Name" := '';
                                CostBudgetRegister."Cost Budget Name" := CostBudgetEntryTarget."Budget Name";
                                CostBudgetRegister.Source := CostBudgetRegister.Source::"Transfer from G/L Budget";
                                CostBudgetRegister."From Budget Entry No." := FirstGLBudgetEntryNo;
                                CostBudgetRegister."To Budget Entry No." := LastGLBudgetEntryNo;
                                CostBudgetRegister."From Cost Budget Entry No." := FirstCostBudgetEntryNo;
                                CostBudgetRegister."To Cost Budget Entry No." := LastCostBudgetEntryNo;
                                CostBudgetRegister."No. of Entries" := NoInserted;
                                CostBudgetRegister.Amount := TotalAmount;
                                CostBudgetRegister."User ID" := USERID;
                                CostBudgetRegister."Processed Date" := TODAY;
                                CostBudgetRegister.INSERT;
                              END;

               ReqFilterFields=Budget Name,G/L Account No.,Date,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Budget Dimension 4 Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   CostBudgetEntryTarget.INIT;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 21  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to...;
                             ESP=Copiar a...;
                             DEA=Kopieren nach...] }

      { 1   ;3   ;Field     ;
                  Name=Budget Name;
                  Lookup=Yes;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.;
                             DEA=Budgetname];
                  SourceExpr=CostBudgetEntryTarget."Budget Name";
                  TableRelation="Cost Budget Name" }

      { 2   ;3   ;Field     ;
                  Name=Cost Type No.;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Type No.;
                             ESP=Nß tipo coste;
                             DEA=Kostenartnr.];
                  SourceExpr=CostBudgetEntryTarget."Cost Type No.";
                  TableRelation="Cost Type" }

      { 4   ;3   ;Field     ;
                  Name=Cost Center Code;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Center Code;
                             ESP=C¢digo centro coste;
                             DEA=Kostenstellencode];
                  SourceExpr=CostBudgetEntryTarget."Cost Center Code";
                  TableRelation="Cost Center" }

      { 5   ;3   ;Field     ;
                  Name=Cost Object Code;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Object Code;
                             ESP=C¢digo objeto coste;
                             DEA=KostentrÑgercode];
                  SourceExpr=CostBudgetEntryTarget."Cost Object Code";
                  TableRelation="Cost Object" }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=[ENU=Date Change Formula;
                             ESP=F¢rmula de cambio fecha;
                             DEA=Datumswechsel Formel];
                  DateFormula=Yes;
                  SourceExpr=DateFormulaChange;
                  OnValidate=BEGIN
                               EVALUATE(DateFormula,DateFormulaChange);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostBudgetEntryTarget@1000 : Record 1109;
      CostBudgetEntry@1001 : Record 1109;
      GLAccount@1002 : Record 15;
      CostBudgetRegister@1003 : Record 1111;
      CostAccMgt@1004 : Codeunit 1100;
      Window@1005 : Dialog;
      DateFormulaChange@1006 : Code[10];
      DateFormula@1007 : DateFormula;
      NextEntryNo@1008 : Integer;
      NoInserted@1009 : Integer;
      NoSkipped@1010 : Integer;
      FirstGLBudgetEntryNo@1011 : Integer;
      LastGLBudgetEntryNo@1012 : Integer;
      FirstCostBudgetEntryNo@1013 : Integer;
      LastCostBudgetEntryNo@1014 : Integer;
      TotalAmount@1015 : Decimal;
      LastRegisterNo@1016 : Integer;
      Text000@1017 : TextConst 'ENU=Do you want to copy the general ledger budget "%1" to cost budget "%2"?;ESP=®Desea copiar el ppto. contable "%1" en el ppto. costes "%2"?;DEA=Mîchten Sie das Finanzbudget ''%1'' in das Kostenbudget ''%2'' kopieren?';
      Text002@1019 : TextConst 'ENU=Copying budget entries\No of entries #1#####\Copied        #2#####;ESP=Copiando movs. ppto.\Nß movs. #1#####\Copiados        #2#####;DEA=Budgetposten werden kopiert...\Postenanzahl #1#####\Kopiert      #2#####';
      Text003@1020 : TextConst '@@@="%2=budget name;%3=integer value";ENU=%1 entries generated in budget %2.\\%3 entries were skipped because there were either no corresponding G/L accounts defined or cost center and cost object were missing.\\Copy budget?;ESP=%1 movimientos generados en el presupuesto %2.\\Se omitieron %3 movs. porque no se definieron las cuentas correspondientes o faltaba el centro de coste o el objeto de coste.\\®Desea copiar el presupuesto?;DEA=Im Budget ''%2'' wurden %1 Posten generiert.\\%3 Posten wurden Åbersprungen, da entweder keine entsprechenden Sachkonten definiert oder keine Kostenstelle und kein KostentrÑger angegeben waren.\\Budget kopieren?';
      Text004@1021 : TextConst 'ENU=Define name of source budget.;ESP=Defina el nombre del presupuesto de origen.;DEA=Definieren Sie den Namen des Herkunftsbudgets.';
      Text005@1022 : TextConst 'ENU=Define name of target budget.;ESP=Defina el nombre del presupuesto de destino.;DEA=Definieren Sie den Namen des Zielbudgets.';
      Text006@1023 : TextConst 'ENU=No entries were copied. %1 entries were skipped because no corresponding general ledger accounts were defined or because cost center and cost object were missing.;ESP=No se ha copiado ning£n movimiento. Se omitieron %1 movs. porque no se definieron cuentas correspondientes o faltaba el centro de coste o el objeto de coste.;DEA=Keine EintrÑge kopiert. %1 Posten wurden Åbersprungen, da keine entsprechenden Sachkonten definiert oder keine Kostenstelle und kein KostentrÑger angegeben waren.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


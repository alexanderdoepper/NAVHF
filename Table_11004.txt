OBJECT Table 11004 Data Export Record Source
{
  OBJECT-PROPERTIES
  {
    Date=05.04.13;
    Time=12:00:00;
    Version List=NAVDACH7.00.00.34587;
  }
  PROPERTIES
  {
    DataCaptionFields=Data Export Code,Data Exp. Rec. Type Code;
    OnInsert=BEGIN
               TESTFIELD("Table No.");
               CheckTableID;
             END;

    OnModify=BEGIN
               CheckTableID;
             END;

    OnDelete=BEGIN
               GDPdURecDefField.RESET;
               GDPdURecDefField.SETRANGE("Data Export Code","Data Export Code");
               GDPdURecDefField.SETRANGE("Data Exp. Rec. Type Code","Data Exp. Rec. Type Code");
               GDPdURecDefField.SETRANGE("Table No.","Table No.");
               GDPdURecDefField.DELETEALL;

               GDPdUTableRelation.RESET;
               GDPdUTableRelation.SETRANGE("Data Export Code","Data Export Code");
               GDPdUTableRelation.SETRANGE("Data Exp. Rec. Type Code","Data Exp. Rec. Type Code");
               GDPdUTableRelation.SETRANGE("To Table No.","Table No.");
               GDPdUTableRelation.DELETEALL;

               GDPdURecDefTable.RESET;
               GDPdURecDefTable.SETRANGE("Data Export Code","Data Export Code");
               GDPdURecDefTable.SETRANGE("Data Exp. Rec. Type Code","Data Exp. Rec. Type Code");
               GDPdURecDefTable.SETRANGE("Relation To Table No.","Table No.");
               IF GDPdURecDefTable.FIND('-') THEN
                 REPEAT
                   GDPdURecDefTable.DELETE(TRUE);
                 UNTIL GDPdURecDefTable.NEXT = 0;
             END;

    OnRename=BEGIN
               ERROR(Text11000,TABLECAPTION);
             END;

    CaptionML=[DEU=Datenexport - Datensatzquelle;
               ENU=Data Export Record Source];
  }
  FIELDS
  {
    { 1   ;   ;Data Export Code    ;Code10        ;TableRelation="Data Export";
                                                   CaptionML=[DEU=Datenexportcode;
                                                              ENU=Data Export Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Data Exp. Rec. Type Code;Code10    ;TableRelation="Data Export Record Type";
                                                   CaptionML=[DEU=Datenexport - Datensatz-Typcode;
                                                              ENU=Data Export Record Type Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                GDPdUManagement@1140000 : Codeunit 11000;
                                                              BEGIN
                                                                CheckTableID;
                                                                CALCFIELDS("Table Name");
                                                                VALIDATE("Export Table Name",GDPdUManagement.FormatForIndexXML("Table Name"));
                                                                FindDataFilterField;
                                                              END;

                                                   CaptionML=[DEU=Tabellennr.;
                                                              ENU=Table No.];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 4   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[DEU=Tabellenname;
                                                              ENU=Table Name];
                                                   Editable=No }
    { 5   ;   ;Indentation         ;Integer       ;OnValidate=VAR
                                                                GDPdUMgt@1140000 : Codeunit 11000;
                                                              BEGIN
                                                                IF Indentation <> xRec.Indentation THEN BEGIN
                                                                  GDPdUMgt.UpdateDefTableIndentation(Rec,xRec.Indentation);
                                                                  IF Indentation <> 0 THEN
                                                                    "No. Of Sessions" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=EinrÅckung;
                                                              ENU=Indentation];
                                                   MinValue=0 }
    { 6   ;   ;Fields Selected     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Data Exp. Rec. Field" WHERE (Data Export Code=FIELD(Data Export Code),
                                                                                                   Data Exp. Rec. Type Code=FIELD(Data Exp. Rec. Type Code),
                                                                                                   Table No.=FIELD(Table No.)));
                                                   CaptionML=[DEU=Felder ausgewÑhlt;
                                                              ENU=Fields Selected];
                                                   Editable=No }
    { 7   ;   ;Relation To Table No.;Integer      ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   CaptionML=[DEU=Relation zu Tabellennr.;
                                                              ENU=Relation To Table No.];
                                                   BlankZero=Yes }
    { 8   ;   ;Relation To Table Name;Text80      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Relation To Table No.)));
                                                   CaptionML=[DEU=Relation zu Tabellenname;
                                                              ENU=Relation To Table Name];
                                                   Editable=No }
    { 9   ;   ;Period Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Type=FILTER(Date),
                                                                                  Class=CONST(Normal));
                                                   OnValidate=BEGIN
                                                                CheckPeriodFieldInTableFilter;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1140000 : Record 2000000041;
                                                            BEGIN
                                                              TESTFIELD("Table No.");
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETRANGE(Type,Field.Type::Date);
                                                              Field.SETRANGE(Class,Field.Class::Normal);
                                                              IF PAGE.RUNMODAL(PAGE::"Data Exp. Field List",Field) = ACTION::LookupOK THEN
                                                                VALIDATE("Period Field No.",Field."No.");
                                                            END;

                                                   CaptionML=[DEU=Periodenfeldnr.;
                                                              ENU=Period Field No.];
                                                   BlankZero=Yes }
    { 10  ;   ;Period Field Name   ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Period Field No.)));
                                                   CaptionML=[DEU=Periodenfeldname;
                                                              ENU=Period Field Name];
                                                   Editable=No }
    { 11  ;   ;Table Relation Defined;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Data Exp. Table Relationship" WHERE (Data Export Code=FIELD(Data Export Code),
                                                                                                           Data Exp. Rec. Type Code=FIELD(Data Exp. Rec. Type Code),
                                                                                                           To Table No.=FIELD(Table No.),
                                                                                                           From Table No.=FIELD(Relation To Table No.)));
                                                   CaptionML=[DEU=Tabellenrelation definiert;
                                                              ENU=Table Relation Defined];
                                                   Editable=No }
    { 12  ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 13  ;   ;Export File Name    ;Text250       ;OnValidate=VAR
                                                                FileMgmt@1140000 : Codeunit 419;
                                                              BEGIN
                                                                TESTFIELD("Export File Name");
                                                                IF NOT FileMgmt.IsValidFileName("Export File Name") THEN
                                                                  ERROR(Text11003,"Export File Name");
                                                              END;

                                                   CaptionML=[DEU=Exportdateiname;
                                                              ENU=Export File Name] }
    { 20  ;   ;No. Of Sessions     ;Integer       ;OnValidate=BEGIN
                                                                IF "No. Of Sessions" < 0 THEN
                                                                  ERROR(Text11002,FIELDCAPTION("No. Of Sessions"));
                                                                IF (xRec."No. Of Sessions" <> "No. Of Sessions") AND
                                                                   ("No. Of Sessions" <> 0)
                                                                THEN
                                                                  TESTFIELD(Indentation,0);
                                                              END;

                                                   CaptionML=[DEU=Anzahl Sessions;
                                                              ENU=No. Of Sessions] }
    { 30  ;   ;Table Filter        ;TableFilter   ;OnValidate=BEGIN
                                                                CheckPeriodFieldInTableFilter;
                                                              END;

                                                   TableIDExpr="Table No.";
                                                   CaptionML=[DEU=Tabellenfilter;
                                                              ENU=Table Filter] }
    { 31  ;   ;Key No.             ;Integer       ;OnValidate=VAR
                                                                Key@1101100000 : Record 2000000063;
                                                              BEGIN
                                                                IF "Key No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  Key.GET("Table No.","Key No.");
                                                                END;
                                                                IF ("Key No." <> 0) AND (xRec."Key No." <> "Key No.") THEN
                                                                  FindSeqNumberAmongActiveKeys;
                                                              END;

                                                   OnLookup=VAR
                                                              Key@1101100000 : Record 2000000063;
                                                              Keys@1101100001 : Page 11026;
                                                            BEGIN
                                                              TESTFIELD("Table No.");
                                                              IF "Key No." <> 0 THEN BEGIN
                                                                Key.GET("Table No.","Key No.");
                                                                Keys.SETRECORD(Key);
                                                              END;
                                                              Key.FILTERGROUP(1);
                                                              Key.SETRANGE(TableNo,"Table No.");
                                                              Key.SETRANGE(Enabled,TRUE);
                                                              Keys.SETTABLEVIEW(Key);
                                                              Keys.LOOKUPMODE := TRUE;
                                                              IF Keys.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Keys.GETRECORD(Key);
                                                                VALIDATE("Key No.",Key."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=SchlÅsselnr.;
                                                              ENU=Key No.] }
    { 32  ;   ;Date Filter Field No.;Integer      ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Type=CONST(Date),
                                                                                  Class=CONST(FlowFilter));
                                                   CaptionML=[DEU=Feldnummer Datumsfilter;
                                                              ENU=Date Filter Field No.] }
    { 41  ;   ;Active Key Seq. No. ;Integer       ;CaptionML=[DEU=Aktiver SchlÅssel - Reihenfolge;
                                                              ENU=Active Key Seq. No.] }
    { 50  ;   ;Export Table Name   ;Text20        ;OnValidate=BEGIN
                                                                TESTFIELD("Export Table Name");
                                                                VALIDATE("Export File Name","Export Table Name" + '.txt');
                                                              END;

                                                   CaptionML=[DEU=Tabellenname exportieren;
                                                              ENU=Export Table Name] }
  }
  KEYS
  {
    {    ;Data Export Code,Data Exp. Rec. Type Code,Line No.;
                                                   Clustered=Yes }
    {    ;Data Export Code,Data Exp. Rec. Type Code,Relation To Table No.,Table No. }
    {    ;No. Of Sessions                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text11000@1140000 : TextConst 'DEU=Sie dÅrfen eine %1 nicht umbenennen.;ENU=You must not rename a %1.';
      Text11001@1140001 : TextConst 'DEU=Der Datensatz existiert bereits.;ENU=The record already exists.';
      GDPdURecDefTable@1140002 : Record 11004;
      GDPdURecDefField@1140003 : Record 11005;
      GDPdUTableRelation@1140004 : Record 11006;
      Text11002@1140005 : TextConst 'DEU=Das Feld %1 kann nicht negativ sein.;ENU=The %1 field cannot be negative.';
      Text11003@1140006 : TextConst 'DEU=%1 ist kein gÅltiger Dateiname.;ENU=%1 is not a valid file name.';
      Text11004@1140007 : TextConst 'DEU=Sie kînnen das Periodenfeld %1 nicht im Tabellenfilter verwenden.;ENU=You cannot use the period field %1 in the table filter.';
      DataExportManagement@1140008 : Codeunit 11000;

    LOCAL PROCEDURE CheckTableID@1140000();
    BEGIN
      GDPdURecDefTable.RESET;
      GDPdURecDefTable.SETRANGE("Data Export Code","Data Export Code");
      GDPdURecDefTable.SETRANGE("Data Exp. Rec. Type Code","Data Exp. Rec. Type Code");
      GDPdURecDefTable.SETRANGE("Table No.","Table No.");
      GDPdURecDefTable.SETFILTER("Line No.",'<>%1',"Line No.");
      IF GDPdURecDefTable.FIND('-') THEN
        ERROR(Text11001);
    END;

    PROCEDURE FindSeqNumberAmongActiveKeys@1140003();
    VAR
      RecRef@1140000 : RecordRef;
      KeyRef@1140003 : KeyRef;
      KeyCounter@1140001 : Integer;
      ActiveKeyCounter@1140002 : Integer;
    BEGIN
      IF "Table No." <> 0 THEN BEGIN
        RecRef.OPEN("Table No.");
        ActiveKeyCounter := 0;
        FOR KeyCounter := 1 TO RecRef.KEYCOUNT DO BEGIN
          KeyRef := RecRef.KEYINDEX(KeyCounter);
          IF KeyRef.ACTIVE THEN
            ActiveKeyCounter += 1;
          IF "Key No." = KeyCounter THEN
            "Active Key Seq. No." := ActiveKeyCounter;
        END;
      END;
    END;

    PROCEDURE IsPeriodFieldInTableFilter@1140001() : Boolean;
    VAR
      TableFilterText@1140000 : Text;
    BEGIN
      IF "Period Field No." = 0 THEN
        EXIT(FALSE);

      EVALUATE(TableFilterText,FORMAT("Table Filter"));
      IF TableFilterText = '' THEN
        EXIT(FALSE);

      CALCFIELDS("Period Field Name");
      EXIT(STRPOS(TableFilterText,"Period Field Name" + '=') <> 0);
    END;

    LOCAL PROCEDURE CheckPeriodFieldInTableFilter@1140006();
    BEGIN
      IF IsPeriodFieldInTableFilter THEN
        ERROR(Text11004,"Period Field Name");
    END;

    PROCEDURE FindDataFilterField@1140002();
    VAR
      Field@1140000 : Record 2000000041;
    BEGIN
      Field.SETRANGE(TableNo,"Table No.");
      Field.SETRANGE(Type,Field.Type::Date);
      Field.SETRANGE(Class,Field.Class::FlowFilter);
      IF Field.COUNT = 1 THEN BEGIN
        Field.FINDFIRST;
        "Date Filter Field No." := Field."No.";
      END;
    END;

    BEGIN
    END.
  }
}


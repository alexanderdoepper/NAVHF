OBJECT Page 1014 Job Transfer Job Planning Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Transfer Job Planning Line;
               ESP=L°nea planificaci¢n proyecto transf. proy.;
               DEA=In Projektplanungszeile fÅr Projekt Åbertragen];
    SaveValues=Yes;
    SourceTable=Table209;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 InitializeValues;
               END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General;
                           DEA=Allgemein] }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Posting Date;
                           ESP=Fecha registro;
                           DEA=Buchungsdatum];
                SourceExpr=PostingDate }

    { 4   ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Job Journal Template;
                           ESP=Libro del diario proyecto;
                           DEA=Projekt Buch.-Blattvorlage];
                SourceExpr=JobJournalTemplateName;
                TableRelation="Job Journal Template".Name WHERE (Page ID=CONST(201),
                                                                 Recurring=CONST(No));
                OnLookup=BEGIN
                           SelectJobJournalTemplate;
                         END;
                          }

    { 5   ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Job Journal Batch;
                           ESP=Secci¢n diario proyecto;
                           DEA=Projekt Buch.-Blattname];
                SourceExpr=JobJournalBatchName;
                TableRelation="Job Journal Batch".Name WHERE (Journal Template Name=FIELD(Name));
                OnLookup=BEGIN
                           SelectJobJournalBatch;
                         END;
                          }

  }
  CODE
  {
    VAR
      JobJournalTemplateName@1002 : Code[10];
      JobJournalBatchName@1003 : Code[10];
      PostingDate@1000 : Date;

    PROCEDURE InitializeValues@14();
    VAR
      JobJnlTemplate@1000 : Record 209;
      JobJnlBatch@1001 : Record 237;
    BEGIN
      PostingDate := WORKDATE;

      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,FALSE);

      IF JobJnlTemplate.COUNT = 1 THEN BEGIN
        JobJnlTemplate.FINDFIRST;
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        IF JobJnlBatch.COUNT = 1 THEN BEGIN
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        END;
      END;
    END;

    LOCAL PROCEDURE SelectJobJournalTemplate@3();
    VAR
      JobJnlTemplate@1001 : Record 209;
      JobJnlBatch@1000 : Record 237;
    BEGIN
      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,FALSE);

      IF PAGE.RUNMODAL(0,JobJnlTemplate) = ACTION::LookupOK THEN BEGIN
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        IF JobJnlBatch.COUNT = 1 THEN BEGIN
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        END ELSE
          JobJournalBatchName := '';
      END;
    END;

    PROCEDURE SelectJobJournalBatch@5();
    VAR
      JobJnlBatch@1000 : Record 237;
    BEGIN
      JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

      IF PAGE.RUNMODAL(0,JobJnlBatch) = ACTION::LookupOK THEN
        JobJournalBatchName := JobJnlBatch.Name;
    END;

    PROCEDURE GetPostingDate@2() : Date;
    BEGIN
      EXIT(PostingDate);
    END;

    PROCEDURE GetJobJournalTemplateName@1() : Code[10];
    BEGIN
      EXIT(JobJournalTemplateName);
    END;

    PROCEDURE GetJobJournalBatchName@4() : Code[10];
    BEGIN
      EXIT(JobJournalBatchName);
    END;

    BEGIN
    END.
  }
}


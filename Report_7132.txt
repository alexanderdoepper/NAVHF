OBJECT Report 7132 Export Item Budget to Excel
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Item Budget to Excel;
               ESP=Exportar a Excel ppto. pdto.;
               DEA=Artikelbudget nach Excel exp.];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=VAR
                                  Window@1000 : Dialog;
                                  RecNo@1001 : Integer;
                                  TotalRecNo@1002 : Integer;
                                  RowNo@1003 : Integer;
                                  ColumnNo@1004 : Integer;
                                BEGIN
                                  Window.OPEN(
                                    Text000 +
                                    '@1@@@@@@@@@@@@@@@@@@@@@\');

                                  TempExcelBuffer.DELETEALL;
                                  CLEAR(TempExcelBuffer);

                                  ItemBudgetName.GET(AnalysisArea,BudgetName);
                                  GLSetup.GET;

                                  IF DateFilter = '' THEN
                                    ERROR(STRSUBSTNO(Text010,Text003));

                                  IF FindLine('-') THEN
                                    REPEAT
                                      TotalRecNo := TotalRecNo + 1;
                                    UNTIL NextLine(1) = 0;

                                  RowNo := 1;
                                  EnterCell(RowNo,1,Text001,FALSE,FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                  EnterCell(RowNo,2,'',FALSE,FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);

                                  RowNo := RowNo + 1;
                                  EnterFilterInCell(RowNo,BudgetName,ItemBudgetName.TABLECAPTION);

                                  IF GlobalDim1Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(GLSetup."Global Dimension 1 Code");
                                    EnterFilterInCell(RowNo,GlobalDim1Filter,Dim."Filter Caption");
                                  END;

                                  IF GlobalDim2Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(GLSetup."Global Dimension 2 Code");
                                    EnterFilterInCell(RowNo,GlobalDim2Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim1Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 1 Code");
                                    EnterFilterInCell(RowNo,BudgetDim1Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim2Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 2 Code");
                                    EnterFilterInCell(RowNo,BudgetDim2Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim3Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 3 Code");
                                    EnterFilterInCell(RowNo,BudgetDim3Filter,Dim."Filter Caption");
                                  END;

                                  IF ItemFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(RowNo,ItemFilter,Text004);
                                  END;

                                  IF DateFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(RowNo,DateFilter,Text003);
                                  END;

                                  IF SourceNoFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    IF SourceTypeFilter = SourceTypeFilter::Customer THEN
                                      EnterFilterInCell(RowNo,SourceNoFilter,Text005)
                                    ELSE
                                      EnterFilterInCell(RowNo,SourceNoFilter,Text006);
                                  END;

                                  RowNo := RowNo + 2;
                                  EnterFilterInCell(RowNo,LineDimCode,Text008);

                                  RowNo := RowNo + 1;
                                  EnterFilterInCell(RowNo,ColumnDimCode,Text009);

                                  RowNo := RowNo + 1;
                                  CASE ValueType OF
                                    ValueType::"Sales Amount":
                                      ShowValueAsText := Text012;
                                    ValueType::"Cost Amount":
                                      IF AnalysisArea = AnalysisArea::Sales THEN
                                        ShowValueAsText := Text014
                                      ELSE
                                        ShowValueAsText := Text013;
                                    ValueType::Quantity:
                                      ShowValueAsText := Text015;
                                  END;
                                  EnterFilterInCell(RowNo,ShowValueAsText,Text011);

                                  RowNo := RowNo + 2;
                                  IF FindLine('-') THEN BEGIN
                                    IF FindColumn('-') THEN BEGIN
                                      ColumnNo := 1;
                                      EnterCell(RowNo,ColumnNo,Text007,FALSE,TRUE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      REPEAT
                                        ColumnNo := ColumnNo + 1;
                                        EnterCell(RowNo,ColumnNo,ColumnDimCodeBuffer.Code,FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      UNTIL NextColumn(1) = 0;
                                    END;
                                    REPEAT
                                      RecNo := RecNo + 1;
                                      Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
                                      RowNo := RowNo + 1;
                                      ColumnNo := 1;
                                      EnterCell(
                                        RowNo,ColumnNo,LineDimCodeBuffer.Code,LineDimCodeBuffer."Show in Bold",FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      IF FindColumn('-') THEN
                                        REPEAT
                                          ColumnNo := ColumnNo + 1;
                                          ColumnValue :=
                                            ItemBudgetManagement.CalcAmount(
                                              ValueType,TRUE,
                                              ItemStatisticsBuffer,ItemBudgetName,
                                              ItemFilter,SourceTypeFilter,SourceNoFilter,DateFilter,
                                              GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter,
                                              LineDimOption,LineDimCodeBuffer,
                                              ColumnDimOption,ColumnDimCodeBuffer);
                                          IF ColumnValue <> 0 THEN
                                            EnterCell(
                                              RowNo,
                                              ColumnNo,
                                              FORMAT(ColumnValue),
                                              LineDimCodeBuffer."Show in Bold",
                                              FALSE,
                                              FALSE,
                                              '#,##0.00',
                                              TempExcelBuffer."Cell Type"::Number)
                                          ELSE
                                            EnterCell(
                                              RowNo,
                                              ColumnNo,
                                              '',
                                              LineDimCodeBuffer."Show in Bold",
                                              FALSE,
                                              FALSE,
                                              '',
                                              TempExcelBuffer."Cell Type"::Number);
                                        UNTIL NextColumn(1) = 0;
                                    UNTIL NextLine(1) = 0;
                                  END;
                                  Window.CLOSE;

                                  IF Option = Option::"Update Workbook" THEN BEGIN
                                    TempExcelBuffer.UpdateBook(ServerFileName,SheetName);
                                    TempExcelBuffer.WriteSheet('',COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    TempExcelBuffer.OverwriteAndOpenExistingExcel(ClientFileName);
                                  END ELSE BEGIN
                                    TempExcelBuffer.CreateBook(TempExcelBuffer.GetExcelReference(10));
                                    TempExcelBuffer.WriteSheet(
                                      PADSTR(STRSUBSTNO('%1 %2',ItemBudgetName.Name,ItemBudgetName.Description),30),
                                      COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    TempExcelBuffer.OpenExcel;
                                  END;

                                  TempExcelBuffer.GiveUserControl;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               SheetNameEnable := TRUE;
               FileNameEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   UpdateRequestForm;
                 END;

      OnQueryClosePage=BEGIN
                         IF (Option = Option::"Update Workbook") AND (CloseAction = ACTION::OK) THEN
                           ValidateServerFileName;
                       END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Option;
                             ESP=Opci¢n;
                             DEA=Option];
                  OptionCaptionML=[ENU=Create Workbook,Update Workbook;
                                   ESP=Crear libro,Actualizar libro;
                                   DEA=Arbeitsmappe erstellen,Arbeitsmappe aktualisieren];
                  SourceExpr=Option;
                  OnValidate=BEGIN
                               UpdateRequestForm;
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=FileName;
                  CaptionML=[ENU=Workbook File Name;
                             ESP=Nombre fichero libro;
                             DEA=Arbeitsmappendateiname];
                  SourceExpr=FileName;
                  Enabled=FileNameEnable;
                  Editable=FALSE;
                  OnValidate=BEGIN
                               FileNameOnAfterValidate;
                             END;

                  OnAssistEdit=BEGIN
                                 RequestFile;
                                 SheetName := TempExcelBuffer.SelectSheetsName(ServerFileName);
                               END;
                                }

      { 2   ;2   ;Field     ;
                  Name=SheetName;
                  CaptionML=[ENU=Worksheet Name;
                             ESP=Nombre hoja;
                             DEA=Tabellenname];
                  SourceExpr=SheetName;
                  Enabled=SheetNameEnable;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 IF ServerFileName = '' THEN
                                   RequestFile;

                                 SheetName := TempExcelBuffer.SelectSheetsName(ServerFileName);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Analyzing Data...\\;ESP=Analizar Datos...\\;DEA=Daten werden analysiert ...\\';
      Text001@1001 : TextConst 'ENU=Filters;ESP=Filtros;DEA=Filter';
      Text002@1002 : TextConst 'ENU=Update Workbook;ESP=Actualizar libro;DEA=Arbeitsmappe aktualisieren';
      ItemBudgetName@1018 : Record 7132;
      Dim@1022 : Record 348;
      LineDimCodeBuffer@1015 : Record 367;
      ColumnDimCodeBuffer@1040 : Record 367;
      ItemStatisticsBuffer@1041 : Record 5821;
      TempExcelBuffer@1006 : TEMPORARY Record 370;
      GLSetup@1013 : Record 98;
      ItemBudgetManagement@1007 : Codeunit 7130;
      FileMgt@1049 : Codeunit 419;
      LineDimCode@1043 : Text[30];
      ColumnDimCode@1042 : Text[30];
      DateFilter@1036 : Text[30];
      InternalDateFilter@1003 : Text[30];
      ShowValueAsText@1047 : Text[30];
      FileName@1011 : Text;
      ServerFileName@1050 : Text;
      ClientFileName@1051 : Text;
      SheetName@1010 : Text[250];
      BudgetName@1019 : Code[10];
      GlobalDim1Filter@1027 : Code[250];
      GlobalDim2Filter@1026 : Code[250];
      BudgetDim1Filter@1025 : Code[250];
      BudgetDim2Filter@1024 : Code[250];
      BudgetDim3Filter@1023 : Code[250];
      SourceNoFilter@1031 : Code[250];
      ItemFilter@1030 : Code[250];
      ColumnValue@1009 : Decimal;
      Option@1012 : 'Create Workbook,Update Workbook';
      AnalysisArea@1020 : 'Sales,Purchase,Inventory';
      ValueType@1021 : 'Sales Amount,Cost Amount,Quantity';
      Text003@1028 : TextConst 'ENU=Date Filter;ESP=Filtro fecha;DEA=Datumsfilter';
      Text004@1029 : TextConst 'ENU=Item Filter;ESP=Filtro producto;DEA=Artikelfilter';
      Text005@1033 : TextConst 'ENU=Customer Filter;ESP=Filtro cliente;DEA=Debitorenfilter';
      Text006@1034 : TextConst 'ENU=Vendor Filter;ESP=Filtro proveedor;DEA=Kreditorenfilter';
      SourceTypeFilter@1032 : ' ,Customer,Vendor,Item';
      PeriodType@1037 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      LineDimOption@1039 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';
      ColumnDimOption@1038 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';
      PeriodInitialized@1035 : Boolean;
      Text007@1004 : TextConst 'ENU=Table Data;ESP=Datos de la tabla;DEA=Tabellendaten';
      Text008@1044 : TextConst 'ENU=Show as Lines;ESP=Muestra como l°neas;DEA=Zeilenansicht';
      Text009@1045 : TextConst 'ENU=Show as Columns;ESP=Muestra como columnas;DEA=Spaltenansicht';
      Text010@1005 : TextConst 'ENU=%1 must not be blank.;ESP=%1 no debe estar en blanco.;DEA=%1 darf nicht leer sein.';
      Text011@1008 : TextConst 'ENU=Show Value as;ESP=Mostrar valor como;DEA=Wert anzeigen als';
      Text012@1014 : TextConst 'ENU=Sales Amount;ESP=Importe ventas;DEA=Verkaufsbetrag';
      Text013@1016 : TextConst 'ENU=Cost Amount;ESP=Importe coste;DEA=Einstandsbetrag';
      Text014@1017 : TextConst 'ENU=COGS Amount;ESP=Importe CV;DEA=Lagerverbrauchsbetrag';
      Text015@1046 : TextConst 'ENU=Quantity;ESP=Cantidad;DEA=Menge';
      FileNameEnable@19078740 : Boolean INDATASET;
      SheetNameEnable@19012330 : Boolean INDATASET;
      Text016@1048 : TextConst 'ENU=You must enter a file name.;ESP=Debe introd. nombre fichero.;DEA=Sie mÅssen einen Dateinamen angeben.';

    PROCEDURE SetOptions@3(NewAnalysisArea@1008 : Integer;NewBudgName@1007 : Code[10];NewValueType@1006 : Integer;NewGlobalDim1Filter@1005 : Code[250];NewGlobalDim2Filter@1004 : Code[250];NewBudgDim1Filter@1003 : Code[250];NewBudgDim2Filter@1002 : Code[250];NewBudgDim3Filter@1001 : Code[250];NewDateFilter@1000 : Text[30];NewSourceTypeFilter@1011 : Integer;NewSourceNoFilter@1010 : Code[250];NewItemFilter@1009 : Code[250];NewInternalDateFilter@1013 : Text[30];NewPeriodInitialized@1012 : Boolean;NewPeriodType@1014 : Integer;NewLineDimOption@1016 : Integer;NewColumnDimOption@1015 : Integer;NewLineDimCode@1018 : Text[30];NewColumnDimCode@1017 : Text[30]);
    BEGIN
      AnalysisArea := NewAnalysisArea;
      BudgetName := NewBudgName;
      ValueType := NewValueType;
      GlobalDim1Filter := NewGlobalDim1Filter;
      GlobalDim2Filter := NewGlobalDim2Filter;
      BudgetDim1Filter := NewBudgDim1Filter;
      BudgetDim2Filter := NewBudgDim2Filter;
      BudgetDim3Filter := NewBudgDim3Filter;
      DateFilter := NewDateFilter;
      ItemFilter := NewItemFilter;
      SourceTypeFilter := NewSourceTypeFilter;
      SourceNoFilter := NewSourceNoFilter;
      InternalDateFilter := NewInternalDateFilter;
      PeriodInitialized := NewPeriodInitialized;
      PeriodType := NewPeriodType;
      LineDimOption := NewLineDimOption;
      ColumnDimOption := NewColumnDimOption;
      LineDimCode := NewLineDimCode;
      ColumnDimCode := NewColumnDimCode;
    END;

    LOCAL PROCEDURE UpdateRequestForm@1();
    BEGIN
      PageUpdateRequestForm;
    END;

    LOCAL PROCEDURE EnterFilterInCell@5(RowNo@1000 : Integer;Filter@1001 : Text[250];FieldName@1002 : Text[100]);
    BEGIN
      EnterCell(RowNo,1,FieldName,FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
      EnterCell(RowNo,2,Filter,FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
    END;

    LOCAL PROCEDURE EnterCell@2(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1003 : Boolean;Italic@1004 : Boolean;UnderLine@1005 : Boolean;NumberFormat@1006 : Text[30];CellType@1007 : Option);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := CellValue;
      TempExcelBuffer.Formula := '';
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Italic := Italic;
      TempExcelBuffer.Underline := UnderLine;
      TempExcelBuffer.NumberFormat := NumberFormat;
      TempExcelBuffer."Cell Type" := CellType;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE FindLine@4(Which@1000 : Text[1024]) : Boolean;
    BEGIN
      EXIT(
        ItemBudgetManagement.FindRec(
          ItemBudgetName,LineDimOption,LineDimCodeBuffer,Which,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,PeriodInitialized,InternalDateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE NextLine@8(Steps@1000 : Integer) : Integer;
    BEGIN
      EXIT(
        ItemBudgetManagement.NextRec(
          ItemBudgetName,LineDimOption,LineDimCodeBuffer,Steps,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE FindColumn@11(Which@1000 : Text[1024]) : Boolean;
    BEGIN
      EXIT(
        ItemBudgetManagement.FindRec(
          ItemBudgetName,ColumnDimOption,ColumnDimCodeBuffer,Which,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,PeriodInitialized,InternalDateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE NextColumn@12(Steps@1000 : Integer) : Integer;
    BEGIN
      EXIT(
        ItemBudgetManagement.NextRec(
          ItemBudgetName,ColumnDimOption,ColumnDimCodeBuffer,Steps,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    PROCEDURE RequestFile@6();
    BEGIN
      IF FileName = '' THEN
        ClientFileName := FileMgt.OpenFileDialog(Text002,FileName,'')
      ELSE
        ClientFileName := FileMgt.OpenFileDialog(Text002,'.xlsx','');

      FileName := FileMgt.GetFileName(ClientFileName);
      ServerFileName := FileMgt.UploadFileSilent(ClientFileName);
      ValidateServerFileName;
    END;

    LOCAL PROCEDURE FileNameOnAfterValidate@19040090();
    BEGIN
      RequestFile;
    END;

    LOCAL PROCEDURE PageUpdateRequestForm@19031953();
    BEGIN
      IF Option = Option::"Update Workbook" THEN BEGIN
        FileNameEnable := TRUE;
        SheetNameEnable := TRUE;
      END ELSE BEGIN
        FileName := '';
        SheetName := '';
        FileNameEnable := FALSE;
        SheetNameEnable := FALSE;
      END;
    END;

    LOCAL PROCEDURE ValidateServerFileName@9();
    BEGIN
      IF ServerFileName = '' THEN BEGIN
        FileName := '';
        SheetName := '';
        ERROR(Text016);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


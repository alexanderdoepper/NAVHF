OBJECT Codeunit 7323 Whse.-Act.-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5767;
    OnRun=BEGIN
            WhseActivLine.COPY(Rec);
            Code;
            COPY(WhseActivLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Receive,Receive &and Invoice;ESP=&Recibir,Recibir &y facturar;DEA=&Liefern,Liefern &und fakturieren';
      WhseActivLine@1007 : Record 5767;
      WhseActivityPost@1008 : Codeunit 7324;
      Selection@1003 : Integer;
      Text001@1000 : TextConst 'ENU=&Ship,Ship &and Invoice;ESP=&Enviar,Enviar &y facturar;DEA=&Liefern,Liefern &und fakturieren';
      Text002@1001 : TextConst 'ENU=Do you want to post the %1 and %2?;ESP=¨Desea registrar el %1 y %2?;DEA=M”chten Sie die %1 und die/den %2 buchen?';
      PrintDoc@1004 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH WhseActivLine DO BEGIN
        IF "Activity Type" = "Activity Type"::"Invt. Put-away" THEN BEGIN
          IF ("Source Document" = "Source Document"::"Prod. Output") OR
             ("Source Document" = "Source Document"::"Inbound Transfer") OR
             ("Source Document" = "Source Document"::"Prod. Consumption")
          THEN BEGIN
            IF NOT CONFIRM(Text002,FALSE,"Activity Type","Source Document") THEN
              EXIT;
          END ELSE BEGIN
            Selection := STRMENU(Text000,2);
            IF Selection = 0 THEN
              EXIT;
          END;
        END ELSE
          IF ("Source Document" = "Source Document"::"Prod. Consumption") OR
             ("Source Document" = "Source Document"::"Outbound Transfer")
          THEN BEGIN
            IF NOT CONFIRM(Text002,FALSE,"Activity Type","Source Document") THEN
              EXIT;
          END ELSE BEGIN
            Selection := STRMENU(Text001,2);
            IF Selection = 0 THEN
              EXIT;
          END;

        WhseActivityPost.SetInvoiceSourceDoc(Selection = 2);
        WhseActivityPost.PrintDocument(PrintDoc);
        WhseActivityPost.RUN(WhseActivLine);
        CLEAR(WhseActivityPost);
      END;
    END;

    PROCEDURE PrintDocument@2(SetPrint@1000 : Boolean);
    BEGIN
      PrintDoc := SetPrint;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 11011 Sales VAT Advance Notification
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVDACH7.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF xRec.FINDLAST THEN;
               Period := xRec.Period;
               "XSL-Filename" := xRec."XSL-Filename";
               "XSD-Filename" := xRec."XSD-Filename";
               "Contact for Tax Office" := xRec."Contact for Tax Office";

               IF "No." = '' THEN BEGIN
                 GLSetup.GET;
                 GLSetup.TESTFIELD("Sales VAT Advance Notif. Nos.");
                 NoSeriesMgt.InitSeries(GLSetup."Sales VAT Advance Notif. Nos.",xRec."No. Series",WORKDATE,"No.","No. Series");
               END;
             END;

    OnDelete=BEGIN
               IF NOT Testversion THEN BEGIN
                 CALCFIELDS("Transmission successful");
                 IF "Transmission successful" THEN
                   ERROR(Text1140000,TABLECAPTION);
               END;
               VATTransLogEntry.SETCURRENTKEY("Sales VAT Adv. Notif. No.");
               VATTransLogEntry.SETRANGE("Sales VAT Adv. Notif. No.","No.");
               VATTransLogEntry.DELETEALL;
             END;

    OnRename=BEGIN
               CheckEditable;
             END;

    CaptionML=[ENU=Sales VAT Advance Notification;
               DEA=Umsatzsteuervoranmeldung];
    LookupPageID=Page11017;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  GLSetup.GET;
                                                                  NoSeriesMgt.TestManual(GLSetup."Sales VAT Advance Notif. Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              DEA=Nr.] }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              DEA=Beschreibung] }
    { 4   ;   ;XML Submission Document;BLOB       ;CaptionML=[ENU=XML Submission Document;
                                                              DEA=XML-öbertragungsdokument] }
    { 5   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                CheckEditable;
                                                                IF "Starting Date" <> 0D THEN
                                                                  CheckDate("Starting Date",Period);
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              DEA=Startdatum] }
    { 6   ;   ;Period              ;Option        ;OnValidate=BEGIN
                                                                CheckEditable;
                                                                IF "Starting Date" <> 0D THEN
                                                                  CheckDate("Starting Date",Period);
                                                              END;

                                                   CaptionML=[ENU=Period;
                                                              DEA=Periode];
                                                   OptionCaptionML=[ENU=Month,Quarter;
                                                                    DEA=Monat,Quartal];
                                                   OptionString=Month,Quarter }
    { 7   ;   ;XML-File Creation Date;Date        ;CaptionML=[ENU=XML-File Creation Date;
                                                              DEA=XML-Datei erstellt am];
                                                   Editable=No }
    { 8   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              DEA=Nummernserie];
                                                   Editable=No }
    { 9   ;   ;XSL-Filename        ;Text250       ;CaptionML=[ENU=XSL-Filename;
                                                              DEA=XSL Dateiname] }
    { 10  ;   ;XSD-Filename        ;Text250       ;CaptionML=[ENU=XSD-Filename;
                                                              DEA=XSD Dateiname] }
    { 11  ;   ;Statement Template Name;Code10     ;TableRelation="VAT Statement Template";
                                                   OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Statement Template Name;
                                                              DEA=Abrechnung Vorlagenname];
                                                   Editable=No }
    { 12  ;   ;Statement Name      ;Code10        ;TableRelation="VAT Statement Name".Name WHERE (Statement Template Name=FIELD(Statement Template Name));
                                                   OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Statement Name;
                                                              DEA=Abrechnungsname];
                                                   Editable=No }
    { 13  ;   ;Incl. VAT Entries (Closing);Option ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Incl. VAT Entries (Closing);
                                                              DEA=MwSt.-Posten inkl. (Abgeschl.)];
                                                   OptionCaptionML=[ENU=Open,Closed,Open and Closed;
                                                                    DEA=Offen,Geschlossen,Offen und geschlossen];
                                                   OptionString=Open,Closed,Open and Closed }
    { 14  ;   ;Incl. VAT Entries (Period);Option  ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Incl. VAT Entries (Period);
                                                              DEA=MwSt.-Posten inkl. (Periode)];
                                                   OptionCaptionML=[ENU=Before and Within Period,Within Period;
                                                                    DEA=Vor und innerhalb Periode,Innerhalb Periode];
                                                   OptionString=Before and Within Period,Within Period }
    { 15  ;   ;Corrected Notification;Boolean     ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Corrected Notification;
                                                              DEA=Berichtigte Anmeldung] }
    { 16  ;   ;Offset Amount of Refund;Boolean    ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Offset Amount of Refund;
                                                              DEA=Erstattungsbetrag verrechnen] }
    { 17  ;   ;Cancel Order for Direct Debit;Boolean;
                                                   OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Cancel Order for Direct Debit;
                                                              DEA=EinzugsermÑchtigung widerrufen] }
    { 18  ;   ;Transmission successful;Boolean    ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Transmission Log Entry" WHERE (Sales VAT Adv. Notif. No.=FIELD(No.),
                                                                                                     Transmission successful=CONST(Yes)));
                                                   CaptionML=[ENU=Transmission successful;
                                                              DEA=öbertragung erfolgreich];
                                                   Editable=No }
    { 19  ;   ;Amounts in Add. Rep. Currency;Boolean;
                                                   InitValue=No;
                                                   CaptionML=[ENU=Amounts in Add. Rep. Currency;
                                                              DEA=BetrÑge in BerichtswÑhrung] }
    { 20  ;   ;Testversion         ;Boolean       ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Testversion;
                                                              DEA=Testversion] }
    { 24  ;   ;Additional Information;Text250     ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Additional Information;
                                                              DEA=ZusÑtzliche Informationen] }
    { 25  ;   ;Contact for Tax Office;Text30      ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Contact for Tax Office;
                                                              DEA=Ansprechpartner fÅr Finanzamt] }
    { 26  ;   ;Contact Phone No.   ;Text20        ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Contact Phone No.;
                                                              DEA=Kontakt Telefonnr.] }
    { 27  ;   ;Contact E-Mail      ;Text70        ;OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Contact E-Mail;
                                                              DEA=Kontakt-E-Mail] }
    { 30  ;   ;Documents Submitted Separately;Boolean;
                                                   OnValidate=BEGIN
                                                                CheckEditable;
                                                              END;

                                                   CaptionML=[ENU=Documents Submitted Separately;
                                                              DEA=Getrennt Åbermittelte Belege] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Starting Date                            }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description                          }
  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=You cannot delete a XML-File of a %1 after the transmission was successful.;DEA=Sie kînnen eine XML-Datei in einer %1 nicht lîschen, nachdem die öbertragung erfolgreich war.';
      Text1140001@1140001 : TextConst 'ENU=Places of %1 in area %2 must be %3.;DEA=Die %1 im Bundesland %2 muss %3stellig sein.';
      Text1140002@1140002 : TextConst 'ENU=Doc;DEA=Doc';
      Text1140006@1140006 : TextConst 'ENU=You must specify a beginning of a month as starting date of the statement period.;DEA=Sie mÅssen als Anfangszeitpunkt des Abrechnungszeitraums einen Monatsanfang wÑhlen.';
      Text1140007@1140007 : TextConst 'ENU=The starting date is not the first date of a quarter.;DEA=Das Startdatum ist kein Quartalsbeginn.';
      Text1140008@1140008 : TextConst 'ENU=Do you want to delete the XML-File for the %1?;DEA=Wollen Sie die XML-Datei fÅr die %1 lîschen?';
      Text1140009@1140009 : TextConst 'ENU=The file or path %1 does not exist.;DEA=Die Datei oder der Pfad %1 sind nicht vorhanden.';
      Text1140010@1140010 : TextConst 'ENU=You must create the XML-File before it can be shown.;DEA=Sie mÅssen die XML-Datei erstellen, bevor sie angezeigt werden kann.';
      Text1140011@1140011 : TextConst 'ENU=You cannot change the value of this field anymore after the XML-File for the %1 has been created.;DEA=Sie kînnen den Wert dieses Feldes nicht mehr Ñndern, nachdem die XML-Datei fÅr die %1 erstellt wurde.';
      CompanyInfo@1140012 : Record 79;
      GLSetup@1140013 : Record 98;
      VATTransLogEntry@1140014 : Record 11012;
      SalesVATAdvNotif@1140015 : Record 11011;
      NoSeriesMgt@1140016 : Codeunit 396;
      LineNo@1140017 : ARRAY [6] OF Code[10];
      TotalAmount@1140018 : Decimal;
      TotalBase@1140019 : Decimal;
      TotalUnrealizedAmount@1140020 : Decimal;
      TotalUnrealizedBase@1140021 : Decimal;
      Amount@1140022 : Decimal;
      StartDate@1140023 : Date;
      EndDate@1140024 : Date;
      Selection@1140026 : 'Open,Closed,Open and Closed';
      PeriodSelection@1140027 : 'Before and Within Period,Within Period';
      ToFile@1140029 : Text[1024];

    PROCEDURE AssistEdit@1140000(OldSalesVATAdvNotif@1140000 : Record 11011) : Boolean;
    BEGIN
      WITH SalesVATAdvNotif DO BEGIN
        SalesVATAdvNotif := Rec;
        GLSetup.GET;
        GLSetup.TESTFIELD("Sales VAT Advance Notif. Nos.");
        IF NoSeriesMgt.SelectSeries(GLSetup."Sales VAT Advance Notif. Nos.",OldSalesVATAdvNotif."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := SalesVATAdvNotif;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE Show@1140001();
    VAR
      XMLSubDoc@1140000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLInStream@1140001 : InStream;
    BEGIN
      IF "XML-File Creation Date" = 0D THEN
        ERROR(Text1140010);
      XMLSubDoc := XMLSubDoc.XmlDocument;
      CALCFIELDS("XML Submission Document");
      "XML Submission Document".CREATEINSTREAM(XMLInStream);
      XMLSubDoc.Load(XMLInStream);
      ShowXMLDocument(XMLSubDoc);
    END;

    PROCEDURE ShowXMLDocument@1140002(XMLDoc@1140000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      SalesVATAdvNotif2@1140001 : Record 11011;
      FileMgr@1140012 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File" RUNONCLIENT;
      XMLDOMNewProcessInst@1140002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlProcessingInstruction";
      XMLDOMElement@1140003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      XMLOutStream@1140004 : OutStream;
      XMLInStream@1140005 : InStream;
      FileOutStream@1140006 : OutStream;
      XMLFile@1140007 : File;
      FileName@1140008 : Text[250];
      TextLine@1140009 : Text[1024];
    BEGIN
      GetFileName(FileName);
      TESTFIELD("XSL-Filename");

      IF NOT FileMgr.Exists("XSL-Filename") THEN
        ERROR(Text1140009,"XSL-Filename");

      XMLDOMElement := XMLDoc.DocumentElement;
      XMLDOMNewProcessInst := XMLDoc.CreateProcessingInstruction(
          'xml-stylesheet','type="text/xsl" href="' + "XSL-Filename" + '"');
      XMLDoc.InsertBefore(XMLDOMNewProcessInst,XMLDOMElement);

      IF EXISTS("XSD-Filename") THEN
        XMLDOMElement.SetAttribute('schemaLocation',"XSD-Filename");

      SalesVATAdvNotif2."XML Submission Document".CREATEOUTSTREAM(XMLOutStream);
      XMLDoc.Save(XMLOutStream);
      SalesVATAdvNotif2."XML Submission Document".CREATEINSTREAM(XMLInStream);
      XMLFile.CREATE(FileName);
      XMLFile.CREATEOUTSTREAM(FileOutStream);
      WHILE NOT XMLInStream.EOS DO BEGIN
        XMLInStream.READTEXT(TextLine);
        FileOutStream.WRITETEXT(TextLine);
      END;
      XMLFile.CLOSE;

      ToFile := Text1140002 + '.xml';
      IF NOT DOWNLOAD(FileName,'','','',ToFile) THEN
        EXIT;
    END;

    LOCAL PROCEDURE GetFileName@1140003(VAR FileName@1140000 : Text[250]);
    VAR
      i@1140001 : Integer;
      DocNo@1140002 : Text[30];
      RBMgt@1140003 : Codeunit 419;
    BEGIN
      REPEAT
        IF i <> 0 THEN
          DocNo := FORMAT(i);
        FileName := RBMgt.ServerTempFileName('xml');
        IF NOT EXISTS(FileName) THEN
          EXIT;
        i := i + 1;
      UNTIL i = 999;
    END;

    PROCEDURE CheckDate@1140004(StartDate2@1140000 : Date;PeriodType@1140001 : 'Month,Quarter');
    BEGIN
      IF DATE2DMY(StartDate2,1) <> 1 THEN
        ERROR(Text1140006);
      IF (Period = Period::Quarter) AND (((DATE2DMY(StartDate2,2) + 2) / 3) <> ROUND((DATE2DMY(StartDate2,2) + 2) / 3)) THEN
        ERROR(Text1140007);
    END;

    PROCEDURE CalcEndDate@1140005(StartDate2@1140000 : Date;PeriodType@1140001 : 'Month,Quarter') : Date;
    VAR
      DateFormula@1140002 : DateFormula;
    BEGIN
      CASE Period OF
        Period::Quarter:
          EVALUATE(DateFormula,'<+3M-1D>');
        ELSE
          EVALUATE(DateFormula,'<+1M-1D>');
      END;
      EXIT(CALCDATE(DateFormula,StartDate2));
    END;

    LOCAL PROCEDURE CheckEditable@1140006();
    BEGIN
      IF "XML-File Creation Date" <> 0D THEN
        ERROR(Text1140011,TABLECAPTION);
    END;

    PROCEDURE DeleteXMLSubDoc@1140007();
    BEGIN
      IF NOT Testversion THEN
        TESTFIELD("Transmission successful",FALSE);
      IF NOT CONFIRM(Text1140008,FALSE,TABLECAPTION) THEN
        EXIT;
      CLEAR("XML Submission Document");
      "XML-File Creation Date" := 0D;
      "Statement Template Name" := '';
      "Statement Name" := '';
      MODIFY;
      VATTransLogEntry.SETCURRENTKEY("Sales VAT Adv. Notif. No.");
      VATTransLogEntry.SETRANGE("Sales VAT Adv. Notif. No.","No.");
      VATTransLogEntry.DELETEALL;
    END;

    PROCEDURE CheckVATNo@1140008(VAR PosTaxoffice@1140000 : Integer;VAR NumberTaxOffice@1140001 : Integer;VAR PosArea@1140002 : Integer;VAR NumberArea@1140003 : Integer;VAR PosDistinction@1140004 : Integer;VAR NumberDistinction@1140005 : Integer) VATNo@1140006 : Text[30];
    BEGIN
      CompanyInfo.GET;
      CompanyInfo.TESTFIELD("Tax Office Area");
      CompanyInfo.TESTFIELD("Registration No.");

      VATNo := DELCHR(CompanyInfo."Registration No.");
      VATNo := DELCHR(VATNo,'=','/');

      CASE CompanyInfo."Tax Office Area" OF
        8,4,2,6,3,7,1,16:               // resedually old areas
          BEGIN
            PosTaxoffice := 9;
            NumberTaxOffice := 2;       // Tax Office No.
            PosArea := 12;
            NumberArea := 3;            // Area No.
            PosDistinction := 16;
            NumberDistinction := 4;     // Distinction No.
          END;
        9,10,11,12,13,14,15:            // Bavaria, Saarland and new areas
          BEGIN
            PosTaxoffice := 8;
            NumberTaxOffice := 3;
            PosArea := 12;
            NumberArea := 3;
            PosDistinction := 16;
            NumberDistinction := 4;
          END;
        5:                              // Nordrhein-Westfalen
          BEGIN
            PosTaxoffice := 8;
            NumberTaxOffice := 3;
            PosArea := 12;
            NumberArea := 4;
            PosDistinction := 17;
            NumberDistinction := 3;
          END;
      END;

      IF STRLEN(VATNo) <> NumberTaxOffice + NumberArea + NumberDistinction + 1 THEN
        ERROR(
          Text1140001,
          CompanyInfo.FIELDCAPTION("Registration No."),
          CompanyInfo."Tax Office Area",NumberTaxOffice + NumberArea + NumberDistinction + 1);
    END;

    PROCEDURE CalcTaxFigures@1140009(VATStmtName@1140000 : Record 257;VAR TaxAmount@1140001 : ARRAY [100] OF Decimal;VAR TaxBase@1140002 : ARRAY [100] OF Decimal;VAR TaxUnrealizedAmount@1140003 : ARRAY [100] OF Decimal;VAR TaxUnrealizedBase@1140004 : ARRAY [100] OF Decimal;VAR Continued@1140005 : Decimal;VAR TotalLine1@1140006 : Decimal;VAR TotalLine2@1140007 : Decimal;VAR TotalLine3@1140008 : Decimal);
    VAR
      VATStmtLine@1140009 : Record 256;
      KeyFigure@1140010 : Integer;
      i@1140011 : Integer;
    BEGIN
      VATStmtLine.SETRANGE("Statement Template Name",VATStmtName."Statement Template Name");
      VATStmtLine.SETRANGE("Statement Name",VATStmtName.Name);
      IF VATStmtLine.FIND('-') THEN
        REPEAT
          IF NOT EVALUATE(KeyFigure,VATStmtLine."Row No.") THEN
            KeyFigure := 0;
          IF (KeyFigure > 0) AND (KeyFigure <= 100) THEN BEGIN
            TotalAmount := 0;
            TotalBase := 0;
            TotalUnrealizedAmount := 0;
            TotalUnrealizedBase := 0;
            CalcLineTotal(VATStmtLine,0);
            IF VATStmtLine."Print with" = VATStmtLine."Print with"::"Opposite Sign" THEN BEGIN
              TaxAmount[KeyFigure] := TaxAmount[KeyFigure] - TotalAmount;
              TaxBase[KeyFigure] := TaxBase[KeyFigure] - TotalBase;
              TaxUnrealizedAmount[KeyFigure] := TaxUnrealizedAmount[KeyFigure] - TotalUnrealizedAmount;
              TaxUnrealizedBase[KeyFigure] := TaxUnrealizedBase[KeyFigure] - TotalUnrealizedBase;
            END ELSE BEGIN
              TaxAmount[KeyFigure] := TaxAmount[KeyFigure] + TotalAmount;
              TaxBase[KeyFigure] := TaxBase[KeyFigure] + TotalBase;
              TaxUnrealizedAmount[KeyFigure] := TaxUnrealizedAmount[KeyFigure] + TotalUnrealizedAmount;
              TaxUnrealizedBase[KeyFigure] := TaxUnrealizedBase[KeyFigure] + TotalUnrealizedBase;
            END;
            CASE KeyFigure OF
              51,86,36,80,97,93,98,96:
                Continued := Continued - TotalAmount;
              47,53,74,85,65:
                TotalLine1 := TotalLine1 - TotalAmount;
              66,61,62,67,63,64,59:
                TotalLine2 := TotalLine2 + TotalAmount;
              69,39:
                TotalLine3 := TotalLine3 - TotalAmount;
            END;
          END;
        UNTIL VATStmtLine.NEXT = 0;
      FOR i := 21 TO 100 DO BEGIN
        IF ABS(TaxBase[i]) < 1 THEN
          CASE i OF
            35:
              TaxAmount[36] := 0;
            76:
              TaxAmount[80] := 0;
            95:
              TaxAmount[98] := 0;
            94:
              TaxAmount[96] := 0;
            52:
              TaxAmount[53] := 0;
            73:
              TaxAmount[74] := 0;
            84:
              TaxAmount[85] := 0;
            57:
              TaxAmount[58] := 0;
            46:
              TaxAmount[47] := 0;
            78:
              TaxAmount[79] := 0;
          END;
        TaxBase[i] := ROUND(TaxBase[i],1,'<');
        TaxUnrealizedBase[i] := ROUND(TaxUnrealizedBase[i],1,'<');
      END;
    END;

    LOCAL PROCEDURE CalcLineTotal@1140010(VATStmtLine2@1140000 : Record 256;Level@1140001 : Integer) : Boolean;
    VAR
      GLAcc@1140002 : Record 15;
      VATEntry@1140003 : Record 254;
      i@1140004 : Integer;
      ErrorText@1140005 : Text[80];
    BEGIN
      CASE VATStmtLine2.Type OF
        VATStmtLine2.Type::"Account Totaling":
          IF VATStmtLine2."Account Totaling" <> '' THEN BEGIN
            GLAcc.SETFILTER("No.",VATStmtLine2."Account Totaling");
            GLAcc.SETRANGE("Date Filter",StartDate,EndDate);
            IF GLAcc.FIND('-') THEN BEGIN
              Amount := 0;
              REPEAT
                GLAcc.CALCFIELDS("Net Change","Additional-Currency Net Change");
                Amount := ConditionalAdd(Amount,GLAcc."Net Change",GLAcc."Additional-Currency Net Change");
              UNTIL GLAcc.NEXT = 0;
              CalcTotalAmount(VATStmtLine2);
            END;
          END;
        VATStmtLine2.Type::"VAT Entry Totaling":
          BEGIN
            VATEntry.SETCURRENTKEY(Type,Closed,"VAT Bus. Posting Group","VAT Prod. Posting Group",
              "Tax Jurisdiction Code","Use Tax","Posting Date");
            VATEntry.SETRANGE(Type,VATStmtLine2."Gen. Posting Type");
            CASE Selection OF
              Selection::Open:
                VATEntry.SETRANGE(Closed,FALSE);
              Selection::Closed:
                VATEntry.SETRANGE(Closed,TRUE);
            END;
            VATEntry.SETRANGE("VAT Bus. Posting Group",VATStmtLine2."VAT Bus. Posting Group");
            VATEntry.SETRANGE("VAT Prod. Posting Group",VATStmtLine2."VAT Prod. Posting Group");

            IF PeriodSelection = PeriodSelection::"Before and Within Period" THEN
              VATEntry.SETRANGE("Posting Date",0D,EndDate)
            ELSE
              VATEntry.SETRANGE("Posting Date",StartDate,EndDate);

            Amount := 0;
            CASE VATStmtLine2."Amount Type" OF
              VATStmtLine2."Amount Type"::Amount:
                BEGIN
                  VATEntry.CALCSUMS(Amount,"Additional-Currency Amount");
                  Amount := ConditionalAdd(0,VATEntry.Amount,VATEntry."Additional-Currency Amount");
                END;
              VATStmtLine2."Amount Type"::Base:
                BEGIN
                  VATEntry.CALCSUMS(Base,"Additional-Currency Base");
                  Amount := ConditionalAdd(0,VATEntry.Base,VATEntry."Additional-Currency Base");
                END;
              VATStmtLine2."Amount Type"::"Unrealized Amount":
                BEGIN
                  VATEntry.CALCSUMS("Unrealized Amount","Add.-Currency Unrealized Amt.");
                  Amount := ConditionalAdd(0,VATEntry."Unrealized Amount",VATEntry."Add.-Currency Unrealized Amt.");
                END;
              VATStmtLine2."Amount Type"::"Unrealized Base":
                BEGIN
                  VATEntry.CALCSUMS("Unrealized Base","Add.-Currency Unrealized Base");
                  Amount := ConditionalAdd(0,VATEntry."Unrealized Base",VATEntry."Add.-Currency Unrealized Base");
                END;
              ELSE
                VATStmtLine2.TESTFIELD("Amount Type");
            END;
            CalcTotalAmount(VATStmtLine2);
          END;
        VATStmtLine2.Type::"Row Totaling":
          BEGIN
            IF Level >= ARRAYLEN(LineNo) THEN
              EXIT(FALSE);
            Level := Level + 1;
            LineNo[Level] := VATStmtLine2."Row No.";

            IF VATStmtLine2."Row Totaling" = '' THEN
              EXIT(TRUE);
            VATStmtLine2.SETRANGE("Statement Template Name",VATStmtLine2."Statement Template Name");
            VATStmtLine2.SETRANGE("Statement Name",VATStmtLine2."Statement Name");
            VATStmtLine2.SETFILTER("Row No.",VATStmtLine2."Row Totaling");
            IF VATStmtLine2.FIND('-') THEN
              REPEAT
                IF NOT CalcLineTotal(VATStmtLine2,Level) THEN BEGIN
                  IF Level > 1 THEN
                    EXIT(FALSE);
                  FOR i := 1 TO ARRAYLEN(LineNo) DO
                    ErrorText := ErrorText + LineNo[i] + ' => ';
                  ErrorText := ErrorText + '...';
                  VATStmtLine2.FIELDERROR("Row No.",ErrorText);
                END;
              UNTIL VATStmtLine2.NEXT = 0;
          END;
        VATStmtLine2.Type::Description:
          ;
      END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CalcTotalAmount@1140011(VATStmtLine@1140000 : Record 256);
    BEGIN
      IF VATStmtLine."Calculate with" = VATStmtLine."Calculate with"::"Opposite Sign" THEN
        Amount := -Amount;
      CASE VATStmtLine."Amount Type" OF
        VATStmtLine."Amount Type"::Amount:
          TotalAmount := TotalAmount + Amount;
        VATStmtLine."Amount Type"::Base:
          TotalBase := TotalBase + Amount;
        VATStmtLine."Amount Type"::"Unrealized Amount":
          TotalUnrealizedAmount := TotalUnrealizedAmount + Amount;
        VATStmtLine."Amount Type"::"Unrealized Base":
          TotalUnrealizedBase := TotalUnrealizedBase + Amount;
      END;
    END;

    LOCAL PROCEDURE ConditionalAdd@1140012(Amount2@1140000 : Decimal;AmountToAdd@1140001 : Decimal;AddCurrAmountToAdd@1140002 : Decimal) : Decimal;
    BEGIN
      IF "Amounts in Add. Rep. Currency" THEN
        EXIT(Amount2 + AddCurrAmountToAdd);

      EXIT(Amount2 + AmountToAdd);
    END;

    PROCEDURE SetCalcParameters@1140013(StartDate2@1140000 : Date;EndDate2@1140001 : Date;Selection2@1140002 : 'Open,Closed,Open and Closed';PeriodSelection2@1140003 : 'Before and Within Period,Within Period';UseAmtsInAddCurr2@1140004 : Boolean);
    BEGIN
      StartDate := StartDate2;
      EndDate := EndDate2;
      Selection := Selection2;
      PeriodSelection := PeriodSelection2;
      "Amounts in Add. Rep. Currency" := UseAmtsInAddCurr2;
    END;

    BEGIN
    END.
  }
}


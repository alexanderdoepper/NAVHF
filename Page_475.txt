OBJECT Page 475 VAT Statement Preview Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00,NAVDACH7.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Lines;
               ESP=L¡neas;
               DEA=Zeilen];
    LinksAllowed=No;
    SourceTable=Table256;
    PageType=ListPart;
    RefreshOnActivate=Yes;
    OnAfterGetRecord=BEGIN
                       VATStatementGermany.CalcLineTotal(Rec,TotalAmount,TotalEmpty,TotalBase,TotalUnrealizedAmount,TotalUnrealizedBase,0);
                       IF "Print with" = "Print with"::"Opposite Sign" THEN BEGIN
                         TotalEmpty := -TotalEmpty;
                         TotalBase := -TotalBase;
                         TotalAmount := -TotalAmount;
                         TotalUnrealizedAmount := -TotalUnrealizedAmount;
                         TotalUnrealizedBase := -TotalUnrealizedBase;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Row No." }

    { 4   ;2   ;Field     ;
                SourceExpr=Description }

    { 19  ;2   ;Field     ;
                SourceExpr=Type }

    { 14  ;2   ;Field     ;
                SourceExpr="Amount Type" }

    { 6   ;2   ;Field     ;
                SourceExpr="VAT Bus. Posting Group" }

    { 8   ;2   ;Field     ;
                SourceExpr="VAT Prod. Posting Group" }

    { 10  ;2   ;Field     ;
                SourceExpr="Tax Jurisdiction Code";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr="Use Tax";
                Visible=FALSE }

    { 1140000;2;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Amount;
                           DEA=Betrag];
                BlankZero=Yes;
                SourceExpr=TotalEmpty;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              IF Type <> Type::"Row Totaling" THEN
                                IF Type <> Type::"Account Totaling" THEN
                                  FIELDERROR(Type);
                              DrillDownOnEntries;
                            END;
                             }

    { 1140002;2;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Base Amount;
                           DEA=Bemessungsgrundlage];
                BlankZero=Yes;
                SourceExpr=TotalBase;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              DrillDownOnEntries;
                            END;
                             }

    { 1140004;2;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=VAT Amount;
                           DEA=MwSt.-Betrag];
                BlankZero=Yes;
                SourceExpr=TotalAmount;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              DrillDownOnEntries;
                            END;
                             }

    { 1140006;2;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Unrealized Base Amount;
                           DEA=Unrealisierte Bemessungsgrundlage];
                BlankZero=Yes;
                SourceExpr=TotalUnrealizedBase;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              DrillDownOnEntries;
                            END;
                             }

    { 1140008;2;Field     ;
                Name=UnrealizedVATAmount;
                DrillDown=Yes;
                CaptionML=[ENU=Unrealized VAT Amount;
                           DEA=Unrealisierter MwSt.-Betrag];
                BlankZero=Yes;
                SourceExpr=TotalUnrealizedAmount;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              DrillDownOnEntries;
                            END;
                             }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Drilldown not possible when %1 is %2.;ESP=No se puede utilizar AssistButton cuando %1 es %2;DEA=Drilldown nicht m”glich, wenn %1 %2 ist.';
      GLEntry@1001 : Record 17;
      VATEntry@1002 : Record 254;
      Selection@1006 : 'Open,Closed,Open and Closed';
      PeriodSelection@1007 : 'Before and Within Period,Within Period';
      UseAmtsInAddCurr@1008 : Boolean;
      VATStatementGermany@1140000 : Report 11005;
      TotalAmount@1140001 : Decimal;
      TotalEmpty@1140002 : Decimal;
      TotalBase@1140003 : Decimal;
      TotalUnrealizedAmount@1140004 : Decimal;
      TotalUnrealizedBase@1140005 : Decimal;

    PROCEDURE UpdateForm@1(VAR VATStmtName@1000 : Record 257;NewSelection@1001 : 'Open,Closed,Open and Closed';NewPeriodSelection@1002 : 'Before and Within Period,Within Period';NewUseAmtsInAddCurr@1003 : Boolean);
    BEGIN
      SETRANGE("Statement Template Name",VATStmtName."Statement Template Name");
      SETRANGE("Statement Name",VATStmtName.Name);
      VATStmtName.COPYFILTER("Date Filter","Date Filter");
      Selection := NewSelection;
      PeriodSelection := NewPeriodSelection;
      UseAmtsInAddCurr := NewUseAmtsInAddCurr;
      VATStatementGermany.InitializeRequest(VATStmtName,Rec,Selection,PeriodSelection,FALSE,UseAmtsInAddCurr);
      CurrPage.UPDATE;
    END;

    PROCEDURE DrillDownOnEntries@1140000();
    BEGIN
      CASE Type OF
        Type::"Account Totaling":
          BEGIN
            GLEntry.SETFILTER("G/L Account No.","Account Totaling");
            IF PeriodSelection = PeriodSelection::"Before and Within Period" THEN
              GLEntry.SETRANGE("Posting Date",0D,GETRANGEMAX("Date Filter"))
            ELSE
              COPYFILTER("Date Filter",GLEntry."Posting Date");
            PAGE.RUN(PAGE::"General Ledger Entries",GLEntry);
          END;
        Type::"VAT Entry Totaling":
          BEGIN
            VATEntry.RESET;
            IF NOT
               VATEntry.SETCURRENTKEY(
                 Type,Closed,"VAT Bus. Posting Group","VAT Prod. Posting Group","Posting Date")
            THEN
              VATEntry.SETCURRENTKEY(
                Type,Closed,"Tax Jurisdiction Code","Use Tax","Posting Date");
            VATEntry.SETRANGE(Type,"Gen. Posting Type");
            VATEntry.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
            VATEntry.SETRANGE("VAT Prod. Posting Group","VAT Prod. Posting Group");
            VATEntry.SETRANGE("Tax Jurisdiction Code","Tax Jurisdiction Code");
            VATEntry.SETRANGE("Use Tax","Use Tax");
            IF GETFILTER("Date Filter") <> '' THEN
              IF PeriodSelection = PeriodSelection::"Before and Within Period" THEN
                VATEntry.SETRANGE("Posting Date",0D,GETRANGEMAX("Date Filter"))
              ELSE
                COPYFILTER("Date Filter",VATEntry."Posting Date");
            CASE Selection OF
              Selection::Open:
                VATEntry.SETRANGE(Closed,FALSE);
              Selection::Closed:
                VATEntry.SETRANGE(Closed,TRUE);
              Selection::"Open and Closed":
                VATEntry.SETRANGE(Closed);
            END;
            PAGE.RUN(PAGE::"VAT Entries",VATEntry);
          END;
        Type::"Row Totaling",
        Type::Description:
          ERROR(Text000,FIELDCAPTION(Type),Type);
      END;
    END;

    BEGIN
    END.
  }
}


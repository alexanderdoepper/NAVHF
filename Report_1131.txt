OBJECT Report 1131 Cost Allocation
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Cost Allocation;
               ESP=Asignaci¢n costes;
               DEA=Kostenzuteilung];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   InitializeRequest(1,99,WORKDATE,'','');
                 END;

  }
  DATASET
  {
    { 7485;    ;DataItem;CostAllocationSource;
               DataItemTable=Table1106;
               DataItemTableView=SORTING(Level,Valid From,Valid To,Cost Type Range)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               SETRANGE(Level,FromLevel,ToLevel);
                               SETRANGE(Variant,AllocVariant);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT AllocSourceErrorFound THEN
                                    AllocSourceErrorFound := ("Cost Center Code" = '') AND ("Cost Object Code" = '')
                                  ELSE
                                    CurrReport.SKIP;
                                END;

               OnPostDataItem=BEGIN
                                IF AllocSourceErrorFound OR AllocTargetErrorFound THEN BEGIN
                                  ShowAllocations(NOT AllocSourceErrorFound);
                                  CurrReport.QUIT;
                                END;
                              END;
                               }

    { 4986;1   ;DataItem;CostAllocationTarget;
               DataItemTable=Table1107;
               DataItemTableView=SORTING(ID,Line No.)
                                 ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  IF NOT AllocTargetErrorFound THEN
                                    AllocTargetErrorFound := ("Target Cost Center" = '') AND ("Target Cost Object" = '')
                                  ELSE
                                    CurrReport.BREAK;
                                END;

               DataItemLink=ID=FIELD(ID) }

    { 1662;    ;DataItem;                    ;
               DataItemTable=Table1106;
               DataItemTableView=SORTING(Level,Valid From,Valid To,Cost Type Range);
               OnPreDataItem=BEGIN
                               "Cost Entry".LOCKTABLE;
                               CostRegister.LOCKTABLE;
                               CostBudgetRegister.LOCKTABLE;
                               LOCKTABLE;
                               "Cost Allocation Target".LOCKTABLE;
                               "Cost Budget Entry".LOCKTABLE;

                               CostAccSetup.GET;
                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Cost Allocation");

                               IF AllocDate = 0D THEN
                                 ERROR(Text000);

                               IF FromLevel > ToLevel THEN
                                 ERROR(Text001);

                               IF CostBudgetName.Name <> '' THEN BEGIN
                                 AllocateBudget := TRUE;
                                 MsgText := Text002 + ' ' + CostBudgetName.Name;
                               END ELSE
                                 MsgText := Text003;

                               IF CurrReport.USEREQUESTPAGE THEN
                                 IF NOT CONFIRM(Text004,TRUE,MsgText,FromLevel,ToLevel,AllocDate,AllocVariant) THEN
                                   ERROR('');

                               SETRANGE(Level,FromLevel,ToLevel);
                               SETRANGE(Variant,AllocVariant);
                               SETRANGE("Valid From",0D,AllocDate);
                               SETFILTER("Valid To",'%1|>=%2',0D,AllocDate);
                               SETRANGE(Blocked,FALSE);

                               IF AllocateBudget THEN
                                 SETFILTER("Allocation Source Type",'%1|%2',"Allocation Source Type"::Both,"Allocation Source Type"::Budget)
                               ELSE
                                 SETRANGE("Allocation Source Type","Allocation Source Type"::Both,"Allocation Source Type"::Actual);

                               Window.OPEN(Text005)
                             END;

               OnAfterGetRecord=BEGIN
                                  IF (LastLevel <> 0) AND (Level > LastLevel) THEN BEGIN
                                    IF EntriesPerLevel > 0 THEN
                                      PostCostJournalLines;
                                    LastCostJourLineNo := 0;
                                    EntriesPerLevel := 0;
                                  END;

                                  LastLevel := Level;

                                  SourceTotalAmount := 0;
                                  Window.UPDATE(1,FORMAT(Level));
                                  Window.UPDATE(2,FORMAT(ID));
                                END;

               OnPostDataItem=BEGIN
                                IF TotalEntries = 0 THEN
                                  ERROR(Text006);

                                IF EntriesPerLevel > 0 THEN
                                  PostCostJournalLines;

                                Window.CLOSE;
                                IF CurrReport.USEREQUESTPAGE THEN
                                  MESSAGE(Text007,TotalEntries);
                              END;
                               }

    { 5276;1   ;DataItem;                    ;
               DataItemTable=Table1104;
               DataItemTableView=SORTING(Entry No.)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               IF AllocateBudget THEN
                                 CurrReport.BREAK;

                               IF "Cost Allocation Source"."Cost Center Code" <> '' THEN BEGIN
                                 SETCURRENTKEY("Cost Center Code","Cost Type No.",Allocated);
                                 SETFILTER("Cost Center Code","Cost Allocation Source"."Cost Center Code");
                               END ELSE BEGIN
                                 SETCURRENTKEY("Cost Object Code","Cost Type No.",Allocated);
                                 SETFILTER("Cost Object Code","Cost Allocation Source"."Cost Object Code");
                               END;
                               SETFILTER("Cost Type No.","Cost Allocation Source"."Cost Type Range");
                               SETRANGE(Allocated,FALSE);
                               SETRANGE("Posting Date",0D,AllocDate);

                               SourceTotalAmount := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  SourceTotalAmount := SourceTotalAmount + Amount;
                                  NoOfEntries := NoOfEntries + 1;
                                  Window.UPDATE(3,FORMAT(NoOfEntries));
                                END;

               OnPostDataItem=BEGIN
                                IF CostRegister.FINDLAST THEN;
                                MODIFYALL("Allocated with Journal No.",CostRegister."No." + 1);
                                MODIFYALL(Allocated,TRUE);
                              END;
                               }

    { 3233;1   ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Entry No.)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               IF NOT AllocateBudget THEN
                                 CurrReport.BREAK;

                               IF "Cost Allocation Source"."Cost Center Code" <> '' THEN BEGIN
                                 SETCURRENTKEY("Budget Name","Cost Center Code","Cost Type No.",Allocated);
                                 SETFILTER("Cost Center Code","Cost Allocation Source"."Cost Center Code");
                               END ELSE BEGIN
                                 SETCURRENTKEY("Budget Name","Cost Object Code","Cost Type No.",Allocated);
                                 SETFILTER("Cost Object Code","Cost Allocation Source"."Cost Object Code");
                               END;
                               SETRANGE("Budget Name",CostBudgetName.Name);
                               SETFILTER("Cost Type No.","Cost Allocation Source"."Cost Type Range");
                               SETRANGE(Allocated,FALSE);
                               SETRANGE(Date,0D,AllocDate);

                               SourceTotalAmount := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  SourceTotalAmount := SourceTotalAmount + Amount;
                                  NoOfEntries := NoOfEntries + 1;
                                  Window.UPDATE(3,FORMAT(NoOfEntries));
                                END;

               OnPostDataItem=BEGIN
                                IF CostBudgetRegister.FINDLAST THEN;
                                MODIFYALL("Allocated with Journal No.",CostBudgetRegister."No." + 1);
                                MODIFYALL(Allocated,TRUE);
                              END;
                               }

    { 9203;1   ;DataItem;                    ;
               DataItemTable=Table1107;
               DataItemTableView=SORTING(ID,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE(ID,"Cost Allocation Source".ID);
                               "Cost Allocation Source".CALCFIELDS("Total Share");
                               AllocTotalAmount := 0;

                               AllocSourceText := Text008;
                               IF "Cost Allocation Source"."Cost Type Range" <> '' THEN
                                 AllocSourceText := AllocSourceText + ' ' + "Cost Allocation Source"."Cost Type Range" + ' (' + Text017 + ')';
                               IF "Cost Allocation Source"."Cost Center Code" <> '' THEN
                                 AllocSourceText := AllocSourceText + ' ' + "Cost Allocation Source"."Cost Center Code" + ' (' + Text018 + ')';
                               IF "Cost Allocation Source"."Cost Object Code" <> '' THEN
                                 AllocSourceText := AllocSourceText + ' ' + "Cost Allocation Source"."Cost Object Code" + ' (' + Text019 + ')';

                               AllocTargetText := Text009;
                             END;

               OnAfterGetRecord=BEGIN
                                  CASE "Allocation Target Type" OF
                                    "Allocation Target Type"::"All Costs":
                                      BEGIN
                                        AllocAmount := ROUND(SourceTotalAmount / 100 * Percent,0.01);
                                        AllocRatio := STRSUBSTNO(Text010,
                                            Share,"Cost Allocation Source"."Total Share",SourceTotalAmount);
                                        IF ABS(AllocTotalAmount + AllocAmount - SourceTotalAmount) < 0.05 THEN
                                          AllocAmount := SourceTotalAmount - AllocTotalAmount;
                                      END;
                                    "Allocation Target Type"::"Percent per Share":
                                      BEGIN
                                        AllocAmount := ROUND(Share / 100 * "Percent per Share",0.01);
                                        AllocRatio := STRSUBSTNO(Text011,
                                            "Percent per Share",Share);
                                      END;
                                    "Allocation Target Type"::"Amount per Share":
                                      BEGIN
                                        AllocAmount := ROUND(Share * "Amount per Share",0.01);
                                        AllocRatio := STRSUBSTNO(Text012,
                                            "Amount per Share",Share);
                                      END;
                                  END;

                                  IF STRLEN(AllocTargetText) < MAXSTRLEN(TempCostJnlLine.Description) THEN BEGIN
                                    IF "Target Cost Center" <> '' THEN
                                      AllocTargetText := AllocTargetText + "Target Cost Center" + ', '
                                    ELSE
                                      AllocTargetText := AllocTargetText + "Target Cost Object" + ', ';
                                  END;

                                  AllocTotalAmount := AllocTotalAmount + AllocAmount;

                                  WriteJournalLine("Target Cost Type","Target Cost Center","Target Cost Object",AllocAmount,
                                    AllocSourceText,AllocRatio,"Cost Allocation Source".ID,FALSE);
                                END;

               OnPostDataItem=BEGIN
                                IF STRLEN(AllocTargetText) > MAXSTRLEN(TempCostJnlLine.Description) THEN
                                  AllocTargetText := COPYSTR(AllocTargetText,1,MAXSTRLEN(TempCostJnlLine.Description) - 10) + Text013;

                                AllocRatio := STRSUBSTNO(Text014,"Cost Allocation Source".Level,AllocDate);

                                WriteJournalLine("Cost Allocation Source"."Credit to Cost Type","Cost Allocation Source"."Cost Center Code",
                                  "Cost Allocation Source"."Cost Object Code",-AllocTotalAmount,
                                  AllocTargetText,AllocRatio,"Cost Allocation Source".ID,TRUE);

                                CostAccSetup."Last Allocation Doc. No." := INCSTR(CostAccSetup."Last Allocation Doc. No.");
                                CostAccSetup.MODIFY;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   CostBudgetName.INIT;
                 END;

    }
    CONTROLS
    {
      { 1   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 3   ;2   ;Field     ;
                  Name=From Alloc. Level;
                  CaptionML=[ENU=From Alloc. Level;
                             ESP=Desde nivel asig.;
                             DEA=Von Verteilungsebene];
                  SourceExpr=FromLevel;
                  MinValue=1;
                  MaxValue=99 }

      { 4   ;2   ;Field     ;
                  Name=To Alloc. Level;
                  CaptionML=[ENU=To Alloc. Level;
                             ESP=Hasta nivel asig.;
                             DEA=Bis Verteilungsebene];
                  SourceExpr=ToLevel;
                  MinValue=1;
                  MaxValue=99 }

      { 5   ;2   ;Field     ;
                  Name=Allocation Date;
                  CaptionML=[ENU=Allocation Date;
                             ESP=Fecha asignaci¢n;
                             DEA=Zuordnungsdatum];
                  SourceExpr=AllocDate }

      { 6   ;2   ;Field     ;
                  Name=Group;
                  CaptionML=[ENU=Group;
                             ESP=Grupo;
                             DEA=Gruppe];
                  BlankZero=Yes;
                  SourceExpr=AllocVariant }

      { 7   ;2   ;Field     ;
                  Name=Budget Name;
                  Lookup=Yes;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.;
                             DEA=Budgetname];
                  BlankZero=Yes;
                  SourceExpr=CostBudgetName.Name;
                  TableRelation="Cost Budget Name" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostRegister@1000 : Record 1105;
      CostBudgetRegister@1050 : Record 1111;
      TempCostJnlLine@1001 : TEMPORARY Record 1101;
      CostAccSetup@1002 : Record 1108;
      CostBudgetName@1003 : Record 1110;
      SourceCodeSetup@1004 : Record 242;
      Window@1005 : Dialog;
      FromLevel@1006 : Integer;
      ToLevel@1007 : Integer;
      AllocVariant@1008 : Code[10];
      LastLevel@1009 : Integer;
      NoOfEntries@1010 : Integer;
      EntriesPerLevel@1011 : Integer;
      TotalEntries@1012 : Integer;
      AllocDate@1013 : Date;
      SourceTotalAmount@1014 : Decimal;
      AllocAmount@1015 : Decimal;
      AllocSourceText@1016 : Text[80];
      AllocTargetText@1017 : Text[100];
      AllocRatio@1018 : Text[80];
      AllocTotalAmount@1019 : Decimal;
      LastCostJourLineNo@1020 : Integer;
      TotalDebit@1021 : Decimal;
      TotalCredit@1022 : Decimal;
      AllocateBudget@1023 : Boolean;
      MsgText@1024 : Text[80];
      AllocSourceErrorFound@1025 : Boolean;
      AllocTargetErrorFound@1026 : Boolean;
      Text000@1027 : TextConst 'ENU=Allocation date must be defined.;ESP=Debe definirse la fecha de asignaci¢n.;DEA=Geben Sie das Zuordnungsdatum an.';
      Text001@1028 : TextConst 'ENU=From Alloc. Level must not be higher than To Alloc. Level.;ESP=Desde nivel asig. no puede ser superior a Hasta nivel asig.;DEA=Die Von-Verteilungsebene darf nicht grî·er sein als die Bis-Verteilungsebene.';
      Text002@1029 : TextConst 'ENU=Budget cost from budget;ESP=Coste presupuestario;DEA=Budgetkosten aus Budget';
      Text003@1030 : TextConst 'ENU=Actual cost;ESP=Coste real;DEA=Ist-Kosten';
      Text004@1031 : TextConst 'ENU=%1 will be allocated for levels %2 to %3.\Posting date: %4,  group: "%5"\Do you want to start the job?;ESP=%1 se asignar† para los niveles del %2 al %3.\Fecha registro: %4, grupo: "%5"\®Desea iniciar el proyecto?;DEA=''%1'' wird fÅr die Ebenen %2 bis %3 verteilt.\Buchungsdatum: %4, Gruppe: %5\Mîchten Sie das Projekt starten?';
      Text005@1032 : TextConst 'ENU="Cost allocation\Level                      #1####### \Source ID                  #2####### \Sum source entries         #3####### \Write allocation entries   #4####### ";ESP="Asignaci¢n costes\Nivel                         #1####### \Id. origen                  #2####### \Suma movs. proced.         #3####### \Escribir movs. asignaci¢n   #4####### ";DEA="Kostenzuteilung\Ebene                       #1####### \Herkunfts-ID                #2####### \Herkunftsposten summieren   #3####### \Zuordnungen schreiben       #4####### "';
      Text006@1033 : TextConst 'ENU=No entries have been created for the selected allocations.;ESP=No se han creado movimientos para las asignaciones seleccionadas.;DEA=FÅr die ausgewÑhlten Zuordnungen wurden keine Posten erstellt.';
      Text007@1034 : TextConst 'ENU=%1 allocation entries have been generated and processed.;ESP=Se generaron y procesaron %1 movimientos de asignaci¢n.;DEA=%1 Zuordnungen wurden generiert und verarbeitet.';
      Text008@1035 : TextConst 'ENU=Alloc. Source:;ESP=Origen asignaci¢n:;DEA=Zuordnungsquelle:';
      Text009@1036 : TextConst 'ENU="Alloc. Target: ";ESP="Destino asignaci¢n: ";DEA="Zuordnungsziel: "';
      Text010@1037 : TextConst 'ENU=Alloc: %1 of %2 shares. Base LCY %3;ESP=Asig.: %1 de %2 partes. %3 DL base;DEA=Verteilung: %1 von %2 Anteilen. Zugrunde liegende MW: %3';
      Text011@1038 : TextConst 'ENU=Alloc: %1 pct of %2 shares;ESP=Asig.: %1 por ciento de %2 partes;DEA=Verteilung: %1 Prozent von %2 Anteilen';
      Text012@1039 : TextConst 'ENU=Alloc: %1 LCY of %2 shares;ESP=Asig.: DL %1 de %2 partes;DEA=Verteilung: %1 MW von %2 Anteilen';
      Text013@1040 : TextConst 'ENU=" ... etc.";ESP=" ... etc.";DEA=" ...usw."';
      Text014@1041 : TextConst 'ENU=Alloc. level %1 of %2;ESP=Nivel asig. %1 de %2;DEA=Verteilungsebene %1 von %2';
      Text015@1042 : TextConst 'ENU=Not all cost types for debit and credit are defined for allocation ID %1.;ESP=No se han definido todos los tipos coste de debe y haber para Id. asignaci¢n %1.;DEA=FÅr die Verteilungs-ID %1 wurden nicht alle Kostenarten fÅr Soll und Haben definiert.';
      Text016@1043 : TextConst 'ENU=For allocation ID %1, cost center or cost object must be defined for debit and credit. Cost center: "%2", cost object "%3".;ESP=Debe definirse centro de coste u objeto de coste de debe y haber para id. asig. %1. Centro de coste: "%2", objeto de coste "%3".;DEA=FÅr die Verteilungs-ID %1 muss die Kostenstelle oder der KostentrÑger fÅr Soll und Haben angegeben werden. Kostenstelle: "%2", KostentrÑger: "%3".';
      Text017@1044 : TextConst 'ENU=Cost Type;ESP=Tipo coste;DEA=Kostenart';
      Text018@1045 : TextConst 'ENU=Cost Center;ESP=Centro coste;DEA=Kostenstelle';
      Text019@1046 : TextConst 'ENU=Cost Object;ESP=Objeto coste;DEA=KostentrÑger';
      Text020@1047 : TextConst 'ENU=One or more allocation targets do not have a cost center or cost object defined. The allocation cannot continue.;ESP=Uno o m†s destinos de asignaci¢n no tienen un centro de coste u objeto de coste definido. La asignaci¢n no puede continuar.;DEA=FÅr mindestens ein Verteilungsziel wurde keine Kostenstelle oder kein KostentrÑger definiert. Die Verteilung kann nicht fortgesetzt werden.';
      Text021@1048 : TextConst 'ENU=One or more allocation sources do not have a cost center or cost object defined. The allocation cannot continue.;ESP=Uno o m†s or°genes de asignaci¢n no tienen un centro de coste u objeto de coste definido. La asignaci¢n no puede continuar.;DEA=FÅr mindestens eine Verteilungsquelle wurde keine Kostenstelle oder kein KostentrÑger definiert. Die Verteilung kann nicht fortgesetzt werden.';
      Text022@1049 : TextConst 'ENU=Posting Cost Entries @1@@@@@@@@@@\;ESP=Registrando movimientos de coste                @1@@@@@@@@@@\;DEA=Kostenposten werden gebucht...              @1@@@@@@@@@@\';

    PROCEDURE ShowAllocations@2(CheckTargets@1001 : Boolean);
    VAR
      CostAllocations@1003 : Report 1129;
      ConfirmText@1002 : Text[1024];
      SkipSourcesWithoutTargets@1004 : Boolean;
    BEGIN
      IF CheckTargets THEN BEGIN
        ConfirmText := Text020;
        CostAllocationTarget.SETFILTER("Target Cost Center",'%1','');
        CostAllocationTarget.SETFILTER("Target Cost Object",'%1','');
      END ELSE BEGIN
        ConfirmText := Text021;
        CostAllocationSource.SETFILTER("Cost Center Code",'%1','');
        CostAllocationSource.SETFILTER("Cost Object Code",'%1','');
        CostAllocationTarget.SETFILTER(ID,'<%1','');
      END;
      IF CONFIRM(ConfirmText,TRUE) THEN BEGIN
        SkipSourcesWithoutTargets := CheckTargets;
        CostAllocations.InitializeRequest(CostAllocationSource,CostAllocationTarget,SkipSourcesWithoutTargets);
        CostAllocations.RUN;
      END;
    END;

    PROCEDURE WriteJournalLine@1(CostTypeCode@1000 : Code[20];CostCenterCode@1001 : Code[20];CostObjectCode@1002 : Code[20];PostAmount@1003 : Decimal;Text@1004 : Text[100];AllocKey@1005 : Text[80];AllocID@1006 : Code[10];Allocated2@1007 : Boolean);
    BEGIN
      IF PostAmount = 0 THEN
        EXIT;

      IF CostTypeCode = '' THEN
        ERROR(Text015,"Cost Allocation Source".ID);

      IF ((CostCenterCode = '') AND (CostObjectCode = '')) OR ((CostCenterCode <> '') AND (CostObjectCode <> '')) THEN
        ERROR(Text016,"Cost Allocation Source".ID,CostCenterCode,CostObjectCode);

      WITH TempCostJnlLine DO BEGIN
        LastCostJourLineNo := LastCostJourLineNo + 10000;
        "Line No." := LastCostJourLineNo;
        "Posting Date" := AllocDate;
        "Cost Type No." := CostTypeCode;
        "Cost Center Code" := CostCenterCode;
        "Cost Object Code" := CostObjectCode;
        "Document No." := INCSTR(CostAccSetup."Last Allocation Doc. No.");
        Description := COPYSTR(Text,1,MAXSTRLEN(Description));
        Amount := PostAmount;
        "System-Created Entry" := TRUE;
        Allocated := Allocated2;
        "Allocation Description" := AllocKey;
        "Allocation ID" := AllocID;
        "Source Code" := SourceCodeSetup."Cost Allocation";
        "Budget Name" := CostBudgetName.Name;
        INSERT;

        IF Amount > 0 THEN
          TotalDebit := TotalDebit + Amount
        ELSE
          TotalCredit := TotalCredit + Amount;
      END;

      EntriesPerLevel := EntriesPerLevel + 1;
      TotalEntries := TotalEntries + 1;
      Window.UPDATE(4,FORMAT(TotalEntries));
    END;

    PROCEDURE PostCostJournalLines@12();
    VAR
      CostJnlLine@1000 : Record 1101;
      CAJnlPostLine@1001 : Codeunit 1102;
      Window2@1002 : Dialog;
      CostJnlLineStep@1004 : Integer;
      JournalLineCount@1005 : Integer;
    BEGIN
      TempCostJnlLine.RESET;
      Window2.OPEN(
        Text022);
      IF TempCostJnlLine.COUNT > 0 THEN
        JournalLineCount := 10000 * 100000 DIV TempCostJnlLine.COUNT;
      IF TempCostJnlLine.FINDSET THEN BEGIN
        REPEAT
          CostJnlLineStep := CostJnlLineStep + JournalLineCount;
          Window2.UPDATE(1,CostJnlLineStep DIV 100000);
          CostJnlLine := TempCostJnlLine;
          CAJnlPostLine.RunWithCheck(CostJnlLine);
        UNTIL TempCostJnlLine.NEXT = 0;
        TempCostJnlLine.DELETEALL;
      END;
      Window2.CLOSE;
    END;

    PROCEDURE InitializeRequest@3(NewFromLevel@1000 : Integer;NewToLevel@1001 : Integer;NewAllocDate@1002 : Date;NewAllocVariant@1003 : Code[10];NewCostBudgetName@1004 : Code[10]);
    BEGIN
      FromLevel := NewFromLevel;
      ToLevel := NewToLevel;
      AllocDate := NewAllocDate;
      AllocVariant := NewAllocVariant;
      CostBudgetName.Name := NewCostBudgetName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


OBJECT Table 5005351 Phys. Inventory Order Line
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVDACH7.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetPhysInvtOrderHeader;
               PhysInvtOrderHeader.TESTFIELD(Status,PhysInvtOrderHeader.Status::Open);
             END;

    OnDelete=BEGIN
               TestStatusOpen;
               TESTFIELD("In Recording Lines",FALSE);

               ExpectPhysInvtTrackLine.RESET;
               ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
               ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");
               ExpectPhysInvtTrackLine.DELETEALL;

               ReservEntry.RESET;
               ReservEntry.SETCURRENTKEY(
                 "Source Type","Source Subtype","Source ID",
                 "Source Batch Name","Source Prod. Order Line",
                 "Source Ref. No.");
               ReservEntry.SETRANGE("Source Type",DATABASE::"Phys. Inventory Order Line");
               ReservEntry.SETRANGE("Source Subtype",0);
               ReservEntry.SETRANGE("Source ID","Document No.");
               ReservEntry.SETRANGE("Source Batch Name",'');
               ReservEntry.SETRANGE("Source Prod. Order Line",0);
               ReservEntry.SETRANGE("Source Ref. No.","Line No.");
               ReservEntry.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text1005,TABLECAPTION);
             END;

    CaptionML=[ENU=Phys. Inventory Order Line;
               DEA=Inventurauftragszeile];
    PasteIsValid=No;
    LookupPageID=Page5005353;
    DrillDownPageID=Page5005353;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;TableRelation="Phys. Inventory Order Header";
                                                   CaptionML=[ENU=Document No.;
                                                              DEA=Belegnr.] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              DEA=Zeilennr.] }
    { 12  ;   ;In Recording Lines  ;Boolean       ;CaptionML=[ENU=In Recording Lines;
                                                              DEA=In Erfassungszeilen enthalten];
                                                   Editable=No }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                TempPhysInvtOrderLine@1140000 : TEMPORARY Record 5005351;
                                                              BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("In Recording Lines",FALSE);

                                                                TempPhysInvtOrderLine := Rec;
                                                                INIT;
                                                                "Item No." := TempPhysInvtOrderLine."Item No.";
                                                                "Variant Code" := '';
                                                                ResetQtyExpected;

                                                                IF "Item No." = '' THEN
                                                                  EXIT;

                                                                GetPhysInvtOrderHeader;
                                                                GetItem;
                                                                Item.TESTFIELD(Blocked,FALSE);

                                                                VALIDATE(Description,Item.Description);
                                                                VALIDATE("Description 2",Item."Description 2");

                                                                VALIDATE("Base Unit of Measure Code",Item."Base Unit of Measure");
                                                                IF "Base Unit of Measure Code" <> '' THEN BEGIN
                                                                  UnitOfMeasure.GET("Base Unit of Measure Code");
                                                                  "Unit of Measure" := UnitOfMeasure.Description;
                                                                END ELSE
                                                                  "Unit of Measure" := '';

                                                                "Shelf/Bin No." := Item."Shelf No.";
                                                                "Item Category Code" := Item."Item Category Code";
                                                                "Product Group Code" := Item."Product Group Code";

                                                                VALIDATE("Gen. Bus. Posting Group",PhysInvtOrderHeader."Gen. Bus. Posting Group");
                                                                VALIDATE("Gen. Prod. Posting Group",Item."Gen. Prod. Posting Group");
                                                                VALIDATE("Inventory Posting Group",Item."Inventory Posting Group");

                                                                CreateDim(DATABASE::Item,"Item No.");

                                                                "Location Code" := PhysInvtOrderHeader."Location Code";
                                                                "Bin Code" := PhysInvtOrderHeader."Bin Code";

                                                                "Use Tracking Lines" :=
                                                                  ItemTrkgMgtPhysInvtLine.SuggestUseTrackingLines(Item);

                                                                GetFieldsFromSKU;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              DEA=Artikelnr.] }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("In Recording Lines",FALSE);

                                                                IF "Variant Code" <> xRec."Variant Code" THEN BEGIN
                                                                  ResetQtyExpected;
                                                                  GetFieldsFromSKU;
                                                                END;

                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;

                                                                TESTFIELD("Item No.");

                                                                ItemVariant.GET("Item No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                                "Description 2" := ItemVariant."Description 2";
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              DEA=Variantencode] }
    { 22  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("In Recording Lines",FALSE);

                                                                IF "Location Code" <> xRec."Location Code" THEN
                                                                  ResetQtyExpected;

                                                                GetFieldsFromSKU;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              DEA=Lagerortcode] }
    { 23  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                Location@1140000 : Record 14;
                                                              BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("In Recording Lines",FALSE);

                                                                IF "Bin Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Location Code");
                                                                  Location.GET("Location Code");
                                                                  Location.TESTFIELD("Bin Mandatory",TRUE);
                                                                  Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                END;

                                                                IF "Bin Code" <> xRec."Bin Code" THEN
                                                                  ResetQtyExpected;
                                                              END;

                                                   CaptionML=[ENU=Bin Code;
                                                              DEA=Lagerplatzcode] }
    { 30  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              DEA=Beschreibung] }
    { 31  ;   ;Description 2       ;Text30        ;CaptionML=[ENU=Description 2;
                                                              DEA=Beschreibung 2] }
    { 32  ;   ;Unit of Measure     ;Text10        ;CaptionML=[ENU=Unit of Measure;
                                                              DEA=Einheit] }
    { 40  ;   ;Base Unit of Measure Code;Code10   ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Base Unit of Measure Code;
                                                              DEA=Basiseinheitencode];
                                                   Editable=No }
    { 50  ;   ;Qty. Expected (Base);Decimal       ;CaptionML=[ENU=Qty. Expected (Base);
                                                              DEA=Erwartete Menge (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 51  ;   ;Qty. Exp. Calculated;Boolean       ;CaptionML=[ENU=Qty. Exp. Calculated;
                                                              DEA=Berechnete erw. Menge];
                                                   Editable=No }
    { 52  ;   ;Qty. Exp. Track. Lines (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Expect. Phys. Inv. Track. Line"."Quantity (Base)" WHERE (Order No=FIELD(Document No.),
                                                                                                                             Order Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Qty. Exp. Track. Lines (Base);
                                                              DEA=Erw. Menge Verf.-Zeile (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Use Tracking Lines  ;Boolean       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;

                                                                IF "Use Tracking Lines" <> xRec."Use Tracking Lines" THEN
                                                                  ResetQtyExpected;
                                                              END;

                                                   CaptionML=[ENU=Use Tracking Lines;
                                                              DEA=Verfolgungszeilen verwenden] }
    { 55  ;   ;Last Item Ledger Entry No.;Integer ;TableRelation="Item Ledger Entry";
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Item Ledger Entry No.;
                                                              DEA=Letzte Artikelposten Lfd. Nr.];
                                                   Editable=No }
    { 60  ;   ;Unit Amount         ;Decimal       ;CaptionML=[ENU=Unit Amount;
                                                              DEA=StÅckpreis];
                                                   AutoFormatType=2 }
    { 62  ;   ;Unit Cost           ;Decimal       ;CaptionML=[ENU=Unit Cost;
                                                              DEA=Einstandspreis];
                                                   AutoFormatType=2 }
    { 70  ;   ;No. Finished Rec.-Lines;Integer    ;CaptionML=[ENU=No. Finished Rec.-Lines;
                                                              DEA=Anzahl beendete Erfassungszeilen];
                                                   Editable=No }
    { 71  ;   ;Qty. Recorded (Base);Decimal       ;CaptionML=[ENU=Qty. Recorded (Base);
                                                              DEA=Erfasste Menge (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 72  ;   ;Quantity (Base)     ;Decimal       ;CaptionML=[ENU=Quantity (Base);
                                                              DEA=Menge (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 73  ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              DEA=Postenart];
                                                   OptionCaptionML=[ENU=" ,Positive Adjmt.,Negative Adjmt.";
                                                                    DEA=" ,Zugang,Abgang"];
                                                   OptionString=[ ,Positive Adjmt.,Negative Adjmt.];
                                                   Editable=No }
    { 74  ;   ;Pos. Qty. (Base)    ;Decimal       ;CaptionML=[ENU=Pos. Qty. (Base);
                                                              DEA=Positive Menge (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 75  ;   ;Neg. Qty. (Base)    ;Decimal       ;CaptionML=[ENU=Neg. Qty. (Base);
                                                              DEA=Negative Menge (Basis)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 76  ;   ;Without difference  ;Boolean       ;CaptionML=[ENU=Without difference;
                                                              DEA=Ohne Differenz];
                                                   Editable=No }
    { 80  ;   ;Recorded without Order;Boolean     ;CaptionML=[ENU=Recorded without Order;
                                                              DEA=Ohne Auftrag erfasst];
                                                   Editable=No }
    { 90  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              DEA=Shortcutdimensionscode 1];
                                                   CaptionClass='1,2,1' }
    { 91  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              DEA=Shortcutdimensionscode 2];
                                                   CaptionClass='1,2,2' }
    { 100 ;   ;Shelf/Bin No.       ;Code10        ;CaptionML=[ENU=Shelf/Bin No.;
                                                              DEA=Regalnr.] }
    { 110 ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              DEA=GeschÑftsbuchungsgruppe] }
    { 111 ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              DEA=Produktbuchungsgruppe] }
    { 112 ;   ;Inventory Posting Group;Code10     ;TableRelation="Inventory Posting Group";
                                                   CaptionML=[ENU=Inventory Posting Group;
                                                              DEA=Lagerbuchungsgruppe] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              DEA=Dimensionssatz-ID];
                                                   Editable=No }
    { 5704;   ;Item Category Code  ;Code10        ;TableRelation="Item Category";
                                                   CaptionML=[ENU=Item Category Code;
                                                              DEA=Artikelkategoriencode] }
    { 5707;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   CaptionML=[ENU=Product Group Code;
                                                              DEA=Produktgruppencode] }
    { 7380;   ;Phys Invt Counting Period Code;Code10;
                                                   TableRelation="Phys. Invt. Counting Period";
                                                   CaptionML=[ENU=Phys Invt Counting Period Code;
                                                              DEA=InventurhÑufigkeitscode];
                                                   Editable=No }
    { 7381;   ;Phys Invt Counting Period Type;Option;
                                                   CaptionML=[ENU=Phys Invt Counting Period Type;
                                                              DEA=InventurhÑufigk.-Codeherkunft];
                                                   OptionCaptionML=[ENU=" ,Item,SKU";
                                                                    DEA=" ,Artikel,Lagerhaltungsdaten"];
                                                   OptionString=[ ,Item,SKU];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
    {    ;Document No.,Item No.,Variant Code,Location Code,Bin Code }
    {    ;Document No.,Entry Type,Without difference }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Document No.,Line No.,Description        }
  }
  CODE
  {
    VAR
      Text1140001@1140001 : TextConst 'ENU="You cannot set %1 = yes, because there is a difference between the values of the fields %2 and %3. \";DEA=Sie kînnen %1 nicht auf Ja setzten, weil eine Differenz zwischen den Werten der Felder %2 and %3 existiert.\';
      Text1140002@1140002 : TextConst 'ENU=Identified values in the line:  %1 %2 %3 %4.;DEA=Identifizierende Werte der Zeile:  %1 %2 %3 %4.';
      Text1140003@1140003 : TextConst 'ENU=The value of field %1 and the sum of all %2, field %3, of the order line are different.;DEA=Der Wert des Feldes %1 und die Summe aller %2, Feld %3 der Auftragszeile sind unterschiedlich.';
      Text1140004@1140004 : TextConst 'ENU="You must specify a %1 or a %2 in %3 %4 when you set %5 = yes.";DEA=Sie mÅssen %1 oder %2 in %3 %4 angeben, wenn Sie %5 auf Ja setzen.';
      PhysInvtOrderHeader@1140005 : Record 5005350;
      Item@1140006 : Record 27;
      ItemVariant@1140007 : Record 5401;
      UnitOfMeasure@1140008 : Record 204;
      ExpectPhysInvtTrackLine@1140010 : Record 5005361;
      ReservEntry@1140011 : Record 337;
      SKU@1140013 : Record 5700;
      BinContent@1140014 : Record 7302;
      DimManagement@1140015 : Codeunit 408;
      Text1005@1140016 : TextConst 'ENU=You cannot rename a %1.;DEA=Sie kînnen einen %1 nicht umbenennen.';
      ItemTrkgMgtPhysInvtLine@1140018 : Codeunit 5005364;

    LOCAL PROCEDURE GetPhysInvtOrderHeader@1140000();
    BEGIN
      TESTFIELD("Document No.");
      IF "Document No." <> PhysInvtOrderHeader."No." THEN
        PhysInvtOrderHeader.GET("Document No.");
    END;

    LOCAL PROCEDURE GetItem@1140001();
    BEGIN
      TESTFIELD("Item No.");
      IF "Item No." <> Item."No." THEN
        Item.GET("Item No.");
    END;

    LOCAL PROCEDURE TestStatusOpen@1140002();
    BEGIN
      GetPhysInvtOrderHeader;
      PhysInvtOrderHeader.TESTFIELD(Status,PhysInvtOrderHeader.Status::Open);
    END;

    PROCEDURE CalcQtyAndTrackLinesExpected@1140003();
    VAR
      ItemLedgEntry@1140000 : Record 32;
      WhseEntry@1140001 : Record 7312;
    BEGIN
      IF EmptyLine THEN
        EXIT;

      GetItem;

      GetPhysInvtOrderHeader;
      PhysInvtOrderHeader.TESTFIELD("Posting Date");

      TestStatusOpen;

      CalcQtyAndLastItemLedgExpected(
        "Qty. Expected (Base)",
        "Last Item Ledger Entry No.");

      // Create Expected Phys. Invt. Tracking Lines:
      IF "Use Tracking Lines" THEN BEGIN
        ExpectPhysInvtTrackLine.RESET;
        ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
        ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");
        ExpectPhysInvtTrackLine.DELETEALL;

        IF NOT ItemTrkgMgtPhysInvtLine.LocationIsBinMandatory("Location Code") THEN BEGIN
          ItemLedgEntry.RESET;

          ItemLedgEntry.SETCURRENTKEY(
            "Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");
          ItemLedgEntry.SETRANGE("Item No.","Item No.");
          ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
          ItemLedgEntry.SETRANGE("Location Code","Location Code");
          ItemLedgEntry.SETRANGE("Posting Date",0D,PhysInvtOrderHeader."Posting Date");
          IF ItemLedgEntry.FIND('-') THEN
            REPEAT
              IF NOT
                 ExpectPhysInvtTrackLine.GET(
                   "Document No.","Line No.",ItemLedgEntry."Serial No.",ItemLedgEntry."Lot No.")
              THEN BEGIN
                ExpectPhysInvtTrackLine.INIT;
                ExpectPhysInvtTrackLine."Order No" := "Document No.";
                ExpectPhysInvtTrackLine."Order Line No." := "Line No.";
                ExpectPhysInvtTrackLine."Serial No." := ItemLedgEntry."Serial No.";
                ExpectPhysInvtTrackLine."Lot No." := ItemLedgEntry."Lot No.";
                ExpectPhysInvtTrackLine."Quantity (Base)" := ItemLedgEntry.Quantity;
                ExpectPhysInvtTrackLine.INSERT;
              END ELSE BEGIN
                ExpectPhysInvtTrackLine."Quantity (Base)" :=
                  ExpectPhysInvtTrackLine."Quantity (Base)" +
                  ItemLedgEntry.Quantity;
                ExpectPhysInvtTrackLine.MODIFY;
              END;
            UNTIL ItemLedgEntry.NEXT = 0;
          ExpectPhysInvtTrackLine.RESET;
          ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
          ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");
          ExpectPhysInvtTrackLine.SETRANGE("Quantity (Base)",0);
          ExpectPhysInvtTrackLine.DELETEALL;
          TestQtyExpected;
        END ELSE BEGIN
          IF ItemTrkgMgtPhysInvtLine.GetTrackingNosFromWhse(Item) THEN BEGIN
            WhseEntry.RESET;
            WhseEntry.SETCURRENTKEY(
              "Location Code","Bin Code","Item No.","Variant Code");
            WhseEntry.SETRANGE("Location Code","Location Code");
            WhseEntry.SETRANGE("Bin Code","Bin Code");
            WhseEntry.SETRANGE("Item No.","Item No.");
            WhseEntry.SETRANGE("Variant Code","Variant Code");
            WhseEntry.SETRANGE("Registering Date",0D,PhysInvtOrderHeader."Posting Date");
            IF WhseEntry.FIND('-') THEN
              REPEAT
                IF NOT
                   ExpectPhysInvtTrackLine.GET(
                     "Document No.","Line No.",WhseEntry."Serial No.",WhseEntry."Lot No.")
                THEN BEGIN
                  ExpectPhysInvtTrackLine.INIT;
                  ExpectPhysInvtTrackLine."Order No" := "Document No.";
                  ExpectPhysInvtTrackLine."Order Line No." := "Line No.";
                  ExpectPhysInvtTrackLine."Serial No." := WhseEntry."Serial No.";
                  ExpectPhysInvtTrackLine."Lot No." := WhseEntry."Lot No.";
                  ExpectPhysInvtTrackLine."Quantity (Base)" := WhseEntry."Qty. (Base)";
                  ExpectPhysInvtTrackLine.INSERT;
                END ELSE BEGIN
                  ExpectPhysInvtTrackLine."Quantity (Base)" :=
                    ExpectPhysInvtTrackLine."Quantity (Base)" +
                    WhseEntry."Qty. (Base)";
                  ExpectPhysInvtTrackLine.MODIFY;
                END;
              UNTIL WhseEntry.NEXT = 0;
            ExpectPhysInvtTrackLine.RESET;
            ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
            ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");
            ExpectPhysInvtTrackLine.SETRANGE("Quantity (Base)",0);
            ExpectPhysInvtTrackLine.DELETEALL;
            TestQtyExpected;
          END ELSE
            // In the case of
            // ItemTrkgMgtPhysInvtLine.LocationIsBinMandatory("Location Code") = Yes and
            // ItemTrkgMgtPhysInvtLine.GetTrackingNosFromWhse(Item) = No
            // Navision cannot create the records of table ExpectPhysInvtTrackLine
            // In this case Navision must create the Reservation Entries by
            // analyzing the ItemLedgerEntries and WhseEntries during the function to Finish the Order.
            ;
        END;
      END;

      "Qty. Exp. Calculated" := TRUE;
    END;

    PROCEDURE CalcQtyAndLastItemLedgExpected@1140004(VAR QtyExpected@1140000 : Decimal;VAR LastItemLedgEntryNo@1140001 : Integer);
    VAR
      ItemLedgEntry@1140002 : Record 32;
      WhseEntry@1140003 : Record 7312;
    BEGIN
      GetPhysInvtOrderHeader;
      PhysInvtOrderHeader.TESTFIELD("Posting Date");

      TestStatusOpen;

      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Item No.");
      ItemLedgEntry.SETRANGE("Item No.","Item No.");
      IF ItemLedgEntry.FINDLAST THEN
        LastItemLedgEntryNo := ItemLedgEntry."Entry No."
      ELSE
        LastItemLedgEntryNo := 0;

      IF ItemTrkgMgtPhysInvtLine.LocationIsBinMandatory("Location Code") THEN BEGIN
        TESTFIELD("Bin Code");
        WhseEntry.RESET;
        WhseEntry.SETCURRENTKEY(
          "Location Code","Bin Code","Item No.","Variant Code");
        WhseEntry.SETRANGE("Location Code","Location Code");
        WhseEntry.SETRANGE("Bin Code","Bin Code");
        WhseEntry.SETRANGE("Item No.","Item No.");
        WhseEntry.SETRANGE("Variant Code","Variant Code");
        WhseEntry.SETRANGE("Registering Date",0D,PhysInvtOrderHeader."Posting Date");
        WhseEntry.CALCSUMS("Qty. (Base)");
        QtyExpected := WhseEntry."Qty. (Base)";
      END ELSE BEGIN
        TESTFIELD("Bin Code",'');
        ItemLedgEntry.RESET;

        ItemLedgEntry.SETCURRENTKEY(
          "Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");
        ItemLedgEntry.SETRANGE("Item No.","Item No.");
        ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
        ItemLedgEntry.SETRANGE("Location Code","Location Code");
        ItemLedgEntry.SETRANGE("Posting Date",0D,PhysInvtOrderHeader."Posting Date");
        ItemLedgEntry.CALCSUMS(Quantity);
        QtyExpected := ItemLedgEntry.Quantity;
      END;
    END;

    PROCEDURE ResetQtyExpected@1140005();
    BEGIN
      "Qty. Expected (Base)" := 0;

      ExpectPhysInvtTrackLine.RESET;
      ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
      ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");
      ExpectPhysInvtTrackLine.DELETEALL;
      CALCFIELDS("Qty. Exp. Track. Lines (Base)");

      "Qty. Exp. Calculated" := FALSE;
    END;

    PROCEDURE TestQtyExpected@1140006();
    BEGIN
      IF "Use Tracking Lines" THEN BEGIN
        IF ItemTrkgMgtPhysInvtLine.LocationIsBinMandatory("Location Code") AND
           NOT ItemTrkgMgtPhysInvtLine.GetTrackingNosFromWhse(Item)
        THEN
          EXIT;

        CALCFIELDS("Qty. Exp. Track. Lines (Base)");
        IF "Qty. Expected (Base)" <> "Qty. Exp. Track. Lines (Base)" THEN
          ERROR(
            STRSUBSTNO(
              Text1140001 +
              '%2: %4' +
              '%3: %5',
              FIELDCAPTION("Use Tracking Lines"),
              FIELDCAPTION("Qty. Expected (Base)"),
              FIELDCAPTION("Qty. Exp. Track. Lines (Base)"),
              "Qty. Expected (Base)",
              "Qty. Exp. Track. Lines (Base)" ) + '\ ' +
            STRSUBSTNO(
              Text1140002,
              "Item No.","Variant Code","Location Code","Bin Code") );
      END;
    END;

    PROCEDURE TestQtyRecorded@1140007();
    VAR
      PhysInvtRecordingLine@1140000 : Record 5005353;
      Sum@1140001 : Decimal;
    BEGIN
      Sum := 0;

      PhysInvtRecordingLine.RESET;
      PhysInvtRecordingLine.SETCURRENTKEY("Order No.","Order Line No.");
      PhysInvtRecordingLine.SETRANGE("Order No.","Document No.");
      PhysInvtRecordingLine.SETRANGE("Order Line No.","Line No.");
      IF PhysInvtRecordingLine.FIND('-') THEN
        REPEAT
          PhysInvtRecordingLine.TESTFIELD("Item No.","Item No.");
          PhysInvtRecordingLine.TESTFIELD("Variant Code","Variant Code");
          PhysInvtRecordingLine.TESTFIELD("Location Code","Location Code");
          PhysInvtRecordingLine.TESTFIELD("Bin Code","Bin Code");
          IF "Use Tracking Lines" THEN
            IF (PhysInvtRecordingLine."Quantity (Base)" <> 0) AND
               (PhysInvtRecordingLine."Serial No." = '') AND
               (PhysInvtRecordingLine."Lot No." = '')
            THEN
              ERROR(
                STRSUBSTNO(
                  Text1140004,
                  PhysInvtRecordingLine.FIELDCAPTION("Serial No."),
                  PhysInvtRecordingLine.FIELDCAPTION("Lot No."),
                  PhysInvtRecordingLine.TABLECAPTION,
                  PhysInvtRecordingLine."Recording No.",
                  FIELDCAPTION("Use Tracking Lines") ) + '\' +
                STRSUBSTNO(
                  Text1140002,
                  "Item No.","Variant Code","Location Code","Bin Code") );
          Sum := Sum + PhysInvtRecordingLine."Quantity (Base)";
        UNTIL PhysInvtRecordingLine.NEXT = 0;

      IF "Qty. Recorded (Base)" <> Sum THEN
        ERROR(
          STRSUBSTNO(
            Text1140003,
            FIELDCAPTION("Qty. Recorded (Base)"),
            PhysInvtRecordingLine.TABLECAPTION,
            PhysInvtRecordingLine.FIELDCAPTION("Quantity (Base)") ) + '/ ' +
          STRSUBSTNO(
            Text1140002,
            "Item No.","Variant Code","Location Code","Bin Code") );
    END;

    PROCEDURE CalcCosts@1140008();
    VAR
      ItemJnlLine@1140000 : Record 83;
      Text1000@1140001 : TextConst 'ENU=Unknown Entry Type.;DEA=Unbekannter Eintragstyp.';
    BEGIN
      IF EmptyLine THEN
        EXIT;
      TESTFIELD("Item No.");

      TestStatusOpen;
      TESTFIELD("Qty. Exp. Calculated",TRUE);
      TESTFIELD("In Recording Lines",TRUE);

      GetPhysInvtOrderHeader;

      ItemJnlLine.INIT;
      ItemJnlLine."Posting Date" := PhysInvtOrderHeader."Posting Date";
      CASE "Entry Type" OF
        "Entry Type"::"Positive Adjmt.":
          ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::"Positive Adjmt.";
        "Entry Type"::"Negative Adjmt.":
          ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::"Negative Adjmt.";
        ELSE
          ERROR(Text1000);
      END;

      ItemJnlLine.VALIDATE("Item No.","Item No.");
      ItemJnlLine.VALIDATE("Variant Code","Variant Code");
      ItemJnlLine.VALIDATE("Location Code","Location Code");

      VALIDATE("Unit Amount",ItemJnlLine."Unit Amount");
      VALIDATE("Unit Cost",ItemJnlLine."Unit Cost");
    END;

    PROCEDURE EmptyLine@1140009() : Boolean;
    BEGIN
      EXIT(
        ("Item No." = '') AND
        ("Variant Code" = '') AND
        ("Location Code" = '') AND
        ("Bin Code" = '') );
    END;

    PROCEDURE ShowDimensions@1140010();
    BEGIN
      IF EmptyLine THEN
        EXIT;
      "Dimension Set ID" :=
        DimManagement.EditDimensionSet2("Dimension Set ID",STRSUBSTNO('%1 %2',TABLECAPTION,"Document No.","Line No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE CreateDim@1140011(Type1@1140000 : Integer;No1@1140001 : Code[20]);
    VAR
      SourceCodeSetup@1140002 : Record 242;
      TableID@1140003 : ARRAY [10] OF Integer;
      No@1140004 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      GetPhysInvtOrderHeader;
      "Dimension Set ID" :=
        DimManagement.GetDefaultDimID(TableID,No,SourceCodeSetup."Phys. Invt. Order","Shortcut Dimension 1 Code",
          "Shortcut Dimension 2 Code",PhysInvtOrderHeader."Dimension Set ID",0);
      DimManagement.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@1140012(FieldNo@1140000 : Integer;VAR ShortcutDimCode@1140001 : Code[20]);
    BEGIN
      DimManagement.ValidateShortcutDimValues(FieldNo,ShortcutDimCode,"Dimension Set ID");
    END;

    PROCEDURE LookupShortcutDimCode@1140013(FieldNo@1140000 : Integer;VAR ShortcutDimCode@1140001 : Code[20]);
    BEGIN
      DimManagement.LookupDimValueCode(FieldNo,ShortcutDimCode);
      DimManagement.ValidateShortcutDimValues(FieldNo,ShortcutDimCode,"Dimension Set ID");
    END;

    PROCEDURE ShowShortcutDimCode@1140014(VAR ShortcutDimCode@1140000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimManagement.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    PROCEDURE ShowPhysInvtRecordingLines@1140015();
    VAR
      PhysInvtRecordingLine@1140000 : Record 5005353;
    BEGIN
      IF EmptyLine THEN
        EXIT;
      TESTFIELD("Item No.");

      TESTFIELD("In Recording Lines",TRUE);

      PhysInvtRecordingLine.RESET;
      PhysInvtRecordingLine.SETCURRENTKEY("Order No.","Order Line No.");
      PhysInvtRecordingLine.SETRANGE("Order No.","Document No.");
      PhysInvtRecordingLine.SETRANGE("Order Line No.","Line No.");

      PAGE.RUNMODAL(0,PhysInvtRecordingLine);
    END;

    PROCEDURE ShowExpectPhysInvtTrackLines@1140016();
    BEGIN
      IF EmptyLine THEN
        EXIT;
      TESTFIELD("Item No.");

      TESTFIELD("Qty. Exp. Calculated",TRUE);

      ExpectPhysInvtTrackLine.RESET;
      ExpectPhysInvtTrackLine.SETRANGE("Order No","Document No.");
      ExpectPhysInvtTrackLine.SETRANGE("Order Line No.","Line No.");

      PAGE.RUNMODAL(0,ExpectPhysInvtTrackLine);
    END;

    PROCEDURE ShowItemTrackingLines@1140017(Which@1140000 : 'All,Positive,Negative');
    BEGIN
      IF EmptyLine THEN
        EXIT;
      TESTFIELD("Item No.");

      PhysInvtOrderHeader.GET("Document No.");                                                 // KS.02
      PhysInvtOrderHeader.TESTFIELD(Status,PhysInvtOrderHeader.Status::Finished);

      ReservEntry.RESET;
      ReservEntry.SETCURRENTKEY(
        "Source Type","Source Subtype","Source ID",
        "Source Batch Name","Source Prod. Order Line",
        "Source Ref. No.");
      ReservEntry.SETRANGE("Source Type",5005351);
      ReservEntry.SETRANGE("Source Subtype",0);
      ReservEntry.SETRANGE("Source ID","Document No.");
      ReservEntry.SETRANGE("Source Batch Name",'');
      ReservEntry.SETRANGE("Source Prod. Order Line",0);
      ReservEntry.SETRANGE("Source Ref. No.","Line No.");
      CASE Which OF
        Which::All:
          ReservEntry.SETRANGE(Positive);
        Which::Positive:
          ReservEntry.SETRANGE(Positive,TRUE);
        Which::Negative:
          ReservEntry.SETRANGE(Positive,FALSE);
      END;

      PAGE.RUNMODAL(PAGE::"Phys. Invt. Item Track. List",ReservEntry);
    END;

    PROCEDURE ShowItemLedgerEntries@1140018();
    VAR
      ItemLedgEntry@1140000 : Record 32;
    BEGIN
      IF EmptyLine THEN
        EXIT;

      TESTFIELD("Item No.");
      GetPhysInvtOrderHeader;
      PhysInvtOrderHeader.TESTFIELD("Posting Date");

      ItemLedgEntry.RESET;

      ItemLedgEntry.SETCURRENTKEY(
        "Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");
      ItemLedgEntry.SETRANGE("Item No.","Item No.");
      ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
      ItemLedgEntry.SETRANGE("Location Code","Location Code");
      ItemLedgEntry.SETRANGE("Posting Date",0D,PhysInvtOrderHeader."Posting Date");

      PAGE.RUNMODAL(0,ItemLedgEntry);
    END;

    PROCEDURE ShowPhysInvtLedgerEntries@1140019();
    VAR
      PhysInvtLedgEntry@1140000 : Record 281;
    BEGIN
      IF EmptyLine THEN
        EXIT;

      TESTFIELD("Item No.");
      GetPhysInvtOrderHeader;
      PhysInvtOrderHeader.TESTFIELD("Posting Date");

      PhysInvtLedgEntry.RESET;
      PhysInvtLedgEntry.SETCURRENTKEY(
        "Item No.","Variant Code",
        "Global Dimension 1 Code","Global Dimension 2 Code",
        "Location Code","Posting Date");
      PhysInvtLedgEntry.SETRANGE("Item No.","Item No.");
      PhysInvtLedgEntry.SETRANGE("Variant Code","Variant Code");
      PhysInvtLedgEntry.SETRANGE("Location Code","Location Code");
      PhysInvtLedgEntry.SETRANGE("Posting Date",0D,PhysInvtOrderHeader."Posting Date");

      PAGE.RUNMODAL(0,PhysInvtLedgEntry);
    END;

    PROCEDURE GetFieldsFromSKU@1140020();
    BEGIN
      IF SKU.GET("Location Code","Item No.","Variant Code") THEN
        VALIDATE("Shelf/Bin No.",SKU."Shelf No.");
    END;

    PROCEDURE ShowBinContentItem@1140021();
    BEGIN
      BinContent.RESET;
      BinContent.SETCURRENTKEY("Item No.");
      BinContent.SETRANGE("Item No.","Item No.");

      PAGE.RUNMODAL(0,BinContent);
    END;

    PROCEDURE ShowBinContentBin@1140022();
    BEGIN
      BinContent.RESET;
      BinContent.SETCURRENTKEY("Location Code","Bin Code");
      BinContent.SETRANGE("Location Code","Location Code");
      BinContent.SETRANGE("Bin Code","Bin Code");

      PAGE.RUNMODAL(0,BinContent);
    END;

    BEGIN
    END.
  }
}


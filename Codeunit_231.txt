OBJECT Codeunit 231 Gen. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;ESP=no puede contener un filtro cuando se registra un diario peri¢dico;DEA=kann nicht gefiltert werden, wenn wiederkehrende Buch.-BlÑtter gebucht werden.';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;ESP=®Confirma que desea registrar las l°neas del diario?;DEA=Mîchten Sie die Buch.-Blattzeilen buchen?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;ESP=No hay nada que registrar.;DEA=Es gibt nichts zu buchen.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;ESP=Se han registrado correctamente las l°neas del diario.;DEA=Die Buch.-Blattzeilen sind gebucht worden.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;ESP=Se registraron correctamente las l°neas diario. Se encuentra en el diario %1.;DEA=Die Buch.-Blattzeilen sind gebucht worden. Sie arbeiten nun in Buch.-Blatt %1.';
      Text005@1009 : TextConst 'ENU=Using %1 for Declining Balance can result in misleading numbers for subsequent years. You should manually check the postings and correct them if necessary. Do you want to continue?;ESP=Utilizar %1 para saldo regresivo puede provocar n£meros enga§osos en a§os subsiguientes. Debe comprobar manualmente los registros y corregirlos si es necesario. ®Desea continuar?;DEA=Bei Verwendung von ''%1'' fÅr die degressive AfA ergeben sich fÅr die Folgejahre unter UmstÑnden irrefÅhrende Zahlen. öberprÅfen Sie die Buchungen manuell, und korrigieren Sie sie gegebenenfalls. Soll der Vorgang fortgesetzt werden?';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GenJnlPostBatch@1007 : Codeunit 13;
      TempJnlBatchName@1008 : Code[10];
      Text006@1010 : TextConst '@@@=Source Code in Genenral Journal Template must not be equal to Job G/L WIP in Source Code Setup.;ENU=%1 in %2 must not be equal to %3 in %4.;ESP=%1 en %2 no debe ser igual a %3 en %4.;DEA=''%1'' in ''%2'' darf nicht ''%3'' in ''%4'' entsprechen.';

    LOCAL PROCEDURE Code@1();
    VAR
      FALedgEntry@1000 : Record 5601;
      SourceCodeSetup@1001 : Record 242;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate.Type = GenJnlTemplate.Type::Jobs THEN BEGIN
          SourceCodeSetup.GET;
          IF GenJnlTemplate."Source Code" = SourceCodeSetup."Job G/L WIP" THEN
            ERROR(Text006,GenJnlTemplate.FIELDCAPTION("Source Code"),GenJnlTemplate.TABLECAPTION,
              SourceCodeSetup.FIELDCAPTION("Job G/L WIP"),SourceCodeSetup.TABLECAPTION);
        END;
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        IF "Account Type" = "Account Type"::"Fixed Asset" THEN BEGIN
          FALedgEntry.SETRANGE("FA No.","Account No.");
          FALedgEntry.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          IF FALedgEntry.FINDFIRST AND "Depr. Acquisition Cost" THEN
            IF NOT CONFIRM(Text005,FALSE,FIELDCAPTION("Depr. Acquisition Cost")) THEN
              EXIT;
        END;

        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.RUN(GenJnlLine);

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 5075 Logged Segment
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Logged Segment;
               ESP=Segmento archivado;
               DEA=Protokolliertes Segment];
    LookupPageID=Page5139;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.;
                                                              DEA=Lfd. Nr.] }
    { 2   ;   ;Segment No.         ;Code20        ;CaptionML=[ENU=Segment No.;
                                                              ESP=N§ segmento;
                                                              DEA=Segmentnr.] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n;
                                                              DEA=Beschreibung] }
    { 4   ;   ;No. of Interactions ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Logged Segment Entry No.=FIELD(Entry No.),
                                                                                                    Canceled=FIELD(Canceled)));
                                                   CaptionML=[ENU=No. of Interactions;
                                                              ESP=N§ de interacciones;
                                                              DEA=Anzahl Aktivit„ten];
                                                   Editable=No }
    { 5   ;   ;No. of Campaign Entries;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Campaign Entry" WHERE (Register No.=FIELD(Entry No.),
                                                                                             Canceled=FIELD(Canceled)));
                                                   CaptionML=[ENU=No. of Campaign Entries;
                                                              ESP=Movs. n§ campa¤a;
                                                              DEA=Anzahl Kampagnenposten];
                                                   Editable=No }
    { 6   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creaci¢n;
                                                              DEA=Errichtungsdatum] }
    { 7   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. usuario;
                                                              DEA=Benutzer-ID] }
    { 8   ;   ;Canceled            ;Boolean       ;CaptionML=[ENU=Canceled;
                                                              ESP=Cancelado;
                                                              DEA=Storniert] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Segment No.                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Segment No.,Creation Date }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 is marked %3.\Do you wish to remove the checkmark?;ESP=%1 %2 est  marcado %3.\¨Desea quitar la marca?;DEA=%1 %2 ist als %3 gekennzeichnet.\M”chten Sie das Kennzeichen entfernen?';
      Text002@1002 : TextConst 'ENU=Do you wish to mark %1 %2 as %3?;ESP=¨Confirma que desea se¤alar %1 %2 como %3?;DEA=M”chten Sie den %1 %2 als %3 kennzeichnen?';
      Text005@1004 : TextConst 'ENU=Do you wish to remove the checkmark from the selected %1 lines?;ESP=¨Confirma que desea eliminar la marca de activaci¢n para las l¡n. %1 selec.?;DEA=M”chten Sie die Markierung von den gew„hlten %1 entfernen?';
      Text006@1003 : TextConst 'ENU="Do you wish to mark the selected %1 lines as %2? ";ESP="¨Confirma que desea se¤alar las l¡neas %1 seleccionadas como %2? ";DEA="M”chten Sie die ausgew„hlten %1 als %2 kennzeichnen? "';

    PROCEDURE ToggleCanceledCheckmark@2();
    VAR
      MasterCanceledCheckmark@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN BEGIN
        IF ConfirmToggleCanceledCheckmark(COUNT) THEN BEGIN
          MasterCanceledCheckmark := NOT Canceled;
          REPEAT
            SetCanceledCheckmark(MasterCanceledCheckmark);
          UNTIL NEXT = 0
        END;
      END
    END;

    PROCEDURE SetCanceledCheckmark@1(CanceledCheckmark@1002 : Boolean);
    VAR
      InteractLogEntry@1000 : Record 5065;
      CampaignEntry@1001 : Record 5072;
    BEGIN
      Canceled := CanceledCheckmark;
      MODIFY;

      CampaignEntry.SETCURRENTKEY("Register No.");
      CampaignEntry.SETRANGE("Register No.","Entry No.");
      CampaignEntry.MODIFYALL(Canceled,Canceled);

      InteractLogEntry.SETCURRENTKEY("Logged Segment Entry No.");
      InteractLogEntry.SETRANGE("Logged Segment Entry No.","Entry No.");
      InteractLogEntry.MODIFYALL(Canceled,Canceled);
    END;

    PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer) : Boolean;
    BEGIN
      IF NumberOfSelectedLines = 1 THEN BEGIN
        IF Canceled THEN
          EXIT(CONFIRM(Text000,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));

        EXIT(CONFIRM(Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END;

      IF Canceled THEN
        EXIT(CONFIRM(Text005,TRUE,TABLECAPTION));

      EXIT(CONFIRM(Text006,TRUE,TABLECAPTION,FIELDCAPTION(Canceled)));
    END;

    BEGIN
    END.
  }
}


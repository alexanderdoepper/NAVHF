OBJECT Report 789 Delete Phys. Inventory Ledger
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 281=rimd,
                TableData 5815=rimd;
    CaptionML=[ENU=Delete Phys. Inventory Ledger;
               ESP=Eliminar movs. inv. f°sico;
               DEA=Inventurposten lîschen];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3360;    ;DataItem;                    ;
               DataItemTable=Table281;
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(STRSUBSTNO(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date")));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007,
                                 "Item No.",
                                 "Posting Date",
                                 NoOfDeleted);

                               IF PhysInvtLedgEntry2.FINDLAST THEN;
                               LastEntryNo := PhysInvtLedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");
                             END;

               OnAfterGetRecord=VAR
                                  InvtPeriodEntry@1000 : Record 5815;
                                  InvtPeriod@1001 : Record 5814;
                                BEGIN
                                  IF NOT InvtPeriod.IsValidDate("Posting Date") THEN
                                    InvtPeriod.ShowError("Posting Date");

                                  PhysInvtLedgEntry2 := "Phys. Inventory Ledger Entry";
                                  WITH PhysInvtLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("Item No.","Variant Code","Location Code","Posting Date");
                                    COPYFILTERS("Phys. Inventory Ledger Entry");
                                    SETRANGE("Item No.","Item No.");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                                    SETRANGE("Inventory Posting Group","Inventory Posting Group");
                                    SETRANGE("Entry Type","Entry Type");

                                    Window.UPDATE(1);
                                    Window.UPDATE(2);

                                    DELETE;

                                    InvtPeriodEntry.RemoveItemRegNo("Entry No.",TRUE);

                                    NoOfDeleted := NoOfDeleted + 1;
                                    Window.UPDATE(3);
                                  END;

                                  IF NoOfDeleted >= LastNoOfDeleted + 10 THEN BEGIN
                                    LastNoOfDeleted := NoOfDeleted;
                                    COMMIT;
                                  END;
                                END;

               ReqFilterFields=Item No.,Inventory Posting Group }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial;
                             DEA=Startdatum];
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final;
                             DEA=Enddatum];
                  SourceExpr=EntrdDateComprReg."Ending Date" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to delete the entries?;ESP=Este trabajo por lotes elimina movimientos. Es muy importante realizar una copia de seguridad de la base de datos antes de ejecutar el trabajo por lotes.\\®Desea eliminar los movimientos?;DEA=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten lîschen?';
      Text003@1003 : TextConst 'ENU=%1 must be specified.;ESP=Se debe indicar %1.;DEA=%1 muss angegeben werden.';
      Text004@1004 : TextConst 'ENU=Deleting phys. inventory ledger entries...\\;ESP=Borrando movs. inventario f°sico...\\;DEA=Lîschen von Inventurposten...\\';
      Text005@1005 : TextConst 'ENU=Item No.             #1##########\;ESP=Nß producto          #1##########\;DEA=Artikel Nr.          #1##########\';
      Text006@1006 : TextConst 'ENU=Date                 #2######\\;ESP=Fecha                #2######\\;DEA=Datum                #2######\\';
      Text007@1007 : TextConst 'ENU=No. of entries del.  #3######;ESP=Nß movs. borrados    #3######;DEA=Anz. gelîsch. Posten #3######';
      EntrdDateComprReg@1008 : Record 87;
      PhysInvtLedgEntry2@1009 : Record 281;
      DateComprMgt@1010 : Codeunit 356;
      Window@1011 : Dialog;
      LastEntryNo@1012 : Integer;
      NoOfDeleted@1013 : Integer;
      LastNoOfDeleted@1014 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


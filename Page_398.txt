OBJECT Page 398 Sales Credit Memo Statistics
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Sales Credit Memo Statistics;
               ESP=Estad. abono ventas;
               DEA=Verkaufsgutschriftsstatistik];
    LinksAllowed=No;
    SourceTable=Table114;
    PageType=ListPlus;
    OnAfterGetRecord=VAR
                       CostCalcMgt@1000 : Codeunit 5836;
                     BEGIN
                       CLEARALL;

                       IF "Currency Code" = '' THEN
                         Currency.InitRoundingPrecision
                       ELSE
                         Currency.GET("Currency Code");

                       SalesCrMemoLine.SETRANGE("Document No.","No.");
                       IF SalesCrMemoLine.FIND('-') THEN
                         REPEAT
                           CustAmount := CustAmount + SalesCrMemoLine.Amount;
                           AmountInclVAT := AmountInclVAT + SalesCrMemoLine."Amount Including VAT";
                           IF "Prices Including VAT" THEN
                             InvDiscAmount := InvDiscAmount + SalesCrMemoLine."Inv. Discount Amount" / (1 + SalesCrMemoLine."VAT %" / 100)
                           ELSE
                             InvDiscAmount := InvDiscAmount + SalesCrMemoLine."Inv. Discount Amount";
                           CostLCY := CostLCY + (SalesCrMemoLine.Quantity * SalesCrMemoLine."Unit Cost (LCY)");
                           LineQty := LineQty + SalesCrMemoLine.Quantity;
                           TotalNetWeight := TotalNetWeight + (SalesCrMemoLine.Quantity * SalesCrMemoLine."Net Weight");
                           TotalGrossWeight := TotalGrossWeight + (SalesCrMemoLine.Quantity * SalesCrMemoLine."Gross Weight");
                           TotalVolume := TotalVolume + (SalesCrMemoLine.Quantity * SalesCrMemoLine."Unit Volume");
                           IF SalesCrMemoLine."Units per Parcel" > 0 THEN
                             TotalParcels := TotalParcels + ROUND(SalesCrMemoLine.Quantity / SalesCrMemoLine."Units per Parcel",1,'>');
                           IF SalesCrMemoLine."VAT %" <> VATpercentage THEN
                             IF VATpercentage = 0 THEN
                               VATpercentage := SalesCrMemoLine."VAT %"
                             ELSE
                               VATpercentage := -1;
                           TotalAdjCostLCY := TotalAdjCostLCY + CostCalcMgt.CalcSalesCrMemoLineCostLCY(SalesCrMemoLine);
                         UNTIL SalesCrMemoLine.NEXT = 0;
                       VATAmount := AmountInclVAT - CustAmount;
                       InvDiscAmount := ROUND(InvDiscAmount,Currency."Amount Rounding Precision");

                       IF VATpercentage <= 0 THEN
                         VATAmountText := Text000
                       ELSE
                         VATAmountText := STRSUBSTNO(Text001,VATpercentage);

                       IF "Currency Code" = '' THEN
                         AmountLCY := CustAmount
                       ELSE
                         AmountLCY :=
                           CurrExchRate.ExchangeAmtFCYToLCY(
                             WORKDATE,"Currency Code",CustAmount,"Currency Factor");

                       ProfitLCY := AmountLCY - CostLCY;

                       IF AmountLCY <> 0 THEN
                         ProfitPct := ROUND(100 * ProfitLCY / AmountLCY,0.1);

                       AdjProfitLCY := AmountLCY - TotalAdjCostLCY;
                       IF AmountLCY <> 0 THEN
                         AdjProfitPct := ROUND(100 * AdjProfitLCY / AmountLCY,0.1);

                       IF Cust.GET("Bill-to Customer No.") THEN
                         Cust.CALCFIELDS("Balance (LCY)")
                       ELSE
                         CLEAR(Cust);

                       CASE TRUE OF
                         Cust."Credit Limit (LCY)" = 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" < 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" > 1:
                           CreditLimitLCYExpendedPct := 10000;
                         ELSE
                           CreditLimitLCYExpendedPct := ROUND(Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" * 10000,1);
                       END;

                       SalesCrMemoLine.CalcVATAmountLines(Rec,TempVATAmountLine);
                       CurrPage.Subform.PAGE.SetTempVATAmountLine(TempVATAmountLine);
                       CurrPage.Subform.PAGE.InitGlobals("Currency Code",FALSE,FALSE,FALSE,FALSE,"VAT Base Discount %");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 35  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General;
                           DEA=Allgemein] }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           ESP=Importe;
                           DEA=Betrag];
                SourceExpr=CustAmount + InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ESP=Importe dto. factura;
                           DEA=Rechnungsrabattbetrag];
                SourceExpr=InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Total;
                           ESP=Total;
                           DEA=Gesamt];
                SourceExpr=CustAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=VAT Amount;
                           ESP=Importe IVA;
                           DEA=MwSt.-Betrag];
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(VATAmountText) }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Total Incl. VAT;
                           ESP=Total IVA incl.;
                           DEA=Total inkl. MwSt.];
                SourceExpr=AmountInclVAT;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Sales (LCY);
                           ESP=Ventas (DL);
                           DEA=Verkauf (MW)];
                SourceExpr=AmountLCY;
                AutoFormatType=1 }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit (LCY);
                           ESP=Bfß original (DL);
                           DEA=DB ursprÅnglich (MW)];
                SourceExpr=ProfitLCY;
                AutoFormatType=1 }

    { 39  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit (LCY);
                           ESP=Beneficio actualizado (DL);
                           DEA=DB reguliert (MW)];
                SourceExpr=AdjProfitLCY;
                AutoFormatType=1 }

    { 32  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit %;
                           ESP=% Bfß original;
                           DEA=DB ursprÅnglich %];
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct }

    { 41  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit %;
                           ESP=% beneficio actualizado;
                           DEA=DB reguliert %];
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ESP=Cantidad;
                           DEA=Menge];
                DecimalPlaces=0:5;
                SourceExpr=LineQty }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ESP=Lotes;
                           DEA=Pakete];
                DecimalPlaces=0:5;
                SourceExpr=TotalParcels }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ESP=Peso neto;
                           DEA=Nettogewicht];
                DecimalPlaces=0:5;
                SourceExpr=TotalNetWeight }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ESP=Peso bruto;
                           DEA=Bruttogewicht];
                DecimalPlaces=0:5;
                SourceExpr=TotalGrossWeight }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ESP=Volumen;
                           DEA=Volumen];
                DecimalPlaces=0:5;
                SourceExpr=TotalVolume }

    { 28  ;2   ;Field     ;
                CaptionML=[ENU=Original Cost (LCY);
                           ESP=Coste original (DL);
                           DEA=Urspr.-Einstandsbetrag (MW)];
                SourceExpr=CostLCY;
                AutoFormatType=1 }

    { 36  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Cost (LCY);
                           ESP=Coste actualizado (DL);
                           DEA=Lagerwert reguliert (MW)];
                SourceExpr=TotalAdjCostLCY;
                AutoFormatType=1 }

    { 44  ;2   ;Field     ;
                CaptionML=[ENU=Cost Adjmt. Amount (LCY);
                           ESP=Importe coste ajustado (DL);
                           DEA=Kostenregulierungsbetrag (MW)];
                SourceExpr=TotalAdjCostLCY - CostLCY;
                AutoFormatType=1;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries;
                         END;
                          }

    { 18  ;1   ;Part      ;
                Name=Subform;
                PagePartID=Page576;
                Editable=FALSE }

    { 1903289601;1;Group  ;
                CaptionML=[ENU=Customer;
                           ESP=Cliente;
                           DEA=Debitor] }

    { 24  ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           ESP=Saldo (DL);
                           DEA=Saldo (MW)];
                SourceExpr=Cust."Balance (LCY)";
                AutoFormatType=1 }

    { 21  ;2   ;Field     ;
                CaptionML=[ENU=Credit Limit (LCY);
                           ESP=CrÇdito m†ximo (DL);
                           DEA=Kreditlimit (MW)];
                SourceExpr=Cust."Credit Limit (LCY)";
                AutoFormatType=1 }

    { 1   ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Expended % of Credit Limit (LCY);
                           ESP=% L°mite crÇdito usado (DL);
                           DEA=Kreditlimit (MW) Åberschritten %];
                ToolTipML=[ENU=Expended % of Credit Limit (LCY);
                           ESP=% L°mite crÇdito usado (DL);
                           DEA=Kreditlimit (MW) Åberschritten %];
                SourceExpr=CreditLimitLCYExpendedPct }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT Amount;ESP=Importe IVA;DEA=MwSt.-Betrag';
      Text001@1001 : TextConst 'ENU=%1% VAT;ESP=%1% IVA;DEA=%1% MwSt.';
      CurrExchRate@1002 : Record 330;
      SalesCrMemoLine@1003 : Record 115;
      Cust@1004 : Record 18;
      TempVATAmountLine@1005 : TEMPORARY Record 290;
      Currency@1022 : Record 4;
      TotalAdjCostLCY@1025 : Decimal;
      CustAmount@1006 : Decimal;
      AmountInclVAT@1007 : Decimal;
      InvDiscAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      CostLCY@1010 : Decimal;
      ProfitLCY@1011 : Decimal;
      ProfitPct@1012 : Decimal;
      AdjProfitLCY@1026 : Decimal;
      AdjProfitPct@1023 : Decimal;
      LineQty@1013 : Decimal;
      TotalNetWeight@1014 : Decimal;
      TotalGrossWeight@1015 : Decimal;
      TotalVolume@1016 : Decimal;
      TotalParcels@1017 : Decimal;
      AmountLCY@1018 : Decimal;
      CreditLimitLCYExpendedPct@1019 : Decimal;
      VATpercentage@1020 : Decimal;
      VATAmountText@1021 : Text[30];

    BEGIN
    END.
  }
}


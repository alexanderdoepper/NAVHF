OBJECT Report 5756 Create Warehouse Location
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Warehouse Location;
               ESP=Crear un almacÇn;
               DEA=Lagerort erstellen];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF LocCode = '' THEN
                    ERROR(Text001);
                  IF AdjBinCode = '' THEN
                    ERROR(Text002);

                  ItemLedgEntry.RESET;
                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                  IF ItemLedgEntry.FIND('-') THEN
                    REPEAT
                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                      ItemLedgEntry.SETRANGE(Open,TRUE);
                      ItemLedgEntry.SETRANGE("Location Code",LocCode);
                      Found := ItemLedgEntry.FIND('-');
                      IF Found AND NOT HideValidationDialog THEN
                        IF NOT CONFIRM(
                             Text010 + Text011 + Text012 + Text013 + Text014 +
                             Text015 + Text016 + Text017,FALSE,Location.TABLECAPTION,LocCode)
                        THEN
                          CurrReport.QUIT;
                      ItemLedgEntry.SETRANGE("Location Code");
                      ItemLedgEntry.SETRANGE(Open);
                      ItemLedgEntry.FIND('+');
                      ItemLedgEntry.SETRANGE("Item No.");
                    UNTIL (ItemLedgEntry.NEXT = 0) OR Found;

                  IF NOT Found THEN
                    ERROR(Text018,Location.TABLECAPTION,Location.FIELDCAPTION(Code),LocCode);
                  CLEAR(ItemLedgEntry);

                  WhseEntry.RESET;
                  WhseEntry.SETCURRENTKEY("Location Code");
                  WhseEntry.SETRANGE("Location Code",LocCode);
                  IF WhseEntry.FINDFIRST THEN
                    ERROR(
                      Text019,Location.TABLECAPTION,LocCode,WhseEntry.TABLECAPTION);

                  TempWhseJnlLine.RESET;
                  TempWhseJnlLine.DELETEALL;

                  LastLineNo := 0;

                  WITH ItemLedgEntry DO BEGIN
                    SETCURRENTKEY(
                      "Item No.","Location Code",Open,"Variant Code","Unit of Measure Code","Lot No.","Serial No.");

                    Location.GET(LocCode);
                    Location.TESTFIELD("Adjustment Bin Code",'');
                    CheckWhseDocs;

                    Bin.GET(LocCode,AdjBinCode);

                    IF FIND('-') THEN BEGIN
                      IF NOT HideValidationDialog THEN BEGIN
                        Window.OPEN(STRSUBSTNO(Text020,FIELDCAPTION("Location Code"),"Location Code") + Text003);
                        i := 1;
                        CountItemLedgEntries := COUNT;
                      END;

                      REPEAT
                        IF NOT HideValidationDialog THEN BEGIN
                          Window.UPDATE(100,i);
                          Window.UPDATE(102,ROUND(i / CountItemLedgEntries * 10000,1));
                        END;

                        SETRANGE("Item No.","Item No.");
                        IF FIND('-') THEN BEGIN
                          SETRANGE("Location Code",LocCode);
                          SETRANGE(Open,TRUE);
                          IF FIND('-') THEN
                            REPEAT
                              SETRANGE("Variant Code","Variant Code");
                              IF FIND('-') THEN
                                REPEAT
                                  SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                  IF FIND('-') THEN
                                    REPEAT
                                      SETRANGE("Lot No.","Lot No.");
                                      IF FIND('-') THEN
                                        REPEAT
                                          SETRANGE("Serial No.","Serial No.");

                                          CALCSUMS("Remaining Quantity");

                                          IF "Remaining Quantity" < 0 THEN
                                            ERROR(
                                              STRSUBSTNO(Text005,BuildErrorText) +
                                              STRSUBSTNO(Text009,CheckNegInv.OBJECTID));

                                          IF "Remaining Quantity" > 0 THEN
                                            CreateWhseJnlLine;
                                          FIND('+');
                                          SETRANGE("Serial No.");
                                        UNTIL NEXT = 0;

                                      FIND('+');
                                      SETRANGE("Lot No.");
                                    UNTIL NEXT = 0;

                                  FIND('+');
                                  SETRANGE("Unit of Measure Code")
                                UNTIL NEXT = 0;

                              FIND('+');
                              SETRANGE("Variant Code");
                            UNTIL NEXT = 0;
                        END;

                        SETRANGE(Open);
                        SETRANGE("Location Code");
                        FIND('+');
                        IF NOT HideValidationDialog THEN
                          i := i + COUNT;
                        SETRANGE("Item No.");
                      UNTIL NEXT = 0;
                    END;
                  END;
                END;

    OnPostReport=BEGIN
                   Location."Bin Mandatory" := TRUE;
                   Location.VALIDATE("Directed Put-away and Pick",TRUE);
                   Location.VALIDATE("Adjustment Bin Code",AdjBinCode);
                   Location.MODIFY;

                   IF TempWhseJnlLine.FIND('-') THEN
                     REPEAT
                       WhseJnlLinePost.RUN(TempWhseJnlLine);
                     UNTIL TempWhseJnlLine.NEXT = 0;

                   IF NOT HideValidationDialog THEN BEGIN
                     Window.CLOSE;
                     MESSAGE(Text004);
                   END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Location Code;
                             ESP=C¢d. almacÇn;
                             DEA=Lagerortcode];
                  SourceExpr=LocCode;
                  OnValidate=BEGIN
                               Location.GET(LocCode);
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                             END;

                  OnLookup=BEGIN
                             CLEAR(Location);
                             IF LocCode <> '' THEN
                               Location.Code := LocCode;
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                               LocCode := Location.Code;
                             END;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Bin Code;
                             ESP=C¢d. ubicaci¢n ajuste;
                             DEA=Ausgleichslagerplatzcode];
                  SourceExpr=AdjBinCode;
                  OnValidate=BEGIN
                               IF AdjBinCode <> '' THEN
                                 IF LocCode <> '' THEN
                                   Bin.GET(LocCode,AdjBinCode)
                                 ELSE BEGIN
                                   Bin.SETRANGE(Code,AdjBinCode);
                                   Bin.FINDFIRST;
                                   LocCode := Bin."Location Code";
                                 END;
                             END;

                  OnLookup=BEGIN
                             Bin.RESET;
                             IF LocCode <> '' THEN
                               Bin.SETRANGE("Location Code",LocCode);

                             IF PAGE.RUNMODAL(0,Bin) = ACTION::LookupOK THEN BEGIN
                               IF LocCode = '' THEN
                                 LocCode := Bin."Location Code";
                               AdjBinCode := Bin.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempWhseJnlLine@1007 : TEMPORARY Record 7311;
      Item@1020 : Record 27;
      ItemLedgEntry@1011 : Record 32;
      WhseEntry@1040 : Record 7312;
      ItemUnitOfMeasure@1001 : Record 5404;
      Location@1002 : Record 14;
      Bin@1003 : Record 7354;
      WhseRcptHeader@1028 : Record 7316;
      WhseShipHeader@1027 : Record 7320;
      WhseActivHeader@1026 : Record 5766;
      WhseWkshLine@1025 : Record 7326;
      WhseJnlLinePost@1010 : Codeunit 7301;
      WMSMgt@1019 : Codeunit 7302;
      CheckNegInv@1014 : Report 5757;
      Window@1013 : Dialog;
      LocCode@1000 : Code[10];
      Text001@1042 : TextConst 'ENU=Please enter a location code!;ESP=Por favor introduzca c¢d. almacÇn;DEA=Bitte geben Sie einen Lagerortcode ein.';
      Text002@1012 : TextConst 'ENU=Please enter an adjustment bin code!;ESP=Por favor introduzca un c¢d. ubicaci¢n de ajuste;DEA=Bitte geben Sie einen Ausgleichslagerplatzcode ein.';
      Text003@1004 : TextConst 'ENU=Count #100##### @102@@@@@@@@;ESP=Cuenta #100##### @102@@@@@@@@;DEA=Anzahl #100##### @102@@@@@@@@';
      AdjBinCode@1009 : Code[20];
      i@1005 : Integer;
      CountItemLedgEntries@1029 : Integer;
      LastLineNo@1008 : Integer;
      Text004@1015 : TextConst 'ENU=Conversion successfully completed!;ESP=≠Conversi¢n realizada con Çxito!;DEA=Umwandlung erfolgreich abgeschlossen.';
      Text005@1016 : TextConst 'ENU=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion!\\%1.\\;ESP=Hay inventario negativo en almacÇn. Debe corregir este inventario negativo en el programa antes de lanzar la conversi¢n\\%1.\\;DEA=Der Lagerort weist negative BestÑnde auf. Bereinigen Sie bitte diese negativen LagerbestÑnde, bevor Sie mit der Umwandlung fortfahren.\\%1.\\';
      HideValidationDialog@1018 : Boolean;
      Text006@1022 : TextConst 'ENU=%1 %2 cannot be converted because at least one %3 is not completely posted yet!\\Post or delete all of them before restarting the conversion batch job!;ESP=%1 %2 no se pueden convertir por que no est† registrado todav°a al menos un %3\\Registre o borre todos antes de reiniciar el proceso de conversi¢n!;DEA=Der %1 %2 kann nicht umgewandelt werden, da mindestens ein %3 noch nicht vollstÑndig gebucht ist.\\Buchen oder lîschen Sie diese bitte alle, bevor Sie diese Stapelverarbeitung erneut starten.';
      Text007@1023 : TextConst 'ENU=%1 %2 cannot be converted because at least one %3 is not completely registered yet!\\Register or delete all of them before restarting the conversion batch job!;ESP=%1 %2 no pueden convertirse por que no est† completamente registrado todav°a al menos un %3!\\Registre o borre todos antes de reiniciar el proceso de conversi¢n!;DEA=Der %1 %2 kann nicht umgewandelt werden, da mindestens eine %3 noch nicht vollstÑndig registriert ist.\\Registrieren oder lîschen Sie diese bitte alle, bevor Sie diese Stapelverarbeitung erneut starten.';
      Text008@1024 : TextConst 'ENU=%1 %2 cannot be converted because at least one %3 exists!\\Delete all of them before restarting the conversion batch job!;ESP=%1 %2 no puede convertirse por que existe al menos un %3\\Borre todos antes de reiniciar el proceso de conversi¢n;DEA=Der %1 %2 kann nicht umgewandelt werden, da mindestens eine %3 existiert.\\Lîschen Sie diese bitte alle, bevor Sie diese Stapelverarbeitung erneut starten.';
      Text009@1030 : TextConst 'ENU=Run %1 for a report of all negative inventory in the location.;ESP=Ejecute %1 para un informe de todo el inventario negativo en el almacÇn.;DEA=Verwenden Sie bitte den Bericht %1, um alle negativen LagerbestÑnde des Lagerortes zu ermitteln.';
      Text010@1039 : TextConst 'ENU=Inventory exists on this location. By clicking Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\;ESP=Hay inventario en este almacÇn. Si selecciona S° en la advertencia, confirmar† que quiere permitir habilitar este almacÇn para usar el Sistema de Gesti¢n de almacenes para lanzar un proceso que crea movs. de almacÇn para el inventario de este almacÇn.\\;DEA=Dieser Lagerort weist LagerbestÑnde auf. Klicken Sie bitte auf Ja, um zu bestÑtigen, dass dieser Lagerort fÅr den Logistik Bereich zur VerfÅgung gestellt werden soll. Dies erfolgt mit Hilfe einer Stapelverarbeitung, die Lagerplatzposten fÅr den Lagerbestand dieses Lagerortes erstellt.\\';
      Text011@1038 : TextConst 'ENU="If you wish to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. ";ESP="Si quiere continuar, debe asegurarse primero de que no haya inventario negativo en el almacÇn. No est† permitido tener inventario negativo en un almacÇn que use una l¢gica de gesti¢n de almacÇn y que quedar† ajustado registr†ndose una cantidad adecuada de inventario. ";DEA="Bevor Sie fortfahren, stellen Sie bitte sicher, dass keine negativen LagerbestÑnde im Lagerort vorhanden sind. Negative LagerbestÑnde in Lagerorten, die im Lagerverwaltungssystem verwendet werden, sind nicht zulÑssig und mÅssen durch eine entsprechende Lagerbuchung bereinigt werden. "';
      Text012@1037 : TextConst 'ENU=You can perform a check for negative inventory by using report Check on Negative Inventory.\\;ESP=Puede comprobar el inventario negativo utilizando el informe Comprobar inventario negativo.\\;DEA=Sie kînnen eine PrÅfung auf negative LagerbestÑnde durchfÅhren. Verwenden Sie hierzu bitte den Bericht Neg. LagerbestÑnde ermitteln.\\';
      Text013@1036 : TextConst 'ENU="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If the program finds negative inventory, it will stop the batch job with an error message. ";ESP="Si est† seguro de que no hay inventario negativo en el almacÇn, ejecute el proceso de conversi¢n. Si el programa encuentra inventario negativo, detendr† el proceso con un mensage de error. ";DEA="Wenn Sie sichergestellt haben, dass in diesem Lagerort keine negativen LagerbestÑnde existieren, setzen Sie diese Stapelverarbeitung fort. Sollten dennoch negative BestÑnde vorhanden sein, wird die Stapelverarbeitung mit einer Fehlermeldung abgebrochen. "';
      Text014@1035 : TextConst 'ENU=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\;ESP=El resultado de este proceso es la creaci¢n de movs. iniciales de almacÇn. Debe ajustar estos movs. iniciales de almacÇn en la ubic. de ajuste registrando un diario de inventario f°sico o un diario producto almacÇn para asignar zonas y ubicaciones a los productos.\;DEA=Diese Stapelverarbeitung wird Erîffnungslagerplatzposten erstellen. Sie mÅssen diese Erîffnungslagerplatzposten im Ausgleichslagerplatz ausgleichen, indem Sie ein Logistik Inventur Buch.-Blatt oder ein Logistik Artikel Buch.-Blatt fÅr die Zuweisung von Zonen und LagerplÑtzen buchen.\';
      Text015@1034 : TextConst 'ENU=You must create zones and bins before posting a warehouse physical inventory.\\;ESP=Debe crear zonas y ubicaciones antes de registrar un inventario f°sico en almacÇn.\\;DEA=Sie mÅssen erst Zonen und LagerplÑtze erstellen, bevor Sie eine Logistik Inventur buchen kînnen.\\';
      Text016@1033 : TextConst 'ENU=%1 %2 will be a warehouse management location after the batch job has run successfully! This conversion cannot be reversed or undone after it has run!;ESP=%1 %2 ser† un almacÇn despuÇs de que se haya ejecutado correctamente el proceso. Esta conversi¢n no es reversible o no puede deshacerse despuÇs ejecutarse.;DEA=Nach erfolgreichem Abschluss der Stapelverarbeitung wird dieser %1 %2 ein Lagerort fÅr das Lagerverwaltungssystem sein. Diese Umwandlung kann anschlie·end nicht zurÅckgefÅhrt oder rÅckgÑngig gemacht werden.';
      Text017@1032 : TextConst 'ENU=\\Do you really want to proceed?;ESP=\\®Est† seguro de querer continuar?;DEA=\\Mîchten Sie wirklich fortfahren?';
      Text018@1031 : TextConst 'ENU=There is nothing to convert for %1 %2 ''%3''.;ESP=No hay nada que convertir para %1 %2 ''%3''.;DEA=FÅr den %1 %2 ''%3'' gibt es nichts zu konvertieren.';
      Text019@1041 : TextConst 'ENU=%1 %2 cannot be converted because at least one %3 exists for this %1!;ESP=≠%1 %2 no puede convertirse por que existe al menos un %3 para este %1!;DEA=Der %1 %2 kann nicht umgewandelt werden, da mindestens ein %3 fÅr diesen %1 existiert.';
      Text020@1006 : TextConst 'ENU=%1 %2 will be converted to a WMS location!\\This might take some time so please be patient!;ESP=≠%1 %2 se convertir† en un almacÇn!\\≠Esto puede llevar tiempo, por favor sea paciente!;DEA=Der %1 %2 wird in einen Lagerort fÅr das Lagerverwaltungssystem umgewandelt.\\Dies kann einige Zeit in Anspruch nehmen. Haben Sie bitte etwas Geduld.';
      Found@1017 : Boolean;

    PROCEDURE CheckWhseDocs@5();
    BEGIN
      WhseRcptHeader.SETCURRENTKEY("Location Code");
      WhseRcptHeader.SETRANGE("Location Code",Location.Code);
      IF WhseRcptHeader.FINDFIRST THEN
        ERROR(
          Text006,
          Location.TABLECAPTION,
          Location.Code,
          WhseRcptHeader.TABLECAPTION);

      WhseShipHeader.SETCURRENTKEY("Location Code");
      WhseShipHeader.SETRANGE("Location Code",Location.Code);
      IF WhseShipHeader.FINDFIRST THEN
        ERROR(
          Text006,
          Location.TABLECAPTION,
          Location.Code,
          WhseShipHeader.TABLECAPTION);

      WhseActivHeader.SETCURRENTKEY("Location Code");
      WhseActivHeader.SETRANGE("Location Code",Location.Code);
      IF WhseActivHeader.FINDFIRST THEN
        ERROR(
          Text007,
          Location.TABLECAPTION,
          Location.Code,
          WhseActivHeader.Type);

      WhseWkshLine.SETCURRENTKEY("Item No.","Location Code");
      WhseWkshLine.SETRANGE("Location Code",Location.Code);
      IF WhseWkshLine.FINDFIRST THEN
        ERROR(
          Text008,
          Location.TABLECAPTION,
          Location.Code,
          WhseWkshLine.TABLECAPTION);
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@4();
    BEGIN
      LastLineNo := LastLineNo + 10000;

      WITH ItemLedgEntry DO BEGIN
        TempWhseJnlLine.INIT;
        TempWhseJnlLine."Entry Type" := TempWhseJnlLine."Entry Type"::"Positive Adjmt.";
        TempWhseJnlLine."Line No." := LastLineNo;
        TempWhseJnlLine."Location Code" := "Location Code";
        TempWhseJnlLine."Registering Date" := TODAY;
        TempWhseJnlLine."Item No." := "Item No.";
        TempWhseJnlLine.Quantity := ROUND("Remaining Quantity" / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Base)" := "Remaining Quantity";
        TempWhseJnlLine."Qty. (Absolute)" := ROUND(ABS("Remaining Quantity") / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Absolute, Base)" := ABS("Remaining Quantity");
        TempWhseJnlLine."User ID" := USERID;
        TempWhseJnlLine."Variant Code" := "Variant Code";
        IF "Unit of Measure Code" = '' THEN BEGIN
          Item.GET("Item No.");
          "Unit of Measure Code" := Item."Base Unit of Measure";
        END;
        TempWhseJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        TempWhseJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempWhseJnlLine."Lot No." := "Lot No.";
        TempWhseJnlLine."Serial No." := "Serial No.";
        TempWhseJnlLine.VALIDATE("Zone Code",Bin."Zone Code");
        TempWhseJnlLine."Bin Code" := AdjBinCode;
        TempWhseJnlLine."To Bin Code" := AdjBinCode;
        GetItemUnitOfMeasure("Item No.","Unit of Measure Code");
        TempWhseJnlLine.Cubage :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Cubage;
        TempWhseJnlLine.Weight :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Weight;

        WMSMgt.CheckWhseJnlLine(TempWhseJnlLine,0,0,FALSE);
        TempWhseJnlLine.INSERT;
      END;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@15(ItemNo@1000 : Code[20];UOMCode@1001 : Code[10]);
    BEGIN
      IF (ItemUnitOfMeasure."Item No." <> ItemNo) OR
         (ItemUnitOfMeasure.Code <> UOMCode)
      THEN
        IF NOT ItemUnitOfMeasure.GET(ItemNo,UOMCode) THEN
          ItemUnitOfMeasure.INIT;
    END;

    LOCAL PROCEDURE BuildErrorText@7() : Text[250];
    VAR
      Text@1000 : Text[250];
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        Text :=
          STRSUBSTNO(
            '%1: %2, %3: %4',
            FIELDCAPTION("Location Code"),"Location Code",
            FIELDCAPTION("Item No."),"Item No.");
        IF "Variant Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Variant Code"),"Variant Code");
        IF "Unit of Measure Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Unit of Measure Code"),
              "Unit of Measure Code");
        IF "Lot No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Lot No."),"Lot No.");
        IF "Serial No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Serial No."),"Serial No.");
      END;
      EXIT(Text);
    END;

    PROCEDURE InitializeRequest@2(LocationCode@1000 : Code[10];AdjustmentBinCode@1001 : Code[20]);
    BEGIN
      LocCode := LocationCode;
      AdjBinCode := AdjustmentBinCode;
    END;

    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


OBJECT Page 1040 Copy Job
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Job;
               ESP=Copiar proyecto;
               DEA=Projekt kopieren];
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 PlanningLineType := PlanningLineType::"Schedule+Contract";
                 LedgerEntryType := LedgerEntryType::"Usage+Sale";
                 ValidateSource;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN BEGIN
                         ValidateUserInput;
                         CopyJob.SetCopyOptions(CopyJobPrices,CopyQuantity,CopyDimensions,Source,PlanningLineType,LedgerEntryType);
                         CopyJob.SetJobTaskRange(FromJobTaskNo,ToJobTaskNo);
                         CopyJob.SetJobTaskDateRange(FromDate,ToDate);
                         CopyJob.CopyJob(SourceJob,TargetJobNo,TargetJobDescription,TargetBillToCustomerNo);
                         MESSAGE(Text001);
                       END
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 15  ;1   ;Group     ;
                CaptionML=[ENU=Copy from;
                           ESP=Copiar desde;
                           DEA=Kopieren von] }

    { 11  ;2   ;Field     ;
                CaptionML=[ENU=Job No.;
                           ESP=Nß proyecto;
                           DEA=Projektnr.];
                SourceExpr=SourceJobNo;
                TableRelation=Job;
                OnValidate=BEGIN
                             IF (SourceJobNo <> '') AND NOT SourceJob.GET(SourceJobNo) THEN
                               ERROR(Text003,SourceJob.TABLECAPTION,SourceJobNo);
                             TargetJobDescription := SourceJob.Description;
                             TargetBillToCustomerNo := SourceJob."Bill-to Customer No.";

                             FromJobTaskNo := '';
                             ToJobTaskNo := '';
                           END;
                            }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Job Task No. from;
                           ESP=Nß tarea proyecto desde;
                           DEA=Projektaufgabennr. von];
                SourceExpr=FromJobTaskNo;
                OnValidate=VAR
                             JobTask@1000 : Record 1001;
                           BEGIN
                             IF (FromJobTaskNo <> '') AND NOT JobTask.GET(SourceJob."No.",FromJobTaskNo) THEN
                               ERROR(Text003,JobTask.TABLECAPTION,FromJobTaskNo);
                           END;

                OnLookup=VAR
                           JobTask@1000 : Record 1001;
                         BEGIN
                           IF SourceJob."No." <> '' THEN BEGIN
                             JobTask.SETRANGE("Job No.",SourceJob."No.");
                             IF PAGE.RUNMODAL(PAGE::"Job Task List",JobTask) = ACTION::LookupOK THEN
                               FromJobTaskNo := JobTask."Job Task No.";
                           END;
                         END;
                          }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Job Task No. to;
                           ESP=Nß tarea proyecto hasta;
                           DEA=Projektaufgabennr. bis];
                SourceExpr=ToJobTaskNo;
                OnValidate=VAR
                             JobTask@1000 : Record 1001;
                           BEGIN
                             IF (ToJobTaskNo <> '') AND NOT JobTask.GET(SourceJobNo,ToJobTaskNo) THEN
                               ERROR(Text003,JobTask.TABLECAPTION,ToJobTaskNo);
                           END;

                OnLookup=VAR
                           JobTask@1000 : Record 1001;
                         BEGIN
                           IF SourceJobNo <> '' THEN BEGIN
                             JobTask.SETRANGE("Job No.",SourceJobNo);
                             IF PAGE.RUNMODAL(PAGE::"Job Task List",JobTask) = ACTION::LookupOK THEN
                               ToJobTaskNo := JobTask."Job Task No.";
                           END;
                         END;
                          }

    { 13  ;2   ;Field     ;
                Name=From Source;
                CaptionML=[ENU=Source;
                           ESP=Origen;
                           DEA=Herkunft];
                OptionCaptionML=[ENU=Job Planning Lines,Job Ledger Entries,None;
                                 ESP=L°neas planificaci¢n proyecto,Movs. proyectos,Ninguno;
                                 DEA=Projektplanzeilen,Projektposten,Keine];
                SourceExpr=Source;
                OnValidate=BEGIN
                             ValidateSource;
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=Planning Line Type;
                CaptionML=[ENU=Incl. Planning Line Type;
                           ESP=Incl. tipo l°nea planificaci¢n;
                           DEA=Inkl. Planzeilenart];
                OptionCaptionML=[ENU=Schedule+Contract,Schedule,Contract;
                                 ESP=Previsi¢n+Contrato,Previsi¢n,Contrato;
                                 DEA=Plan+Vertrag,Plan,Vertrag];
                SourceExpr=PlanningLineType;
                Enabled=PlanningLineTypeEnable }

    { 25  ;2   ;Field     ;
                Name=Ledger Entry Line Type;
                CaptionML=[ENU=Incl. Ledger Entry Line Type;
                           ESP=Incl. tipo l°nea mov.;
                           DEA=Inkl. Postenzeilenart];
                OptionCaptionML=[ENU=Usage+Sale,Usage,Sale;
                                 ESP=Consumo+Venta,Consumo,Venta;
                                 DEA=Verbrauch+Verkauf,Verbrauch,Verkauf];
                SourceExpr=LedgerEntryType;
                Enabled=LedgerEntryLineTypeEnable }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Starting Date;
                           ESP=Fecha inicial;
                           DEA=Startdatum];
                SourceExpr=FromDate }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Ending Date;
                           ESP=Fecha final;
                           DEA=Enddatum];
                SourceExpr=ToDate }

    { 5   ;1   ;Group     ;
                CaptionML=[ENU=Copy to;
                           ESP=Copiar a;
                           DEA=Kopieren in] }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Job No.;
                           ESP=Nß proyecto;
                           DEA=Projektnr.];
                SourceExpr=TargetJobNo }

    { 31  ;2   ;Field     ;
                CaptionML=[ENU=Job Description;
                           ESP=Descripci¢n proyecto;
                           DEA=Projektbeschreibung];
                SourceExpr=TargetJobDescription }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Bill-To Customer No.;
                           ESP=Factura-a Nß cliente;
                           DEA=Rech. an Deb.-Nr.];
                SourceExpr=TargetBillToCustomerNo;
                TableRelation=Customer }

    { 6   ;1   ;Group     ;
                CaptionML=[ENU=Apply;
                           ESP=Aplicar;
                           DEA=Anwenden] }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Copy Job Prices;
                           ESP=Copiar precios proyecto;
                           DEA=Projektpreise kopieren];
                SourceExpr=CopyJobPrices }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Copy Quantity;
                           ESP=Copiar cantidad;
                           DEA=Menge kopieren];
                SourceExpr=CopyQuantity }

    { 28  ;2   ;Field     ;
                CaptionML=[ENU=Copy Dimensions;
                           ESP=Copiar dimensiones;
                           DEA=Dimensionen kopieren];
                SourceExpr=CopyDimensions }

  }
  CODE
  {
    VAR
      SourceJob@1010 : Record 167;
      CopyJob@1013 : Codeunit 1006;
      SourceJobNo@1017 : Code[20];
      FromJobTaskNo@1009 : Code[20];
      ToJobTaskNo@1004 : Code[20];
      TargetJobNo@1008 : Code[20];
      TargetJobDescription@1007 : Text[50];
      TargetBillToCustomerNo@1014 : Code[20];
      FromDate@1006 : Date;
      ToDate@1005 : Date;
      Source@1003 : 'Job Planning Lines,Job Ledger Entries,None';
      PlanningLineType@1002 : 'Schedule+Contract,Schedule,Contract';
      LedgerEntryType@1001 : 'Usage+Sale,Usage,Sale';
      Text001@1016 : TextConst 'ENU=The job was successfully copied.;ESP=El proyecto se copi¢ correctamente.;DEA=Das Projekt wurde erfolgreich kopiert.';
      Text002@1019 : TextConst 'ENU=Job No. %1 will be assigned to the new Job. Do you want to continue?;ESP=El Nß proyecto %1 se asignar† al trabajo nuevo. ®Desea continuar?;DEA=Dem neuen Projekt wird die Projektnummer %1 zugewiesen. Soll der Vorgang fortgesetzt werden?';
      Text003@1018 : TextConst '@@@=Job Task 1000 does not exist.;ENU=%1 %2 does not exist.;ESP=No existe %1 %2.;DEA=%1 %2 existiert nicht.';
      CopyJobPrices@1012 : Boolean;
      CopyQuantity@1011 : Boolean;
      CopyDimensions@1000 : Boolean;
      PlanningLineTypeEnable@1020 : Boolean INDATASET;
      LedgerEntryLineTypeEnable@1021 : Boolean INDATASET;
      Text004@1015 : TextConst 'ENU=Provide a valid source %1.;ESP=Proporcione un origen v†lido %1.;DEA=Geben Sie ein gÅltiges Herkunftselement vom Typ ''%1'' an.';

    LOCAL PROCEDURE ValidateUserInput@1();
    VAR
      JobsSetup@1001 : Record 315;
      NoSeriesManagement@1000 : Codeunit 396;
    BEGIN
      IF (SourceJobNo = '') OR NOT SourceJob.GET(SourceJob."No.") THEN
        ERROR(Text004,SourceJob.TABLECAPTION);

      IF TargetJobNo = '' THEN BEGIN
        JobsSetup.GET;
        JobsSetup.TESTFIELD("Job Nos.");
        TargetJobNo := NoSeriesManagement.GetNextNo(JobsSetup."Job Nos.",0D,TRUE);
        IF NOT CONFIRM(Text002,TRUE,TargetJobNo) THEN BEGIN
          TargetJobNo := '';
          ERROR('');
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateSource@2();
    BEGIN
      CASE TRUE OF
        Source = Source::"Job Planning Lines":
          BEGIN
            PlanningLineTypeEnable := TRUE;
            LedgerEntryLineTypeEnable := FALSE;
          END;
        Source = Source::"Job Ledger Entries":
          BEGIN
            PlanningLineTypeEnable := FALSE;
            LedgerEntryLineTypeEnable := TRUE;
          END;
        Source = Source::None:
          BEGIN
            PlanningLineTypeEnable := FALSE;
            LedgerEntryLineTypeEnable := FALSE;
          END;
      END;
    END;

    PROCEDURE SetFromJob@3(SourceJob2@1000 : Record 167);
    BEGIN
      SourceJob := SourceJob2;
      SourceJobNo := SourceJob."No.";
      TargetJobDescription := SourceJob.Description;
      TargetBillToCustomerNo := SourceJob."Bill-to Customer No.";
    END;

    BEGIN
    END.
  }
}


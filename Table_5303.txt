OBJECT Table 5303 Outlook Synch. Filter
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Filter Type;
    OnInsert=BEGIN
               TESTFIELD("Field No.");
               VALIDATE(Value);
               UpdateFilterExpression;
             END;

    OnModify=BEGIN
               VALIDATE(Value);
               UpdateFilterExpression;
             END;

    CaptionML=[ENU=Outlook Synch. Filter;
               ESP=Filtro sinc. Outlook;
               DEA=Outlook-Synch.-Filter];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              ESP=GUID registro;
                                                              DEA=Datensatz-GUID];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Filter Type         ;Option        ;CaptionML=[ENU=Filter Type;
                                                              ESP=Filtro tipo;
                                                              DEA=Filterart];
                                                   OptionCaptionML=[ENU=Condition,Table Relation;
                                                                    ESP=Condici¢n,Relaci¢n tablas;
                                                                    DEA=Bedingung,Tabellenrelation];
                                                   OptionString=Condition,Table Relation }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea;
                                                              DEA=Zeilennr.] }
    { 4   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=N§ tabla;
                                                              DEA=Tabellennr.] }
    { 5   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                              BEGIN
                                                                IF "Field No." = 0 THEN BEGIN
                                                                  CLEAR(RecRef);
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                  ERROR(Text005,RecRef.CAPTION);
                                                                END;

                                                                IF "Field No." <> xRec."Field No." THEN
                                                                  IF Type <> Type::FIELD THEN
                                                                    Value := '';
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF "Table No." <> 0 THEN
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.")
                                                              ELSE
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.");

                                                              IF FieldNo > 0 THEN
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo;
                                                              DEA=Feldnr.] }
    { 7   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF ("Filter Type" = "Filter Type"::Condition) AND (Type = Type::FIELD) THEN
                                                                  ERROR(Text001,FORMAT(Type),FIELDCAPTION("Filter Type"),FORMAT("Filter Type"));

                                                                IF Type <> xRec.Type THEN
                                                                  Value := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo;
                                                              DEA=Art];
                                                   OptionCaptionML=[ENU=CONST,FILTER,FIELD;
                                                                    ESP=CONST,FILTRO,CAMPO;
                                                                    DEA=KONST,FILTER,FELD];
                                                   OptionString=CONST,FILTER,FIELD }
    { 8   ;   ;Value               ;Text250       ;OnValidate=BEGIN
                                                                ValidateFieldValuePair;
                                                              END;

                                                   OnLookup=VAR
                                                              OSynchTypeConversion@1003 : Codeunit 5302;
                                                              RecRef@1002 : RecordRef;
                                                              FldRef@1001 : FieldRef;
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF Type <> Type::FIELD THEN BEGIN
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(Rec.FIELDNO(Type));
                                                                ERROR(Text003,FIELDCAPTION(Type),OSynchTypeConversion.GetSubStrByNo(Type::FIELD + 1,FldRef.OPTIONCAPTION));
                                                              END;

                                                              FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.");

                                                              IF FieldNo <> 0 THEN
                                                                VALIDATE("Master Table Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              ESP=Valor;
                                                              DEA=Wert] }
    { 9   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Master Table No.;
                                                              ESP=N§ tabla principal;
                                                              DEA=Mastertabellennr.] }
    { 10  ;   ;Master Table Field No.;Integer     ;TableRelation=Field.No. WHERE (TableNo=FIELD(Master Table No.));
                                                   OnValidate=BEGIN
                                                                IF Field.GET("Master Table No.","Master Table Field No.") THEN
                                                                  VALIDATE(Value,Field."Field Caption");
                                                              END;

                                                   CaptionML=[ENU=Master Table Field No.;
                                                              ESP=N§ campo tabla principal;
                                                              DEA=Mastertabellen-Feldnr.] }
    { 99  ;   ;FilterExpression    ;Text250       ;CaptionML=[ENU=FilterExpression;
                                                              ESP=FilterExpression;
                                                              DEA=FilterExpression] }
  }
  KEYS
  {
    {    ;Record GUID,Filter Type,Line No.        ;Clustered=Yes }
    {    ;Table No.,Field No.                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1004 : Record 2000000041;
      OSynchSetupMgt@1005 : Codeunit 5300;
      OSynchTypeConversion@1006 : Codeunit 5302;
      Text001@1008 : TextConst 'ENU=You cannot select the %1 option when %2 is %3.;ESP=No puede seleccionar la opci¢n %1 cuando %2 es %3.;DEA=Die %1-Option kann nicht ausgew„hlt werden, wenn %2 %3 ist.';
      Text002@1007 : TextConst 'ENU=This value cannot be converted to the selected field datatype.;ESP=Este valor no se puede convertir al tipo de datos de campo seleccionado.;DEA=Dieser Wert kann nicht in den Datentyp des ausgew„hlten Felds konvertiert werden.';
      Text003@1003 : TextConst 'ENU=You can only open a lookup table when the %1 field contains the %2 value.;ESP=S¢lo puede abrir una tabla de b£squeda si el campo %1 contiene el valor %2.;DEA=Sie k”nnen eine Lookuptabelle nur ”ffnen, wenn das %1-Feld den Wert ''%2'' enth„lt.';
      Text004@1002 : TextConst 'ENU=This is not a valid option for the %1 field. The possible options are: ''%2''.;ESP=Esta opci¢n no es v lida para el campo %1. Las posibles opciones son: ''%2''.;DEA=Dies ist keine gltige Option fr das %1-Feld. Die m”glichen Optionen sind: ''%2''.';
      Text005@1001 : TextConst 'ENU=Choose a valid field in the %1 table.;ESP=Elija un campo v lido en la tabla %1.;DEA=W„hlen Sie ein gltiges Feld in der %1-Tabelle aus.';
      Text006@1009 : TextConst 'ENU=The value in this field cannot be longer than %1.;ESP=El valor de este campo no puede ser mayor que %1.;DEA=Der Wert in diesem Feld darf nicht l„nger als %1 sein.';

    PROCEDURE SetTablesNo@1(TableLeftNo@1000 : Integer;TableRightNo@1001 : Integer);
    BEGIN
      "Table No." := TableLeftNo;
      "Master Table No." := TableRightNo;
    END;

    PROCEDURE ValidateFieldValuePair@2();
    VAR
      RecRef@1000 : RecordRef;
      FldRef@1001 : FieldRef;
      NameString@1004 : Text[250];
      TempBool@1003 : Boolean;
    BEGIN
      TESTFIELD("Table No.");

      CLEAR(RecRef);
      CLEAR(FldRef);
      RecRef.OPEN("Table No.",TRUE);

      IF "Field No." = 0 THEN
        ERROR(Text005,RecRef.CAPTION);

      Field.GET("Table No.","Field No.");
      FldRef := RecRef.FIELD("Field No.");

      CASE Type OF
        Type::CONST:
          CASE Field.Type OF
            Field.Type::Option:
              IF NOT OSynchTypeConversion.EvaluateOptionField(FldRef,Value) THEN
                ERROR(Text004,Field."Field Caption",FldRef.OPTIONSTRING);
            Field.Type::Code,Field.Type::Text:
              BEGIN
                IF STRLEN(Value) > Field.Len THEN
                  ERROR(Text006,Field.Len);
                IF NOT EVALUATE(FldRef,Value) THEN
                  ERROR(Text002);
              END;
            Field.Type::Boolean:
              BEGIN
                IF NOT EVALUATE(TempBool,Value) THEN
                  ERROR(Text002);
                Value := FORMAT(TempBool);
              END;
            ELSE
              IF NOT EVALUATE(FldRef,Value) THEN
                ERROR(Text002);
          END;
        Type::FILTER:
          BEGIN
            IF Field.Type = Field.Type::Option THEN BEGIN
              IF NOT OSynchTypeConversion.EvaluateFilterOptionField(FldRef,Value,FALSE) THEN
                ERROR(Text004,Field."Field Caption",FldRef.OPTIONSTRING);
            END;
            FldRef.SETFILTER(Value);
          END;
        Type::FIELD:
          BEGIN
            NameString := Value;
            IF NOT OSynchSetupMgt.ValidateFieldName(NameString,"Master Table No.") THEN BEGIN
              RecRef.CLOSE;
              RecRef.OPEN("Master Table No.",TRUE);
              ERROR(Text005,RecRef.CAPTION);
            END;

            Value := NameString;
          END;
      END;
      RecRef.CLOSE;
    END;

    PROCEDURE RecomposeFilterExpression@3() FilterExpression : Text[250];
    BEGIN
      FilterExpression := OSynchSetupMgt.ComposeFilterExpression("Record GUID","Filter Type");
    END;

    PROCEDURE GetFieldCaption@5() : Text;
    BEGIN
      IF Field.GET("Table No.","Field No.") THEN
        EXIT(Field."Field Caption");
      EXIT('');
    END;

    PROCEDURE GetFilterExpressionValue@4() : Text[250];
    VAR
      ValueStartIndex@1000 : Integer;
    BEGIN
      ValueStartIndex := STRPOS(FilterExpression,'(');
      EXIT(COPYSTR(FilterExpression,ValueStartIndex + 1,STRLEN(FilterExpression) - ValueStartIndex - 1));
    END;

    PROCEDURE UpdateFilterExpression@6();
    VAR
      TempRecordRef@1000 : RecordRef;
      ViewExpression@1001 : Text;
      WhereIndex@1002 : Integer;
      TempBoolean@1003 : Boolean;
    BEGIN
      FilterExpression := '';
      IF Type <> Type::FIELD THEN
        IF "Table No." <> 0 THEN BEGIN
          TempRecordRef.OPEN("Table No.");

          ViewExpression := GetFieldCaption + STRSUBSTNO('=FILTER(%1)',Value);

          ViewExpression := STRSUBSTNO('WHERE(%1)',ViewExpression);
          TempRecordRef.SETVIEW(ViewExpression);

          ViewExpression := TempRecordRef.GETVIEW(FALSE);
          WhereIndex := STRPOS(ViewExpression,'WHERE(') + 6;
          FilterExpression := COPYSTR(ViewExpression,WhereIndex,STRLEN(ViewExpression) - WhereIndex);

          IF Field.GET("Table No.","Field No.") THEN
            IF Field.Type = Field.Type::Boolean THEN BEGIN
              EVALUATE(TempBoolean,Value);
              IF TempBoolean THEN
                FilterExpression := COPYSTR(StringReplace(FilterExpression,Value,'1'),1,250)
              ELSE
                FilterExpression := COPYSTR(StringReplace(FilterExpression,Value,'0'),1,250);
            END;
        END;
    END;

    LOCAL PROCEDURE StringReplace@8(Input@1000 : Text;Find@1001 : Text;Replace@1002 : Text) : Text;
    VAR
      Pos@1003 : Integer;
    BEGIN
      Pos := STRPOS(Input,Find);
      WHILE Pos <> 0 DO BEGIN
        Input := DELSTR(Input,Pos,STRLEN(Find));
        Input := INSSTR(Input,Replace,Pos);
        Pos := STRPOS(Input,Find);
      END;
      EXIT(Input);
    END;

    BEGIN
    END.
  }
}


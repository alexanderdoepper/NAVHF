OBJECT Codeunit 6 Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=08.06.01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=50;
    OnRun=BEGIN
            AccountingPeriod.COPY(Rec);
            Code;
            Rec := AccountingPeriod;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;ESP=Antes de cerrar un ejercicio hay que crear el siguiente.;DEA=Sie mssen ein neues Gesch„ftsjahr er”ffnen, bevor Sie das alte abschlieáen k”nnen.';
      Text002@1001 : TextConst 'ENU="This function closes the fiscal year from %1 to %2. ";ESP="Esta funci¢n cierra el ejercicio comprendido entre %1 y %2. ";DEA="Diese Funktion schlieát das Gesch„ftsjahr von %1 bis %2 ab. "';
      Text003@1002 : TextConst 'ENU=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\;ESP=Cuando se ha cerrado un ejercicio no se pueden cambiar los periodos que lo componen ni se puede volver a abrir.\\;DEA=Abgeschlossene Gesch„ftsjahre k”nnen nicht wieder ge”ffnet und ihre Perioden nicht ge„ndert werden.\\';
      Text004@1003 : TextConst 'ENU=Do you want to close the fiscal year?;ESP=¨Confirma que desea cerrar el ejercicio?;DEA=M”chten Sie das Gesch„ftsjahr abschlieáen?';
      AccountingPeriod@1004 : Record 50;
      AccountingPeriod2@1005 : Record 50;
      AccountingPeriod3@1006 : Record 50;
      FiscalYearStartDate@1007 : Date;
      FiscalYearEndDate@1008 : Date;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH AccountingPeriod DO BEGIN
        AccountingPeriod2.SETRANGE(Closed,FALSE);
        AccountingPeriod2.FIND('-');

        FiscalYearStartDate := AccountingPeriod2."Starting Date";
        AccountingPeriod := AccountingPeriod2;
        TESTFIELD("New Fiscal Year",TRUE);

        AccountingPeriod2.SETRANGE("New Fiscal Year",TRUE);
        IF AccountingPeriod2.FIND('>') THEN BEGIN
          FiscalYearEndDate := CALCDATE('<-1D>',AccountingPeriod2."Starting Date");

          AccountingPeriod3 := AccountingPeriod2;
          AccountingPeriod2.SETRANGE("New Fiscal Year");
          AccountingPeriod2.FIND('<');
        END ELSE
          ERROR(Text001);

        IF NOT
           CONFIRM(
             Text002 +
             Text003 +
             Text004,FALSE,
             FiscalYearStartDate,FiscalYearEndDate)
        THEN
          EXIT;

        RESET;

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod2."Starting Date");
        MODIFYALL(Closed,TRUE);

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod3."Starting Date");
        MODIFYALL("Date Locked",TRUE);

        RESET;
      END;
    END;

    BEGIN
    END.
  }
}


OBJECT Report 1191 Suggest Res. Price Chg. (Res.)
{
  OBJECT-PROPERTIES
  {
    Date=28.03.13;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Res. Price Chg. (Res.);
               ESP=Prop. mod. recurso (recurso);
               DEA=Res.-VK-Preisvorschlag (Res.)];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);

                  IF ToCurrency.Code = '' THEN BEGIN
                    ToCurrency.InitRoundingPrecision;
                  END ELSE BEGIN
                    ToCurrency.FIND;
                    ToCurrency.TESTFIELD("Unit-Amount Rounding Precision");
                  END;

                  WITH ResPriceChg DO BEGIN
                    "Currency Code" := ToCurrency.Code;
                    "Work Type Code" := ToWorkType.Code;
                  END;
                END;

  }
  DATASET
  {
    { 5508;    ;DataItem;                    ;
               DataItemTable=Table156;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000)
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  WITH ResPriceChg DO BEGIN
                                    Type := Type::Resource;
                                    Code := Resource."No.";
                                    "New Unit Price" :=
                                      ROUND(
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ToCurrency.Code,
                                          Resource."Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ToCurrency.Code)),
                                        ToCurrency."Unit-Amount Rounding Precision");

                                    IF "New Unit Price" > PriceLowerLimit THEN
                                      "New Unit Price" := "New Unit Price" * UnitPriceFactor;
                                    IF RoundingMethod.Code <> '' THEN BEGIN
                                      RoundingMethod."Minimum Amount" := "New Unit Price";
                                      IF RoundingMethod.FIND('=<') THEN BEGIN
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added Before";

                                        IF RoundingMethod.Precision > 0 THEN
                                          "New Unit Price" :=
                                            ROUND(
                                              "New Unit Price",
                                              RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added After";
                                      END;
                                    END;

                                    ResPrice.SETRANGE(Type,Type);
                                    ResPrice.SETRANGE(Code,Code);
                                    ResPrice.SETRANGE("Currency Code",ToCurrency.Code);
                                    ResPrice.SETRANGE("Work Type Code",ToWorkType.Code);
                                    IF ResPrice.FINDLAST THEN BEGIN
                                      "Current Unit Price" := ResPrice."Unit Price";
                                      PriceAlreadyExists := TRUE
                                    END ELSE BEGIN
                                      "Current Unit Price" := 0;
                                      PriceAlreadyExists := FALSE;
                                    END;

                                    IF PriceAlreadyExists OR CreateNewPrices THEN BEGIN
                                      ResPriceChg2 := ResPriceChg;
                                      IF ResPriceChg2.FIND('=') THEN
                                        MODIFY
                                      ELSE
                                        INSERT;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Resource Group No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF UnitPriceFactor = 0 THEN
                     UnitPriceFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones;
                             DEA=Optionen] }

      { 15  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to Res. Price Change...;
                             ESP=Copia a modif. precio recurso...;
                             DEA=In Res.-VK-Preisvorschlag kopieren...] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Currency Code;
                             ESP=C¢d. divisa;
                             DEA=W„hrungscode];
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=Work Type;
                             ESP=Tipo trabajo;
                             DEA=Arbeitstyp];
                  SourceExpr=ToWorkType.Code;
                  TableRelation="Work Type" }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Only Amounts Above;
                             ESP=S¢lo importes superiores a;
                             DEA=Nur Betr„ge ber];
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             ESP=Factor ajuste;
                             DEA=Korrekturfaktor];
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPriceFactor;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             ESP=M‚todo redondeo;
                             DEA=Rundungsmethode];
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Create New Prices;
                             ESP=Crear tarifa nueva;
                             DEA=Neue Preise generieren];
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing items  #1##########;ESP=Procesando prods. #1##########;DEA=Artikel werden durchgelesen #1##########';
      RoundingMethod@1001 : Record 42;
      ToCurrency@1002 : Record 4;
      CurrExchRate@1003 : Record 330;
      ToWorkType@1010 : Record 200;
      ResPriceChg2@1009 : Record 335;
      ResPriceChg@1008 : Record 335;
      ResPrice@1013 : Record 201;
      Window@1012 : Dialog;
      PriceAlreadyExists@1004 : Boolean;
      CreateNewPrices@1005 : Boolean;
      UnitPriceFactor@1006 : Decimal;
      PriceLowerLimit@1007 : Decimal;

    PROCEDURE InitializeCopyToResPrice@2(CurrencyCode@1000 : Code[10];WorkTypeCode@1002 : Code[10]);
    BEGIN
      ToCurrency.Code := CurrencyCode;
      ToWorkType.Code := WorkTypeCode;
    END;

    PROCEDURE InitializeRequest@1(PriceLowerLimitFrom@1003 : Decimal;UnitPriceFactorFrom@1004 : Decimal;RoundingMethodCode@1005 : Code[10];CreateNewPricesFrom@1006 : Boolean);
    BEGIN
      PriceLowerLimit := PriceLowerLimitFrom;
      UnitPriceFactor := UnitPriceFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
      CreateNewPrices := CreateNewPricesFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

